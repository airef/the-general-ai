;====First Attack

;====GOAL: g-required-attack-num and g-full-attack-num

(defrule
(up-compare-goal g-attack-status <= gv-first-attack)
(or
	(goal g-initial-strategy gv-krush)
	(goal g-initial-strategy gv-crush))
=>
(set-goal g-required-attack-num 6)
)

(defrule
(up-compare-goal g-attack-status <= gv-first-attack)
(goal g-initial-strategy gv-eagles-revenge)
=>
(set-goal g-required-attack-num 8)
)

(defrule
(up-compare-goal g-attack-status <= gv-first-attack)
(goal g-strategy-type gv-drush)
=>
(set-goal g-required-attack-num 3)
)

(defrule
(up-compare-goal g-attack-status <= gv-first-attack)
(goal g-strategy-type gv-flush)
=>
(set-goal g-required-attack-num 8)
)

;(defrule
;(up-compare-goal g-attack-status <= gv-first-attack)
;(goal g-strategy-type gv-default)
;=>
;(set-goal g-required-attack-num 8)
;)

;Start First Attack

(defrule
(up-compare-goal g-attack-status < gv-first-attack-marching)
(goal g-attacking NO)
;(goal g-town-under-attack NO)
;(or
	(military-population g:>= g-required-attack-num)
;	(up-compare-goal g-military-superiority > 4))
(players-building-count any-enemy > 0)
(goal g-save-for-next-age NO)
=>
(up-modify-goal g-attacking c:= YES)
(set-goal g-tsa YES)
(up-modify-goal g-attack-status c:= gv-first-attack-marching)
(up-modify-goal g-temp g:= g-closest-enemy-building-distance)
(up-modify-goal g-temp c:/ 2)
(up-set-timer c: t-attack g: g-temp)
(chat-to-player my-player-number "Start first attack - marching")
)

(defrule
(goal g-attack-status gv-first-attack-marching)
(up-compare-goal g-attacking >= YES)
(up-timer-status t-attack == timer-triggered)
(military-population g:>= g-full-attack-num)
=>
(set-goal g-attack-status gv-first-attack)
(disable-timer t-attack)
(enable-timer t-attack 300)
)

(defrule
(up-compare-goal g-attack-status >= gv-first-attack-marching)
=>
(up-get-fact military-population 0 g-temp)
;(up-chat-data-to-self "Military: %d" g: g-temp)
;(up-chat-data-to-self "Req: %d" g: g-required-attack-num)
;(up-chat-data-to-self "Full: %d" g: g-full-attack-num)
)


;Stop attacking after first attack

(defrule
(up-compare-goal g-attack-status == gv-first-attack-marching)
(up-compare-goal g-attacking >= YES)
(military-population g:< g-required-attack-num)
=>
(set-goal g-attacking NO)
(set-goal g-tsa NO)
(set-goal g-attack-status gv-before-later-attacks)
(chat-to-player my-player-number "Stop first attack - lost soldiers before reaching enemy")
(up-modify-sn sn-maximum-town-size s:= sn-safe-town-size)
(up-update-targets)
(up-retreat-now)
)

; (defrule
; (goal g-attack-status gv-first-attack)
; (up-compare-goal g-attacking >= YES)
; (up-compare-goal g-required-attack-num > 0)
; =>
; (up-modify-goal g-temp g:= g-required-attack-num)
; (up-modify-goal g-temp c:%* 125)
; )

(defrule
(goal g-attack-status gv-first-attack)
(up-compare-goal g-attacking >= YES)
(military-population g:< g-required-attack-num)
=>
(set-goal g-attacking NO)
(set-goal g-tsa NO)
(up-modify-goal g-attack-status c:= gv-before-later-attacks)
(chat-to-player my-player-number "Stop first attack - lost too many soldiers")
(up-modify-sn sn-maximum-town-size s:= sn-safe-town-size)
(up-update-targets)
(up-retreat-now)
)

; (defrule
; (goal g-attack-status gv-first-attack)
; (up-compare-goal g-attacking >= YES)
; (military-population g:< g-temp)
; (or
; 	(and
; 		(up-compare-goal g-military-superiority < -6)
; 		(players-current-age target-player >= castle-age))
; 	(up-compare-goal g-military-superiority < -12))
; =>
; (set-goal g-attacking NO)
; (set-goal g-tsa NO)
; (up-modify-goal g-attack-status c:= gv-before-later-attacks)
; (chat-to-player my-player-number "Stop first attack - outnumbered")
; (up-modify-sn sn-maximum-town-size s:= sn-safe-town-size)
; (up-update-targets)
; (up-retreat-now)
; )

(defrule
(up-compare-goal g-attack-status == gv-first-attack)
(up-compare-goal g-attacking >= YES)
(goal g-save-for-next-age YES)
=>
(set-goal g-attacking NO)
(set-goal g-tsa NO)
(up-modify-goal g-attack-status c:= gv-before-later-attacks)
(chat-to-player my-player-number "Stop first attack - save for next age")
(up-modify-sn sn-maximum-town-size s:= sn-safe-town-size)
(up-update-targets)
(up-retreat-now)
)

(defrule
(up-compare-goal g-attack-status == gv-first-attack)
(up-compare-goal g-attacking >= YES)
(or
	(game-time >= 1800)
	(timer-triggered t-attack))
=>
(up-modify-goal g-attacking c:= NO)
(set-goal g-tsa NO)
(up-modify-goal g-attack-status c:= gv-before-later-attacks)
(chat-to-player my-player-number "Move on to later attacks")
(up-modify-sn sn-maximum-town-size s:= sn-safe-town-size)
(up-update-targets)
(up-retreat-now)
)

#load-if-defined ADD-BACK-IN-WHEN-G-TOWN-UNDER-ATTACK-ONLY-APPLIES-TO-SAFE-TOWN-SIZE

	(defrule
	(up-compare-goal g-attack-status == gv-first-attack)
	(up-compare-goal g-attacking >= YES)
	(goal g-town-under-attack YES)
	=>
	(up-modify-goal g-attacking c:= NO)
	(set-goal g-tsa NO)
	(up-modify-goal g-attack-status c:= gv-before-later-attacks)
	(chat-to-player my-player-number "stop first attack to defend town")
	(up-modify-sn sn-maximum-town-size s:= sn-safe-town-size)
	(up-update-targets)
	(up-retreat-now)
	)
	
#end-if

;Adjust Required Attack Num amount if first attack once full-attack-num is reached

(defrule
(up-compare-goal g-attacking >= YES)
(up-compare-goal g-attack-status < gv-before-later-attacks)
(up-compare-goal g-required-attack-num < 15)
=>
(up-modify-goal g-full-attack-num g:= g-closest-enemy-building-distance)
(up-modify-goal g-full-attack-num c:/ 40)
(up-modify-goal g-full-attack-num g:+ g-required-attack-num)
(up-modify-goal g-required-attack-num g:= g-full-attack-num)
(up-modify-goal g-required-attack-num c:%* 80)
(up-modify-goal g-temp g:= g-full-attack-num)
(up-modify-goal g-temp c:- 3)
(up-modify-goal g-required-attack-num g:min g-temp)
)

;====Later Attacks

;Set desired attack numbers

(defrule
(up-compare-goal g-attack-status > gv-first-attack)
(goal g-strategy-type gv-drush)
=>
(set-goal g-full-attack-num 15)
(set-goal g-required-attack-num 10)
)

(defrule
(up-compare-goal g-attack-status > gv-first-attack)
(goal g-strategy-type gv-flush)
=>
(set-goal g-full-attack-num 25)
(set-goal g-required-attack-num 14)
)

(defrule
(up-compare-goal g-attack-status > gv-first-attack)
(up-compare-goal g-strategy-type >= gv-fc)
(current-age == castle-age)
=>
(set-goal g-full-attack-num 30)
(set-goal g-required-attack-num 20)
)

(defrule
(up-compare-goal g-attack-status > gv-first-attack)
(up-compare-goal g-strategy-type >= gv-fc)
(current-age == imperial-age)
=>
(set-goal g-full-attack-num 50)
(set-goal g-required-attack-num 35)
)

;Start attacking for later attacks

(defrule
(up-compare-goal g-attack-status >= gv-before-later-attacks)
(up-compare-goal g-attacking >= YES)
(up-compare-goal g-required-attack-num > 0)
=>
(up-modify-goal g-temp g:= g-required-attack-num)
(up-modify-goal g-temp c:%* 125)
)

(defrule
(up-compare-goal g-attack-status >= gv-before-later-attacks)
(goal g-attacking NO)
;(goal g-town-under-attack NO)
(military-population g:>= g-full-attack-num)
(military-population g:>= g-required-attack-num)
(or
	(military-population g:>= g-temp-2)
	(up-compare-goal g-military-superiority g:>= -13))
(goal g-save-for-next-age NO)
=>
(set-goal g-attacking YES)
(set-goal g-tsa YES)
(set-goal g-attack-status gv-later-attacks-marching)
(up-modify-goal g-temp g:= g-closest-enemy-building-distance)
(up-modify-goal g-temp c:/ 2)
(up-set-timer c: t-attack g: g-temp)
(chat-to-player my-player-number "Start later attacks, high mil pop")
)

(defrule
(up-compare-goal g-attack-status >= gv-before-later-attacks)
(goal g-attacking NO)
;(goal g-town-under-attack NO)
=>
(up-modify-goal g-temp g:= g-full-attack-num)
(up-modify-goal g-temp c:%* 30)
(up-modify-goal g-temp c:max 12)
)

(defrule
(up-compare-goal g-attack-status >= gv-before-later-attacks)
(goal g-attacking NO)
;(goal g-town-under-attack NO)
(up-compare-goal g-military-superiority g:>= g-temp)
(goal g-save-for-next-age NO)
=>
(up-modify-goal g-attacking c:= YES)
(set-goal g-tsa YES)
(set-goal g-attack-status gv-later-attacks-marching)
(up-modify-goal g-temp g:= g-closest-enemy-building-distance)
(up-modify-goal g-temp c:/ 2)
(up-set-timer c: t-attack g: g-temp)
(chat-to-player my-player-number "Start later attacks, sup numbers")
)

(defrule
(goal g-attack-status gv-later-attacks-marching)
(up-compare-goal g-attacking >= YES)
(up-timer-status t-attack == timer-triggered)
(military-population g:>= g-full-attack-num)
=>
(set-goal g-attack-status gv-later-attacks)
)

;Stop attacking after later attacks

;(defrule
;(up-compare-goal g-attack-status == gv-later-attacks-marching)
;(up-compare-goal g-attacking >= YES)
;(military-population g:< g-required-attack-num)
;=>
;(set-goal g-attacking NO)
;(set-goal g-tsa NO)
;(set-goal g-attack-status gv-before-later-attacks)
;(chat-to-player my-player-number "Stop later attack - lost soldiers before reaching enemy")
;(up-modify-sn sn-maximum-town-size s:= sn-safe-town-size)
;(up-update-targets)
;(up-retreat-now)
;)

(defrule
(up-compare-goal g-attack-status >= gv-later-attacks-marching)
(up-compare-goal g-attacking >= YES)
(up-compare-goal g-required-attack-num > 0)
=>
(up-modify-goal g-temp g:= g-required-attack-num)
(up-modify-goal g-temp c:%* 125)
)

(defrule
(up-compare-goal g-attacking >= YES)
(up-compare-goal g-attack-status >= gv-later-attacks-marching)
(or
	(military-population g:< g-required-attack-num)
	(and
		(military-population g:< g-temp)
		(up-compare-goal g-military-superiority < -5)))
=>
(up-modify-goal g-attacking c:= NO)
(set-goal g-tsa NO)
(set-goal g-attack-status gv-before-later-attacks)
(chat-to-player my-player-number "Stop attacking")
(up-modify-sn sn-maximum-town-size s:= sn-safe-town-size)
(up-update-targets)
(up-retreat-now)
)

(defrule
(up-compare-goal g-attack-status >= gv-later-attacks-marching)
(up-compare-goal g-attacking >= YES)
(goal g-save-for-next-age YES)
=>
(up-modify-goal g-attacking c:= NO)
(set-goal g-tsa NO)
(set-goal g-attack-status gv-before-later-attacks)
(chat-to-player my-player-number "Stop attacking for next age")
(up-modify-sn sn-maximum-town-size s:= sn-safe-town-size)
(up-update-targets)
(up-retreat-now)
)

#load-if-defined ADD-BACK-IN-WHEN-G-TOWN-UNDER-ATTACK-ONLY-APPLIES-TO-SAFE-TOWN-SIZE

	(defrule
	(up-compare-goal g-attacking >= YES)
	(up-compare-goal g-attack-status >= gv-later-attacks-marching)
	(goal g-town-under-attack YES)
	=>
	(up-modify-goal g-attacking c:= NO)
	(set-goal g-tsa NO)
	(set-goal g-attack-status gv-before-later-attacks)
	(chat-to-player my-player-number "Stop attacking to defend town")
	(up-modify-sn sn-maximum-town-size s:= sn-safe-town-size)
	(up-update-targets)
	(up-retreat-now)
	)
	
#end-if

;Projectile Retreat

(defrule
(goal g-projectile-retreat NO)
(up-compare-goal g-attacking >= YES)
(or
	(up-projectile-detected projectile-town-center < 5000)
	(or
		(up-projectile-detected projectile-castle < 5000)
		(up-projectile-detected projectile-bombard-tower < 5000)))
(military-population < 20)
(up-compare-goal g-siege-class < 2)
=>
(up-modify-sn sn-maximum-town-size c:- 4)
(chat-local-to-self "retreat from projectiles")
(up-update-targets)
(up-retreat-now)
(enable-timer t-projectile-retreat PROJECTILE-RETREAT-TIME)
(set-goal g-projectile-retreat YES)
)

(defrule
(goal g-projectile-retreat NO)
(up-compare-goal g-attacking >= YES)
(or
	(up-projectile-detected projectile-town-center < 5000)
	(or
		(up-projectile-detected projectile-castle < 5000)
		(up-projectile-detected projectile-bombard-tower < 5000)))
(military-population < 35)
(up-compare-goal g-military-superiority < 0)
(up-compare-goal g-siege-class < 2)
=>
(up-modify-sn sn-maximum-town-size c:- 4)
(chat-local-to-self "retreat from projectiles")
(up-update-targets)
(up-retreat-now)
(enable-timer t-projectile-retreat PROJECTILE-RETREAT-TIME)
(set-goal g-projectile-retreat YES)
)

#load-if-defined FIX

	(defrule
	(goal g-projectile-retreat NO)
	(goal g-attacking NO)
	(or
		(up-projectile-detected projectile-town-center < 5000)
		(or
			(up-projectile-detected projectile-castle < 5000)
			(or
				(up-projectile-detected projectile-bombard-tower < 5000)
				(up-projectile-detected projectile-watch-tower < 5000))))
	(up-enemy-buildings-in-town == 0)
	(up-compare-goal g-town-under-attack != YES)
	=>
	(chat-local-to-self "retreat from projectiles")
	(up-update-targets)
	(up-retreat-now)
	(enable-timer t-projectile-retreat PROJECTILE-RETREAT-TIME)
	(set-goal g-projectile-retreat YES)
	)

#end-if

(defrule
(timer-triggered t-projectile-retreat)
(goal g-projectile-retreat YES)
(up-compare-goal g-attacking >= YES)
(not
	(up-projectile-detected projectile-town-center < 5000))
(not
	(up-projectile-detected projectile-castle < 5000))
(not
	(up-projectile-detected projectile-bombard-tower < 5000))
=>
(up-update-targets)
(set-goal g-projectile-retreat NO)
)

(defrule
(timer-triggered t-projectile-retreat)
(goal g-projectile-retreat YES)
(goal g-attacking NO)
=>
(up-update-targets)
(set-goal g-projectile-retreat NO)
)

;Increase Town Size for TSA

(defrule
(up-compare-goal g-attacking >= YES)
(up-compare-goal g-tsa >= YES)
=>
(up-modify-goal g-temp g:= g-closest-enemy-building-distance)	;set to nearest building of target enemy player
;(up-modify-goal g-temp g:min g-nearest-enemy-defenses-distance)	;or nearest castle/tower of all enemies
(up-modify-goal g-temp c:+ 5)	;give some wiggle room to TSA and allow for multiple building target options
)

(defrule
(up-compare-goal g-attacking >= YES)
(goal g-tsa YES)
(up-compare-goal g-temp s:> sn-maximum-town-size)
=>
;(set-goal g-tsa gv-increasing-tsa)
(up-modify-sn sn-maximum-town-size g:= g-temp)	;nearest enemy building distance + 5
(up-chat-data-to-self "Increase TSA: %d" s: sn-maximum-town-size)
)

(defrule
(up-compare-goal g-attacking >= YES)
(goal g-tsa YES)
(up-compare-goal g-temp s:< sn-maximum-town-size)
(up-enemy-buildings-in-town > 0)
;(timer-triggered t-tsa)
=>
;(disable-timer t-tsa)
;(set-goal g-tsa gv-decreasing-tsa)
(up-modify-sn sn-maximum-town-size g:= g-temp)
(up-modify-sn sn-maximum-town-size c:min 255)
(up-chat-data-to-self "Decrease TSA: %d" s: sn-maximum-town-size)
)

; (defrule
; (up-compare-goal g-attacking >= YES)
; (goal g-tsa YES)
; (up-compare-goal g-temp s:<= sn-maximum-town-size)
; (up-enemy-buildings-in-town == 0)
; (up-compare-sn sn-maximum-town-size g:< g-map-size-ts)
; =>
; (up-modify-sn sn-maximum-town-size c:+ 10)
; (up-chat-data-to-self "No buildings in town - increase TSA to %d" s: sn-maximum-town-size)
; )

;(defrule
;(up-compare-goal g-attacking >= YES)
;(nor
;	(goal g-tsa NO)
;	(goal g-tsa gv-holding-tsa))
;(up-enemy-buildings-in-town == 0)
;(players-building-count any-enemy > 0)
;=>
;(set-goal g-tsa gv-increasing-tsa)
;(up-modify-sn sn-maximum-town-size c:+ 4)
;(up-chat-data-to-self "Increase TS: %d" s: sn-maximum-town-size)
;)

;(defrule
;(up-compare-goal g-attacking == YES)
;(or
;	(goal g-tsa gv-starting-tsa)
;	(goal g-tsa gv-decreasing-tsa))
;(up-compare-sn sn-maximum-town-size g:>= g-temp)
;=>
;(set-goal g-tsa gv-holding-tsa)
;(enable-timer t-tsa 10)
;(chat-local-to-self "hold TSA")
;)

;(defrule
;(goal g-attacking YES)
;(goal g-tsa gv-holding-tsa)
;(up-enemy-buildings-in-town == 0)
;(timer-triggered t-tsa)
;=>
;(disable-timer t-tsa)
;(set-goal g-tsa gv-increasing-tsa)
;(chat-local-to-self "Don't decrease. Buildings no longer in town.")
;)

(defrule
(up-compare-goal g-attacking >= YES)
=>
(up-chat-data-to-self "Closest enemy dist: %d" g: g-closest-enemy-building-distance)
(up-chat-data-to-self "Nearest enemy defense: %d" g: g-nearest-enemy-defenses-distance)
(up-chat-data-to-self "Required: %d" g: g-required-attack-num)
(up-chat-data-to-self "Full: %d" g: g-full-attack-num)
(up-chat-data-to-self "Max TS: %d" s: sn-maximum-town-size)
)

;If enemies have no buildings left, switch to attack groups

(defrule
(up-compare-goal g-attacking == YES)
(up-compare-goal g-tsa >= YES)
(players-building-count every-enemy == 0)
(game-time > 600)
(up-compare-sn sn-maximum-town-size g:>= g-map-size-ts)
(up-compare-sn sn-number-attack-groups == 0)
=>
(set-goal g-tsa NO)
(up-modify-sn sn-number-explore-groups c:= 4)
(up-modify-sn sn-total-number-explorers c:= 4)
(up-modify-sn sn-number-attack-groups c:= 20)
(up-modify-sn sn-percent-attack-soldiers c:= 100)
(up-modify-sn sn-task-ungrouped-soldiers c:= 1)
(chat-local-to-self "No enemy buildings. Switch to attack groups.")
)

(defrule
(up-compare-goal g-attacking == YES)
(up-compare-sn sn-number-attack-groups != 0)
(players-building-count every-enemy != 0)
(game-time > 600)
=>
(set-goal g-tsa YES)
(up-modify-sn sn-number-explore-groups c:= 1)
(up-modify-sn sn-total-number-explorers c:= 1)
(up-modify-sn sn-number-attack-groups c:= 0)
(up-modify-sn sn-task-ungrouped-soldiers c:= 0)
(chat-local-to-self "Found enemy buildings. Switch back to TSA.")
)