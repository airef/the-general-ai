;Personality.per

#load-if-not-defined VIETNAMESE-CIV

	(defrule
	(true)
	=>
	(up-get-fact player-number 0 g-initial-chat-delay)
	(up-modify-goal g-initial-chat-delay c:* 3)
	(up-modify-goal g-initial-chat-delay c:+ 10)
	(disable-self)
	)

#else
		
	(defrule
	(true)
	=>
	(set-goal g-initial-chat-delay 34)
	(disable-self)
	)
	
#end-if

;=========================<>=========================
;				       CHATTING
;=========================<>=========================

	(defrule
	(game-time >= 10)
	=>
	(chat-to-all CHAT-GENERAL-VERSION)
	(disable-self)
	)

	#load-if-defined AZTEC-CIV
		
		;Chat
		(defrule
		(goal g-general CUAUHTEMOC)
		(current-age == dark-age)
		(game-time g:> g-initial-chat-delay)
		=>
		(chat-to-all CHAT-GENERAL1)
		(disable-self)
		)
		
		(defrule
		(goal g-general CUAUHTEMOC)
		(current-age == feudal-age)
		=>
		(chat-to-all CHAT-GENERAL2)
		(disable-self)
		)
		
		(defrule
		(goal g-general CUAUHTEMOC)
		(current-age == castle-age)
		=>
		(chat-to-all CHAT-GENERAL3)
		(disable-self)
		)
		
		(defrule
		(goal g-general CUAUHTEMOC)
		(current-age == imperial-age)
		=>
		(chat-to-all CHAT-GENERAL4)
		(disable-self)
		)
		
		(defrule
		(goal g-general MONTEZUMA)
		(game-time g:> g-initial-chat-delay)
		=>
		(chat-to-all CHAT-GENERAL5)
		(chat-to-all CHAT-GENERAL6)
		(disable-self)
		)

	#end-if

	#load-if-defined BRITON-CIV
		
		;Longshanks
		(defrule
		(goal g-general EDWARD-LONGSHANKS)
		(game-time g:> g-initial-chat-delay)
		=>
		(set-strategic-number sn-focus-player-number 1)
		(set-goal g-temp 500)
		(disable-self)
		)
		
			(defrule
			(goal g-temp 500)
			(goal g-general EDWARD-LONGSHANKS)
			(game-time g:> g-initial-chat-delay)
			=>
			(up-store-player-name focus-player)
			)
			
			(defrule
			(goal g-temp 500)
			(up-compare-text c: TEXT-WILLIAM-WALLACE >= 0)
			(goal g-general EDWARD-LONGSHANKS)
			(game-time g:> g-initial-chat-delay)
			=>
			(set-goal g-temp 501)
			)
			
			(defrule
			(goal g-temp 500)
			(goal g-general EDWARD-LONGSHANKS)
			(game-time g:> g-initial-chat-delay)
			(up-compare-sn sn-focus-player-number < MAX-PLAYERS)
			=>
			(up-modify-sn sn-focus-player-number c:+ 1)
			(up-jump-rule -3)
			)
		
		(defrule
		(goal g-temp 500)
		(goal g-general EDWARD-LONGSHANKS)
		=>
		(chat-to-enemies CHAT-GENERAL1)
		(chat-to-enemies CHAT-GENERAL2)
		(disable-self)
		)
		
		(defrule
		(goal g-temp 501)
		(goal g-general EDWARD-LONGSHANKS)
		(player-in-game focus-player)
		(stance-toward focus-player enemy)
		=>
		(chat-to-all CHAT-GENERAL3)
		(disable-self)
		)
		
		(defrule
		(goal g-temp 501)
		(goal g-general EDWARD-LONGSHANKS)
		(player-in-game focus-player)
		(stance-toward focus-player ally)
		=>
		(chat-to-all CHAT-GENERAL4)
		(disable-self)
		)
			
		;Henry V
		(defrule
		(goal g-general HENRY-V)
		(game-time g:> g-initial-chat-delay)
		=>
		(chat-to-allies CHAT-GENERAL5)
		(chat-to-allies CHAT-GENERAL6)
		(disable-self)
		)
		
		;King Arthur
		(defrule
		(goal g-general ARTHUR)
		(game-time g:> g-initial-chat-delay)
		=>
		(chat-to-all CHAT-GENERAL7)
		(disable-self)
		)

	#end-if

	#load-if-defined FRANKISH-CIV
		
		(defrule
		(goal g-general LA-HIRE)
		(game-time g:> g-initial-chat-delay)
		=>
		(chat-to-all CHAT-GENERAL1)
		(disable-self)
		)

	#end-if

	#load-if-defined KOREAN-CIV
		
		;Chat
		(defrule
		(goal g-general WANG-KON)
		(game-time g:> g-initial-chat-delay)
		=>
		(generate-random-number 100)
		(up-get-fact random-number 0 g-temp)
		(disable-self)
		)

		(defrule
		(goal g-general WANG-KON)
		(game-time g:> g-initial-chat-delay)
		(up-compare-goal g-temp <= 50)
		=>
		(chat-to-enemies CHAT-GENERAL1)
		(chat-to-enemies CHAT-GENERAL2)
		(disable-self)
		)

		(defrule
		(goal g-general YI-SUN-SHIN)
		(game-time g:> g-initial-chat-delay)
		(cc-players-unit-type-count 0 ocean-fish-class == 0)
		(cc-players-unit-type-count 0 shore-fish-class == 0)
		=>
		(chat-to-all CHAT-GENERAL3)
		(disable-self)
		)

	#end-if

;=========================<>=========================
;			   		 SEND TAUNTS
;=========================<>=========================

	(defrule
	(player-in-game any-ally)
	(goal g-town-under-attack YES)
	(up-compare-goal g-team-partner-identity == THE-GENERAL)
	=>
	(up-modify-sn sn-focus-player-number g:= g-team-partner)
	)

	(defrule
	(player-in-game any-ally)
	(goal g-town-under-attack YES)
	(up-compare-goal g-team-partner-identity == THE-MORRIGAN)
	=>
	(up-modify-sn sn-focus-player-number g:= g-team-partner)
	)

	(defrule
	(player-in-game any-ally)
	(goal g-town-under-attack YES)
	(up-compare-goal g-team-partner-identity != THE-GENERAL)
	(up-compare-goal g-team-partner-identity != THE-MORRIGAN)
	=>
	(up-find-player ally find-closest g-temp)
	(up-modify-sn sn-focus-player-number g:= g-temp)
	)

	(defrule
	(player-in-game any-ally)
	(players-military-population any-ally >= 4)
	(goal g-town-under-attack YES)
	(up-compare-goal g-enemy-units-in-town >= 3)
	(military-population g:< g-enemy-units-in-town)
	(goal g-game-focus DEFENSIVE)
	(up-compare-goal g-team-partner >= 1)
	(up-timer-status t-ask-for-ally-assistance != timer-running)
	(up-allied-goal focus-player g-town-under-attack == NO)
	(or
		(up-compare-goal g-team-partner-identity == THE-GENERAL)
		(up-compare-goal g-team-partner-identity == THE-MORRIGAN))
	=>
	(chat-to-player focus-player "48")
	(chat-to-player focus-player "222")
	(chat-to-player-using-id focus-player 11447)	;"I'm being attacked, please help!"
	(chat-to-player-using-id my-player-number 11447)
	(enable-timer t-ask-for-ally-assistance 120)
	)
	(defrule
	(player-in-game any-ally)
	(players-military-population any-ally >= 4)
	(goal g-town-under-attack YES)
	(up-compare-goal g-enemy-units-in-town >= 3)
	(military-population g:< g-enemy-units-in-town)
	(goal g-game-focus DEFENSIVE)
	(up-timer-status t-ask-for-ally-assistance != timer-running)
	(up-compare-goal g-team-partner-identity != THE-GENERAL)
	(up-compare-goal g-team-partner-identity != THE-MORRIGAN)
	=>
	(chat-to-player focus-player "48")
	(chat-to-player focus-player "222")
	(chat-to-player-using-id focus-player 11447)	;"I'm being attacked, please help!"
	(chat-to-player-using-id my-player-number 11447)
	(enable-timer t-ask-for-ally-assistance 120)
	)

	(defrule
	(player-in-game any-ally)
	(players-military-population any-ally >= 4)
	(goal g-town-under-attack YES)
	(up-compare-goal g-enemy-units-in-town >= 3)
	(military-population g:< g-enemy-units-in-town)
	(goal g-game-focus DEFENSIVE)
	(up-compare-goal g-team-partner < 1)
	(up-timer-status t-ask-for-ally-assistance != timer-running)
	=>
	(chat-to-allies "48")
	(chat-to-allies "222")
	(chat-to-allies-using-id 11447)	;"I'm being attacked, please help!"
	(chat-to-player-using-id my-player-number 11447)
	(enable-timer t-ask-for-ally-assistance 120)
	)

;=========================<>=========================
;			   RESPOND TO PLAYER TAUNTS
;=========================<>=========================

	;--------------
	;	Taunt 16
	;--------------

		#load-if-defined VIETNAMESE-CIV
			
			(defrule
			(player-in-game any-human)
			(player-in-game any-enemy)
			(player-in-game any-ally)
			=>
			(set-goal g-get-enemy-locations BEGIN-FINDING-PLAYERS)	;start = 0, then loops through player numbers
			(disable-self)
			)
			
			(defrule
			(game-time g:>= g-initial-chat-delay)
			(goal g-get-enemy-locations BEGIN-FINDING-PLAYERS)
			=>
			(chat-to-allies "Vietnamese scouts are the best.")
			(disable-self)
			)
			
			(defrule
			(game-time g:>= g-initial-chat-delay)
			(goal g-get-enemy-locations BEGIN-FINDING-PLAYERS)
			=>
			(set-goal g-get-enemy-locations 1)
			(up-modify-goal g-initial-chat-delay c:+ 6)
			)
			
				(defrule
				(game-time g:>= g-initial-chat-delay)
				(up-compare-goal g-get-enemy-locations > BEGIN-FINDING-PLAYERS)
				(up-compare-goal g-get-enemy-locations < DONE-FINDING-PLAYERS)
				=>
				(up-modify-sn sn-focus-player-number g:= g-get-enemy-locations)	;is p1 for first loop
				)
			
				(defrule
				(game-time g:>= g-initial-chat-delay)
				(up-compare-goal g-get-enemy-locations > BEGIN-FINDING-PLAYERS)
				(up-compare-goal g-get-enemy-locations < DONE-FINDING-PLAYERS)
				(nand
					(player-in-game focus-player)
					(stance-toward focus-player enemy))
				=>
				(up-jump-rule 3)
				)
				
				(defrule
				(game-time g:>= g-initial-chat-delay)
				(up-compare-goal g-get-enemy-locations > BEGIN-FINDING-PLAYERS)
				(up-compare-goal g-get-enemy-locations < DONE-FINDING-PLAYERS)
				(player-in-game focus-player)
				(stance-toward focus-player enemy)
				=>
				(up-full-reset-search)
				(up-set-target-point g-position-self-x)
				(up-find-remote c: town-center c: 1)
				)
				
				(defrule
				(game-time g:>= g-initial-chat-delay)
				(up-compare-goal g-get-enemy-locations > BEGIN-FINDING-PLAYERS)
				(up-compare-goal g-get-enemy-locations < DONE-FINDING-PLAYERS)
				(player-in-game focus-player)
				(stance-toward focus-player enemy)
				(up-set-target-object search-remote c: 0)
				=>
				(up-get-player-color focus-player g-temp)
				(up-chat-data-to-player every-ally "Found %s." c: 7031232)
				(up-store-player-name focus-player)
				(up-chat-data-to-player every-ally "%s is here." c: 7031232)
				(up-get-point position-object g-point-x)
				(up-set-target-point g-point-x)
				(up-send-flare g-point-x)
				)
				
				(defrule
				(game-time g:>= g-initial-chat-delay)
				(up-compare-goal g-get-enemy-locations > BEGIN-FINDING-PLAYERS)
				(up-compare-goal g-get-enemy-locations < DONE-FINDING-PLAYERS)
				(player-in-game focus-player)
				(stance-toward focus-player enemy)
				(up-set-target-object search-remote c: 0)
				=>
				(up-modify-goal g-get-enemy-locations c:+ 1)
				(up-modify-sn sn-focus-player-number g:= g-get-enemy-locations)
				(up-modify-goal g-initial-chat-delay c:+ 6)
				(up-jump-rule -5)
				)
				
				(defrule
				(game-time g:>= g-initial-chat-delay)
				(up-compare-goal g-get-enemy-locations > BEGIN-FINDING-PLAYERS)
				(up-compare-goal g-get-enemy-locations < DONE-FINDING-PLAYERS)
				=>
				(up-modify-goal g-get-enemy-locations c:+ 1)
				(up-modify-sn sn-focus-player-number g:= g-get-enemy-locations)
				(up-jump-rule -6)
				)
				
			(defrule
			(goal g-get-enemy-locations DONE-FINDING-PLAYERS)
			(game-time g:>= g-initial-chat-delay)
			=>
			(chat-to-allies "Send taunt 16, and I will flare enemy locations again.")
			(set-goal g-get-enemy-locations PENDING)
			)
				
			(defrule
			(or
				(taunt-detected any-human-ally 16)
				(taunt-detected any-computer 16))
			=>
			(acknowledge-taunt this-any-human-ally 16)
			(acknowledge-taunt this-any-computer 16)
			(chat-to-player this-any-ally "Here are the enemy locations.")
			(up-get-fact game-time 0 g-initial-chat-delay)
			(set-goal g-get-enemy-locations BEGIN-FINDING-PLAYERS)
			)
			
		#end-if

	;------------------
	;	Taunt 48/222
	;------------------

		#load-if-defined UP-ALLY-IN-GAME

			(load "The General 1.3/Functions/Reset")

			(defrule
			(or
				(taunt-detected TAUNT-PLAYER 48)
				(taunt-detected TAUNT-PLAYER 222))
			=>
			(up-get-player-fact THIS-ANY-TAUNT-PLAYER player-number 0 g-ally-requesting-aid)
			(up-modify-sn sn-focus-player-number g:= g-ally-requesting-aid)
			(acknowledge-taunt THIS-ANY-TAUNT-PLAYER 48)
			(acknowledge-taunt THIS-ANY-TAUNT-PLAYER 222)
			)

			(defrule
			(or
				(up-compare-sn sn-focus-player-number == my-player-number)
				(players-stance focus-player enemy))
			=>
			(set-goal g-ally-requesting-aid PENDING)
			)

			;Don't assist if not using DUC
			; (defrule
			; (up-compare-goal g-ally-requesting-aid > 0)
			; (up-compare-flag g-attack-method-flag != USE-DUC)
			; =>
			; (up-modify-sn sn-focus-player-number g:= g-ally-requesting-aid)
			; (chat-to-player focus-player TAUNT-48-NO-DUC)
			; (chat-to-player my-player-number TAUNT-48-NO-DUC)
			; (set-goal g-ally-requesting-aid PENDING)
			; )

			(defrule
			(up-compare-goal g-ally-requesting-aid > 0)
			(goal g-town-under-attack YES)
			=>
			(up-modify-sn sn-focus-player-number g:= g-ally-requesting-aid)
			(chat-to-player focus-player TAUNT-48-UNDER-ATTACK)
			(chat-to-allies TAUNT-48-UNDER-ATTACK)
			(set-goal g-ally-requesting-aid PENDING)
			)

			;If already assisting, don't change behavior
			(defrule
			(up-compare-goal g-ally-requesting-aid > 0)
			(up-compare-goal g-ally-requesting-aid s:== csn-ally-to-defend)
			(goal g-attack-objective HELP-ALLY-DEFEND)
			(goal g-attacking YES)
			=>
			(up-modify-sn sn-focus-player-number g:= g-ally-requesting-aid)
			(chat-to-player focus-player TAUNT-48-ALREADY-ASSISTING)
			(chat-to-player my-player-number TAUNT-48-ALREADY-ASSISTING)
			(set-goal g-ally-requesting-aid PENDING)
			)

			;If already promised aid, let them know
			(defrule
			(up-compare-goal g-ally-requesting-aid > 0)
			(up-compare-goal g-ally-requesting-aid s:== csn-ally-to-defend)
			(goal g-attack-objective HELP-ALLY-DEFEND)
			(goal g-attacking NO)
			=>
			(up-modify-sn sn-focus-player-number g:= g-ally-requesting-aid)
			(chat-to-player focus-player TAUNT-48-CANT-ASSIST-YET)
			(chat-to-player my-player-number TAUNT-48-CANT-ASSIST-YET)
			(set-goal g-ally-requesting-aid PENDING)
			)

			;If ally is dead, don't defend
			(defrule
			(up-compare-goal g-ally-requesting-aid > 0)
			(up-compare-goal g-ally-requesting-aid s:== csn-ally-to-defend)
			(goal g-attack-objective HELP-ALLY-DEFEND)
			(players-population focus-player < 40)
			(or
				(game-time >= 2000)
				(players-population focus-player < 25))
			=>
			(up-modify-sn sn-focus-player-number g:= g-ally-requesting-aid)
			(chat-to-player focus-player TAUNT-48-TOO-WEAK)
			(chat-to-player my-player-number TAUNT-48-TOO-WEAK)
			(set-goal g-ally-requesting-aid PENDING)
			)

			;Set csn-ally-to-defend
			(load "The General 1.3/Functions/Reset Mini")

			(defrule
			(up-compare-goal g-attack-objective != HELP-ALLY-DEFEND)
			(up-compare-goal g-team-partner > 0)
			(up-compare-sn csn-ally-to-defend g:!= g-team-partner)
			=>
			(up-modify-sn csn-ally-to-defend g:= g-team-partner)
			)

			(defrule
			(goal g-attack-objective HELP-ALLY-DEFEND)
			=>
			(up-modify-goal g-temp2 s:= csn-ally-to-defend)
			)

			(defrule
			(up-compare-goal g-ally-requesting-aid > 0)
			=>
			(up-modify-goal g-temp2 g:= g-ally-requesting-aid)
			)

			;If ally is not a player we want to defend, don't assist
			(defrule
			(up-compare-goal g-temp2 > 0)
			(goal g-position FLANK)
			(up-players-in-game ally > 2)	;at least 3 players alive on team
			(up-compare-goal g-temp2 g:!= g-team-partner)
			(up-compare-goal g-temp2 s:!= csn-ally-to-defend)
			=>
			(set-goal g-temp2 0)
			(set-goal g-ally-requesting-aid PENDING)
			)
			
			;Check if ally needs defending
			(defrule
			(up-compare-goal g-temp2 > 0)
			=>
			(up-modify-sn sn-focus-player-number g:= g-temp2)
			(up-full-reset-search)
			(up-find-remote c: town-center c: 10)
			(up-find-remote c: house c: 10)
			(up-find-remote c: lumber-camp c: 5)
			(up-clean-search search-remote object-data-id search-order-asc)
			)
			
			(defrule
			(up-compare-goal g-temp2 > 0)
			(up-set-target-object search-remote c: 0)
			=>
			(up-get-point position-object g-point-x)
			(up-set-target-point g-point-x)
			(up-full-reset-search)
			(set-goal g-temp 222)
			(up-filter-distance c: -1 c: 40)
			(up-filter-include cmdid-military -1 -1 -1)
			(set-strategic-number sn-focus-player-number 1)
			)
			
				(defrule
				(goal g-temp 222)
				(up-compare-goal g-point-x >= 0)
				(up-compare-sn sn-focus-player-number <= MAX-PLAYERS)
				(nand
					(player-in-game focus-player)
					(stance-toward focus-player enemy))
				=>
				(up-modify-sn sn-focus-player-number c:+ 1)
				(up-jump-rule -1)
				)
			
				(defrule
				(goal g-temp 222)
				(up-compare-goal g-point-x >= 0)
				(up-compare-sn sn-focus-player-number <= MAX-PLAYERS)
				=>
				(up-find-remote c: any-objects c: 10)
				(up-get-search-state g-local-total)
				)
			
				(defrule
				(goal g-temp 222)
				(up-compare-goal g-point-x >= 0)
				(up-compare-sn sn-focus-player-number < MAX-PLAYERS)
				(up-compare-goal g-remote-total < 4)
				=>
				(up-modify-sn sn-focus-player-number c:+ 1)
				(up-jump-rule -3)
				)
			
			(defrule
			(goal g-temp 222)
			(up-compare-goal g-point-x >= 0)
			(up-compare-goal g-remote-total < 4)
			=>
			(set-goal g-attack-objective PENDING)
			; (chat-to-all "stop ally assistance")
			)
				
			(defrule
			(goal g-temp 222)
			(up-compare-goal g-ally-requesting-aid > 0)
			(up-compare-goal g-point-x >= 0)
			(up-compare-goal g-remote-total < 4)
			=>
			(up-modify-sn sn-focus-player-number g:= g-ally-requesting-aid)
			(chat-to-player focus-player TAUNT-48-ALLY-SAFE)
			(chat-to-player my-player-number TAUNT-48-ALLY-SAFE)
			(set-goal g-ally-requesting-aid PENDING)
			)

			;Respond to taunt
			(defrule
			(goal g-temp 222)
			(up-compare-goal g-ally-requesting-aid > 0)
			(up-compare-goal g-attack-objective != HELP-ALLY-DEFEND)
			(goal g-attacking YES)
			=>
			(set-goal g-attack-objective HELP-ALLY-DEFEND)
			(up-modify-sn csn-ally-to-defend g:= g-temp2)
			; (up-chat-data-to-all "Defend new ally p%d" s: csn-ally-to-defend)
			(up-modify-sn sn-focus-player-number g:= g-ally-requesting-aid)
			(chat-to-player focus-player TAUNT-48-STOP-ATTACK)
			(chat-to-player my-player-number TAUNT-48-STOP-ATTACK)
			(set-goal g-ally-requesting-aid PENDING)
			)

			(defrule
			(goal g-temp 222)
			(up-compare-goal g-ally-requesting-aid > 0)
			(up-compare-goal g-attack-objective != HELP-ALLY-DEFEND)
			(goal g-attacking NO)
			=>
			(set-goal g-attack-objective HELP-ALLY-DEFEND)
			(up-modify-sn csn-ally-to-defend g:= g-temp2)
			; (up-chat-data-to-all "Defend new ally2 p%d" s: csn-ally-to-defend)
			(enable-timer t-ally-assistance-chat-delay 2)
			(set-goal g-ally-requesting-aid PENDING)
			; (chat-to-all "start ally defense timer")
			)

			(defrule
			(up-timer-status t-ally-assistance-chat-delay == timer-triggered)
			(goal g-attack-objective HELP-ALLY-DEFEND)
			(goal g-attacking NO)
			=>
			(up-modify-sn sn-focus-player-number s:= csn-ally-to-defend)
			(chat-to-player focus-player TAUNT-48-CANT-ASSIST-YET)
			(chat-to-player my-player-number TAUNT-48-CANT-ASSIST-YET)
			(disable-timer t-ally-assistance-chat-delay)
			)

		#end-if


;=========================<>=========================
;				RESPOND TO PLAYER CHAT
;=========================<>=========================

	(defrule
	(true)
	=>
	(up-store-player-chat target-player)
	)

	(defrule
	(player-in-game target-player)
	(stance-toward target-player enemy)
	(up-compare-text c: TEXT-GG == 0)
	(up-compare-text c: TEXT-GG2 < 0)
	=>
	(chat-to-player every-enemy TEXT-GG)
	(disable-self)
	)