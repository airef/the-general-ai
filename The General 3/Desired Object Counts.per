;Desired Unit Counts.per

;=========================<>=========================
;				        RESET
;=========================<>=========================

	(defrule
	(true)
	=>
	(set-goal g-desired-num-military 0)
	(set-goal g-desired-num-navy 0)
	(set-goal g-desired-num-villager 0)
	(set-goal g-desired-num-fishing-ship 0)
	(set-goal g-desired-num-trade-cart 0)
	(set-goal g-desired-num-trade-cog 0)
	(set-goal g-desired-num-militia 0)
	(set-goal g-desired-num-spearman 0)
	(set-goal g-desired-num-eagle-warrior 0)
	(set-goal g-desired-num-archer 0)
	(set-goal g-desired-num-skirmisher 0)
	(set-goal g-desired-num-cavalry-archer 0)
	(set-goal g-desired-num-hand-cannoneer 0)
	(set-goal g-desired-num-scout-cavalry 0)
	(set-goal g-desired-num-knight 0)
	)

	(defrule
	(true)
	=>
	(set-goal g-desired-num-camel 0)
	(set-goal g-desired-num-battering-ram 0)
	(set-goal g-desired-num-mangonel 0)
	(set-goal g-desired-num-scorpion 0)
	(set-goal g-desired-num-bombard-cannon 0)
	(set-goal g-desired-num-trebuchet 0)
	(set-goal g-desired-num-galley 0)
	(set-goal g-desired-num-fire-ship 0)
	(set-goal g-desired-num-demolition-ship 0)
	(set-goal g-desired-num-cannon-galleon 0)
	(set-goal g-desired-num-monk 0)
	(set-goal g-desired-num-missionary 0)
	(set-goal g-desired-num-transport-ship 0)
	(set-goal g-desired-num-unique-unit 0)
	(set-goal g-desired-num-unique-ship 0)
	)

	(defrule
	(true)
	=>
	(set-goal g-desired-num-slinger 0)
	(set-goal g-desired-num-genitour 0)
	(set-goal g-desired-num-condottiero 0)
	(set-goal g-desired-num-battle-elephant 0)
	(set-goal g-desired-num-steppe-lancer 0)
	(set-goal g-desired-num-infantry 0)
	(set-goal g-desired-num-foot-archers 0)
	(set-goal g-desired-num-cavalry-archers 0)
	(set-goal g-desired-num-cavalry 0)
	(set-goal g-desired-num-siege 0)
	(set-goal g-desired-num-hand-cannoneers 0)
	(set-goal g-desired-num-monks 0)
	(set-goal g-desired-num-town-center 0)
	(set-goal g-desired-num-house 0)
	(set-goal g-desired-num-mill 0)
	)

	(defrule
	(true)
	=>
	(set-goal g-desired-num-mining-camp 0)
	(set-goal g-desired-num-lumber-camp 0)
	(set-goal g-desired-num-dock 0)
	(set-goal g-desired-num-farm 0)
	(set-goal g-desired-num-blacksmith 0)
	(set-goal g-desired-num-market 0)
	(set-goal g-desired-num-monastery 0)
	(set-goal g-desired-num-university 0)
	(set-goal g-desired-num-wonder 0)
	(set-goal g-desired-num-barracks 0)
	(set-goal g-desired-num-archery-range 0)
	(set-goal g-desired-num-stable 0)
	(set-goal g-desired-num-siege-workshop 0)
	(set-goal g-desired-num-outpost 0)
	(set-goal g-desired-num-watch-tower 0)
	)

	(defrule
	(true)
	=>
	(set-goal g-desired-num-bombard-tower 0)
	(set-goal g-desired-num-castle 0)
	(set-goal g-desired-num-feitoria 0)
	(set-goal g-desired-num-krepost 0)
	(set-goal g-desired-num-gold-camp 0)
	(set-goal g-desired-num-stone-camp 0)
	)



;=========================<>=========================
;				 NEW UNIT LINE GOALS
;====================================================

	;-------------------
	;	g-tarkan-line
	;-------------------

		(defrule
		(true)
		=>
		(up-get-fact unit-type-count-total tarkan-line g-tarkan-line)
		)

		#load-if-defined UP-GAME-WK

			(defrule
			(true)
			=>
			(up-get-fact unit-type-count-total stable-tarkan g-temp)
			(up-modify-goal g-tarkan-line g:+ g-temp)
			(up-get-fact unit-type-count-total elite-stable-tarkan g-temp)
			(up-modify-goal g-tarkan-line g:+ g-temp)
			)

		#end-if

	;---------------------
	;	g-mangonel-line
	;---------------------

		(defrule
		(true)
		=>
		(up-get-fact unit-type-count-total mangonel-line g-mangonel-line)
		)

		#load-if-defined UP-GAME-WK
		#load-if-not-defined DE-AVAILABLE

			(defrule
			(true)
			=>
			(up-get-fact unit-type-count-total wk-onager g-temp)
			(up-modify-goal g-mangonel-line g:+ g-temp)
			)
			
		#end-if
		#end-if
		
	;------------------------
	;	g-unique-ship-line
	;------------------------

		(defrule
		(true)
		=>
		(set-goal g-unique-ship-line 0)
		)

		#load-if-defined KOREAN-CIV

			(defrule
			(true)
			=>
			(up-get-fact unit-type-count-total turtle-ship-line g-unique-ship-line)
			)
			
		#end-if

		#load-if-defined PORTUGUESE-CIV

			(defrule
			(true)
			=>
			(up-get-fact unit-type-count-total caravel g-unique-ship-line)
			(up-get-fact unit-type-count-total elite-caravel g-temp)
			(up-modify-goal g-unique-ship-line g:+ g-temp)
			)
			
		#end-if

		#load-if-defined VIKING-CIV

			(defrule
			(true)
			=>
			(up-get-fact unit-type-count-total longboat-line g-unique-ship-line)
			)
			
		#end-if
		
	;-----------------------
	;	g-skirmisher-line
	;-----------------------

		(defrule
		(true)
		=>
		(up-get-fact unit-type-count-total skirmisher-line g-skirmisher-line)
		)

		#load-if-defined UP-GAME-WK

			(defrule
			(true)
			=>
			(up-get-fact unit-type-count-total imperial-skirmisher g-temp)
			(up-modify-goal g-skirmisher-line g:+ g-temp)
			)
			
		#end-if
		
	;------------------------
	;	g-eagle-scout-line
	;------------------------

		(defrule
		(true)
		=>
		(up-get-fact unit-type-count-total eagle-warrior-line g-eagle-scout-line)
		)

		#load-if-defined UP-GAME-WK

			(defrule
			(true)
			=>
			(up-get-fact unit-type-count-total heavy-eagle-warrior g-temp)	;castle age eagle warrior in WK
			(up-modify-goal g-eagle-scout-line g:+ g-temp)
			)

		#end-if
		
	;---------------------
	;	g-genitour-line
	;---------------------

		(defrule
		(true)
		=>
		(up-get-fact unit-type-count-total genitour g-genitour-line)
		(up-get-fact unit-type-count-total elite-genitour g-temp)
		(up-modify-goal g-genitour-line g:+ g-temp)
		)
		
	;----------------------------
	;	g-battle-elephant-line
	;----------------------------

		(defrule
		(true)
		=>
		(up-get-fact unit-type-count-total battle-elephant g-battle-elephant-line)
		(up-get-fact unit-type-count-total elite-battle-elephant g-temp)
		(up-modify-goal g-battle-elephant-line g:+ g-temp)
		)
	
	;------------------------
	;	g-fire-galley-line
	;------------------------

		(defrule
		(true)
		=>
		(up-get-fact unit-type-count-total fire-ship-line g-fire-galley-line)
		)

		#load-if-defined UP-GAME-WK

			(defrule
			(true)
			=>
			(up-get-fact unit-type-count-total fire-ship-wk g-temp)
			(up-modify-goal g-fire-galley-line g:+ g-temp)
			)
			
		#end-if
		
	;----------------------------
	;	g-demolition-ship-line
	;----------------------------

		(defrule
		(true)
		=>
		(up-get-fact unit-type-count-total demolition-ship-line g-demolition-raft-line)
		)

		#load-if-defined UP-GAME-WK

			(defrule
			(true)
			=>
			(up-get-fact unit-type-count-total demolition-ship-wk g-temp)
			(up-modify-goal g-demolition-raft-line g:+ g-temp)
			)
			
		#end-if
		
	;------------------------
	;	g-watch-tower-line
	;------------------------

		(defrule
		(true)
		=>
		(up-get-fact unit-type-count-total watch-tower g-watch-tower-line)
		(up-get-fact unit-type-count-total guard-tower g-temp)
		(up-modify-goal g-watch-tower-line g:+ g-temp)
		(up-get-fact unit-type-count-total keep g-temp)
		(up-modify-goal g-watch-tower-line g:+ g-temp)
		)
		
	;------------------
	;	g-camel-line
	;------------------

		(defrule
		(true)
		=>
		(up-get-fact unit-type-count-total camel-line g-camel-line)
		)

		#load-if-defined UP-GAME-WK

			(defrule
			(true)
			=>
			(up-get-fact unit-type-count-total imperial-camel g-temp)
			(up-modify-goal g-camel-line g:+ g-temp)
			)
			
		#end-if

	;------------------------
	;	g-unique-unit-line
	;------------------------

		#load-if-not-defined BULGARIANS
		
			(defrule
			(true)
			=>
			(up-get-fact unit-type-count-total my-unique-unit g-unique-unit-line)
			(up-get-fact unit-type-count-total my-elite-unique-unit g-temp)
			(up-modify-goal g-unique-unit-line g:+ g-temp)
			)

		#end-if

		#load-if-defined GOTHIC-CIV

			(defrule
			(true)
			=>
			(up-get-fact unit-type-count-total barracks-huskarl g-temp)
			(up-modify-goal g-unique-unit-line g:+ g-temp)
			(up-get-fact unit-type-count-total barracks-elite-huskarl g-temp)
			(up-modify-goal g-unique-unit-line g:+ g-temp)
			)

		#end-if

		#load-if-defined HUN-CIV

			(defrule
			(true)
			=>
			(up-get-fact unit-type-count-total stable-tarkan g-temp)
			(up-modify-goal g-unique-unit-line g:+ g-temp)
			(up-get-fact unit-type-count-total elite-stable-tarkan g-temp)
			(up-modify-goal g-unique-unit-line g:+ g-temp)
			)

		#end-if

		#load-if-defined BULGARIANS-CIV

			(defrule
			(true)
			=>
			(up-get-fact unit-type-count-total konnik-set g-unique-unit-line)
			)

		#end-if

		#load-if-defined SICILIANS-CIV

			(defrule
			(true)
			=>
			(up-get-fact unit-type-count-total serjeant-line g-unique-unit-line)
			(up-get-fact unit-type-count-total donjon-serjeant-line g-temp)
			(up-modify-goal g-unique-unit-line g:+ g-temp)
			)

		#end-if
		
	;-------------------
	;	g-siege-class
	;-------------------

		(defrule
		(true)
		=>
		(up-get-fact unit-type-count-total siege-weapon-class g-siege-class)
		(up-get-fact unit-type-count-total scorpion-class g-temp)
		(up-modify-goal g-siege-class g:+ g-temp)
		(up-get-fact unit-type-count-total petard-class g-temp)
		(up-modify-goal g-siege-class g:+ g-temp)
		(up-modify-goal g-siege-class c:+ trebuchet-set)
		)

;=========================<>=========================
;				   	   MILITARY
;=========================<>=========================

	;---------------------
	;	Base Strategies
	;---------------------

		;-----------
		;	Reset
		;-----------

			(defrule
			(true)
			=>
			(set-goal g-primary-unit 0)
			)

			(defrule
			(up-compare-goal g-age-status >= MID-IMPERIAL)
			=>
			(set-goal g-support-unit 0)
			)

		;------------------
		;	Primary Unit
		;------------------

			(defrule
			(or
				(goal g-current-strategy DRUSH)
				(or
					(goal g-current-strategy FL-LATHER)
					(or
						(goal g-current-strategy FI-FARMERTRON)
						(goal g-current-strategy EARLYI-CHAMPFLOOD))))
			=>
			(set-goal g-primary-unit militiaman)
			)

			(defrule
			(or
				(goal g-current-strategy MIDF-FULL-MAA)
				(or
					(goal g-current-strategy MIDC-SWORDS)
					(or
						(goal g-current-strategy MIDI-CHAMPIONS)
						(goal g-current-strategy LATEI-CHAMPIONS))))
			=>
			(set-goal g-primary-unit militiaman)
			)

			(defrule
			(or
				(goal g-current-strategy MIDC-PIKES)
				(or
					(goal g-current-strategy MIDI-HALBS)
					(or
						(goal g-current-strategy BOOM-IMPERIAL)
						(goal g-current-strategy LATEI-TRASHALBS))))
			=>
			(set-goal g-primary-unit spearman)
			)

			(defrule
			(or
				(goal g-current-strategy FL-EYRIES)
				(goal g-current-strategy MIDF-FULL-EAGLES))
			=>
			(set-goal g-primary-unit eagle-warrior)
			)

			(defrule
			(or
				(goal g-current-strategy FC-EAGLES-REVENGE)
				(or
					(goal g-current-strategy EARLYC-EAGLES)
					(or
						(goal g-current-strategy MIDC-EAGLES)
						(or
							(goal g-current-strategy FI-AGEOFSTONEWALLS)
							(or
								(goal g-current-strategy EARLYI-EL-DORADO)
								(or
									(goal g-current-strategy MIDI-EAGLES)
									(goal g-current-strategy BOOM-EAGLES)))))))
			=>
			(set-goal g-primary-unit eagle-warrior)
			)

			(defrule
			(goal g-current-strategy MIDI-CONDOS)
			=>
			(set-goal g-primary-unit condottiero)
			)

			(defrule
			(or
				(goal g-current-strategy FL-RANGER)
				(or
					(goal g-current-strategy FL-GUERILLA)
					(or
						(goal g-current-strategy FC-STRONGBOW)
						(or
							(goal g-current-strategy EARLYI-ARBINGER)
							(or
								(goal g-current-strategy BOOM-XBOWS)
								(goal g-current-strategy LATEI-TRASHBOWS))))))
			=>
			(set-goal g-primary-unit archer)
			)

			(defrule
			(or
				(goal g-current-strategy MIDF-FULL-ARCHERS)
				(or
					(goal g-current-strategy MIDF-FULL-RANGED)
					(or
						(goal g-current-strategy EARLYC-XBOWS)
						(or
							(goal g-current-strategy MIDC-XBOWS)
							(or
								(goal g-current-strategy MIDC-RANGED)
								(goal g-current-strategy MIDI-ARBS))))))
			=>
			(set-goal g-primary-unit archer)
			)

			(defrule
			(or
				(goal g-current-strategy FL-SKIRMMETA)
				(or
					(goal g-current-strategy MIDF-FULL-SKIRMS)
					(or
						(goal g-current-strategy MIDC-SKIRMS)
						(or
							(goal g-current-strategy MIDI-SKIRMS)
							(goal g-current-strategy LATEI-TRASHKIRMS)))))
			=>
			(set-goal g-primary-unit skirmisher)
			)

			(defrule
			(or
				(goal g-current-strategy MIDC-CAV-ARCHERS)
				(or
					(goal g-current-strategy EARLYI-HORDE)
					(goal g-current-strategy MIDI-CAV-ARCHERS)))
			=>
			(set-goal g-primary-unit cavalry-archer)
			)

			(defrule
			(or
				(goal g-current-strategy FI-SNIPER)
				(goal g-current-strategy MIDI-CANNONEERS))
			=>
			(set-goal g-primary-unit hand-cannoneer)
			)

			(defrule
			(or
				(goal g-current-strategy MIDC-SLINGERS)
				(goal g-current-strategy MIDI-SLINGERS))
			=>
			(set-goal g-primary-unit slinger)
			)

			(defrule
			(or
				(goal g-current-strategy MIDC-GENITOURS)
				(or
					(goal g-current-strategy MIDI-GENITOURS)
					(goal g-current-strategy LATEI-TRASHGENS)))
			=>
			(set-goal g-primary-unit genitour)
			)

			(defrule
			(or
				(goal g-current-strategy FL-VULTURE)
				(or
					(goal g-current-strategy MIDF-FULL-SCOUTS)
					(or
						(goal g-current-strategy MIDF-SCOUT-SKIRM)
						(or
							(goal g-current-strategy MIDC-LIGHT)
							(or
								(goal g-current-strategy MIDI-HUSSARS)
								(or
									(goal g-current-strategy LATEI-TRASHUSSARS)
									(goal g-current-strategy LATEI-HUSSARS)))))))
			=>
			(set-goal g-primary-unit scout-cavalry)
			)

			(defrule
			(or
				(goal g-current-strategy FC-KRUSH)
				(or
					(goal g-current-strategy EARLYC-KNIGHTS)
					(or
						(goal g-current-strategy MIDC-KNIGHTS)
						(or
							(goal g-current-strategy EARLYI-PALASTORM)
							(or
								(goal g-current-strategy MIDI-PALADINS)
								(goal g-current-strategy BOOM-KNIGHTS))))))
			=>
			(set-goal g-primary-unit knight)
			)

			(defrule
			(or
				(goal g-current-strategy FC-CRUSH)
				(or
					(goal g-current-strategy EARLYC-CAMELS)
					(or
						(goal g-current-strategy MIDC-CAMELS)
						(or
							(goal g-current-strategy EARLYI-CAMELSTORM)
							(or
								(goal g-current-strategy MIDI-CAMELS)
								(goal g-current-strategy BOOM-CAMELS))))))
			=>
			(set-goal g-primary-unit camel)
			)

			(defrule
			(or
				(goal g-current-strategy MIDC-CIRCUS)
				(or
					(goal g-current-strategy EARLYI-CIRCUS-STOMP)
					(goal g-current-strategy MIDI-CIRCUS)))
			=>
			(set-goal g-primary-unit battle-elephant)
			)

			(defrule
			(or
				(goal g-current-strategy MIDC-LANCERS)
				(goal g-current-strategy MIDI-LANCERS))
			=>
			(set-goal g-primary-unit steppe-lancer)
			)

			(defrule
			(or
				(goal g-current-strategy FC-CASTLE-DROP)
				(or
					(goal g-current-strategy MIDC-UNIQUE)
					(or
						(goal g-current-strategy EARLYI-UNIQUE)
						(or
							(goal g-current-strategy MIDI-UNIQUE)
							(goal g-current-strategy LATEI-UNIQUE)))))
			=>
			(set-goal g-primary-unit my-unique-unit)
			)

			(defrule
			(or
				(goal g-current-strategy FC-DEMON)
				(or
					(goal g-current-strategy MIDC-MONKS)
					(goal g-current-strategy MIDI-MONKS)))
			=>
			(set-goal g-primary-unit monk)
			)

		;--------------------------
		;	Desired Military Pop
		;--------------------------

			(defrule
			(goal g-age-status SAVE-FOR-CASTLE)
			(goal g-strategy-type FAST-CASTLE)
			(goal g-town-under-attack NO)
			=>
			(set-goal g-desired-num-military 5)
			)
			
			(defrule
			(current-age == feudal-age)
			(up-compare-goal g-age-status >= SAVE-FOR-CASTLE)
			(goal g-strategy-type FAST-CASTLE)
			(or
				(goal g-town-under-attack YES)
				(goal g-age-status TO-CASTLE))
			=>
			(up-get-fact-max any-enemy military-population 0 g-desired-num-military)
			(up-modify-goal g-desired-num-military c:max 0)
			(up-modify-goal g-desired-num-military c:+ 10)
			(up-modify-goal g-desired-num-military c:min 20)
			)

			(defrule
			(goal g-age-status EARLY-CASTLE)
			(up-compare-goal g-primary-unit-class != monastery-class)
			=>
			(up-get-fact-max any-enemy military-population 0 g-desired-num-military)
			(up-modify-goal g-desired-num-military c:max 0)
			(up-modify-goal g-desired-num-military c:+ 15)
			(up-modify-goal g-desired-num-military c:min 60)
			)

			(defrule
			(up-compare-goal g-age-status >= MID-CASTLE)
			(up-compare-goal g-primary-unit-class != monastery-class)
			=>
			(up-get-fact-max any-enemy military-population 0 g-desired-num-military)
			(up-modify-goal g-desired-num-military c:max 0)
			(up-modify-goal g-desired-num-military c:+ 15)
			(up-modify-goal g-desired-num-military c:min 54)
			)

			(defrule
			(current-age >= castle-age)
			(goal g-primary-unit-class monastery-class)
			=>
			(up-get-fact-max any-enemy military-population 0 g-desired-num-military)
			(up-modify-goal g-desired-num-military c:max 0)
			(up-modify-goal g-desired-num-military c:+ 15)
			(up-modify-goal g-desired-num-military c:min 30)
			)

		;-------------
		;	Militia
		;-------------

			(defrule
			(up-compare-goal g-desired-num-military > 0)
			(goal g-primary-unit militiaman)
			(or
				(up-compare-goal g-age-status >= TO-FEUDAL)
				(goal g-strategy-type DRUSH))
			(up-compare-goal g-current-strategy != LATEI-CHAMPIONS)
			=>
			(up-modify-goal g-desired-num-militia g:= g-desired-num-military)
			)

			(defrule
			(up-compare-goal g-desired-num-military > 0)
			(up-compare-goal g-current-age-primary-unit-tech-progress < REQUIRED-COMPLETE)
			(or
				(goal g-previous-strategy MIDF-FULL-MAA)
				(or
					(goal g-previous-strategy MIDC-SWORDS)
					(or
						(goal g-previous-strategy FI-FARMERTRON)
						(or
							(goal g-previous-strategy EARLYI-CHAMPFLOOD)
							(or
								(goal g-previous-strategy MIDI-CHAMPIONS)
								(goal g-previous-strategy LATEI-CHAMPIONS))))))
			=>
			(up-modify-goal g-desired-num-militia g:= g-desired-num-military)
			)

		;--------------
		;	Spearmen
		;--------------

			(defrule
			(up-compare-goal g-desired-num-military > 0)
			(goal g-primary-unit spearman)
			(up-compare-goal g-age-status >= TO-FEUDAL)
			=>
			(up-modify-goal g-desired-num-spearman g:= g-desired-num-military)
			)

			(defrule
			(up-compare-goal g-desired-num-military > 0)
			(up-compare-goal g-current-age-primary-unit-tech-progress < REQUIRED-COMPLETE)
			(or
				(goal g-previous-strategy MIDC-PIKES)
				(or
					(goal g-previous-strategy MIDI-HALBS)
					(or
						(goal g-previous-strategy BOOM-IMPERIAL)
						(goal g-previous-strategy LATEI-TRASHALBS))))
			=>
			(up-modify-goal g-desired-num-spearman g:= g-desired-num-military)
			)

		;--------------------
		;	Eagle Warriors
		;--------------------

			(defrule
			(up-compare-goal g-desired-num-military > 0)
			(goal g-primary-unit eagle-warrior)
			(up-compare-goal g-age-status >= TO-FEUDAL)
			=>
			(up-modify-goal g-desired-num-eagle-warrior g:= g-desired-num-military)
			)

			(defrule
			(up-compare-goal g-desired-num-military > 0)
			(up-compare-goal g-current-age-primary-unit-tech-progress < REQUIRED-COMPLETE)
			(or
				(goal g-previous-strategy MIDF-FULL-EAGLES)
				(or
					(goal g-previous-strategy FC-EAGLES-REVENGE)
					(or
						(goal g-previous-strategy EARLYC-EAGLES)
						(or
							(goal g-previous-strategy MIDC-EAGLES)
							(or
								(goal g-previous-strategy FI-AGEOFSTONEWALLS)
								(or
									(goal g-previous-strategy EARLYI-EL-DORADO)
									(goal g-previous-strategy MIDI-EAGLES)))))))
			=>
			(up-modify-goal g-desired-num-eagle-warrior g:= g-desired-num-military)
			)

		;-----------------
		;	Condottieri
		;-----------------

			(defrule
			(up-compare-goal g-desired-num-military > 0)
			(goal g-primary-unit condottiero)
			(up-compare-goal g-age-status >= TO-FEUDAL)
			=>
			(up-modify-goal g-desired-num-condottiero g:= g-desired-num-military)
			)

			(defrule
			(up-compare-goal g-desired-num-military > 0)
			(up-compare-goal g-current-age-primary-unit-tech-progress < REQUIRED-COMPLETE)
			(goal g-previous-strategy MIDI-CONDOS)
			=>
			(up-modify-goal g-desired-num-condottiero g:= g-desired-num-military)
			)

		;-------------
		;	Archers
		;-------------

			(defrule
			(up-compare-goal g-desired-num-military > 0)
			(goal g-primary-unit archer)
			(up-compare-goal g-age-status >= TO-FEUDAL)
			=>
			(up-modify-goal g-desired-num-archer g:= g-desired-num-military)
			)

			(defrule
			(or
				(goal g-current-strategy FL-GUERILLA)
				(or
					(goal g-current-strategy MIDF-FULL-RANGED)
					(goal g-current-strategy MIDC-RANGED)))
			=>
			(up-modify-goal g-desired-num-archer g:= g-desired-num-military)
			(up-modify-goal g-desired-num-archer c:%* 60)
			(set-goal g-support-unit skirmisher)
			(up-modify-goal g-desired-num-skirmisher g:= g-desired-num-military)
			(up-modify-goal g-desired-num-skirmisher c:%* 40)
			)

			(defrule
			(up-compare-goal g-desired-num-military > 0)
			(up-compare-goal g-current-age-primary-unit-tech-progress < REQUIRED-COMPLETE)
			(or
				(goal g-previous-strategy MIDF-FULL-ARCHERS)
				(or
					(goal g-previous-strategy MIDF-FULL-RANGED)
					(or
						(goal g-previous-strategy FC-STRONGBOW)
						(or
							(goal g-previous-strategy EARLYC-XBOWS)
							(or
								(goal g-previous-strategy MIDC-XBOWS)
								(goal g-previous-strategy MIDC-RANGED))))))
			=>
			(up-modify-goal g-desired-num-archer g:= g-desired-num-military)
			)

			(defrule
			(up-compare-goal g-desired-num-military > 0)
			(up-compare-goal g-current-age-primary-unit-tech-progress < REQUIRED-COMPLETE)
			(or
				(goal g-previous-strategy EARLYI-ARBINGER)
				(or
					(goal g-previous-strategy MIDI-ARBS)
					(or
						(goal g-previous-strategy BOOM-XBOWS)
						(goal g-previous-strategy LATEI-TRASHBOWS))))
			=>
			(up-modify-goal g-desired-num-archer g:= g-desired-num-military)
			)

		;-----------------
		;	Skirmishers
		;-----------------

			(defrule
			(up-compare-goal g-desired-num-military > 0)
			(goal g-primary-unit skirmisher)
			(up-compare-goal g-age-status >= TO-FEUDAL)
			=>
			(up-modify-goal g-desired-num-skirmisher g:= g-desired-num-military)
			)

			(defrule
			(up-compare-goal g-desired-num-military > 0)
			(up-compare-goal g-current-age-primary-unit-tech-progress < REQUIRED-COMPLETE)
			(or
				(goal g-previous-strategy MIDF-FULL-SKIRMS)
				(or
					(goal g-previous-strategy MIDC-SKIRMS)
					(or
						(goal g-previous-strategy MIDI-SKIRMS)
						(goal g-previous-strategy LATEI-TRASHKIRMS))))
			=>
			(up-modify-goal g-desired-num-skirmisher g:= g-desired-num-military)
			)

		;---------------------
		;	Cavalry Archers
		;---------------------

			(defrule
			(up-compare-goal g-desired-num-military > 0)
			(goal g-primary-unit cavalry-archer)
			(up-compare-goal g-age-status >= TO-FEUDAL)
			=>
			(up-modify-goal g-desired-num-cavalry-archer g:= g-desired-num-military)
			)

			(defrule
			(up-compare-goal g-desired-num-military > 0)
			(up-compare-goal g-current-age-primary-unit-tech-progress < REQUIRED-COMPLETE)
			(or
				(goal g-previous-strategy MIDC-CAV-ARCHERS)
				(or
					(goal g-previous-strategy EARLYI-HORDE)
					(goal g-previous-strategy MIDI-CAV-ARCHERS)))
			=>
			(up-modify-goal g-desired-num-cavalry-archer g:= g-desired-num-military)
			)

		;---------------------
		;	Hand Cannoneers
		;---------------------

			(defrule
			(up-compare-goal g-desired-num-military > 0)
			(goal g-primary-unit hand-cannoneer)
			(up-compare-goal g-age-status >= TO-FEUDAL)
			=>
			(up-modify-goal g-desired-num-hand-cannoneer g:= g-desired-num-military)
			)

			(defrule
			(up-compare-goal g-desired-num-military > 0)
			(up-compare-goal g-current-age-primary-unit-tech-progress < REQUIRED-COMPLETE)
			(or
				(goal g-previous-strategy FI-SNIPER)
				(goal g-previous-strategy MIDI-CANNONEERS))
			=>
			(up-modify-goal g-desired-num-hand-cannoneer g:= g-desired-num-military)
			)

		;--------------
		;	Slingers
		;--------------

			(defrule
			(up-compare-goal g-desired-num-military > 0)
			(goal g-primary-unit slinger)
			(up-compare-goal g-age-status >= TO-FEUDAL)
			=>
			(up-modify-goal g-desired-num-slinger g:= g-desired-num-military)
			)

			(defrule
			(up-compare-goal g-desired-num-military > 0)
			(up-compare-goal g-current-age-primary-unit-tech-progress < REQUIRED-COMPLETE)
			(or
				(goal g-previous-strategy MIDC-SLINGERS)
				(goal g-previous-strategy MIDI-SLINGERS))
			=>
			(up-modify-goal g-desired-num-slinger g:= g-desired-num-military)
			)

		;---------------
		;	Genitours
		;---------------

			(defrule
			(up-compare-goal g-desired-num-military > 0)
			(goal g-primary-unit genitour)
			(up-compare-goal g-age-status >= TO-FEUDAL)
			=>
			(up-modify-goal g-desired-num-genitour g:= g-desired-num-military)
			)

			(defrule
			(up-compare-goal g-desired-num-military > 0)
			(up-compare-goal g-current-age-primary-unit-tech-progress < REQUIRED-COMPLETE)
			(or
				(goal g-previous-strategy MIDC-GENITOURS)
				(or
					(goal g-previous-strategy MIDI-GENITOURS)
					(goal g-previous-strategy LATEI-TRASHGENS)))
			=>
			(up-modify-goal g-desired-num-genitour g:= g-desired-num-military)
			)

		;------------
		;	Scouts
		;------------

			(defrule
			(up-compare-goal g-desired-num-military > 0)
			(goal g-primary-unit scout-cavalry)
			(up-compare-goal g-age-status >= TO-FEUDAL)
			=>
			(up-modify-goal g-desired-num-scout-cavalry g:= g-desired-num-military)
			)

			(defrule
			(up-compare-goal g-desired-num-military > 0)
			(up-compare-goal g-current-age-primary-unit-tech-progress < REQUIRED-COMPLETE)
			(or
				(goal g-previous-strategy MIDF-FULL-SCOUTS)
				(or
					(goal g-previous-strategy MIDF-SCOUT-SKIRM)
					(or
						(goal g-previous-strategy MIDC-LIGHT)
						(or
							(goal g-previous-strategy MIDI-HUSSARS)
							(or
								(goal g-previous-strategy LATEI-TRASHUSSARS)
								(goal g-previous-strategy LATEI-HUSSARS))))))
			=>
			(up-modify-goal g-desired-num-scout-cavalry g:= g-desired-num-military)
			)

		;-------------
		;	Knights
		;-------------

			(defrule
			(up-compare-goal g-desired-num-military > 0)
			(goal g-primary-unit knight)
			(up-compare-goal g-age-status >= TO-FEUDAL)
			=>
			(up-modify-goal g-desired-num-knight g:= g-desired-num-military)
			)

			(defrule
			(up-compare-goal g-desired-num-military > 0)
			(up-compare-goal g-current-age-primary-unit-tech-progress < REQUIRED-COMPLETE)
			(or
				(goal g-previous-strategy FC-KRUSH)
				(or
					(goal g-previous-strategy EARLYC-KNIGHTS)
					(or
						(goal g-previous-strategy MIDC-KNIGHTS)
						(or
							(goal g-previous-strategy BOOM-KNIGHTS)
							(or
								(goal g-previous-strategy EARLYI-PALASTORM)
								(goal g-previous-strategy MIDI-PALADINS))))))
			=>
			(up-modify-goal g-desired-num-knight g:= g-desired-num-military)
			)

		;------------
		;	Camels
		;------------

			(defrule
			(up-compare-goal g-desired-num-military > 0)
			(goal g-primary-unit camel)
			(up-compare-goal g-age-status >= TO-FEUDAL)
			=>
			(up-modify-goal g-desired-num-camel g:= g-desired-num-military)
			)

			(defrule
			(up-compare-goal g-desired-num-military > 0)
			(up-compare-goal g-current-age-primary-unit-tech-progress < REQUIRED-COMPLETE)
			(or
				(goal g-previous-strategy FC-CRUSH)
				(or
					(goal g-previous-strategy EARLYC-CAMELS)
					(or
						(goal g-previous-strategy MIDC-CAMELS)
						(or
							(goal g-previous-strategy BOOM-CAMELS)
							(or
								(goal g-previous-strategy EARLYI-CAMELSTORM)
								(goal g-previous-strategy MIDI-CAMELS))))))
			=>
			(up-modify-goal g-desired-num-camel g:= g-desired-num-military)
			)

		;-----------------
		;	Battle Eles
		;-----------------

			(defrule
			(up-compare-goal g-desired-num-military > 0)
			(goal g-primary-unit battle-elephant)
			(up-compare-goal g-age-status >= TO-FEUDAL)
			=>
			(up-modify-goal g-desired-num-battle-elephant g:= g-desired-num-military)
			)

			(defrule
			(up-compare-goal g-desired-num-military > 0)
			(up-compare-goal g-current-age-primary-unit-tech-progress < REQUIRED-COMPLETE)
			(or
				(goal g-previous-strategy MIDC-CIRCUS)
				(or
					(goal g-previous-strategy EARLYI-CIRCUS-STOMP)
					(goal g-previous-strategy MIDI-CIRCUS)))
			=>
			(up-modify-goal g-desired-num-battle-elephant g:= g-desired-num-military)
			)

		;--------------------
		;	Steppe Lancers
		;--------------------

			(defrule
			(up-compare-goal g-desired-num-military > 0)
			(goal g-primary-unit steppe-lancer)
			(up-compare-goal g-age-status >= TO-FEUDAL)
			=>
			(up-modify-goal g-desired-num-steppe-lancer g:= g-desired-num-military)
			)

			(defrule
			(up-compare-goal g-desired-num-military > 0)
			(up-compare-goal g-current-age-primary-unit-tech-progress < REQUIRED-COMPLETE)
			(or
				(goal g-previous-strategy MIDC-LANCERS)
				(goal g-previous-strategy MIDI-LANCERS))
			=>
			(up-modify-goal g-desired-num-steppe-lancer g:= g-desired-num-military)
			)

		;------------------
		;	Unique Units
		;------------------

			(defrule
			(up-compare-goal g-desired-num-military > 0)
			(goal g-primary-unit my-unique-unit)
			(up-compare-goal g-age-status >= TO-FEUDAL)
			=>
			(up-modify-goal g-desired-num-unique-unit g:= g-desired-num-military)
			)

			(defrule
			(up-compare-goal g-desired-num-military > 0)
			(up-compare-goal g-current-age-primary-unit-tech-progress < REQUIRED-COMPLETE)
			(or
				(goal g-previous-strategy FC-CASTLE-DROP)
				(or
					(goal g-previous-strategy MIDC-UNIQUE)
					(or
						(goal g-previous-strategy EARLYI-UNIQUE)
						(or
							(goal g-previous-strategy MIDI-UNIQUE)
							(goal g-previous-strategy LATEI-UNIQUE)))))
			=>
			(up-modify-goal g-desired-num-unique-unit g:= g-desired-num-military)
			)

		;-----------
		;	Monks
		;-----------

			(defrule
			(up-compare-goal g-desired-num-military > 0)
			(goal g-primary-unit monk)
			(up-compare-goal g-age-status >= TO-FEUDAL)
			=>
			(set-goal g-desired-num-monk 25)
			)

			(defrule
			(up-compare-goal g-desired-num-military > 0)
			(up-compare-goal g-current-age-primary-unit-tech-progress < REQUIRED-COMPLETE)
			(or
				(goal g-previous-strategy FC-DEMON)
				(or
					(goal g-previous-strategy MIDC-MONKS)
					(goal g-previous-strategy MIDI-MONKS)))
			=>
			(set-goal g-desired-num-monk 12)
			)

	;--------------------------
	;	Support Units By Civ
	;--------------------------

		#load-if-defined AZTEC-CIV
			
			(load "The General 3.1/Army Comp/Aztecs")

		#end-if

		#load-if-defined BERBERS-CIV
			
			(load "The General 3.1/Army Comp/Berbers")

		#end-if

		#load-if-defined BRITON-CIV
			
			(load "The General 3.1/Army Comp/Britons")

		#end-if

		#load-if-defined BULGARIANS-CIV
			
			(load "The General 3.1/Army Comp/Bulgarians")

		#end-if

		#load-if-defined BURGUNDIANS-CIV
			
			(load "The General 3.1/Army Comp/Burgundians")

		#end-if

		#load-if-defined BURMESE-CIV
			
			(load "The General 3.1/Army Comp/Burmese")

		#end-if
		
		#load-if-defined BYZANTINE-CIV
			
			(load "The General 3.1/Army Comp/Byzantines")

		#end-if

		#load-if-defined CELTIC-CIV
			
			(load "The General 3.1/Army Comp/Celts")

		#end-if

		#load-if-defined CHINESE-CIV
			
			(load "The General 3.1/Army Comp/Chinese")

		#end-if

		#load-if-defined CUMANS-CIV
			
			(load "The General 3.1/Army Comp/Cumans")

		#end-if

		#load-if-defined ETHIOPIAN-CIV
			
			(load "The General 3.1/Army Comp/Ethiopians")

		#end-if

		#load-if-defined FRANKISH-CIV
			
			(load "The General 3.1/Army Comp/Franks")

		#end-if

		#load-if-defined GOTHIC-CIV
			
			(load "The General 3.1/Army Comp/Goths")

		#end-if

		#load-if-defined HUN-CIV
			
			(load "The General 3.1/Army Comp/Huns")

		#end-if

		#load-if-defined INCAN-CIV
			
			(load "The General 3.1/Army Comp/Incas")

		#end-if

		#load-if-defined INDIAN-CIV
			
			(load "The General 3.1/Army Comp/Indians")

		#end-if

		#load-if-defined ITALIAN-CIV
			
			(load "The General 3.1/Army Comp/Italians")

		#end-if

		#load-if-defined JAPANESE-CIV
			
			(load "The General 3.1/Army Comp/Japanese")

		#end-if

		#load-if-defined KHMER-CIV
			
			(load "The General 3.1/Army Comp/Khmer")

		#end-if

		#load-if-defined KOREAN-CIV
			
			(load "The General 3.1/Army Comp/Koreans")

		#end-if

		#load-if-defined LITHUANIANS-CIV
			
			(load "The General 3.1/Army Comp/Lithuanians")

		#end-if

		#load-if-defined MAGYAR-CIV
			
			(load "The General 3.1/Army Comp/Magyars")

		#end-if

		#load-if-defined MALAY-CIV
			
			(load "The General 3.1/Army Comp/Malay")

		#end-if

		#load-if-defined MALIAN-CIV
			
			(load "The General 3.1/Army Comp/Malians")

		#end-if

		#load-if-defined MAYAN-CIV
			
			(load "The General 3.1/Army Comp/Mayans")

		#end-if

		#load-if-defined MONGOL-CIV
			
			(load "The General 3.1/Army Comp/Mongols")

		#end-if

		#load-if-defined PERSIAN-CIV
			
			(load "The General 3.1/Army Comp/Persians")

		#end-if

		#load-if-defined PORTUGUESE-CIV
			
			(load "The General 3.1/Army Comp/Portuguese")

		#end-if

		#load-if-defined SARACEN-CIV
			
			(load "The General 3.1/Army Comp/Saracens")

		#end-if

		#load-if-defined SICILIANS-CIV
			
			(load "The General 3.1/Army Comp/Sicilians")

		#end-if

		#load-if-defined SLAVIC-CIV
			
			(load "The General 3.1/Army Comp/Slavs")

		#end-if

		#load-if-defined SPANISH-CIV
			
			(load "The General 3.1/Army Comp/Spanish")

		#end-if

		#load-if-defined TATARS-CIV
			
			(load "The General 3.1/Army Comp/Tatars")

		#end-if

		#load-if-defined TEUTONIC-CIV
			
			(load "The General 3.1/Army Comp/Teutons")

		#end-if

		#load-if-defined TURKISH-CIV
			
			(load "The General 3.1/Army Comp/Turks")

		#end-if

		#load-if-defined VIETNAMESE-CIV
			
			(load "The General 3.1/Army Comp/Vietnamese")

		#end-if

		#load-if-defined VIKING-CIV
			
			(load "The General 3.1/Army Comp/Vikings")

		#end-if

	;-----------
	;	Monks
	;-----------

		(defrule
		(current-age >= castle-age)
		(up-compare-goal g-enemy-heavy-cavalry-count >= 4)
		(up-compare-goal g-enemy-light-cavalry-count < 4)
		=>
		(up-modify-goal g-desired-num-monk c:max 3)
		)

		(defrule
		(current-age == castle-age)
		(up-compare-goal g-age-status >= MID-CASTLE)
		(up-compare-goal g-game-focus != DEFENSIVE)
		=>
		(up-modify-goal g-desired-num-monk c:max 2)
		)

		(defrule
		(current-age == imperial-age)
		(up-compare-goal g-game-focus != DEFENSIVE)
		=>
		(up-modify-goal g-desired-num-monk c:max 1)
		)

	;-------------------
	;	Trebs/Cannons
	;-------------------

		; (defrule
		; (up-compare-goal g-age-status >= TO-IMPERIAL)
		; (up-compare-goal g-age-status < LATE-IMPERIAL)
		; (up-compare-const BOMBARD-CANNON-AVAILABLE == YES)
		; =>
		; (set-goal g-desired-num-bombard-cannon 5)
		; )

		(defrule
		(up-compare-goal g-age-status >= TO-IMPERIAL)
		(up-compare-goal g-age-status < LATE-IMPERIAL)
		; (or
		; 	(up-compare-const BOMBARD-CANNON-AVAILABLE == NO)
		; 	(or
		; 		(up-research-status c: ri-chemistry < research-complete)
		; 		(and
		; 			(unit-available bombard-cannon)
		; 			(building-type-count siege-workshop == 0))))
		=>
		(set-goal g-desired-num-trebuchet 5)
		)

		; (defrule
		; (up-compare-goal g-age-status >= LATE-IMPERIAL)
		; (up-compare-const BOMBARD-CANNON-AVAILABLE == YES)
		; =>
		; (set-goal g-desired-num-bombard-cannon 1)
		; )

		(defrule
		(up-compare-goal g-age-status >= LATE-IMPERIAL)
		; (or
		; 	(up-compare-const BOMBARD-CANNON-AVAILABLE == NO)
		; 	(or
		; 		(up-research-status c: ri-chemistry < research-complete)
		; 		(and
		; 			(unit-available bombard-cannon)
		; 			(building-type-count siege-workshop == 0))))
		=>
		(set-goal g-desired-num-trebuchet 1)
		)

	;--------------------
	;	Battering Rams
	;--------------------

		(defrule
		(current-age == castle-age)
		(up-compare-goal g-age-status >= MID-CASTLE)
		(up-compare-goal g-target-military-parity > 0)
		(or
			(up-compare-goal g-target-pop-parity >= 5)
			(or
				(military-population >= 20)
				(and
					(up-compare-goal g-game-focus != BOOM)
					(military-population >= 12))))
		(goal g-town-under-attack NO)
		=>
		(set-goal g-desired-num-battering-ram 1)
		)

		(defrule
		(current-age == castle-age)
		(up-compare-goal g-age-status >= MID-CASTLE)
		(up-compare-goal g-enemy-buildings-in-town > 0)
		(military-population g:>= g-min-military-pop)
		(up-compare-goal g-target-military-parity > -10)
		=>
		(set-goal g-desired-num-battering-ram 2)
		)

		(defrule
		(current-age == castle-age)
		(up-compare-goal g-age-status >= MID-CASTLE)
		(up-compare-goal g-primary-unit-class != archery-class)
		(up-compare-goal g-primary-unit-class != archery-cannon-class)
		(up-compare-goal g-target-military-parity > 5)
		(military-population >= 12)
		(or
			(military-population >= 20)
			(up-compare-goal g-game-focus != BOOM))
		(goal g-town-under-attack NO)
		=>
		(set-goal g-desired-num-battering-ram 3)
		)

		(defrule
		(current-age == imperial-age)
		(up-compare-goal g-target-military-parity > 0)
		(or
			(building-type-count castle < 2)
			(or
				(unit-type-count trebuchet-set < 2)
				(goal g-age-status LATE-IMPERIAL)))
		(or
			(up-compare-goal g-target-pop-parity >= 5)
			(military-population >= 20))
		(or
			(goal g-town-under-attack NO)
			(up-compare-goal g-enemy-buildings-in-town > 0))
		=>
		(set-goal g-desired-num-battering-ram 5)
		)

	;---------------
	;	Mangonels
	;---------------

		(defrule
		(up-compare-goal g-age-status >= MID-CASTLE)
		(or
			(goal g-primary-unit-class archery-class)
			(or
				(goal g-primary-unit-class archery-cannon-class)
				(goal g-primary-unit-class monastery-class)))
		(or
			(goal g-game-focus DEFENSIVE)
			(goal g-game-focus REBUILD))
		(up-compare-goal g-enemy-heavy-cavalry-count < 5)
		(up-compare-goal g-enemy-light-cavalry-count < 5)
		(up-compare-goal g-enemy-eagle-warrior-count < 5)
		=>
		(up-modify-goal g-desired-num-mangonel c:max 1)
		)

		(defrule
		(up-compare-goal g-age-status >= MID-CASTLE)
		(or
			(goal g-primary-unit-class archery-class)
			(or
				(goal g-primary-unit-class archery-cannon-class)
				(goal g-primary-unit-class monastery-class)))
		(up-compare-goal g-enemy-heavy-cavalry-count < 8)
		(up-compare-goal g-enemy-light-cavalry-count < 8)
		(up-compare-goal g-enemy-eagle-warrior-count < 8)
		=>
		(up-modify-goal g-desired-num-mangonel c:max 2)
		)

	;----------------------------------------------------------------
	;	Increase military pop when we've completed important techs
	;----------------------------------------------------------------

		(defrule
		(up-compare-goal g-primary-unit-tech-progress >= ALL-IMPERIAL-COMPLETE)
		(up-compare-goal g-support-unit-tech-progress >= ALL-IMPERIAL-COMPLETE)
		(up-compare-goal g-eco-tech-progress >= ALL-IMPERIAL-COMPLETE)
		=>
		(up-modify-goal g-desired-num-military c:+ 15)
		)

	;-----------------------------
	;	Adjust Military Numbers
	;-----------------------------
	
		(load "The General 3.1/Functions/Reset")

		(defrule
		(current-age >= feudal-age)
		=>
		(up-modify-goal g-temp g:= g-desired-num-militia)
		(up-modify-goal g-temp g:+ g-desired-num-spearman)
		(up-modify-goal g-temp g:+ g-desired-num-eagle-warrior)
		(up-modify-goal g-temp g:+ g-desired-num-archer)
		(up-modify-goal g-temp g:+ g-desired-num-skirmisher)
		(up-modify-goal g-temp g:+ g-desired-num-cavalry-archer)
		(up-modify-goal g-temp g:+ g-desired-num-hand-cannoneer)
		(up-modify-goal g-temp g:+ g-desired-num-scout-cavalry)
		(up-modify-goal g-temp g:+ g-desired-num-knight)
		(up-modify-goal g-temp g:+ g-desired-num-camel)
		(up-modify-goal g-temp g:+ g-desired-num-unique-unit)
		(up-modify-goal g-temp g:+ g-desired-num-genitour)
		(up-modify-goal g-temp g:+ g-desired-num-slinger)
		(up-modify-goal g-temp g:+ g-desired-num-condottiero)
		(up-modify-goal g-temp g:+ g-desired-num-battle-elephant)
		)

		(defrule
		(current-age >= feudal-age)
		=>
		(up-modify-goal g-temp g:+ g-desired-num-steppe-lancer)
		(up-modify-goal g-temp-2 g:= g-desired-num-military)
		(up-modify-goal g-temp-2 g:%/ g-temp)
		(up-modify-goal g-desired-num-militia g:%* g-temp-2)
		(up-modify-goal g-desired-num-spearman g:%* g-temp-2)
		(up-modify-goal g-desired-num-eagle-warrior g:%* g-temp-2)
		(up-modify-goal g-desired-num-archer g:%* g-temp-2)
		(up-modify-goal g-desired-num-skirmisher g:%* g-temp-2)
		(up-modify-goal g-desired-num-cavalry-archer g:%* g-temp-2)
		(up-modify-goal g-desired-num-hand-cannoneer g:%* g-temp-2)
		(up-modify-goal g-desired-num-scout-cavalry g:%* g-temp-2)
		(up-modify-goal g-desired-num-knight g:%* g-temp-2)
		(up-modify-goal g-desired-num-camel g:%* g-temp-2)
		)

		(defrule
		(current-age >= castle-age)
		=>
		(up-modify-goal g-desired-num-unique-unit g:%* g-temp-2)
		(up-modify-goal g-desired-num-genitour g:%* g-temp-2)
		(up-modify-goal g-desired-num-slinger g:%* g-temp-2)
		(up-modify-goal g-desired-num-condottiero g:%* g-temp-2)
		(up-modify-goal g-desired-num-battle-elephant g:%* g-temp-2)
		(up-modify-goal g-desired-num-steppe-lancer g:%* g-temp-2)
		)

		#load-if-defined MALAY-CIV

			(defrule
			(up-compare-goal g-desired-num-unique-unit > 0)
			=>
			(up-modify-goal g-desired-num-unique-unit c:* 2)
			)

		#end-if

;=========================<>=========================
;		   PRIMARY AND SUPPORT UNIT NUMBERS
;=========================<>=========================

	(defrule
	(goal g-primary-unit militiaman)
	=>
	(up-modify-goal g-desired-num-primary-unit g:= g-desired-num-militia)
	)

	(defrule
	(goal g-primary-unit spearman)
	=>
	(up-modify-goal g-desired-num-primary-unit g:= g-desired-num-spearman)
	)

	(defrule
	(goal g-primary-unit eagle-warrior)
	=>
	(up-modify-goal g-desired-num-primary-unit g:= g-desired-num-eagle-warrior)
	)

	(defrule
	(goal g-primary-unit condottiero)
	=>
	(up-modify-goal g-desired-num-primary-unit g:= g-desired-num-condottiero)
	)

	(defrule
	(goal g-primary-unit archer)
	=>
	(up-modify-goal g-desired-num-primary-unit g:= g-desired-num-archer)
	)

	(defrule
	(goal g-primary-unit skirmisher)
	=>
	(up-modify-goal g-desired-num-primary-unit g:= g-desired-num-skirmisher)
	)

	(defrule
	(goal g-primary-unit cavalry-archer)
	=>
	(up-modify-goal g-desired-num-primary-unit g:= g-desired-num-cavalry-archer)
	)

	(defrule
	(goal g-primary-unit hand-cannoneer)
	=>
	(up-modify-goal g-desired-num-primary-unit g:= g-desired-num-hand-cannoneer)
	)

	(defrule
	(goal g-primary-unit slinger)
	=>
	(up-modify-goal g-desired-num-primary-unit g:= g-desired-num-slinger)
	)

	(defrule
	(goal g-primary-unit genitour)
	=>
	(up-modify-goal g-desired-num-primary-unit g:= g-desired-num-genitour)
	)

	(defrule
	(goal g-primary-unit scout-cavalry)
	=>
	(up-modify-goal g-desired-num-primary-unit g:= g-desired-num-scout-cavalry)
	)

	(defrule
	(goal g-primary-unit knight)
	=>
	(up-modify-goal g-desired-num-primary-unit g:= g-desired-num-knight)
	)

	(defrule
	(goal g-primary-unit camel)
	=>
	(up-modify-goal g-desired-num-primary-unit g:= g-desired-num-camel)
	)

	(defrule
	(goal g-primary-unit battle-elephant)
	=>
	(up-modify-goal g-desired-num-primary-unit g:= g-desired-num-battle-elephant)
	)

	(defrule
	(goal g-primary-unit steppe-lancer)
	=>
	(up-modify-goal g-desired-num-primary-unit g:= g-desired-num-steppe-lancer)
	)

	(defrule
	(goal g-primary-unit my-unique-unit)
	=>
	(up-modify-goal g-desired-num-primary-unit g:= g-desired-num-unique-unit)
	)

	(defrule
	(goal g-primary-unit monk)
	=>
	(up-modify-goal g-desired-num-primary-unit g:= g-desired-num-monk)
	)

	(defrule
	(goal g-primary-unit missionary)
	=>
	(up-modify-goal g-desired-num-primary-unit g:= g-desired-num-missionary)
	)

	(defrule
	(goal g-support-unit militiaman)
	=>
	(up-modify-goal g-desired-num-support-unit g:= g-desired-num-militia)
	)

	(defrule
	(goal g-support-unit spearman)
	=>
	(up-modify-goal g-desired-num-support-unit g:= g-desired-num-spearman)
	)

	(defrule
	(goal g-support-unit eagle-warrior)
	=>
	(up-modify-goal g-desired-num-support-unit g:= g-desired-num-eagle-warrior)
	)

	(defrule
	(goal g-support-unit condottiero)
	=>
	(up-modify-goal g-desired-num-support-unit g:= g-desired-num-condottiero)
	)

	(defrule
	(goal g-support-unit archer)
	=>
	(up-modify-goal g-desired-num-support-unit g:= g-desired-num-archer)
	)

	(defrule
	(goal g-support-unit skirmisher)
	=>
	(up-modify-goal g-desired-num-support-unit g:= g-desired-num-skirmisher)
	)

	(defrule
	(goal g-support-unit cavalry-archer)
	=>
	(up-modify-goal g-desired-num-support-unit g:= g-desired-num-cavalry-archer)
	)

	(defrule
	(goal g-support-unit hand-cannoneer)
	=>
	(up-modify-goal g-desired-num-support-unit g:= g-desired-num-hand-cannoneer)
	)

	(defrule
	(goal g-support-unit slinger)
	=>
	(up-modify-goal g-desired-num-support-unit g:= g-desired-num-slinger)
	)

	(defrule
	(goal g-support-unit genitour)
	=>
	(up-modify-goal g-desired-num-support-unit g:= g-desired-num-genitour)
	)

	(defrule
	(goal g-support-unit scout-cavalry)
	=>
	(up-modify-goal g-desired-num-support-unit g:= g-desired-num-scout-cavalry)
	)

	(defrule
	(goal g-support-unit knight)
	=>
	(up-modify-goal g-desired-num-support-unit g:= g-desired-num-knight)
	)

	(defrule
	(goal g-support-unit camel)
	=>
	(up-modify-goal g-desired-num-support-unit g:= g-desired-num-camel)
	)

	(defrule
	(goal g-support-unit battle-elephant)
	=>
	(up-modify-goal g-desired-num-support-unit g:= g-desired-num-battle-elephant)
	)

	(defrule
	(goal g-support-unit steppe-lancer)
	=>
	(up-modify-goal g-desired-num-support-unit g:= g-desired-num-steppe-lancer)
	)

	(defrule
	(goal g-support-unit my-unique-unit)
	=>
	(up-modify-goal g-desired-num-support-unit g:= g-desired-num-unique-unit)
	)

	(defrule
	(goal g-support-unit monk)
	=>
	(up-modify-goal g-desired-num-support-unit g:= g-desired-num-monk)
	)

	(defrule
	(goal g-support-unit missionary)
	=>
	(up-modify-goal g-desired-num-support-unit g:= g-desired-num-missionary)
	)

;=========================<>=========================
;			       UNIT CLASS GOALS
;=========================<>=========================

	;----------------------------------
	;	GOAL: g-desired-num-infantry
	;----------------------------------

		(defrule
		(true)
		=>
		(up-modify-goal g-desired-num-infantry g:= g-desired-num-militia)
		(up-modify-goal g-desired-num-infantry g:+ g-desired-num-spearman)
		(up-modify-goal g-desired-num-infantry g:+ g-desired-num-eagle-warrior)
		(up-modify-goal g-desired-num-infantry g:+ g-desired-num-condottiero)
		)

		(defrule
		(or
			(civ-selected aztec)
			(or
				(civ-selected celtic)
				(or
					(civ-selected frankish)
					(or
						(civ-selected gothic)
						(or
							(civ-selected japanese)
							(civ-selected teutonic))))))
		=>
		(up-modify-goal g-desired-num-infantry g:+ g-desired-num-unique-unit)
		)

		(defrule
		(or
			(civ-selected viking)
			(or
				(civ-selected incan)
				(or
					(civ-selected ethiopian)
					(or
						(civ-selected malian)
						(or
							(civ-selected malay)
							(civ-selected sicilians))))))
		=>
		(up-modify-goal g-desired-num-infantry g:+ g-desired-num-unique-unit)
		)

	;----------------------------------
	;	GOAL: g-desired-num-foot-archers
	;----------------------------------

		(defrule
		(true)
		=>
		(up-modify-goal g-desired-num-foot-archers g:= g-desired-num-archer)
		(up-modify-goal g-desired-num-foot-archers g:+ g-desired-num-skirmisher)
		(up-modify-goal g-desired-num-foot-archers g:+ g-desired-num-slinger)
		)

		(defrule
		(or
			(civ-selected briton)
			(or
				(civ-selected chinese)
				(or
					(civ-selected mayan)
					(or
						(civ-selected italian)
						(civ-selected vietnamese)))))
		=>
		(up-modify-goal g-desired-num-foot-archers g:+ g-desired-num-unique-unit)
		)

	;----------------------------------
	;	GOAL: g-desired-num-cavalry-archers
	;----------------------------------

		(defrule
		(true)
		=>
		(up-modify-goal g-desired-num-cavalry-archers g:= g-desired-num-cavalry-archer)
		(up-modify-goal g-desired-num-cavalry-archers g:+ g-desired-num-genitour)
		)

		(defrule
		(or
			(civ-selected korean)
			(or
				(civ-selected mongol)
				(or
					(civ-selected spanish)
					(or
						(civ-selected indian)
						(or
							(civ-selected berbers)
							(or
								(civ-selected burmese)
								(civ-selected cumans)))))))
		=>
		(up-modify-goal g-desired-num-cavalry-archers g:+ g-desired-num-unique-unit)
		)

	;----------------------------------
	;	GOAL: g-desired-num-cavalry
	;----------------------------------

		(defrule
		(true)
		=>
		(up-modify-goal g-desired-num-cavalry g:= g-desired-num-scout-cavalry)
		(up-modify-goal g-desired-num-cavalry g:+ g-desired-num-knight)
		(up-modify-goal g-desired-num-cavalry g:+ g-desired-num-camel)
		(up-modify-goal g-desired-num-cavalry g:+ g-desired-num-battle-elephant)
		(up-modify-goal g-desired-num-cavalry g:+ g-desired-num-steppe-lancer)
		)

		(defrule
		(or
			(civ-selected byzantine)
			(or
				(civ-selected hun)
				(or
					(civ-selected persian)
					(or
						(civ-selected saracen)
						(or
							(civ-selected magyar)
							(civ-selected slavic))))))
		=>
		(up-modify-goal g-desired-num-cavalry g:+ g-desired-num-unique-unit)
		)

		(defrule
		(or
			(civ-selected bulgarians)
			(or
				(civ-selected lithuanians)
				(or
					(civ-selected tatars)
					(civ-selected burgundians))))
		=>
		(up-modify-goal g-desired-num-cavalry g:+ g-desired-num-unique-unit)
		)

	;----------------------------------
	;	GOAL: g-desired-num-siege
	;----------------------------------

		(defrule
		(true)
		=>
		(up-modify-goal g-desired-num-siege g:= g-desired-num-battering-ram)
		(up-modify-goal g-desired-num-siege g:+ g-desired-num-mangonel)
		(up-modify-goal g-desired-num-siege g:+ g-desired-num-scorpion)
		(up-modify-goal g-desired-num-siege g:+ g-desired-num-bombard-cannon)
		(up-modify-goal g-desired-num-siege g:+ g-desired-num-trebuchet)
		)

		(defrule
		(civ-selected portuguese)
		=>
		(up-modify-goal g-desired-num-siege g:+ g-desired-num-unique-unit)
		)

	;----------------------------------
	;	GOAL: g-desired-num-hand-cannoneers
	;----------------------------------

		(defrule
		(true)
		=>
		(up-modify-goal g-desired-num-hand-cannoneers g:= g-desired-num-hand-cannoneer)
		)

		(defrule
		(or
			(civ-selected turkish)
			(or
				(civ-selected spanish)
				(civ-selected burmese)))
		=>
		(up-modify-goal g-desired-num-hand-cannoneers g:+ g-desired-num-unique-unit)
		)

	;----------------------------------
	;	GOAL: g-desired-num-monks
	;----------------------------------

		(defrule
		(true)
		=>
		(up-modify-goal g-desired-num-monks g:= g-desired-num-monk)
		(up-modify-goal g-desired-num-monks g:+ g-desired-num-missionary)
		)

;=========================<>=========================
;				      CIVILIANS
;=========================<>=========================

	;---------------
	;	Villagers
	;---------------

		(defrule
		(or
			(up-compare-goal g-primary-unit-tech-progress < ALL-IMPERIAL-COMPLETE)
			(or
				(up-compare-goal g-support-unit-tech-progress < ALL-IMPERIAL-COMPLETE)
				(up-compare-goal g-eco-tech-progress < ALL-IMPERIAL-COMPLETE)))
		=>
		(set-goal g-desired-num-villager 140)
		)

		(defrule
		(up-compare-goal g-primary-unit-tech-progress >= ALL-IMPERIAL-COMPLETE)
		(or
			(up-compare-goal g-support-unit-tech-progress >= ALL-IMPERIAL-COMPLETE)
			(up-compare-goal g-age-status >= LATE-IMPERIAL))
		(up-compare-goal g-eco-tech-progress >= ALL-IMPERIAL-COMPLETE)
		=>
		(set-goal g-desired-num-villager 125)
		)

	;-----------------
	;	Trade Carts
	;-----------------

		(defrule
		(up-compare-goal g-desired-num-trade-cart < 20)
		(up-compare-goal g-age-status >= MID-IMPERIAL)
		(player-in-game any-ally)
		(players-building-type-count any-ally market > 0)
		(up-object-type-count-total c: town-center g:>= g-desired-num-town-center)
		=>
		(set-goal g-desired-num-trade-cart 20)
		(up-get-fact population-cap 0 g-desired-num-villager)
		(up-modify-goal g-desired-num-villager g:- g-desired-num-military)
		(up-modify-goal g-desired-num-villager g:- g-desired-num-trade-cart)
		(up-modify-goal g-desired-num-villager g:- g-desired-num-monks)
		(up-modify-goal g-desired-num-villager g:- g-desired-num-siege)
		(up-modify-goal g-desired-num-villager c:min SEVENTY-PERCENT-POP)
		)

		(defrule
		(up-compare-goal g-desired-num-trade-cart < 20)
		(or
			(up-compare-goal g-age-status >= LATE-IMPERIAL)
			(game-time >= 2400))
		(game-time < 3000)
		(player-in-game any-ally)
		(players-building-type-count any-ally market > 0)
		(up-object-type-count-total c: town-center g:>= g-desired-num-town-center)
		=>
		(set-goal g-desired-num-trade-cart 20)
		(up-get-fact population-cap 0 g-desired-num-villager)
		(up-modify-goal g-desired-num-villager g:- g-desired-num-military)
		(up-modify-goal g-desired-num-villager g:- g-desired-num-trade-cart)
		(up-modify-goal g-desired-num-villager g:- g-desired-num-monks)
		(up-modify-goal g-desired-num-villager g:- g-desired-num-siege)
		(up-modify-goal g-desired-num-villager c:min SEVENTY-PERCENT-POP)
		)

		(defrule
		(up-compare-goal g-desired-num-trade-cart < 45)
		(or
			(up-compare-goal g-age-status >= LATE-IMPERIAL)
			(game-time >= 3000))
		(player-in-game any-ally)
		(players-building-type-count any-ally market > 0)
		(up-object-type-count-total c: town-center g:>= g-desired-num-town-center)
		=>
		(set-goal g-desired-num-trade-cart 45)
		(up-get-fact population-cap 0 g-desired-num-villager)
		(up-modify-goal g-desired-num-villager g:- g-desired-num-military)
		(up-modify-goal g-desired-num-villager g:- g-desired-num-trade-cart)
		(up-modify-goal g-desired-num-villager g:- g-desired-num-monks)
		(up-modify-goal g-desired-num-villager g:- g-desired-num-siege)
		(up-modify-goal g-desired-num-villager c:min SEVENTY-PERCENT-POP)
		)

		(defrule
		(up-object-type-count c: villager-class g:> g-desired-num-villager)
		(or
			(and
				(military-population <= 15)
				(population > EIGHTY-PERCENT-POP))
			(population > NINETY-FIVE-PERCENT-POP))
		=>
		(delete-unit villager)
		)

		(defrule
		(up-object-type-count c: trade-cart g:> g-desired-num-trade-cart)
		(or
			(and
				(military-population <= 15)
				(population > EIGHTY-PERCENT-POP))
			(population > NINETY-FIVE-PERCENT-POP))
		=>
		(delete-unit trade-cart)
		)

	;------------------------
	;	Required Villagers
	;------------------------

		(defrule
		(current-age >= castle-age)
		(or
			(goal g-game-focus BOOM)
			(or
				(goal g-game-focus REBUILD)
				(goal g-game-focus DEFENSIVE)))
		=>
		(set-goal g-required-num-villager 100)
		(up-modify-goal g-required-num-villager g:min g-desired-num-villager)
		)

		(defrule
		(current-age >= castle-age)
		(or
			(goal g-game-focus BALANCED)
			(goal g-game-focus AGGRESSIVE))
		=>
		(set-goal g-required-num-villager 120)
		(up-modify-goal g-required-num-villager g:min g-desired-num-villager)
		)

		(defrule
		(current-age == dark-age)
		=>
		(set-goal g-required-num-villager 28)
		(up-modify-goal g-required-num-villager g:min g-desired-num-villager)
		)

		(defrule
		(current-age == feudal-age)
		=>
		(set-goal g-required-num-villager 30)
		(up-modify-goal g-required-num-villager g:min g-desired-num-villager)
		)

		;Krush/Crush
		(defrule
		(current-age <= feudal-age)
		(or
			(goal g-current-strategy FC-KRUSH)
			(goal g-current-strategy FC-CRUSH))
		=>
		(up-modify-goal g-required-num-villager c:+ 1)
		(up-modify-goal g-required-num-villager g:min g-desired-num-villager)
		)

;=========================<>=========================
;				      BUILDINGS
;=========================<>=========================

	(load "The General 3.1/Functions/Reset")

	;------------------
	;	Town Centers
	;------------------

		(load "The General 3.1/Functions/Reset")

		;Rebuild
		(defrule
		(up-compare-goal g-desired-num-town-center < 1)
		=>
		(set-goal g-desired-num-town-center 1)
		)

		#load-if-defined CUMANS-CIV

			(defrule
			(goal g-game-focus REBUILD)
			(civilian-population >= 10)
			(up-compare-goal g-age-status >= TO-FEUDAL)
			(up-compare-goal g-desired-num-town-center < 2)
			=>
			(set-goal g-desired-num-town-center 2)
			)

		#else

			(defrule
			(goal g-game-focus REBUILD)
			(civilian-population >= 10)
			(up-compare-goal g-age-status >= TO-CASTLE)
			(up-compare-goal g-desired-num-town-center < 2)
			=>
			(set-goal g-desired-num-town-center 2)
			)

		#end-if

		(defrule
		(goal g-game-focus REBUILD)
		(civilian-population >= 20)
		(current-age >= castle-age)
		(up-compare-goal g-desired-num-town-center < 3)
		=>
		(set-goal g-desired-num-town-center 3)
		)

		(defrule
		(goal g-game-focus REBUILD)
		(civilian-population >= 40)
		(current-age >= castle-age)
		(up-compare-goal g-desired-num-town-center < 4)
		=>
		(set-goal g-desired-num-town-center 4)
		)

		(defrule
		(goal g-game-focus REBUILD)
		(civilian-population >= 60)
		(current-age >= castle-age)
		(up-compare-goal g-desired-num-town-center < 5)
		=>
		(set-goal g-desired-num-town-center 5)
		)

		;Defend
		#load-if-defined CUMANS-CIV

			(defrule
			(goal g-game-focus DEFENSIVE)
			(civilian-population >= 10)
			(up-compare-goal g-age-status >= TO-FEUDAL)
			(up-compare-goal g-desired-num-town-center < 2)
			=>
			(set-goal g-desired-num-town-center 2)
			)

		#else

			(defrule
			(goal g-game-focus DEFENSIVE)
			(civilian-population >= 10)
			(up-compare-goal g-age-status >= TO-CASTLE)
			(up-compare-goal g-desired-num-town-center < 2)
			=>
			(set-goal g-desired-num-town-center 2)
			)

		#end-if

		(defrule
		(goal g-game-focus DEFENSIVE)
		(civilian-population >= 40)
		(building-type-count town-center == 2)
		(current-age >= castle-age)
		(up-compare-goal g-desired-num-town-center < 3)
		=>
		(set-goal g-desired-num-town-center 3)
		)

		;Boom
		(defrule
		(goal g-game-focus BOOM)
		(current-age >= castle-age)
		(up-compare-goal g-desired-num-town-center < 3)
		=>
		(set-goal g-desired-num-town-center 3)
		)

		(defrule
		(goal g-game-focus BOOM)
		(current-age == castle-age)
		(civilian-population >= 100)
		(up-compare-goal g-desired-num-town-center < 4)
		(up-modify-goal g-temp g:= g-desired-num-villager)
		(up-modify-goal g-temp c:- 20)
		(population g:< g-temp)		;not within 20 pop of the number of vils we want
		=>
		(set-goal g-desired-num-town-center 4)
		)

		(defrule
		(goal g-game-focus BOOM)
		(current-age == imperial-age)
		(civilian-population >= 100)
		(up-compare-goal g-desired-num-town-center < 5)
		(up-modify-goal g-temp g:= g-desired-num-villager)
		(up-modify-goal g-temp c:- 20)
		(population g:< g-temp)		;not within 20 pop of the number of vils we want
		=>
		(set-goal g-desired-num-town-center 5)
		)

		;Balanced
		(defrule
		(goal g-game-focus BALANCED)
		(current-age >= castle-age)
		(up-compare-goal g-attack-status >= FIRST-CASTLE-ATTACK)
		(or
			(wood-amount > 400)
			(or
				(up-object-type-count c: siege-workshop g:>= g-desired-num-siege-workshop)
				(up-compare-goal g-age-status >= TO-IMPERIAL)))
		(up-compare-goal g-desired-num-town-center < 2)
		=>
		(set-goal g-desired-num-town-center 2)
		)

		(defrule
		(goal g-game-focus BALANCED)
		(current-age >= castle-age)
		(building-type-count-total town-center >= 2)
		(unit-type-count-total villager >= 42)
		(military-population g:>= g-min-military-pop)
		(up-compare-goal g-attack-status >= FIRST-CASTLE-ATTACK)
		(or
			(wood-amount > 400)
			(or
				(up-object-type-count c: siege-workshop g:>= g-desired-num-siege-workshop)
				(up-compare-goal g-age-status >= TO-IMPERIAL)))
		(up-compare-goal g-desired-num-town-center < 3)
		=>
		(set-goal g-desired-num-town-center 3)
		)

		(defrule
		(goal g-game-focus BALANCED)
		(up-compare-goal g-age-status < TO-IMPERIAL)
		(building-type-count-total town-center >= 3)
		(unit-type-count-total villager >= 100)
		(military-population g:>= g-min-military-pop)
		(up-compare-goal g-attack-status >= FIRST-CASTLE-ATTACK)
		(or
			(wood-amount > 400)
			(up-object-type-count c: siege-workshop g:>= g-desired-num-siege-workshop))
		(up-compare-goal g-desired-num-town-center < 4)
		(up-modify-goal g-temp g:= g-desired-num-villager)
		(up-modify-goal g-temp c:- 20)
		(population g:< g-temp)		;not within 20 pop of the number of vils we want
		=>
		(set-goal g-desired-num-town-center 4)
		)
		
		(defrule
		(goal g-game-focus BALANCED)
		(up-compare-goal g-age-status >= TO-IMPERIAL)
		(building-type-count-total town-center >= 3)
		(unit-type-count-total villager >= 100)
		(up-compare-goal g-desired-num-town-center < 4)
		(up-modify-goal g-temp c:= HUNDRED-PERCENT-POP)
		(up-modify-goal g-temp c:- 20)
		(population g:< g-temp)		;not within 20 pop of max population
		=>
		(set-goal g-desired-num-town-center 4)
		)

		(defrule
		(goal g-game-focus BALANCED)
		(current-age == imperial-age)
		(building-type-count-total town-center >= 4)
		(up-compare-goal g-attack-status >= FIRST-IMPERIAL-ATTACK)
		(unit-type-count-total villager >= 120)
		(up-compare-goal g-desired-num-town-center < 5)
		(up-modify-goal g-temp g:= g-desired-num-villager)
		(up-modify-goal g-temp c:- 20)
		(population g:< g-temp)		;not within 20 pop of the number of vils we want
		=>
		(set-goal g-desired-num-town-center 5)
		)

		;Aggressive
		(defrule
		(goal g-game-focus AGGRESSIVE)
		(current-age >= castle-age)
		(or
			(up-compare-goal g-attack-status >= FIRST-CASTLE-ATTACK)
			(military-population >= 12))
		(up-compare-goal g-desired-num-town-center < 2)
		(military-population >= 4)
		=>
		(set-goal g-desired-num-town-center 2)
		)

		(defrule
		(goal g-game-focus AGGRESSIVE)
		(current-age >= castle-age)
		(up-compare-goal g-attack-status >= FIRST-CASTLE-ATTACK)
		(or
			(unit-type-count-total villager >= 60)
			(or
				(up-compare-goal g-target-military-parity > 0)
				(up-compare-goal g-target-pop-parity > 0)))
		(or
			(up-object-type-count c: siege-workshop g:>= g-desired-num-siege-workshop)
			(up-compare-goal g-target-military-parity < 3))
		(up-compare-goal g-desired-num-town-center < 3)
		=>
		(set-goal g-desired-num-town-center 3)
		)
		
		(defrule
		(goal g-game-focus AGGRESSIVE)
		(current-age == imperial-age)
		(building-type-count-total town-center >= 3)
		(up-compare-goal g-attack-status >= FIRST-IMPERIAL-ATTACK)
		(unit-type-count-total villager >= 120)
		(up-compare-goal g-desired-num-town-center < 4)
		(up-modify-goal g-temp g:= g-desired-num-villager)
		(up-modify-goal g-temp c:- 20)
		(population g:< g-temp)		;not within 20 pop of the number of vils we want
		=>
		(set-goal g-desired-num-town-center 4)
		)

	;------------
	;	Houses
	;------------

		(load "The General 3.1/Functions/Reset")

		(defrule
		(true)
		=>
		(up-get-fact building-type-count-total house g-desired-num-house)
		)

		#load-if-not-defined HUN-CIV
		#load-if-not-defined CHINESE-CIV

			(defrule
			(building-type-count-total house < 1)
			=>
			(set-goal g-desired-num-house 1)
			)
			
		#end-if
		#end-if

		#load-if-not-defined INCAN-CIV
			#load-if-not-defined CHINESE-CIV
				;Figure out how many houses are needed and add it
				;to g-desired-num-house
				(defrule
				(current-age == dark-age)
				(up-compare-goal g-true-housing-headroom < 2)
				(population-headroom > 0)
				(building-type-count-total house >= 1)
				(up-pending-objects c: house == 0)
				=>
				(up-modify-goal g-temp g:= g-true-housing-headroom)
				(up-modify-goal g-temp c:- 1)	;matches number in condition above
				(up-modify-goal g-temp c:z/ -5)	;use negative 5 because g-temp is zero or negative at this point
				(up-modify-goal g-temp c:+ 1)
				(up-modify-goal g-desired-num-house g:+ g-temp)
				)
			#else 	;Chinese civ
				;Figure out how many houses are needed and add it
				;to g-desired-num-house
				(defrule
				(game-time > 2)
				(current-age == dark-age)
				(up-compare-goal g-true-housing-headroom < 2)
				(population-headroom > 0)
				(up-pending-objects c: house == 0)
				=>
				(up-modify-goal g-temp g:= g-true-housing-headroom)
				(up-modify-goal g-temp c:- 1)	;matches number in condition above
				(up-modify-goal g-temp c:z/ -5)	;use negative 5 because g-temp is zero or negative at this point
				(up-modify-goal g-temp c:+ 1)
				(up-modify-goal g-desired-num-house g:+ g-temp)
				)
			#end-if

			(defrule
			(current-age == feudal-age)
			(up-compare-goal g-true-housing-headroom < 5)
			(population-headroom > 0)
			(up-pending-objects c: house == 0)
			=>
			(up-modify-goal g-temp g:= g-true-housing-headroom)
			(up-modify-goal g-temp c:- 5)	;matches number in condition above
			(up-modify-goal g-temp c:z/ -5)	;use negative 5 because g-temp is zero or negative at this point
			(up-modify-goal g-temp c:+ 1)
			(up-modify-goal g-desired-num-house g:+ g-temp)
			)

			(defrule
			(current-age >= castle-age)
			(up-compare-goal g-true-housing-headroom < 10)
			(population-headroom > 0)
			(up-pending-objects c: house == 0)
			=>
			(up-modify-goal g-temp g:= g-true-housing-headroom)
			(up-modify-goal g-temp c:- 10)	;matches number in condition above
			(up-modify-goal g-temp c:z/ -5)	;use negative 5 because g-temp is zero or negative at this point
			(up-modify-goal g-temp c:+ 1)
			(up-modify-goal g-desired-num-house g:+ g-temp)
			)

			(defrule
			(current-age >= castle-age)
			(up-compare-goal g-true-housing-headroom < 5)
			(population-headroom > 0)
			(up-pending-objects c: house <= 1)
			=>
			(up-modify-goal g-desired-num-house c:+ 1)
			)
			
		#else	;Incan Civ

			;Figure out how many houses are needed and add it
			;to g-desired-num-house
			(defrule
			(current-age == dark-age)
			(up-compare-goal g-true-housing-headroom < 2)
			(population-headroom > 0)
			(building-type-count-total house >= 1)
			(up-pending-objects c: house == 0)
			=>
			(up-modify-goal g-temp g:= g-true-housing-headroom)
			(up-modify-goal g-temp c:- 1)	;matches number in condition above
			(up-modify-goal g-temp c:z/ -10)	;use negative 10 because g-temp is zero or negative at this point
			(up-modify-goal g-temp c:+ 1)
			(up-modify-goal g-desired-num-house g:+ g-temp)
			)

			(defrule
			(current-age == feudal-age)
			(up-compare-goal g-true-housing-headroom < 5)
			(population-headroom > 0)
			(up-pending-objects c: house == 0)
			=>
			(up-modify-goal g-temp g:= g-true-housing-headroom)
			(up-modify-goal g-temp c:- 3)	;matches number in condition above
			(up-modify-goal g-temp c:z/ -10)	;use negative 10 because g-temp is zero or negative at this point
			(up-modify-goal g-temp c:+ 1)
			(up-modify-goal g-desired-num-house g:+ g-temp)
			)

			(defrule
			(current-age >= castle-age)
			(up-compare-goal g-true-housing-headroom < 10)
			(population-headroom > 0)
			(up-pending-objects c: house == 0)
			=>
			(up-modify-goal g-temp g:= g-true-housing-headroom)
			(up-modify-goal g-temp c:- 5)	;matches number in condition above
			(up-modify-goal g-temp c:z/ -10)	;use negative 10 because g-temp is zero or negative at this point
			(up-modify-goal g-temp c:+ 1)
			(up-modify-goal g-desired-num-house g:+ g-temp)
			)
			
		#end-if

		;Extra houses so we don't get pop capped when houses are destroyed
		#load-if-not-defined HUN-CIV
			#load-if-not-defined INCAN-CIV

				(defrule
				(up-research-status c: 487 < research-complete)	;487 = Nomads
				(population >= EIGHTY-PERCENT-POP)
				(population-headroom < 5)
				=>
				(up-modify-goal g-temp c:+ 6)
				)

			#else

				(defrule
				(population >= EIGHTY-PERCENT-POP)
				(population-headroom < 5)
				=>
				(up-modify-goal g-temp c:+ 3)
				)
			#end-if
		#end-if

	;-----------
	;	Mills
	;-----------

		(load "The General 3.1/Functions/Reset")

		;We want mill if we have a lumber camp or we have excess wood and it's time to gather berries
		#load-if-defined JAPANESE-CIV

			(defrule
			(or
				(building-type-count-total lumber-camp > 0)
				(wood-amount > 100))
			(up-gaia-type-count c: forage-class > 0)
			(building-type-count-total mill == 0)
			(or
				(unit-type-count villager >= 14)
				(or
					(and
						(game-time > 180)
						(unit-type-count villager-food == 0))
					(unit-type-count villager-forager > 0)))
			=>
			(up-modify-goal g-desired-num-mill c:max 1)
			)

		#else
		
			(defrule
			(or
				(building-type-count-total lumber-camp > 0)
				(wood-amount > 200))
			(up-gaia-type-count c: forage-class > 0)
			(building-type-count-total mill == 0)
			(or
				(unit-type-count villager >= 14)
				(or
					(and
						(game-time > 180)
						(unit-type-count villager-food == 0))
					(unit-type-count villager-forager > 0)))
			=>
			(up-modify-goal g-desired-num-mill c:max 1)
			)

		#end-if

		;We want a mill if we have foragers and no sheep and initial houses are done
		#load-if-defined HUN-CIV

			(defrule
			(game-time > 2)
			(or
				(unit-type-count villager-forager > 3)
				(goal g-current-sheep-count 0))
			(up-gaia-type-count c: forage-class > 0)
			(building-type-count-total mill == 0)
			=>
			(up-modify-goal g-desired-num-mill c:max 1)
			)

		#else
		
			(defrule
			(game-time > 2)
			(building-type-count house >= 1)
			(or
				(unit-type-count villager-forager > 3)
				(goal g-current-sheep-count 0))
			(up-gaia-type-count c: forage-class > 0)
			(building-type-count-total mill == 0)
			=>
			(up-modify-goal g-desired-num-mill c:max 1)
			)

		#end-if

		(load "The General 3.1/Functions/Reset")

		;We want second mill if we have extra berries
		(defrule
		(unit-type-count villager-forager > 0)
		(unit-type-count villager >= 22)
		(building-type-count-total mill == 1)
		(up-compare-goal g-desired-num-mill < 2)
		=>
		(set-strategic-number sn-focus-player-number 0)
		(up-full-reset-search)
		(up-set-target-point g-position-self-x)
		(up-filter-distance c: -1 c: 20)
		(up-find-remote c: forage-bush c: 40)
		(up-get-search-state g-local-total)
		)

		(defrule
		(unit-type-count villager-forager > 0)
		(unit-type-count villager >= 22)
		(building-type-count-total mill == 1)
		(up-compare-goal g-desired-num-mill < 2)
		(up-compare-goal g-remote-total > 6)	;more than standard berries
		=>
		(set-goal g-desired-num-mill 2)
		)

		;We want second mill if we have leftover deer
		(defrule
		(unit-type-count villager >= 22)
		(building-type-count-total mill == 1)
		(up-compare-goal g-desired-num-mill < 2)
		(up-compare-goal g-current-deer-count >= 2)
		(dropsite-min-distance deer-hunting >= 5)
		(dropsite-min-distance deer-hunting <= 20)
		(up-compare-goal g-scouting-status >= AUTO-SCOUT)
		=>
		(set-goal g-desired-num-mill 2)
		(set-strategic-number sn-preferred-mill-placement 1)	;for deer
		)

		;Backup in case we never built a mill after 5 minutes
		(defrule
		(or
			(up-compare-goal g-desired-num-farm > 0)
			(up-compare-goal g-age-status >= SAVE-FOR-FEUDAL))
		(building-type-count-total mill == 0)
		(up-compare-goal g-desired-num-mill < 1)
		=>
		(set-goal g-desired-num-mill 1)
		)

	;------------------
	;	Mining Camps
	;------------------

		(defrule
		(game-time > 30)
		(up-gaia-type-count-total c: gold > 0)
		(building-type-count-total lumber-camp > 0)
		(up-compare-goal g-current-gold-camp-total < 1)
		(or
			(and
				(unit-type-count villager-gold > 1)
				(dropsite-min-distance gold > 4))
			(up-compare-goal g-desired-num-gold-vils > 0))
		(dropsite-min-distance gold < 30)
		=>
		(set-goal g-desired-num-gold-camp 1)
		)

		(defrule
		(current-age >= feudal-age)
		(up-gaia-type-count-total c: gold > 0)
		(building-type-count-total lumber-camp > 0)
		(up-compare-goal g-current-gold-camp-total == 1)
		(or
			(goal g-town-under-attack YES)
			(or
				(goal g-game-focus DEFENSIVE)
				(or
					(unit-type-count 221 > 0)		;dead male miner
					(unit-type-count 229 > 0))))	;dead female miner
		(up-compare-goal g-desired-num-gold-vils > 0)
		(dropsite-min-distance gold < 30)
		=>
		(set-goal g-desired-num-gold-camp 2)
		)

		(defrule
		(game-time > 30)
		(up-gaia-type-count-total c: stone > 0)
		(building-type-count-total lumber-camp > 0)
		(up-compare-goal g-current-stone-camp-total < 1)
		(or
			(and
				(unit-type-count villager-stone > 1)
				(dropsite-min-distance stone > 4))
			(up-compare-goal g-desired-num-stone-vils > 0))
		(dropsite-min-distance stone < 30)
		=>
		(set-goal g-desired-num-stone-camp 1)
		)

		(defrule
		(current-age >= feudal-age)
		(up-gaia-type-count-total c: stone > 0)
		(building-type-count-total lumber-camp > 0)
		(up-compare-goal g-current-stone-camp-total == 1)
		(or
			(goal g-town-under-attack YES)
			(or
				(goal g-game-focus DEFENSIVE)
				(or
					(unit-type-count 221 > 0)		;dead male miner
					(unit-type-count 229 > 0))))	;dead female miner
		(up-compare-goal g-desired-num-stone-vils > 0)
		(goal g-desired-num-gold-vils 0)
		(dropsite-min-distance stone < 30)
		=>
		(set-goal g-desired-num-stone-camp 2)
		)

		(load "The General 3.1/Functions/Reset")

		;Request enough mining camps to ensure max 8 gold miners per mining camp or TC
		;First find how many TCs and mining camps are close to gold, stored in g-temp
		(defrule
		(up-compare-goal g-desired-num-gold-vils > 5)
		(building-type-count town-center == 0)
		(dropsite-min-distance gold < 255)	;still gold on the map
		(unit-type-count villager >= 60)
		(up-compare-goal g-current-gold-camp-total < 3)
		=>
		(up-modify-goal g-temp g:= g-current-gold-camp-total)
		(up-jump-rule 4)
		)

			(defrule
			(up-compare-goal g-desired-num-gold-vils > 5)
			(dropsite-min-distance gold < 255)	;still gold on the map
			(or
				(building-type-count-total town-center >= 2)
				(unit-type-count villager >= 60))
			(up-compare-goal g-current-gold-camp-total < 3)
			=>
			(up-full-reset-search)
			(up-find-local c: town-center c: 20)
			(up-filter-status c: status-pending c: list-active)
			(up-find-status-local c: town-center-foundation c: 5)
			(up-get-search-state g-local-total)
			(set-goal i 0)
			(up-modify-goal g-temp g:= g-current-gold-camp-total)
			)

				(defrule
				(up-compare-goal i g:< g-local-total)
				(up-compare-goal g-desired-num-gold-vils > 5)
				(dropsite-min-distance gold < 255)	;still gold on the map
				(or
					(building-type-count-total town-center >= 2)
					(unit-type-count villager >= 60))
				(up-compare-goal g-current-gold-camp-total < 3)
				(up-set-target-object search-local g: i)
				=>
				(up-get-point position-object g-point-x)
				(up-set-target-point g-point-x)
				(up-reset-search 0 0 1 1)
				(up-reset-filters)
				(up-filter-distance c: -1 c: 6)
				(up-filter-status c: status-resource c: list-active)
				(up-find-resource c: gold c: 1)
				(up-get-search-state g-local-total)
				)

				(defrule
				(up-compare-goal i g:< g-local-total)
				(up-compare-goal g-desired-num-gold-vils > 5)
				(dropsite-min-distance gold < 255)	;still gold on the map
				(or
					(building-type-count-total town-center >= 2)
					(unit-type-count villager >= 60))
				(up-compare-goal g-current-gold-camp-total < 3)
				(up-set-target-object search-local g: i)
				(up-compare-goal g-remote-total > 0)            ;gold mines found
				=>
				(up-modify-goal i c:+ 1)
				(up-modify-goal g-temp c:+ 1)
				(up-jump-rule -2)
				)

				(defrule
				(up-compare-goal i g:< g-local-total)
				(up-compare-goal g-desired-num-gold-vils > 5)
				(dropsite-min-distance gold < 255)	;still gold on the map
				(or
					(building-type-count-total town-center >= 2)
					(unit-type-count villager >= 60))
				(up-compare-goal g-current-gold-camp-total < 3)
				(up-set-target-object search-local g: i)
				(goal g-remote-total 0)            ;no gold found
				=>
				(up-modify-goal i c:+ 1)
				(up-jump-rule -3)
				)

		;Calculate number of mining camps needed
		;Formula = Num miners / 8 (rounded up), minus the number of TCs and mining camps close to gold
		(defrule
		(up-compare-goal g-desired-num-gold-vils > 5)
		(building-type-count town-center > 0)
		(dropsite-min-distance gold < 255)	;still gold on the map
		(or
			(building-type-count-total town-center >= 2)
			(unit-type-count villager >= 60))
		(up-compare-goal g-current-gold-camp-total < 3)
		(up-get-fact unit-type-count villager-gold g-temp-2)
		(up-modify-goal g-temp-2 c:z/ 8)
		(up-modify-goal g-temp-2 c:+ 1)
		(up-modify-goal g-temp-2 g:- g-temp)		;g-temp = number of TCs and mining camps close to gold
		(up-compare-goal g-temp-2 > 0)
		=>
		(up-modify-goal g-desired-num-gold-camp g:= g-current-gold-camp-total)
		(up-modify-goal g-desired-num-gold-camp c:+ 1)
		)

		(load "The General 3.1/Functions/Reset")

		;Request enough mining camps to ensure max 8 stone miners per mining camp or TC
		;First find how many TCs are close to stone, stored in g-temp
		(defrule
		(up-compare-goal g-desired-num-stone-vils > 3)
		(building-type-count town-center == 0)
		(dropsite-min-distance stone < 255)	;still gold on the map
		(unit-type-count villager >= 70)
		(up-compare-goal g-current-stone-camp-total < 3)
		=>
		(up-modify-goal g-temp g:= g-current-stone-camp-total)
		(up-jump-rule 4)
		)

			(defrule
			(up-compare-goal g-desired-num-stone-vils > 3)
			(dropsite-min-distance stone < 255)	;still stone on the map
			(or
				(building-type-count-total town-center >= 2)
				(unit-type-count villager >= 70))
			(up-compare-goal g-current-stone-camp-total < 2)
			=>
			(up-full-reset-search)
			(up-find-local c: town-center c: 20)
			(up-filter-status c: status-pending c: list-active)
			(up-find-status-local c: town-center-foundation c: 5)
			(up-get-search-state g-local-total)
			(set-goal i 0)
			(up-modify-goal g-temp g:= g-current-stone-camp-total)
			)

				(defrule
				(up-compare-goal i g:< g-local-total)
				(up-compare-goal g-desired-num-stone-vils > 3)
				(dropsite-min-distance stone < 255)	;still stone on the map
				(or
					(building-type-count-total town-center >= 2)
					(unit-type-count villager >= 70))
				(up-compare-goal g-current-stone-camp-total < 2)
				(up-set-target-object search-local g: i)
				=>
				(up-get-point position-object g-point-x)
				(up-set-target-point g-point-x)
				(up-reset-search 0 0 1 1)
				(up-reset-filters)
				(up-filter-distance c: -1 c: 6)
				(up-filter-status c: status-resource c: list-active)
				(up-find-resource c: stone c: 1)
				(up-get-search-state g-local-total)
				)

				(defrule
				(up-compare-goal i g:< g-local-total)
				(up-compare-goal g-desired-num-stone-vils > 3)
				(dropsite-min-distance stone < 255)	;still stone on the map
				(or
					(building-type-count-total town-center >= 2)
					(unit-type-count villager >= 70))
				(up-compare-goal g-current-stone-camp-total < 2)
				(up-set-target-object search-local g: i)
				(up-compare-goal g-remote-total > 0)            ;stone mines found
				=>
				(up-modify-goal i c:+ 1)
				(up-modify-goal g-temp c:+ 1)
				(up-jump-rule -2)
				)

				(defrule
				(up-compare-goal i g:< g-local-total)
				(up-compare-goal g-desired-num-stone-vils > 3)
				(dropsite-min-distance stone < 255)	;still stone on the map
				(or
					(building-type-count-total town-center >= 2)
					(unit-type-count villager >= 70))
				(up-compare-goal g-current-stone-camp-total < 2)
				(up-set-target-object search-local g: i)
				(goal g-remote-total 0)            ;no stone found
				=>
				(up-modify-goal i c:+ 1)
				(up-jump-rule -3)
				)

		;Calculate number of mining camps needed
		;Formula = Num stone miners / 8 (rounded up), minus the number of TCs and mining camps close to stone
		(defrule
		(up-compare-goal g-desired-num-stone-vils > 3)
		(building-type-count town-center > 0)
		(dropsite-min-distance stone < 255)	;still stone on the map
		(or
			(building-type-count-total town-center >= 2)
			(unit-type-count villager >= 70))
		(up-compare-goal g-current-stone-camp-total < 2)
		(up-get-fact unit-type-count villager-stone g-temp-2)
		(up-modify-goal g-temp-2 c:z/ 8)
		(up-modify-goal g-temp-2 c:+ 1)
		(up-modify-goal g-temp-2 g:- g-temp)		;g-temp = number of TCs and mining camps close to stone
		(up-compare-goal g-temp-2 > 0)
		=>
		(up-modify-goal g-desired-num-stone-camp g:= g-current-stone-camp-total)
		(up-modify-goal g-desired-num-stone-camp c:+ 1)
		)

		;Set g-desired-num-mining-camp
		(defrule
		(or
			(up-compare-goal g-desired-num-gold-camp > 0)
			(up-compare-goal g-desired-num-stone-camp > 0))
		=>
		(up-modify-goal g-desired-num-mining-camp g:= g-desired-num-gold-camp)
		(up-modify-goal g-desired-num-mining-camp g:+ g-desired-num-stone-camp)
		)

	;------------------
	;	Lumber Camps
	;------------------

		(load "The General 3.1/Functions/Reset")

		(defrule
		(game-time > 30)
		(up-gaia-type-count-total c: wood > 10)
		(or
			(and
				(unit-type-count villager-wood > 4)
				(dropsite-min-distance wood > 3))
			(up-compare-goal g-desired-num-wood-vils > 0))
		(building-type-count lumber-camp == 0)
		(up-pending-objects c: lumber-camp == 0)
		(up-compare-goal g-min-forest-distance > PENDING)
		(resource-found wood)
		=>
		(up-modify-goal g-desired-num-lumber-camp c:max 1)
		)

		(defrule
		(building-type-count lumber-camp == 1)
		(game-time > 300)
		(or
			(and
				(unit-type-count villager-wood > 6)
				(dropsite-min-distance wood > 3))
			(up-compare-goal g-desired-num-wood-vils > 5))
		(up-pending-objects c: lumber-camp == 0)
		(resource-found wood)
		=>
		(up-modify-goal g-desired-num-lumber-camp c:max 2)
		)

		(load "The General 3.1/Functions/Reset")

		;Request enough lumber camps to ensure max 8 lumberjacks per lumber camp or TC
		;First find how many TCs are close to wood, stored in g-temp
		(defrule
		(unit-type-count villager-wood > 16)
		(building-type-count town-center > 0)
		(dropsite-min-distance wood < 255)	;still wood on the map
		=>
		(up-full-reset-search)
		(up-find-local c: town-center c: 20)
		(up-filter-status c: status-pending c: list-active)
		(up-find-status-local c: town-center c: 5)
		(up-get-search-state g-local-total)
		(set-goal i 0)
		(set-goal g-temp 0)
		)

			(defrule
			(up-compare-goal i g:< g-local-total)
			(unit-type-count villager-wood > 16)
			(building-type-count town-center > 0)
			(dropsite-min-distance wood < 255)	;still wood on the map
			(up-set-target-object search-local g: i)
			=>
			(up-get-point position-object g-point-x)
			(up-set-target-point g-point-x)
			(up-reset-search 0 0 1 1)
			(up-reset-filters)
			(up-filter-distance c: -1 c: 6)
			(up-find-resource c: wood c: 6)
			(up-filter-status c: status-resource c: list-active)
			(up-find-resource c: wood c: 6)
			(up-get-search-state g-local-total)
			)

			(defrule
			(up-compare-goal i g:< g-local-total)
			(unit-type-count villager-wood > 16)
			(building-type-count town-center > 0)
			(dropsite-min-distance wood < 255)	;still wood on the map
			(up-set-target-object search-local g: i)
			(up-compare-goal g-remote-total >= 6)            ;6+ trees found
			=>
			(up-modify-goal i c:+ 1)
			(up-modify-goal g-temp c:+ 1)
			(up-jump-rule -2)
			)

			(defrule
			(up-compare-goal i g:< g-local-total)
			(unit-type-count villager-wood > 16)
			(building-type-count town-center > 0)
			(dropsite-min-distance wood < 255)	;still wood on the map
			(up-set-target-object search-local g: i)
			(up-compare-goal g-remote-total < 6)            ;6+ trees found
			=>
			(up-modify-goal i c:+ 1)
			(up-jump-rule -3)
			)

		;Calculate number of lumber camps needed
		;Formula = Num lumberjacks / 8 (rounded up), minus the number of TCs close to wood
		(defrule
		(unit-type-count villager-wood > 16)
		(building-type-count town-center > 0)
		(dropsite-min-distance wood < 255)	;still wood on the map
		=>
		(up-get-fact unit-type-count villager-wood g-temp-2)
		(up-modify-goal g-temp-2 c:z/ 8)
		(up-modify-goal g-temp-2 c:+ 1)
		(up-modify-goal g-temp-2 g:- g-temp)		;g-temp = number of TCs close to wood
		(up-modify-goal g-desired-num-lumber-camp g:max g-temp-2)
		)

		;No close lumber camps, request additional lumber camp
		(defrule
		(timer-triggered t-5-sec)
		(or
			(building-type-count lumber-camp > 1)
			(game-time > 600))
		(game-time > 300)
		(dropsite-min-distance wood > 3)
		(dropsite-min-distance wood < 255)	;still wood on the map
		(or
			(unit-type-count villager-wood > 0)
			(up-compare-goal g-desired-num-wood-vils > 0))
		(resource-found wood)
		=>
		(up-get-fact building-type-count-total lumber-camp g-temp)
		(up-modify-goal g-temp c:+ 1)
		(up-modify-goal g-desired-num-lumber-camp g:max g-temp)
		)

		(defrule
		(or
			(goal g-town-under-attack YES)
			(or
				(unit-type-count 228 > 0)	;dead male lumberjack
				(unit-type-count 219 > 0)))	;dead female lumberjack
		=>
		(up-modify-goal g-desired-num-lumber-camp c:+ 1)
		)

	;-----------
	;	Farms
	;-----------

		(load "The General 3.1/Functions/Reset")

		;Start with total food vils and reduce if we have shepherds, foragers, or hunters
		(defrule
		(building-available farm)
		=>
		(up-modify-goal g-desired-num-farm g:= g-desired-num-food-vils)
		)

		;Subtract needed farms if we have shepherds
		(defrule
		(up-compare-goal g-desired-num-farm > 0)
		(building-available farm)
		(or
			(up-set-target-by-id g: g-current-sheep-id)
			(unit-type-count villager-shepherd > 0))
		(up-compare-goal g-current-sheep-count > 0)
		=>
		(up-modify-goal g-desired-num-farm c:- 6)
		(up-modify-goal g-desired-num-farm c:max 0)
		)
		(defrule
		(up-compare-goal g-desired-num-farm > 0)
		(building-available farm)
		(or
			(up-set-target-by-id g: g-current-sheep-id)
			(unit-type-count villager-shepherd > 0))
		(up-compare-goal g-current-sheep-count <= 0)
		=>
		(up-modify-goal g-desired-num-farm c:- 2)
		(up-modify-goal g-desired-num-farm c:max 0)
		)

		;Subtract needed farms if we have forage bushes near mill or have foragers
		(load "The General 3.1/Functions/Reset")

		(defrule
		(up-compare-goal g-desired-num-farm > 0)
		(building-available farm)
		=>
		(up-full-reset-search)
		(up-find-local c: mill c: 1)
		)

		(defrule
		(up-compare-goal g-desired-num-farm > 0)
		(building-available farm)
		(up-set-target-object search-local c: 0)
		=>
		(up-get-point position-object g-point-x)
		(up-set-target-point g-point-x)
		(up-full-reset-search)
		(up-filter-distance c: -1 c: 4)
		(up-filter-status c: status-resource c: list-active)
		(up-find-resource c: forage-class c: 2)
		(up-get-search-state g-local-total)
		)

		(defrule
		(up-compare-goal g-desired-num-farm > 0)
		(building-available farm)
		(up-compare-goal g-remote-total > 1)
		=>
		(up-modify-goal g-desired-num-farm c:- 5)
		(up-modify-goal g-desired-num-farm c:max 0)
		)
		(defrule
		(up-compare-goal g-desired-num-farm > 0)
		(building-available farm)
		(up-compare-goal g-remote-total == 1)
		=>
		(up-modify-goal g-desired-num-farm c:- 2)
		(up-modify-goal g-desired-num-farm c:max 0)
		)

		;Subtract needed farms if we are gathering boar
		(defrule
		(up-compare-goal g-desired-num-farm > 0)
		(building-available farm)
		(up-set-target-by-id g: g-current-boar-id)
		(or
			(up-object-data object-data-carry >= 60)
			(up-compare-goal g-current-boar-count > 0))
		=>
		(up-modify-goal g-desired-num-farm c:- 6)
		(up-modify-goal g-desired-num-farm c:max 0)
		)
		(defrule
		(up-compare-goal g-desired-num-farm > 0)
		(building-available farm)
		(up-set-target-by-id g: g-current-boar-id)
		(up-object-data object-data-carry < 60)
		(up-compare-goal g-current-boar-count <= 0)
		=>
		(up-modify-goal g-desired-num-farm c:- 2)
		(up-modify-goal g-desired-num-farm c:max 0)
		)

		;Subtract needed farms if we are gathering deer
		(defrule
		(up-compare-goal g-desired-num-farm > 0)
		(building-available farm)
		(unit-type-count villager-hunter > 0)
		(or
			(up-set-target-by-id g: g-current-deer-id)
			(dropsite-min-distance deer-hunting < 4))
		(up-compare-goal g-total-decaying-hunt-food >= 30)
		=>
		(up-modify-goal g-desired-num-farm c:- 3)
		(up-modify-goal g-desired-num-farm c:max 0)
		)
		(defrule
		(up-compare-goal g-desired-num-farm > 0)
		(building-available farm)
		(unit-type-count villager-hunter > 0)
		(or
			(up-set-target-by-id g: g-current-deer-id)
			(dropsite-min-distance deer-hunting < 4))
		(up-compare-goal g-total-decaying-hunt-food < 30)
		=>
		(up-modify-goal g-desired-num-farm c:- 2)
		(up-modify-goal g-desired-num-farm c:max 0)
		)

		;cap dark age farms
		(defrule
		(or
			(up-compare-const MIN-SHEEP > 0)
			(or
				(up-compare-const FORAGE-PILE-1 > 0)
				(up-compare-const MIN-BOAR > 0)))
		(up-compare-goal g-age-status < TO-FEUDAL)
		=>
		(up-modify-goal g-desired-num-farm c:min 7)
		)

		(defrule
		(or
			(up-compare-const MIN-SHEEP > 0)
			(or
				(up-compare-const FORAGE-PILE-1 > 0)
				(up-compare-const MIN-BOAR > 0)))
		(goal g-age-status TO-FEUDAL)
		=>
		(up-modify-goal g-desired-num-farm c:min 11)
		)

		(load "The General 3.1/Functions/Reset")

		;Build more mills if we need a lot of farms
		;First, find how many farms our TCs and mills can support
		(defrule
		(up-object-type-count-total c: town-center g:>= g-desired-num-town-center)
		(unit-type-count villager >= 40)
		=>
		(up-full-reset-search)
		(up-find-local c: town-center c: 40)
		(up-filter-status c: status-pending c: list-active)
		(up-find-status-local c: town-center c: 3)
		(up-get-search-state g-local-total)
		(up-modify-goal g-temp g:= g-local-total)
		(up-modify-goal g-temp c:* 12)						;prefer max 12 farms per TC
		(up-full-reset-search)
		(up-find-local c: mill c: 40)
		(up-filter-status c: status-pending c: list-active)
		(up-find-status-local c: mill c: 3)
		(up-get-search-state g-local-total)
		(up-modify-goal g-temp-2 g:= g-local-total)
		(up-modify-goal g-temp-2 c:* 8)						;prefer max 8 farms per mill
		)

		(defrule
		(up-object-type-count-total c: town-center g:>= g-desired-num-town-center)
		(unit-type-count villager >= 40)
		(up-modify-goal g-temp g:+ g-temp-2)				;get total number of farms our TCs and mills can support
		(or	
			(up-compare-goal g-usable-farm-count-total g:> g-temp)
			(up-compare-goal g-desired-num-farm g:> g-temp))
		=>
		(up-get-fact unit-type-count-total mill g-temp-3)
		(up-modify-goal g-desired-num-mill g:max g-temp-3)
		(up-modify-goal g-desired-num-mill c:+ 1)
		)

	;-------------------
	;	Archery Range
	;-------------------

		(defrule
		(up-compare-goal g-age-status >= TO-FEUDAL)
		(or
			(goal g-primary-unit-class archery-class)
			(or
				(goal g-primary-unit-class cavalry-archer-class)
				(goal g-primary-unit-class archery-cannon-class)))
		(up-compare-goal g-primary-unit != my-unique-unit)
		=>
		(up-modify-goal g-desired-num-archery-range c:max 1)
		)

		(defrule
		(up-compare-goal g-age-status >= TO-FEUDAL)
		(or
			(up-compare-goal g-desired-num-archer > 0)
			(or
				(up-compare-goal g-desired-num-skirmisher > 0)
				(or
					(up-compare-goal g-desired-num-cavalry-archer > 0)
					(or
						(up-compare-goal g-desired-num-hand-cannoneer > 0)
						(or
							(up-compare-goal g-desired-num-slinger > 0)
							(up-compare-goal g-desired-num-genitour > 0))))))
		=>
		(up-modify-goal g-desired-num-archery-range c:max 1)
		)

		(defrule
		(current-age >= castle-age)
		(or
			(up-compare-goal g-age-status >= MID-CASTLE)
			(current-age-time >= 600))
		(civilian-population >= 50)
		=>
		(up-modify-goal g-desired-num-archery-range c:max 1)
		)

		(defrule
		(current-age >= feudal-age)
		(or
			(goal g-current-strategy FL-RANGER)
			(or
				(goal g-current-strategy FL-GUERILLA)
				(or
					(goal g-current-strategy FL-SKIRMMETA)
					(or
						(goal g-current-strategy MIDF-FULL-ARCHERS)
						(or
							(goal g-current-strategy MIDF-FULL-RANGED)
							(goal g-current-strategy MIDF-FULL-SKIRMS))))))
		=>
		(up-modify-goal g-desired-num-archery-range c:max 2)
		)

		(defrule
		(current-age == feudal-age)
		(goal g-current-strategy FC-STRONGBOW)
		(or
			(building-type-count-total blacksmith > 0)
			(or
				(building-type-count-total market > 0)
				(building-type-count-total stable > 0)))
		=>
		(up-modify-goal g-desired-num-archery-range c:max 2)
		)

		(defrule
		(up-compare-goal g-age-status >= TO-CASTLE)
		(goal g-current-strategy EARLYC-XBOWS)
		=>
		(up-modify-goal g-desired-num-archery-range c:max 2)
		)

		(defrule
		(up-compare-goal g-age-status >= TO-CASTLE)
		(or
			(goal g-current-strategy FC-STRONGBOW)
			(or
				(goal g-current-strategy MIDC-XBOWS)
				(or
					(goal g-current-strategy MIDC-CAV-ARCHERS)
					(goal g-current-strategy MIDC-RANGED))))
		(up-object-type-count-total c: town-center g:>= g-desired-num-town-center)
		(up-object-type-count-total c: siege-workshop g:>= g-desired-num-siege-workshop)
		(up-object-type-count-total c: monastery g:>= g-desired-num-monastery)
		=>
		(up-modify-goal g-desired-num-archery-range c:max 3)
		)

		(defrule
		(up-compare-goal g-age-status >= TO-IMPERIAL)
		(or
			(goal g-current-strategy FI-SNIPER)
			(or
				(goal g-current-strategy EARLYI-ARBINGER)
				(or
					(goal g-current-strategy EARLYI-HORDE)
					(or
						(goal g-current-strategy MIDI-ARBS)
						(or
							(goal g-current-strategy MIDI-CAV-ARCHERS)
							(goal g-current-strategy LATEI-TRASHBOWS))))))
		=>
		(up-modify-goal g-desired-num-archery-range c:max 4)
		)

		(defrule
		(current-age >= feudal-age)
		=>
		(up-modify-goal g-temp g:= g-desired-num-archer)
		(up-modify-goal g-temp g:+ g-desired-num-skirmisher)
		(up-modify-goal g-temp g:+ g-desired-num-cavalry-archer)
		(up-modify-goal g-temp g:+ g-desired-num-hand-cannoneer)
		(up-modify-goal g-temp g:+ g-desired-num-slinger)
		(up-modify-goal g-temp g:+ g-desired-num-genitour)
		)
		(defrule
		(current-age >= feudal-age)
		(goal g-primary-unit archer)
		=>
		(up-modify-goal g-temp g:+ g-desired-num-archer)
		)
		(defrule
		(current-age >= feudal-age)
		(goal g-primary-unit skirmisher)
		=>
		(up-modify-goal g-temp g:+ g-desired-num-skirmisher)
		)
		(defrule
		(current-age >= feudal-age)
		(goal g-primary-unit cavalry-archer)
		=>
		(up-modify-goal g-temp g:+ g-desired-num-cavalry-archer)
		)
		(defrule
		(current-age >= feudal-age)
		(goal g-primary-unit hand-cannoneer)
		=>
		(up-modify-goal g-temp g:+ g-desired-num-hand-cannoneer)
		)
		(defrule
		(current-age >= feudal-age)
		(goal g-primary-unit slinger)
		=>
		(up-modify-goal g-temp g:+ g-desired-num-slinger)
		)
		(defrule
		(current-age >= feudal-age)
		(goal g-primary-unit genitour)
		=>
		(up-modify-goal g-temp g:+ g-desired-num-genitour)
		)
		(defrule
		(current-age >= feudal-age)
		(goal g-support-unit archer)
		=>
		(up-modify-goal g-temp-2 g:= g-desired-num-archer)
		(up-modify-goal g-temp-2 c:/ 2)
		(up-modify-goal g-temp g:+ g-temp-2)
		)
		(defrule
		(current-age >= feudal-age)
		(goal g-support-unit skirmisher)
		=>
		(up-modify-goal g-temp-2 g:= g-desired-num-skirmisher)
		(up-modify-goal g-temp-2 c:/ 2)
		(up-modify-goal g-temp g:+ g-temp-2)
		)
		(defrule
		(current-age >= feudal-age)
		(goal g-support-unit cavalry-archer)
		=>
		(up-modify-goal g-temp-2 g:= g-desired-num-cavalry-archer)
		(up-modify-goal g-temp-2 c:/ 2)
		(up-modify-goal g-temp g:+ g-temp-2)
		)
		(defrule
		(current-age >= feudal-age)
		(goal g-support-unit condottiero)
		=>
		(up-modify-goal g-temp-2 g:= g-desired-num-condottiero)
		(up-modify-goal g-temp-2 c:/ 2)
		(up-modify-goal g-temp g:+ g-temp-2)
		)
		(defrule
		(current-age >= feudal-age)
		(goal g-support-unit slinger)
		=>
		(up-modify-goal g-temp-2 g:= g-desired-num-slinger)
		(up-modify-goal g-temp-2 c:/ 2)
		(up-modify-goal g-temp g:+ g-temp-2)
		)
		(defrule
		(current-age >= feudal-age)
		(goal g-support-unit genitour)
		=>
		(up-modify-goal g-temp-2 g:= g-desired-num-genitour)
		(up-modify-goal g-temp-2 c:/ 2)
		(up-modify-goal g-temp g:+ g-temp-2)
		)

		(defrule
		(current-age >= castle-age)
		(or
			(goal g-game-focus AGGRESSIVE)
			(goal g-game-focus DEFENSIVE))
		(up-compare-goal g-temp >= 20)
		=>
		(up-modify-goal g-desired-num-archery-range c:max 2)
		)

		(defrule
		(current-age >= castle-age)
		(goal g-game-focus BOOM)
		(up-compare-goal g-temp >= 20)
		(civilian-population > 60)
		=>
		(up-modify-goal g-desired-num-archery-range c:max 2)
		)

		(defrule
		(current-age >= castle-age)
		(or
			(goal g-game-focus AGGRESSIVE)
			(goal g-game-focus DEFENSIVE))
		(up-compare-goal g-temp >= 40)
		(up-object-type-count-total c: town-center g:>= g-desired-num-town-center)
		(up-object-type-count-total c: siege-workshop g:>= g-desired-num-siege-workshop)
		(up-compare-goal g-attack-status >= FIRST-CASTLE-ATTACK)
		=>
		(up-modify-goal g-desired-num-archery-range c:max 3)
		)

		(defrule
		(current-age >= castle-age)
		(goal g-game-focus BOOM)
		(up-compare-goal g-temp >= 40)
		(civilian-population > 60)
		(up-object-type-count-total c: town-center g:>= g-desired-num-town-center)
		(up-object-type-count-total c: siege-workshop g:>= g-desired-num-siege-workshop)
		(up-compare-goal g-attack-status >= FIRST-CASTLE-ATTACK)
		=>
		(up-modify-goal g-desired-num-archery-range c:max 3)
		)

		(defrule
		(current-age >= castle-age)
		(or
			(goal g-game-focus AGGRESSIVE)
			(goal g-game-focus DEFENSIVE))
		(up-compare-goal g-temp >= 80)
		(civilian-population > 60)
		(up-object-type-count-total c: town-center g:>= g-desired-num-town-center)
		(up-object-type-count-total c: siege-workshop g:>= g-desired-num-siege-workshop)
		(up-compare-goal g-attack-status >= FIRST-CASTLE-ATTACK)
		=>
		(up-modify-goal g-desired-num-archery-range c:max 4)
		)

		(defrule
		(current-age == imperial-age)
		(up-compare-goal g-temp >= 20)
		(up-object-type-count-total c: town-center g:>= g-desired-num-town-center)
		(up-object-type-count-total c: siege-workshop g:>= g-desired-num-siege-workshop)
		=>
		(up-modify-goal g-desired-num-archery-range c:max 3)
		)

		(defrule
		(current-age == imperial-age)
		(up-compare-goal g-temp >= 40)
		(civilian-population > 80)
		(up-object-type-count-total c: town-center g:>= g-desired-num-town-center)
		(up-object-type-count-total c: siege-workshop g:>= g-desired-num-siege-workshop)
		=>
		(up-modify-goal g-desired-num-archery-range c:max 5)
		)

		(defrule
		(current-age == imperial-age)
		(up-compare-goal g-temp >= 60)
		(civilian-population > 100)
		(up-object-type-count-total c: town-center g:>= g-desired-num-town-center)
		(up-object-type-count-total c: siege-workshop g:>= g-desired-num-siege-workshop)
		=>
		(up-modify-goal g-desired-num-archery-range c:max 6)
		)

		(defrule
		(current-age == imperial-age)
		(up-compare-goal g-temp >= 80)
		(civilian-population > 120)
		(up-object-type-count-total c: town-center g:>= g-desired-num-town-center)
		(up-object-type-count-total c: siege-workshop g:>= g-desired-num-siege-workshop)
		=>
		(up-modify-goal g-desired-num-archery-range c:max 8)
		)

	;------------
	;	Stable
	;------------

		(defrule
		(up-compare-goal g-age-status >= TO-FEUDAL)
		(goal g-primary-unit-class cavalry-class)
		(up-compare-goal g-primary-unit != my-unique-unit)
		=>
		(up-modify-goal g-desired-num-stable c:max 1)
		)

		(defrule
		(up-compare-goal g-age-status >= TO-FEUDAL)
		(or
			(up-compare-goal g-desired-num-scout-cavalry > 0)
			(or
				(up-compare-goal g-desired-num-knight > 0)
				(or
					(up-compare-goal g-desired-num-camel > 0)
					(or
						(up-compare-goal g-desired-num-battle-elephant > 0)
						(up-compare-goal g-desired-num-steppe-lancer > 0)))))
		=>
		(up-modify-goal g-desired-num-stable c:max 1)
		)

		#load-if-defined HUN-CIV

			(defrule
			(research-completed my-second-unique-research)
			(up-compare-goal g-desired-num-unique-unit > 0)
			=>
			(up-modify-goal g-desired-num-stable c:max 1)
			)

		#end-if

		(defrule
		(current-age >= castle-age)
		(or
			(up-compare-goal g-age-status >= MID-CASTLE)
			(current-age-time >= 600))
		(civilian-population >= 50)
		(up-compare-const SCOUT-CAVALRY-AVAILABLE == YES)
		=>
		(up-modify-goal g-desired-num-stable c:max 1)
		)

		(defrule
		(current-age == feudal-age)
		(goal g-current-strategy MIDF-FULL-SCOUTS)
		(unit-type-count villager >= 32)
		=>
		(up-modify-goal g-desired-num-stable c:max 2)
		)

		(defrule
		(up-compare-goal g-age-status >= TO-CASTLE)
		(or
			(goal g-current-strategy FC-KRUSH)
			(or
				(goal g-current-strategy FC-CRUSH)
				(or
					(goal g-current-strategy EARLYC-KNIGHTS)
					(or
						(goal g-current-strategy EARLYC-CAMELS)
						(or
							(goal g-current-strategy MIDC-KNIGHTS)
							(or
								(goal g-current-strategy MIDC-CAMELS)
								(goal g-current-strategy MIDC-CIRCUS)))))))
		=>
		(up-modify-goal g-desired-num-stable c:max 2)
		)

		(defrule
		(current-age >= feudal-age)
		=>
		(up-modify-goal g-temp g:= g-desired-num-scout-cavalry)
		(up-modify-goal g-temp g:+ g-desired-num-knight)
		(up-modify-goal g-temp g:+ g-desired-num-camel)
		(up-modify-goal g-temp g:+ g-desired-num-battle-elephant)
		(up-modify-goal g-temp g:+ g-desired-num-steppe-lancer)
		)
		#load-if-defined HUN-CIV
			(defrule
			(research-completed my-second-unique-research)
			=>
			(up-modify-goal g-temp g:+ g-desired-num-unique-unit)
			)
		#end-if
		(defrule
		(current-age >= feudal-age)
		(goal g-primary-unit scout-cavalry)
		=>
		(up-modify-goal g-temp g:+ g-desired-num-scout-cavalry)
		)
		(defrule
		(current-age >= feudal-age)
		(goal g-primary-unit knight)
		=>
		(up-modify-goal g-temp g:+ g-desired-num-knight)
		)
		(defrule
		(current-age >= feudal-age)
		(goal g-primary-unit camel)
		=>
		(up-modify-goal g-temp g:+ g-desired-num-camel)
		)
		(defrule
		(current-age >= feudal-age)
		(goal g-primary-unit battle-elephant)
		=>
		(up-modify-goal g-temp g:+ g-desired-num-battle-elephant)
		)
		(defrule
		(current-age >= feudal-age)
		(goal g-primary-unit steppe-lancer)
		=>
		(up-modify-goal g-temp g:+ g-desired-num-steppe-lancer)
		)
		#load-if-defined HUN-CIV
			(defrule
			(research-completed my-second-unique-research)
			(goal g-primary-unit my-unique-unit)
			=>
			(up-modify-goal g-temp g:+ g-desired-num-unique-unit)
			)
		#end-if
		(defrule
		(current-age >= feudal-age)
		(goal g-support-unit scout-cavalry)
		=>
		(up-modify-goal g-temp-2 g:= g-desired-num-scout-cavalry)
		(up-modify-goal g-temp-2 c:/ 2)
		(up-modify-goal g-temp g:+ g-temp-2)
		)
		(defrule
		(current-age >= feudal-age)
		(goal g-support-unit knight)
		=>
		(up-modify-goal g-temp-2 g:= g-desired-num-knight)
		(up-modify-goal g-temp-2 c:/ 2)
		(up-modify-goal g-temp g:+ g-temp-2)
		)
		(defrule
		(current-age >= feudal-age)
		(goal g-support-unit camel)
		=>
		(up-modify-goal g-temp-2 g:= g-desired-num-camel)
		(up-modify-goal g-temp-2 c:/ 2)
		(up-modify-goal g-temp g:+ g-temp-2)
		)
		(defrule
		(current-age >= feudal-age)
		(goal g-support-unit battle-elephant)
		=>
		(up-modify-goal g-temp-2 g:= g-desired-num-battle-elephant)
		(up-modify-goal g-temp-2 c:/ 2)
		(up-modify-goal g-temp g:+ g-temp-2)
		)
		(defrule
		(current-age >= feudal-age)
		(goal g-support-unit steppe-lancer)
		=>
		(up-modify-goal g-temp-2 g:= g-desired-num-steppe-lancer)
		(up-modify-goal g-temp-2 c:/ 2)
		(up-modify-goal g-temp g:+ g-temp-2)
		)
		#load-if-defined HUN-CIV
			(defrule
			(research-completed my-second-unique-research)
			(goal g-support-unit my-unique-unit)
			=>
			(up-modify-goal g-temp-2 g:= g-desired-num-unique-unit)
			(up-modify-goal g-temp-2 c:/ 2)
			(up-modify-goal g-temp g:+ g-temp-2)
			)
		#end-if

		(defrule
		(current-age >= castle-age)
		(or
			(goal g-game-focus AGGRESSIVE)
			(goal g-game-focus DEFENSIVE))
		(up-compare-goal g-temp >= 25)
		=>
		(up-modify-goal g-desired-num-stable c:max 2)
		)

		(defrule
		(current-age >= castle-age)
		(goal g-game-focus BOOM)
		(up-compare-goal g-temp >= 25)
		(civilian-population > 60)
		=>
		(up-modify-goal g-desired-num-stable c:max 2)
		)

		(defrule
		(current-age >= castle-age)
		(or
			(goal g-game-focus AGGRESSIVE)
			(goal g-game-focus DEFENSIVE))
		(up-compare-goal g-temp >= 40)
		(up-object-type-count-total c: town-center g:>= g-desired-num-town-center)
		(up-object-type-count-total c: siege-workshop g:>= g-desired-num-siege-workshop)
		(up-compare-goal g-attack-status >= FIRST-CASTLE-ATTACK)
		=>
		(up-modify-goal g-desired-num-stable c:max 3)
		)

		(defrule
		(current-age >= castle-age)
		(goal g-game-focus BOOM)
		(up-compare-goal g-temp >= 40)
		(civilian-population > 60)
		(up-object-type-count-total c: town-center g:>= g-desired-num-town-center)
		(up-object-type-count-total c: siege-workshop g:>= g-desired-num-siege-workshop)
		(up-compare-goal g-attack-status >= FIRST-CASTLE-ATTACK)
		=>
		(up-modify-goal g-desired-num-stable c:max 3)
		)

		(defrule
		(current-age >= castle-age)
		(or
			(goal g-game-focus AGGRESSIVE)
			(goal g-game-focus DEFENSIVE))
		(up-compare-goal g-temp >= 80)
		(civilian-population > 60)
		(up-object-type-count-total c: town-center g:>= g-desired-num-town-center)
		(up-object-type-count-total c: siege-workshop g:>= g-desired-num-siege-workshop)
		(up-compare-goal g-attack-status >= FIRST-CASTLE-ATTACK)
		=>
		(up-modify-goal g-desired-num-stable c:max 4)
		)

		(defrule
		(current-age == imperial-age)
		(up-compare-goal g-temp >= 20)
		(up-object-type-count-total c: town-center g:>= g-desired-num-town-center)
		(up-object-type-count-total c: siege-workshop g:>= g-desired-num-siege-workshop)
		=>
		(up-modify-goal g-desired-num-stable c:max 3)
		)

		(defrule
		(current-age == imperial-age)
		(up-compare-goal g-temp >= 40)
		(civilian-population > 80)
		(up-object-type-count-total c: town-center g:>= g-desired-num-town-center)
		(up-object-type-count-total c: siege-workshop g:>= g-desired-num-siege-workshop)
		=>
		(up-modify-goal g-desired-num-stable c:max 5)
		)

		(defrule
		(current-age == imperial-age)
		(up-compare-goal g-temp >= 60)
		(civilian-population > 100)
		(up-object-type-count-total c: town-center g:>= g-desired-num-town-center)
		(up-object-type-count-total c: siege-workshop g:>= g-desired-num-siege-workshop)
		=>
		(up-modify-goal g-desired-num-stable c:max 6)
		)

		(defrule
		(current-age == imperial-age)
		(up-compare-goal g-temp >= 80)
		(civilian-population > 120)
		(up-object-type-count-total c: town-center g:>= g-desired-num-town-center)
		(up-object-type-count-total c: siege-workshop g:>= g-desired-num-siege-workshop)
		=>
		(up-modify-goal g-desired-num-stable c:max 8)
		)

	;--------------
	;	Barracks
	;--------------

		(defrule
		(up-compare-goal g-age-status >= TO-FEUDAL)
		(or
			(building-type-count-total mining-camp > 0)
			(and
				(up-compare-goal g-desired-num-gold-vils == 0)
				(up-compare-goal g-desired-num-stone-vils == 0)))
		(up-compare-goal g-current-strategy < 1201)		;not doing a boom imp strategy
		(up-compare-goal g-desired-num-barracks < 1)
		=>
		(up-modify-goal g-desired-num-barracks c:max 1)
		)
	
		(defrule
		(up-compare-goal g-age-status >= TO-FEUDAL)
		(or
			(up-compare-goal g-desired-num-stable > 0)
			(up-compare-goal g-desired-num-archery-range > 0))
		(up-compare-goal g-desired-num-barracks < 1)
		=>
		(up-modify-goal g-desired-num-barracks c:max 1)
		)

		(defrule
		(up-compare-goal g-age-status >= TO-FEUDAL)
		(goal g-primary-unit-class infantry-class)
		(up-compare-goal g-primary-unit != my-unique-unit)
		=>
		(up-modify-goal g-desired-num-barracks c:max 1)
		)

		(defrule
		(current-age >= feudal-age)
		(or
			(up-compare-goal g-desired-num-militia > 0)
			(or
				(up-compare-goal g-desired-num-spearman > 0)
				(or
					(up-compare-goal g-desired-num-eagle-warrior > 0)
					(up-compare-goal g-desired-num-condottiero > 0))))
		=>
		(up-modify-goal g-desired-num-barracks c:max 1)
		)

		#load-if-defined GOTHIC-CIV

			(defrule
			(research-completed my-unique-research)	;Anarchy
			(up-compare-goal g-desired-num-unique-unit > 0)
			=>
			(up-modify-goal g-desired-num-barracks c:max 1)
			)
			
		#end-if

		(defrule
		(current-age == feudal-age)
		(or
			(goal g-current-strategy MIDF-FULL-MAA)
			(goal g-current-strategy MIDF-FULL-EAGLES))
		=>
		(up-modify-goal g-desired-num-barracks c:max 2)
		)

		(defrule
		(up-compare-goal g-age-status >= TO-CASTLE)
		(goal g-current-strategy EARLYC-EAGLES)
		=>
		(up-modify-goal g-desired-num-barracks c:max 2)
		)

		(defrule
		(up-compare-goal g-age-status >= TO-CASTLE)
		(or
			(goal g-current-strategy FC-EAGLES-REVENGE)
			(goal g-current-strategy MIDC-EAGLES))
		=>
		(up-modify-goal g-desired-num-barracks c:max 3)
		)

		(defrule
		(current-age >= feudal-age)
		=>
		(up-modify-goal g-temp g:= g-desired-num-militia)
		(up-modify-goal g-temp g:+ g-desired-num-spearman)
		(up-modify-goal g-temp g:+ g-desired-num-eagle-warrior)
		(up-modify-goal g-temp g:+ g-desired-num-condottiero)
		)
		#load-if-defined GOTHIC-CIV
			(defrule
			(research-completed my-unique-research)	;Anarchy
			=>
			(up-modify-goal g-temp g:+ g-desired-num-unique-unit)
			)
		#end-if
		(defrule
		(current-age >= feudal-age)
		(goal g-primary-unit militiaman)
		=>
		(up-modify-goal g-temp g:+ g-desired-num-militia)
		)
		(defrule
		(current-age >= feudal-age)
		(goal g-primary-unit spearman)
		=>
		(up-modify-goal g-temp g:+ g-desired-num-spearman)
		)
		(defrule
		(current-age >= feudal-age)
		(goal g-primary-unit eagle-warrior)
		=>
		(up-modify-goal g-temp g:+ g-desired-num-eagle-warrior)
		)
		(defrule
		(current-age >= feudal-age)
		(goal g-primary-unit condottiero)
		=>
		(up-modify-goal g-temp g:+ g-desired-num-condottiero)
		)
		#load-if-defined GOTHIC-CIV
			(defrule
			(research-completed my-unique-research)	;Anarchy
			(goal g-primary-unit my-unique-unit)
			=>
			(up-modify-goal g-temp g:+ g-desired-num-unique-unit)
			)
		#end-if
		(defrule
		(current-age >= feudal-age)
		(goal g-support-unit militiaman)
		=>
		(up-modify-goal g-temp-2 g:= g-desired-num-militia)
		(up-modify-goal g-temp-2 c:/ 2)
		(up-modify-goal g-temp g:+ g-temp-2)
		)
		(defrule
		(current-age >= feudal-age)
		(goal g-support-unit spearman)
		=>
		(up-modify-goal g-temp-2 g:= g-desired-num-spearman)
		(up-modify-goal g-temp-2 c:/ 2)
		(up-modify-goal g-temp g:+ g-temp-2)
		)
		(defrule
		(current-age >= feudal-age)
		(goal g-support-unit eagle-warrior)
		=>
		(up-modify-goal g-temp-2 g:= g-desired-num-eagle-warrior)
		(up-modify-goal g-temp-2 c:/ 2)
		(up-modify-goal g-temp g:+ g-temp-2)
		)
		(defrule
		(current-age >= feudal-age)
		(goal g-support-unit condottiero)
		=>
		(up-modify-goal g-temp-2 g:= g-desired-num-condottiero)
		(up-modify-goal g-temp-2 c:/ 2)
		(up-modify-goal g-temp g:+ g-temp-2)
		)
		#load-if-defined GOTHIC-CIV
			(defrule
			(research-completed my-unique-research)	;Anarchy
			(goal g-support-unit my-unique-unit)
			=>
			(up-modify-goal g-temp-2 g:= g-desired-num-unique-unit)
			(up-modify-goal g-temp-2 c:/ 2)
			(up-modify-goal g-temp g:+ g-temp-2)
			)
		#end-if

		(defrule
		(current-age >= castle-age)
		(or
			(goal g-game-focus AGGRESSIVE)
			(goal g-game-focus DEFENSIVE))
		(up-compare-goal g-temp >= 20)
		=>
		(up-modify-goal g-desired-num-barracks c:max 2)
		)

		(defrule
		(current-age >= castle-age)
		(goal g-game-focus BOOM)
		(up-compare-goal g-temp >= 20)
		(civilian-population > 60)
		=>
		(up-modify-goal g-desired-num-barracks c:max 2)
		)

		(defrule
		(current-age >= castle-age)
		(or
			(goal g-game-focus AGGRESSIVE)
			(goal g-game-focus DEFENSIVE))
		(up-compare-goal g-temp >= 40)
		(up-object-type-count-total c: town-center g:>= g-desired-num-town-center)
		(up-object-type-count-total c: siege-workshop g:>= g-desired-num-siege-workshop)
		(up-compare-goal g-attack-status >= FIRST-CASTLE-ATTACK)
		=>
		(up-modify-goal g-desired-num-barracks c:max 3)
		)

		(defrule
		(current-age >= castle-age)
		(goal g-game-focus BOOM)
		(up-compare-goal g-temp >= 40)
		(civilian-population > 60)
		(up-object-type-count-total c: town-center g:>= g-desired-num-town-center)
		(up-object-type-count-total c: siege-workshop g:>= g-desired-num-siege-workshop)
		(up-compare-goal g-attack-status >= FIRST-CASTLE-ATTACK)
		=>
		(up-modify-goal g-desired-num-barracks c:max 3)
		)

		(defrule
		(current-age >= castle-age)
		(or
			(goal g-game-focus AGGRESSIVE)
			(goal g-game-focus DEFENSIVE))
		(up-compare-goal g-temp >= 80)
		(civilian-population > 60)
		(up-object-type-count-total c: town-center g:>= g-desired-num-town-center)
		(up-object-type-count-total c: siege-workshop g:>= g-desired-num-siege-workshop)
		(up-compare-goal g-attack-status >= FIRST-CASTLE-ATTACK)
		=>
		(up-modify-goal g-desired-num-barracks c:max 4)
		)

		(defrule
		(current-age == imperial-age)
		(up-compare-goal g-temp >= 20)
		(up-object-type-count-total c: town-center g:>= g-desired-num-town-center)
		(up-object-type-count-total c: siege-workshop g:>= g-desired-num-siege-workshop)
		=>
		(up-modify-goal g-desired-num-barracks c:max 3)
		)

		(defrule
		(current-age == imperial-age)
		(up-compare-goal g-temp >= 40)
		(civilian-population > 80)
		(up-object-type-count-total c: town-center g:>= g-desired-num-town-center)
		(up-object-type-count-total c: siege-workshop g:>= g-desired-num-siege-workshop)
		=>
		(up-modify-goal g-desired-num-barracks c:max 5)
		)

		(defrule
		(current-age == imperial-age)
		(up-compare-goal g-temp >= 60)
		(civilian-population > 100)
		(up-object-type-count-total c: town-center g:>= g-desired-num-town-center)
		(up-object-type-count-total c: siege-workshop g:>= g-desired-num-siege-workshop)
		=>
		(up-modify-goal g-desired-num-barracks c:max 6)
		)

		(defrule
		(current-age == imperial-age)
		(up-compare-goal g-temp >= 80)
		(civilian-population > 120)
		(up-object-type-count-total c: town-center g:>= g-desired-num-town-center)
		(up-object-type-count-total c: siege-workshop g:>= g-desired-num-siege-workshop)
		=>
		(up-modify-goal g-desired-num-barracks c:max 8)
		)

		#load-if-defined GOTHIC-CIV

			(defrule
			(research-completed my-second-unique-research)	;Perfusion
			=>
			(up-modify-goal g-desired-num-barracks c:%* 150)
			)

		#end-if

	;--------------------
	;	Siege Workshop
	;--------------------

		(defrule
		(or
			(goal g-game-focus BALANCED)
			(goal g-game-focus BOOM))
		(up-compare-goal g-age-status >= TO-CASTLE)
		(up-object-type-count-total c: town-center g:>= g-desired-num-town-center)
		(building-type-count-total town-center >= 3)
		(or
			(up-compare-goal g-desired-num-battering-ram > 0)
			(or
				(up-compare-goal g-desired-num-mangonel > 0)
				(or
					(up-compare-goal g-desired-num-scorpion > 0)
					(up-compare-goal g-desired-num-bombard-cannon > 0))))
		=>
		(up-modify-goal g-desired-num-siege-workshop c:max 1)
		)

		(defrule
		(goal g-game-focus AGGRESSIVE)
		(up-compare-goal g-age-status >= TO-CASTLE)
		(or
			(up-compare-goal g-desired-num-battering-ram > 0)
			(or
				(up-compare-goal g-desired-num-mangonel > 0)
				(or
					(up-compare-goal g-desired-num-scorpion > 0)
					(up-compare-goal g-desired-num-bombard-cannon > 0))))
		(or
			(up-compare-goal g-target-military-parity >= 10)
			(goal g-attacking YES))
		(building-type-count-total town-center >= 2)
		=>
		(up-modify-goal g-desired-num-siege-workshop c:max 1)
		)

		(defrule
		(goal g-game-focus DEFENSIVE)
		(up-compare-goal g-age-status >= TO-CASTLE)
		(or
			(up-compare-goal g-desired-num-battering-ram > 0)
			(or
				(up-compare-goal g-desired-num-mangonel > 0)
				(or
					(up-compare-goal g-desired-num-scorpion > 0)
					(up-compare-goal g-desired-num-bombard-cannon > 0))))
		=>
		(up-modify-goal g-desired-num-siege-workshop c:max 1)
		)

		(defrule
		(current-age >= castle-age)
		=>
		(up-modify-goal g-temp g:= g-desired-num-battering-ram)
		(up-modify-goal g-temp g:+ g-desired-num-mangonel)
		(up-modify-goal g-temp g:+ g-desired-num-scorpion)
		(up-modify-goal g-temp g:+ g-desired-num-bombard-cannon)
		)

		(defrule
		(up-compare-goal g-game-focus != REBUILD)
		(current-age >= castle-age)
		(up-object-type-count-total c: town-center g:>= g-desired-num-town-center)
		(building-type-count-total town-center >= 3)
		(civilian-population >= 80)
		(up-compare-goal g-temp >= 5)
		(up-compare-goal g-desired-num-siege-workshop < 2)
		=>
		(up-modify-goal g-desired-num-siege-workshop c:max 2)
		)

		(defrule
		(up-compare-goal g-game-focus != REBUILD)
		(up-object-type-count-total c: town-center g:>= g-desired-num-town-center)
		(building-type-count-total town-center >= 3)
		(civilian-population >= 100)
		(up-compare-goal g-temp >= 9)
		(up-compare-goal g-desired-num-siege-workshop < 3)
		=>
		(up-modify-goal g-desired-num-siege-workshop c:max 3)
		)

	;----------------
	;	Blacksmith
	;----------------

		(defrule
		(up-compare-goal g-game-focus != REBUILD)
		(or
			(up-compare-goal g-game-focus != DEFENSIVE)
			(military-population >= 10))
		(up-compare-goal g-age-status >= TO-FEUDAL)
		=>
		(up-modify-goal g-desired-num-blacksmith c:max 1)
		)

		(defrule
		(up-compare-goal g-game-focus != REBUILD)
		(goal g-age-status SAVE-FOR-CASTLE)
		(up-compare-goal g-desired-num-blacksmith == 0)
		(up-compare-goal g-desired-num-market == 0)
		(or
			(up-compare-goal g-desired-num-archery-range == 0)
			(up-compare-goal g-desired-num-stable == 0))
		(building-type-count-total blacksmith == 0)
		(building-type-count-total market == 0)
		(or	
			(building-type-count-total archery-range == 0)
			(building-type-count-total stable == 0))
		=>
		(up-modify-goal g-desired-num-blacksmith c:max 1)
		)

		(defrule
		(goal g-game-focus REBUILD)
		(up-compare-goal g-age-status >= TO-FEUDAL)
		(current-age < castle-age)
		(up-object-type-count-total c: town-center > 0)
		(up-object-type-count-total c: lumber-camp g:>= g-desired-num-lumber-camp)
		(up-object-type-count-total c: dock g:>= g-desired-num-dock)
		(up-object-type-count-total c: mining-camp g:>= g-desired-num-mining-camp)
		(up-object-type-count-total c: market g:>= g-desired-num-market)
		(up-object-type-count-total c: barracks g:>= g-desired-num-barracks)
		(up-object-type-count-total c: archery-range g:>= g-desired-num-archery-range)
		(up-object-type-count-total c: stable g:>= g-desired-num-stable)
		=>
		(up-modify-goal g-desired-num-blacksmith c:max 1)
		)

		(defrule
		(goal g-game-focus REBUILD)
		(current-age >= castle-age)
		(up-object-type-count-total c: town-center >= 2)
		(up-object-type-count-total c: town-center g:>= g-desired-num-town-center)
		(up-object-type-count-total c: lumber-camp g:>= g-desired-num-lumber-camp)
		(up-object-type-count-total c: dock g:>= g-desired-num-dock)
		(up-object-type-count-total c: mining-camp g:>= g-desired-num-mining-camp)
		(up-object-type-count-total c: market g:>= g-desired-num-market)
		(up-object-type-count-total c: barracks g:>= g-desired-num-barracks)
		(up-object-type-count-total c: archery-range g:>= g-desired-num-archery-range)
		(up-object-type-count-total c: stable g:>= g-desired-num-stable)
		(up-compare-goal g-age-status >= TO-FEUDAL)
		=>
		(up-modify-goal g-desired-num-blacksmith c:max 1)
		)

		(defrule
		(up-compare-goal g-game-focus != REBUILD)
		(goal g-desired-num-blacksmith 0)
		(up-compare-goal g-desired-num-siege-workshop > 0)
		(up-compare-goal g-desired-num-blacksmith < 1)
		=>
		(up-modify-goal g-desired-num-blacksmith c:max 1)
		)

	;------------
	;	Market
	;------------

		(defrule
		(goal g-current-strategy FC-EAGLES-REVENGE)
		(up-compare-goal g-age-status >= TO-FEUDAL)
		=>
		(up-modify-goal g-desired-num-market c:max 1)
		)

		#load-if-defined BRITON-CIV

			(defrule
			(up-compare-goal g-age-status >= MID-CASTLE)
			(or
				(building-type-count-total town-center >= 3)
				(or
					(wood-amount >= 200)
					(or
						(up-compare-goal g-desired-num-trade-cart > 0)
						(goal g-town-under-attack YES))))
			=>
			(up-modify-goal g-desired-num-market c:max 1)
			)

		#else
			#load-if-defined MALIAN-CIV

				(defrule
				(up-compare-goal g-age-status >= MID-CASTLE)
				(or
					(building-type-count-total town-center >= 3)
					(or
						(wood-amount >= 250)
						(or
							(up-compare-goal g-desired-num-trade-cart > 0)
							(goal g-town-under-attack YES))))
				=>
				(up-modify-goal g-desired-num-market c:max 1)
				)

			#else

				(defrule
				(up-compare-goal g-age-status >= MID-CASTLE)
				(or
					(building-type-count-total town-center >= 3)
					(or
						(wood-amount >= 300)
						(or
							(up-compare-goal g-desired-num-trade-cart > 0)
							(goal g-town-under-attack YES))))
				=>
				(up-modify-goal g-desired-num-market c:max 1)
				)

			#end-if
		#end-if

		(defrule
		(wood-amount >= 450)
		(current-age >= castle-age)
		(building-type-count-total market == 0)
		=>
		(up-modify-goal g-desired-num-market c:max 1)
		)

		(defrule
		(goal g-age-status SAVE-FOR-CASTLE)
		(up-compare-goal g-desired-num-market == 0)
		(up-compare-goal g-desired-num-archery-range == 0)
		(up-compare-goal g-desired-num-stable == 0)
		(building-type-count-total market == 0)
		(building-type-count-total archery-range == 0)
		(building-type-count-total stable == 0)
		=>
		(up-modify-goal g-desired-num-market c:max 1)
		)

		(defrule
		(goal g-age-status SAVE-FOR-CASTLE)
		(or
			(wood-amount > 550)
			(or
				(food-amount > 1000)
				(gold-amount > 500)))
		(building-type-count-total market == 0)
		=>
		(up-modify-goal g-desired-num-market c:max 1)
		)

		(defrule
		(goal g-age-status SAVE-FOR-IMPERIAL)
		(or
			(wood-amount > 550)
			(or
				(food-amount > 1200)
				(gold-amount > 1000)))
		(building-type-count-total market == 0)
		=>
		(up-modify-goal g-desired-num-market c:max 1)
		)

		(defrule
		(goal g-game-focus REBUILD)
		(up-object-type-count-total c: town-center g:>= g-desired-num-town-center)
		=>
		(up-modify-goal g-desired-num-market c:max 1)
		)

		(defrule
		(or
			(up-compare-goal g-age-status >= LATE-IMPERIAL)
			(up-compare-goal g-desired-num-trade-cart > 0))
		=>
		(up-modify-goal g-desired-num-market c:max 3)
		)

	;-----------------------------------
	;	GOAL 126: g-desired-num-monks
	;-----------------------------------

		;Get number of monks and missionaries wanted
		;Also, figure out if we have enough wood to build a monastery and a TC (if a TC is needed)
		(defrule
		(true)
		=>
		(up-modify-goal g-desired-num-monks g:= g-desired-num-monk)
		(up-modify-goal g-desired-num-monks g:+ g-desired-num-missionary)
		(up-get-fact building-type-count-total town-center g-temp)
		(up-modify-goal g-temp-2 g:= g-desired-num-town-center)
		(up-modify-goal g-temp-2 c:max 3)
		(up-modify-goal g-temp g:- g-temp-2)
		(up-modify-goal g-temp c:* -1)
		(up-setup-cost-data 1 g-food-cost)
		(up-add-object-cost c: town-center-foundation g: g-temp)
		(up-get-cost-delta g-food-delta)
		(up-reset-cost-data g-food-cost)
		(up-add-object-cost c: monastery c: 1)
		(up-modify-goal g-wood-delta g:- g-wood-cost)
		)

	;---------------
	;	Monastery
	;---------------

		(defrule
		(up-compare-goal g-age-status >= TO-CASTLE)
		(up-compare-goal g-desired-num-monks > 0)
		(goal g-town-under-attack NO)
		(building-type-count-total town-center >= 3)
		(or
			(up-compare-goal g-wood-delta >= 0)
			(up-object-type-count-total c: town-center g:>= g-desired-num-town-center))
		(up-object-type-count-total c: barracks g:>= g-desired-num-barracks)
		(up-object-type-count-total c: archery-range g:>= g-desired-num-archery-range)
		(up-object-type-count-total c: stable g:>= g-desired-num-stable)
		(up-object-type-count-total c: siege-workshop g:>= g-desired-num-siege-workshop)
		(up-object-type-count-total c: blacksmith g:>= g-desired-num-blacksmith)
		(up-object-type-count-total c: market g:>= g-desired-num-market)
		=>
		(up-modify-goal g-desired-num-monastery c:max 1)
		)

		(defrule
		(up-compare-goal g-age-status >= TO-CASTLE)
		(up-compare-goal g-desired-num-monks > 0)
		(goal g-town-under-attack NO)
		(building-type-count-total town-center >= 2)
		(up-compare-goal g-target-pop-parity > 0)
		=>
		(up-modify-goal g-desired-num-monastery c:max 1)
		)

		(defrule
		(up-compare-goal g-age-status >= TO-CASTLE)
		(goal g-primary-unit-class monastery-class)
		=>
		(up-modify-goal g-desired-num-monastery c:max 3)
		)

		(defrule
		(goal g-age-status SAVE-FOR-IMPERIAL)
		(up-compare-goal g-desired-num-monastery == 0)
		(up-compare-goal g-desired-num-castle == 0)
		(building-type-count-total monastery == 0)
		(building-type-count-total castle == 0)
		=>
		(up-modify-goal g-desired-num-monastery c:max 1)
		)

	;----------------
	;	University
	;----------------

		(defrule
		(up-compare-goal g-game-focus > DEFENSIVE)	;Not REBUILD or DEFENSIVE
		(up-compare-goal g-age-status >= TO-CASTLE)
		(goal g-town-under-attack NO)
		(building-type-count-total town-center >= 3)
		(up-object-type-count-total c: town-center g:>= g-desired-num-town-center)
		(up-object-type-count-total c: barracks g:>= g-desired-num-barracks)
		(up-object-type-count-total c: archery-range g:>= g-desired-num-archery-range)
		(up-object-type-count-total c: stable g:>= g-desired-num-stable)
		(up-object-type-count-total c: siege-workshop g:>= g-desired-num-siege-workshop)
		(up-object-type-count-total c: blacksmith g:>= g-desired-num-blacksmith)
		(up-object-type-count-total c: market g:>= g-desired-num-market)
		(up-object-type-count-total c: monastery g:>= g-desired-num-monastery)
		(military-population > 25)
		=>
		(up-modify-goal g-desired-num-university c:max 1)
		)

		(defrule
		(up-compare-goal g-game-focus > DEFENSIVE)	;Not REBUILD or DEFENSIVE
		(goal g-age-status SAVE-FOR-IMPERIAL)
		(up-compare-goal g-desired-num-university == 0)
		(up-compare-goal g-desired-num-castle == 0)
		(building-type-count-total university == 0)
		(building-type-count-total castle == 0)
		=>
		(up-modify-goal g-desired-num-university c:max 1)
		)

		;Ballistics
		(defrule
		(up-compare-goal g-game-focus != REBUILD)
		(or
			(goal g-primary-unit archer)
			(and
				(goal g-support-unit archer)
				(up-compare-goal g-primary-unit-tech-progress >= SECONDARY-CASTLE-COMPLETE)))
		(up-compare-goal g-archer-tech-progress >= SECONDARY-CASTLE-COMPLETE)
		(up-research-status c: ri-ballistics == research-available)
		(building-type-count-total university == 0)
		(or
			(goal g-town-under-attack NO)
			(wood-amount >= 350))
		=>
		(up-modify-goal g-desired-num-university c:max 1)
		)

		(defrule
		(up-compare-goal g-game-focus != REBUILD)
		(or
			(goal g-primary-unit skirmisher)
			(and
				(goal g-support-unit skirmisher)
				(up-compare-goal g-primary-unit-tech-progress >= SECONDARY-CASTLE-COMPLETE)))
		(up-compare-goal g-skirmisher-tech-progress >= SECONDARY-CASTLE-COMPLETE)
		(up-research-status c: ri-ballistics == research-available)
		(building-type-count-total university == 0)
		(or
			(goal g-town-under-attack NO)
			(wood-amount >= 350))
		=>
		(up-modify-goal g-desired-num-university c:max 1)
		)

		(defrule
		(up-compare-goal g-game-focus != REBUILD)
		(or
			(goal g-primary-unit cavalry-archer)
			(and
				(goal g-support-unit cavalry-archer)
				(up-compare-goal g-primary-unit-tech-progress >= SECONDARY-CASTLE-COMPLETE)))
		(up-compare-goal g-cavalry-archer-tech-progress >= SECONDARY-CASTLE-COMPLETE)
		(up-research-status c: ri-ballistics == research-available)
		(building-type-count-total university == 0)
		(or
			(goal g-town-under-attack NO)
			(wood-amount >= 350))
		=>
		(up-modify-goal g-desired-num-university c:max 1)
		)

		(defrule
		(up-compare-goal g-game-focus != REBUILD)
		(or
			(goal g-primary-unit slinger)
			(and
				(goal g-support-unit slinger)
				(up-compare-goal g-primary-unit-tech-progress >= SECONDARY-CASTLE-COMPLETE)))
		(up-compare-goal g-slinger-tech-progress >= SECONDARY-CASTLE-COMPLETE)
		(up-research-status c: ri-ballistics == research-available)
		(building-type-count-total university == 0)
		(or
			(goal g-town-under-attack NO)
			(wood-amount >= 350))
		=>
		(up-modify-goal g-desired-num-university c:max 1)
		)

		(defrule
		(up-compare-goal g-game-focus != REBUILD)
		(or
			(goal g-primary-unit genitour)
			(and
				(goal g-support-unit genitour)
				(up-compare-goal g-primary-unit-tech-progress >= SECONDARY-CASTLE-COMPLETE)))
		(up-compare-goal g-genitour-tech-progress >= SECONDARY-CASTLE-COMPLETE)
		(up-research-status c: ri-ballistics == research-available)
		(building-type-count-total university == 0)
		(or
			(goal g-town-under-attack NO)
			(wood-amount >= 350))
		=>
		(up-modify-goal g-desired-num-university c:max 1)
		)

		(defrule
		(up-compare-goal g-game-focus != REBUILD)
		(or
			(up-compare-const UNIQUE-UNIT-CLASS == archery-class)
			(up-compare-const UNIQUE-UNIT-CLASS == cavalry-archer-class))
		(or
			(goal g-primary-unit my-unique-unit)
			(and
				(goal g-support-unit my-unique-unit)
				(up-compare-goal g-primary-unit-tech-progress >= SECONDARY-CASTLE-COMPLETE)))
		(up-compare-goal g-unique-unit-tech-progress >= SECONDARY-CASTLE-COMPLETE)
		(up-research-status c: ri-ballistics == research-available)
		(building-type-count-total university == 0)
		(or
			(goal g-town-under-attack NO)
			(wood-amount >= 350))
		=>
		(up-modify-goal g-desired-num-university c:max 1)
		)

		#load-if-defined BURMESE-CIV

			(defrule
			(up-compare-goal g-game-focus != REBUILD)
			(up-compare-const UNIQUE-UNIT-CLASS == cavalry-cannon-class)
			(or
				(goal g-primary-unit my-unique-unit)
				(and
					(goal g-support-unit my-unique-unit)
					(up-compare-goal g-primary-unit-tech-progress >= SECONDARY-CASTLE-COMPLETE)))
			(up-compare-goal g-unique-unit-tech-progress >= SECONDARY-CASTLE-COMPLETE)
			(up-research-status c: ri-ballistics == research-available)
			(building-type-count-total university == 0)
			(or
				(goal g-town-under-attack NO)
				(wood-amount >= 350))
			=>
			(up-modify-goal g-desired-num-university c:max 1)
			)

		#end-if

		;Chemistry
        (defrule
		(up-compare-goal g-game-focus > DEFENSIVE)	;Not REBUILD or DEFENSIVE
		(up-research-status c: ri-chemistry == research-available)
		(or
			(up-compare-goal g-desired-num-hand-cannoneer > 0)
			(or
				(up-compare-goal g-desired-num-bombard-cannon > 0)
				(or
					(up-compare-goal g-desired-num-cannon-galleon > 0)
					(up-compare-goal g-desired-num-bombard-cannon > 0))))
		(building-type-count-total university == 0)
        =>
		(up-modify-goal g-desired-num-university c:max 1)
        )

		;Siege Engineers
		(defrule
		(up-compare-goal g-game-focus > DEFENSIVE)	;Not REBUILD or DEFENSIVE
		(up-compare-goal g-primary-unit-tech-progress >= SECONDARY-IMPERIAL-COMPLETE)
		(up-compare-goal g-desired-num-siege > 0)
		(up-research-status c: ri-siege-engineers == research-available)
		(building-type-count-total university == 0)
		=>
		(up-modify-goal g-desired-num-university c:max 1)
		)

		;Masonry
		(defrule
		(up-compare-goal g-game-focus > DEFENSIVE)	;Not REBUILD or DEFENSIVE
		(current-age == castle-age)
		(up-compare-goal g-primary-unit-tech-progress >= ALL-CASTLE-COMPLETE)
		(up-compare-goal g-support-unit-tech-progress >= ALL-CASTLE-COMPLETE)
		(up-compare-goal g-eco-tech-progress >= ALL-CASTLE-COMPLETE)
		(up-research-status c: ri-masonry == research-available)
		(up-compare-goal g-desired-num-university < 1)
		=>
		(set-goal g-desired-num-university 1)
		)

		;Architecture
		(defrule
		(up-compare-goal g-game-focus > DEFENSIVE)	;Not REBUILD or DEFENSIVE
		(current-age == imperial-age)
		(up-compare-goal g-primary-unit-tech-progress >= SECONDARY-IMPERIAL-COMPLETE)
		(up-compare-goal g-support-unit-tech-progress >= SECONDARY-IMPERIAL-COMPLETE)
		(up-compare-goal g-eco-tech-progress >= ALL-CASTLE-COMPLETE)
		(or
			(up-research-status c: ri-masonry == research-available)
			(up-research-status c: ri-architecture == research-available))
		(up-compare-goal g-desired-num-university < 1)
		=>
		(set-goal g-desired-num-university 1)
		)

	;------------
	;	Wonder
	;------------

		(defrule
		(taunt-detected any-ally 37)
		(can-build wonder)
		=>
		(up-modify-goal g-desired-num-wonder c:max 1)
		(acknowledge-taunt this-any-ally 37)
		(chat-to-player this-any-ally TAUNT-37-BUILD-WONDER)
		)

		(defrule
		(taunt-detected any-ally 37)
		(not
			(can-build wonder))
		=>
		(up-modify-goal g-desired-num-wonder c:max 1)
		(acknowledge-taunt this-any-ally 37)
		(chat-to-player this-any-ally TAUNT-37-CANT-BUILD-WONDER)
		)

	;-----------------
	;	Watch Tower
	;-----------------

		(defrule
		(goal g-town-under-attack NO)
		(goal g-position FLANK)
		(up-compare-goal g-desired-num-watch-tower < 1)
		(up-compare-goal g-enemy-strategy-type < FAST-CASTLE)
		(up-compare-goal g-age-status >= TO-FEUDAL)
		(up-compare-goal g-age-status <= MID-CASTLE)
		=>
		(up-modify-goal g-desired-num-watch-tower c:max 1)
		)

		(defrule
		(goal g-town-under-attack NO)
		(up-compare-goal g-enemy-units-in-town == 0)
		(goal g-position FLANK)
		(up-compare-goal g-desired-num-watch-tower < 1)
		(or
			(goal g-game-focus DEFENSIVE)
			(and
				(up-compare-goal g-target-military-parity <= -8)
				(military-population < 15)))
		(up-compare-goal g-age-status >= TO-FEUDAL)
		(up-compare-goal g-age-status <= MID-CASTLE)
		=>
		(up-modify-goal g-desired-num-watch-tower c:max 1)
		)

		; (defrule
		; (goal g-town-under-attack NO)
		; (up-compare-goal g-enemy-units-in-town == 0)
		; (goal g-position FLANK)
		; (up-compare-goal g-desired-num-watch-tower < 1)
		; (goal g-first-attack-launched YES)
		; (military-population < 12)
		; (up-compare-goal g-target-military-parity > -6)
		; (current-age < imperial-age)
		; (up-compare-goal g-age-status >= TO-FEUDAL)
		; =>
		; (up-modify-goal g-desired-num-watch-tower c:max 1)
		; )

		#load-if-defined UP-3-PLAYER-TEAM

			(defrule
			(goal g-town-under-attack NO)
			(goal g-position FLANK)
			(up-compare-goal g-desired-num-watch-tower < 1)
			(up-compare-goal g-age-status >= TO-FEUDAL)
			(current-age <= castle-age)
			=>
			(up-modify-goal g-desired-num-watch-tower c:max 1)
			)

		#end-if

		#load-if-defined UP-4-PLAYER-TEAM

			(defrule
			(goal g-town-under-attack NO)
			(goal g-position FLANK)
			(up-compare-goal g-desired-num-watch-tower < 1)
			(up-compare-goal g-age-status >= TO-FEUDAL)
			(current-age <= castle-age)
			=>
			(up-modify-goal g-desired-num-watch-tower c:max 1)
			)

		#end-if

		(defrule
		(goal g-town-under-attack YES)
		(up-compare-goal g-desired-num-watch-tower >= 1)
		(up-compare-goal g-age-status >= TO-FEUDAL)
		=>
		(up-modify-goal g-desired-num-watch-tower c:= 0)
		)

		(defrule
		(goal g-game-focus REBUILD)
		(goal g-town-under-attack NO)
		(civilian-population >= 30)
		(building-type-count-total town-center >= 2)
		=>
		(up-modify-goal g-desired-num-watch-tower c:max 4)
		)

		(defrule
		(goal g-game-focus REBUILD)
		(goal g-town-under-attack NO)
		(current-age >= feudal-age)
		(civilian-population >= 25)
		=>
		(up-modify-goal g-desired-num-watch-tower c:max 3)
		)

		(defrule
		(goal g-game-focus REBUILD)
		(goal g-town-under-attack NO)
		(current-age >= feudal-age)
		(civilian-population >= 20)
		=>
		(up-modify-goal g-desired-num-watch-tower c:max 2)
		)

		(defrule
		(goal g-game-focus REBUILD)
		(goal g-town-under-attack NO)
		(current-age >= feudal-age)
		(civilian-population >= 10)
		=>
		(up-modify-goal g-desired-num-watch-tower c:max 1)
		)

		(defrule
		(or
			(unit-type-count 225 > 0)	;dead male builder
			(or
				(unit-type-count 213 > 0)	;dead female builder
				(population >= 70)))
		(up-compare-goal g-desired-num-watch-tower > 0)
		=>
		(set-goal g-desired-num-watch-tower 0)
		)

	;------------
	;	Castle
	;------------

		(defrule
		(up-compare-goal g-age-status >= TO-CASTLE)
		(or
			(up-compare-goal g-desired-num-unique-unit > 0)
			(or
				(up-compare-goal g-desired-num-trebuchet > 0)
				(goal g-current-strategy FC-CASTLE-DROP)))
		=>
		(up-modify-goal g-desired-num-castle c:max 1)
		)

		(load "The General 3.1/Functions/Reset")

		(defrule
		(up-compare-goal g-age-status >= MID-CASTLE)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-object-cost c: castle c: 1)
		(up-add-object-cost c: town-center-foundation c: 1)
		)
			
		(defrule
		(up-compare-goal g-age-status >= MID-CASTLE)
		(building-type-count-total castle == 0)
		(or
			(and
				(up-object-type-count-total c: town-center g:>= g-desired-num-town-center)
				(building-type-count-total town-center >= 3))
			(stone-amount g:>= g-stone-cost))
		=>
		(up-get-fact building-type-count-total castle g-temp)
		(up-modify-goal g-desired-num-castle g:= g-temp)
		(up-modify-goal g-desired-num-castle c:+ 1)
		)

		(defrule
		(up-compare-goal g-age-status >= MID-CASTLE)
		(building-type-count-total castle > 0)
		(or
			(military-population >= 25)
			(or
				(civilian-population >= 100)
				(current-age >= imperial-age)))
		(or
			(and
				(up-object-type-count-total c: town-center g:>= g-desired-num-town-center)
				(building-type-count-total town-center >= 3))
			(stone-amount g:>= g-stone-cost))
		=>
		(up-modify-goal g-desired-num-castle c:max 3)
		)

		(defrule
		(up-compare-goal g-age-status >= MID-CASTLE)
		(goal g-current-strategy FC-CASTLE-DROP)
		(building-type-count castle > 0)
		(military-population >= 6)
		=>
		(up-modify-goal g-desired-num-castle c:max 2)
		)

		(defrule
		(up-compare-goal g-age-status >= MID-CASTLE)
		(goal g-current-strategy MIDC-UNIQUE)
		=>
		(up-modify-goal g-desired-num-castle c:max 2)
		)

		(defrule
		(up-compare-goal g-age-status >= TO-IMPERIAL)
		(or
			(goal g-current-strategy EARLYI-UNIQUE)
			(or
				(goal g-current-strategy MIDI-UNIQUE)
				(goal g-current-strategy LATEI-UNIQUE)))
		=>
		(up-modify-goal g-desired-num-castle c:max 4)
		)

		(defrule
		(goal g-game-focus DEFENSIVE)
		(current-age >= castle-age)
		(or
			(up-compare-goal g-age-status >= MID-CASTLE)
			(unit-type-count villager >= 50))
		(goal g-desired-num-castle 0)
		=>
		(set-goal g-desired-num-castle 1)
		)

		(defrule
		(or
			(unit-type-count 225 > 0)	;dead male builder
			(unit-type-count 213 > 0))	;dead female builder
		(goal g-town-under-attack YES)
		=>
		(set-goal g-desired-num-castle 0)
		)
