;Personality.per

#load-if-not-defined VIETNAMESE-CIV

	(defrule
	(true)
	=>
	(up-get-fact player-number 0 g-initial-chat-delay)
	(up-modify-goal g-initial-chat-delay c:* 3)
	(up-modify-goal g-initial-chat-delay c:+ 10)
	(disable-self)
	)

#else
		
	(defrule
	(true)
	=>
	(set-goal g-initial-chat-delay 34)
	(disable-self)
	)
	
#end-if

;=========================<>=========================
;				       CHATTING
;=========================<>=========================

	(defrule
	(game-time >= 10)
	=>
	(chat-to-all CHAT-GENERAL-VERSION)
	(disable-self)
	)

	#load-if-defined AZTEC-CIV
		
		;Chat
		(defrule
		(goal g-general CUAUHTEMOC)
		(current-age == dark-age)
		(game-time g:> g-initial-chat-delay)
		=>
		(chat-to-all CHAT-CUAUHTEMOC1)
		(disable-self)
		)
		
		(defrule
		(goal g-general CUAUHTEMOC)
		(current-age == feudal-age)
		=>
		(chat-to-all CHAT-CUAUHTEMOC2)
		(disable-self)
		)
		
		(defrule
		(goal g-general CUAUHTEMOC)
		(current-age == castle-age)
		=>
		(chat-to-all CHAT-CUAUHTEMOC3)
		(disable-self)
		)
		
		(defrule
		(goal g-general CUAUHTEMOC)
		(current-age == imperial-age)
		=>
		(chat-to-all CHAT-CUAUHTEMOC4)
		(disable-self)
		)
		
		(defrule
		(goal g-general MONTEZUMA)
		(game-time g:> g-initial-chat-delay)
		=>
		(chat-to-all CHAT-MONTEZUMA1)
		(chat-to-all CHAT-MONTEZUMA2)
		(disable-self)
		)

	#end-if

	#load-if-defined BRITON-CIV
		
		;Longshanks
		(defrule
		(goal g-general EDWARD-LONGSHANKS)
		(game-time g:> g-initial-chat-delay)
		=>
		(set-strategic-number sn-focus-player-number 1)
		(set-goal g-temp 500)
		(disable-self)
		)
		
			(defrule
			(goal g-temp 500)
			(goal g-general EDWARD-LONGSHANKS)
			(game-time g:> g-initial-chat-delay)
			=>
			(up-store-player-name focus-player)
			)
			
			(defrule
			(goal g-temp 500)
			(up-compare-text c: TEXT-WILLIAM-WALLACE >= 0)
			(goal g-general EDWARD-LONGSHANKS)
			(game-time g:> g-initial-chat-delay)
			=>
			(set-goal g-temp 501)
			)
			
			(defrule
			(goal g-temp 500)
			(goal g-general EDWARD-LONGSHANKS)
			(game-time g:> g-initial-chat-delay)
			(up-compare-sn sn-focus-player-number < MAX-PLAYERS)
			=>
			(up-modify-sn sn-focus-player-number c:+ 1)
			(up-jump-rule -3)
			)
		
		(defrule
		(goal g-temp 500)
		(goal g-general EDWARD-LONGSHANKS)
		=>
		(chat-to-enemies CHAT-EDWARD-LONGSHANKS1)
		(chat-to-enemies CHAT-EDWARD-LONGSHANKS2)
		(disable-self)
		)
		
		(defrule
		(goal g-temp 501)
		(goal g-general EDWARD-LONGSHANKS)
		(player-in-game focus-player)
		(stance-toward focus-player enemy)
		=>
		(chat-to-all CHAT-EDWARD-LONGSHANKS3)
		(disable-self)
		)
		
		(defrule
		(goal g-temp 501)
		(goal g-general EDWARD-LONGSHANKS)
		(player-in-game focus-player)
		(stance-toward focus-player ally)
		=>
		(chat-to-all CHAT-EDWARD-LONGSHANKS4)
		(disable-self)
		)
			
		;Henry V
		(defrule
		(goal g-general HENRY-V)
		(game-time g:> g-initial-chat-delay)
		=>
		(chat-to-allies CHAT-HENRY-V1)
		(chat-to-allies CHAT-HENRY-V2)
		(disable-self)
		)
		
		;King Arthur
		(defrule
		(goal g-general ARTHUR)
		(game-time g:> g-initial-chat-delay)
		=>
		(chat-to-all CHAT-ARTHUR1)
		(disable-self)
		)

	#end-if

	#load-if-defined FRANKISH-CIV
		
		(defrule
		(goal g-general LA-HIRE)
		(game-time g:> g-initial-chat-delay)
		=>
		(chat-to-all CHAT-LA-HIRE1)
		(disable-self)
		)

	#end-if

	#load-if-defined KOREAN-CIV
		
		;Chat
		(defrule
		(goal g-general WANG-KON)
		(game-time g:> g-initial-chat-delay)
		=>
		(generate-random-number 100)
		(up-get-fact random-number 0 g-temp)
		(disable-self)
		)

		(defrule
		(goal g-general WANG-KON)
		(game-time g:> g-initial-chat-delay)
		(up-compare-goal g-temp <= 50)
		=>
		(chat-to-enemies CHAT-WANG-KON1)
		(chat-to-enemies CHAT-WANG-KON2)
		(disable-self)
		)

		(defrule
		(goal g-general YI-SUN-SHIN)
		(game-time g:> g-initial-chat-delay)
		(cc-players-unit-type-count 0 ocean-fish-class == 0)
		(cc-players-unit-type-count 0 shore-fish-class == 0)
		=>
		(chat-to-all CHAT-YI-SUN-SHIN1)
		(disable-self)
		)

	#end-if

	#load-if-defined VIETNAMESE-CIV
	#load-if-not-defined DE-AVAILABLE
		
		(defrule
		(true)
		=>
		(set-goal g-get-enemy-locations BEGIN-FINDING-PLAYERS)	;start = 0, then loops through player numbers
		(disable-self)
		)
		
		(defrule
		(game-time g:>= g-initial-chat-delay)
		(goal g-get-enemy-locations BEGIN-FINDING-PLAYERS)
		(player-in-game any-enemy)
		(player-in-game any-ally)
		=>
		(chat-to-allies CHAT-VIETNAMESE1)
		(disable-self)
		)
		
		(defrule
		(game-time g:>= g-initial-chat-delay)
		(goal g-get-enemy-locations BEGIN-FINDING-PLAYERS)
		(player-in-game any-enemy)
		(player-in-game any-ally)
		=>
		(set-goal g-get-enemy-locations 1)
		(up-modify-goal g-initial-chat-delay c:+ 6)
		)
		
			(defrule
			(game-time g:>= g-initial-chat-delay)
			(up-compare-goal g-get-enemy-locations > BEGIN-FINDING-PLAYERS)
			(up-compare-goal g-get-enemy-locations < DONE-FINDING-PLAYERS)
			=>
			(up-modify-sn sn-focus-player-number g:= g-get-enemy-locations)	;is p1 for first loop
			)
		
			(defrule
			(game-time g:>= g-initial-chat-delay)
			(up-compare-goal g-get-enemy-locations > BEGIN-FINDING-PLAYERS)
			(up-compare-goal g-get-enemy-locations < DONE-FINDING-PLAYERS)
			(nand
				(player-in-game focus-player)
				(stance-toward focus-player enemy))
			=>
			(up-jump-rule 3)
			)
			
			(defrule
			(game-time g:>= g-initial-chat-delay)
			(up-compare-goal g-get-enemy-locations > BEGIN-FINDING-PLAYERS)
			(up-compare-goal g-get-enemy-locations < DONE-FINDING-PLAYERS)
			(player-in-game focus-player)
			(stance-toward focus-player enemy)
			=>
			(up-full-reset-search)
			(up-set-target-point g-position-self-x)
			(up-find-remote c: town-center c: 1)
			)
			
			(defrule
			(game-time g:>= g-initial-chat-delay)
			(up-compare-goal g-get-enemy-locations > BEGIN-FINDING-PLAYERS)
			(up-compare-goal g-get-enemy-locations < DONE-FINDING-PLAYERS)
			(player-in-game focus-player)
			(stance-toward focus-player enemy)
			(up-set-target-object search-remote c: 0)
			=>
			(up-get-player-color focus-player g-temp)
			(up-chat-data-to-player every-ally CHAT-VIETNAMESE2 c: 7031232)
			(up-store-player-name focus-player)
			(up-chat-data-to-player every-ally CHAT-VIETNAMESE3 c: 7031232)
			(up-get-point position-object g-point-x)
			(up-set-target-point g-point-x)
			(up-send-flare g-point-x)
			)
			
			(defrule
			(game-time g:>= g-initial-chat-delay)
			(up-compare-goal g-get-enemy-locations > BEGIN-FINDING-PLAYERS)
			(up-compare-goal g-get-enemy-locations < DONE-FINDING-PLAYERS)
			(player-in-game focus-player)
			(stance-toward focus-player enemy)
			(up-set-target-object search-remote c: 0)
			=>
			(up-modify-goal g-get-enemy-locations c:+ 1)
			(up-modify-sn sn-focus-player-number g:= g-get-enemy-locations)
			(up-modify-goal g-initial-chat-delay c:+ 6)
			(up-jump-rule -5)
			)
			
			(defrule
			(game-time g:>= g-initial-chat-delay)
			(up-compare-goal g-get-enemy-locations > BEGIN-FINDING-PLAYERS)
			(up-compare-goal g-get-enemy-locations < DONE-FINDING-PLAYERS)
			=>
			(up-modify-goal g-get-enemy-locations c:+ 1)
			(up-modify-sn sn-focus-player-number g:= g-get-enemy-locations)
			(up-jump-rule -6)
			)
			
		(defrule
		(goal g-get-enemy-locations DONE-FINDING-PLAYERS)
		(game-time g:>= g-initial-chat-delay)
		=>
		(chat-to-allies CHAT-VIETNAMESE4)
		(set-goal g-get-enemy-locations PENDING)
		)
			
		(defrule
		(or
			(taunt-detected any-human-ally 16)
			(taunt-detected any-computer 16))
		=>
		(acknowledge-taunt this-any-human-ally 16)
		(acknowledge-taunt this-any-computer 16)
		(chat-to-player this-any-ally CHAT-VIETNAMESE5)
		(up-get-fact game-time 0 g-initial-chat-delay)
		(set-goal g-get-enemy-locations BEGIN-FINDING-PLAYERS)
		)
		
	#end-if
	#end-if

;=========================<>=========================
;				RESPOND TO PLAYER CHAT
;=========================<>=========================

	(defrule
	(true)
	=>
	(up-store-player-chat target-player)
	)

	(defrule
	(player-in-game target-player)
	(stance-toward target-player enemy)
	(up-compare-text c: TEXT-GG == 0)
	(up-compare-text c: TEXT-GG2 < 0)
	=>
	(chat-to-player every-enemy CHAT-GG)
	(disable-self)
	)