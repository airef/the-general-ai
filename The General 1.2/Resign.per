;Resign rules

#load-if-defined UP-1-PLAYER-TEAM

	(load "The General 1.2/Functions/Reset Mini")

	;Resign if we have < 40% of the enemy pop
	(defrule
	(goal g-resign NO)
	(game-time > 600)
	(population < TWENTY-FIVE-PERCENT-POP)
	(players-population any-enemy >= TWENTY-FIVE-PERCENT-POP)
	(up-get-fact population 0 g-temp)
	(up-get-fact-max any-enemy population 0 g-temp2)
	(up-modify-goal g-temp2 c:%* 40)
	(up-compare-goal g-temp g:< g-temp2)
	(or
		(up-compare-goal g-age-status >= MID-CASTLE)
		(population < 12))
	=>
	(set-goal g-resign DECIDE-TO-RESIGN)
	)
	
#end-if

#load-if-not-defined UP-1-PLAYER-TEAM

	;Ask for resources if out of villagers and don't have food to train them.
	(defrule
	(unit-type-count-total villager == 0)
	(not
		(can-afford-unit villager))
	(building-type-count-total town-center > 0)
	(building-type-count-total market == 0)
	(up-compare-goal g-resign < ASK-FOR-RES)
	(player-in-game any-ally)
	=>
	(chat-to-allies CHAT-REQUEST-FOOD-FOR-VILLAGERS1)
	(chat-to-allies CHAT-REQUEST-FOOD-FOR-VILLAGERS2)
	(set-goal g-resign ASK-FOR-RES)
	; (enable-timer t-resign 30)
	)
	
	;Impossible to develop if wood can't be gathered. Ask for wood.
	(defrule
	(building-type-count-total lumber-camp == 0)
	(building-type-count-total town-center == 0)
	(building-type-count-total market == 0)
	(not
		(can-afford-building lumber-camp))
	(up-compare-goal g-resign < ASK-FOR-RES)
	(player-in-game any-ally)
	=>
	(chat-to-allies CHAT-REQUEST-WOOD-FOR-CAMP2)
	(chat-to-allies CHAT-REQUEST-WOOD-FOR-CAMP2)
	(set-goal g-resign ASK-FOR-RES)
	; (enable-timer t-resign 30)
	)
	
	; ;Tribute resources away before being defeated
	; (defrule
	; (game-time > 600)
	; (unit-type-count villager == 0)
	; (not
	; 	(can-train villager))
	; (players-population any-ally > 60)
	; (goal g-resign ASK-FOR-RES)
	; (timer-triggered t-resign)
	; =>
	; (tribute-to-player this-any-ally wood 100000)
	; (tribute-to-player this-any-ally food 100000)
	; (tribute-to-player this-any-ally gold 100000)
	; (tribute-to-player this-any-ally stone 100000)
	; (chat-to-allies CHAT-DEFEATED)
	; (disable-self)
	; )

	(load "The General 1.2/Functions/Reset Mini")

	;Resign if team has < 20% pop of enemy team
	(defrule
	(up-compare-goal g-resign < DECIDE-TO-RESIGN)
	(population < FIFTY-PERCENT-POP)
	(not
		(player-in-game any-ally))
	(up-get-fact population 0 g-temp)
	(up-get-fact-sum any-ally population 0 g-temp2)
	(up-modify-goal g-temp g:+ g-temp2)
	(up-get-fact-sum any-enemy population 0 g-temp3)
	(up-modify-goal g-temp3 c:%* 20)
	(up-compare-goal g-temp g:< g-temp3)
	=>
	(set-goal g-resign DECIDE-TO-RESIGN)
	)


	(defrule
	(up-compare-goal g-resign < DECIDE-TO-RESIGN)
	(population < FIFTY-PERCENT-POP)
	(civilian-population < THIRTY-FIVE-PERCENT-POP)
	(players-population every-ally < THIRTY-FIVE-PERCENT-POP)
	(players-population any-enemy >= FIFTY-PERCENT-POP)
	(up-get-fact population 0 g-temp)
	(up-get-fact-sum any-ally population 0 g-temp2)
	(up-modify-goal g-temp g:+ g-temp2)
	(up-get-fact-sum any-enemy population 0 g-temp3)
	(up-modify-goal g-temp3 c:%* 20)
	(up-compare-goal g-temp g:< g-temp3)
	=>
	(set-goal g-resign DECIDE-TO-RESIGN)
	)

#end-if

(defrule
(goal g-resign DECIDE-TO-RESIGN)
(up-timer-status t-resign != timer-running)
=>
(chat-to-all CHAT-RESIGN)
(chat-to-all CHAT-GG)
(set-goal g-resign RESIGN)
(enable-timer t-resign 5)
)

(defrule
(goal g-resign RESIGN)
=>
(up-modify-sn sn-focus-player-number g:= g-team-partner)
)

(defrule
(goal g-resign RESIGN)
(player-in-game focus-player)
(stance-toward focus-player ally)
=>
(tribute-to-player focus-player wood 100000)
(tribute-to-player focus-player food 100000)
(tribute-to-player focus-player gold 100000)
(tribute-to-player focus-player stone 100000)
)

(defrule
(up-timer-status t-resign == timer-triggered)
(goal g-resign RESIGN)
=>
(resign)
)