(defrule
(true)
=>
(set-goal g-current-strategy FC-KRUSH)
(set-goal g-dark-strategy FC-KRUSH)
(set-goal g-early-feudal-strategy FC-KRUSH)
(set-goal g-mid-feudal-strategy FC-KRUSH)
(set-goal g-early-castle-strategy FC-KRUSH)
(set-goal g-mid-castle-strategy MIDC-KNIGHTS)
(set-goal g-early-imperial-strategy EARLYI-PALASTORM)
(set-goal g-mid-imperial-strategy MIDI-PALADINS)
(disable-self)
)

;--------------------------------
;	Knights Strategy Switching
;--------------------------------

	(defrule
	(or
		(up-compare-goal g-position != FLANK)
		(or
			(up-compare-goal g-initial-strategy != FC-KRUSH)
			(or
				(up-compare-goal g-age-status < TO-IMPERIAL)
				(up-compare-goal g-age-status > MID-IMPERIAL))))
	=>
	(up-jump-rule 5)
	)

		(defrule
		(true)
		=>
        (set-goal g-mid-castle-strategy MIDC-KNIGHTS)
        (set-goal g-early-imperial-strategy EARLYI-PALASTORM)
        (set-goal g-mid-imperial-strategy MIDI-PALADINS)
		)

		(defrule
		(or
			(up-compare-goal g-enemy-foot-archers-count g:>= g-enemy-spearmen-count)
			(or
				(up-compare-goal g-enemy-cavalry-archers-count g:>= g-enemy-spearmen-count)
				(or
					(up-compare-goal g-enemy-hand-cannoneer-slinger-count g:>= g-enemy-spearmen-count)
					(and
						(up-compare-goal g-enemy-special-unique-unit-count g:>= g-enemy-spearmen-count)
						(and
							(up-compare-flag g-enemy-special-unique-unit-flag == CATAPHRACT)
							(and
								(up-compare-flag g-enemy-special-unique-unit-flag == JAGUAR-WARRIOR)
								(up-compare-flag g-enemy-special-unique-unit-flag == TEUTONIC-KNIGHT)))))))
		=>
		(up-jump-rule 1)
		)
			(defrule
			(up-compare-goal g-enemy-spearmen-count >= 12)
			=>
			(set-goal g-mid-castle-strategy MIDC-SWORDS)
			(set-goal g-early-imperial-strategy EARLYI-CHAMPFLOOD)
			(set-goal g-mid-imperial-strategy MIDI-CHAMPIONS)
			; (chat-to-all "Switching to champs strategy 1")
			)
		(defrule
		(up-compare-flag g-enemy-special-unique-unit-flag == HUSKARL)
		(or
			(up-compare-goal g-enemy-foot-archers-count g:>= g-enemy-special-unique-unit-count)
			(or
				(up-compare-goal g-enemy-cavalry-archers-count g:>= g-enemy-special-unique-unit-count)
				(up-compare-goal g-enemy-hand-cannoneer-slinger-count g:>= g-enemy-special-unique-unit-count)))
		=>
		(up-jump-rule 1)
		)
			(defrule
			(up-compare-goal g-enemy-special-unique-unit-count >= 6)
			(up-compare-flag g-enemy-special-unique-unit-flag == HUSKARL)
			=>
			(set-goal g-mid-castle-strategy MIDC-SWORDS)
			(set-goal g-early-imperial-strategy EARLYI-CHAMPFLOOD)
			(set-goal g-mid-imperial-strategy MIDI-CHAMPIONS)
			; (chat-to-all "Switching to champs strategy 2")
			)

		

;--------------------------------------
;	Late Imperial Strategy Switching
;--------------------------------------

    (defrule
    (goal g-age-status LATE-IMPERIAL)
    (building-type-count castle > 0)
    (or
        (unit-type-count trade-cart >= 6)
        (or
            (up-gaia-type-count c: gold > 4)
            (up-compare-goal g-recent-gold-gathered >= 50)))
    =>
    (set-goal g-late-imperial-strategy LATEI-UNIQUE)
    )

    (defrule
    (goal g-age-status LATE-IMPERIAL)
    (building-type-count castle == 0)
    (or
        (unit-type-count trade-cart >= 6)
        (or
            (up-gaia-type-count c: gold > 4)
            (up-compare-goal g-recent-gold-gathered >= 50)))
    =>
    (set-goal g-late-imperial-strategy LATEI-CHAMPIONS)
    )

    (defrule
    (goal g-age-status LATE-IMPERIAL)
    (unit-type-count trade-cart < 6)
    (up-gaia-type-count c: gold <= 4)
    (up-compare-goal g-recent-gold-gathered < 50)
    =>
    (set-goal g-late-imperial-strategy LATEI-TRASHALBS)
    )