;Strategic Numbers.per

;----------------------------------
;	SN-SAFE-TOWN-SIZE
;	SN-MAXIMUM-TOWN-SIZE
;   CUSTOM-SN-PEACEFUL-TOWN-SIZE
;----------------------------------

    (defrule
    (or
        (up-compare-sn sn-safe-town-size < 10)
        (or
            (up-compare-sn sn-maximum-town-size < 10)
            (up-compare-sn custom-sn-peaceful-town-size < 10)))
    =>
    (set-strategic-number sn-safe-town-size 10)
    (set-strategic-number custom-sn-peaceful-town-size 10)
    (up-modify-sn sn-maximum-town-size c:max 10)
    )

    (defrule
    (current-age >= feudal-age)
    (or
        (up-compare-sn sn-safe-town-size < 14)
        (or
            (up-compare-sn sn-maximum-town-size < 14)
            (up-compare-sn custom-sn-peaceful-town-size < 14)))
    ; (goal g-enemy-buildings-in-town 0)
    (or
        (military-population >= 6)
        (up-compare-goal g-nearest-enemy-defenses-distance >= 21))
    =>
    (set-strategic-number sn-safe-town-size 14)
    (set-strategic-number custom-sn-peaceful-town-size 14)
    (up-modify-sn sn-maximum-town-size c:max 14)
    )

    (defrule
    (current-age >= castle-age)
    (or
        (up-compare-sn sn-safe-town-size < 16)
        (or
            (up-compare-sn sn-maximum-town-size < 16)
            (up-compare-sn custom-sn-peaceful-town-size < 16)))
    ; (goal g-enemy-buildings-in-town 0)
    (or
        (military-population >= 6)
        (up-compare-goal g-nearest-enemy-defenses-distance >= 24))
    =>
    (set-strategic-number sn-safe-town-size 16)
    (set-strategic-number custom-sn-peaceful-town-size 16)
    (up-modify-sn sn-maximum-town-size c:max 16)
    ; (up-chat-data-to-all "Nearest Defenses %d" g: g-nearest-enemy-defenses-distance)
    )

    (defrule
    (population > 60)
    (or
        (up-compare-sn sn-safe-town-size < 18)
        (or
            (up-compare-sn sn-maximum-town-size < 18)
            (up-compare-sn custom-sn-peaceful-town-size < 18)))
    ; (goal g-enemy-buildings-in-town 0)
    (or
        (military-population >= 6)
        (up-compare-goal g-nearest-enemy-defenses-distance >= 27))
    =>
    (set-strategic-number sn-safe-town-size 18)
    (set-strategic-number custom-sn-peaceful-town-size 18)
    (up-modify-sn sn-maximum-town-size c:max 18)
    )

    (defrule
    (or
        (population > 100)
        (building-type-count-total town-center > 2))
    (or
        (up-compare-sn sn-safe-town-size < 22)
        (or
            (up-compare-sn sn-maximum-town-size < 22)
            (up-compare-sn custom-sn-peaceful-town-size < 22)))
    (goal g-enemy-buildings-in-town 0)
    (or
        (military-population >= 6)
        (up-compare-goal g-nearest-enemy-defenses-distance >= 33))
    =>
    (set-strategic-number sn-safe-town-size 22)
    (set-strategic-number custom-sn-peaceful-town-size 22)
    (up-modify-sn sn-maximum-town-size c:max 22)
    )

    (defrule
    (population > 150)
    (goal g-enemy-buildings-in-town 0)
    (or
        (up-compare-sn sn-safe-town-size < 25)
        (or
            (up-compare-sn sn-maximum-town-size < 25)
            (up-compare-sn custom-sn-peaceful-town-size < 25)))
    (or
        (military-population >= 6)
        (up-compare-goal g-nearest-enemy-defenses-distance >= 36))
    =>
    (set-strategic-number sn-safe-town-size 25)
    (set-strategic-number custom-sn-peaceful-town-size 25)
    (up-modify-sn sn-maximum-town-size c:max 25)
    )

    (defrule
    (current-age == imperial-age)
    (goal g-enemy-buildings-in-town 0)
    (or
        (up-compare-sn sn-safe-town-size < 28)
        (or
            (up-compare-sn sn-maximum-town-size < 28)
            (up-compare-sn custom-sn-peaceful-town-size < 28)))
    (or
        (military-population >= 6)
        (up-compare-goal g-nearest-enemy-defenses-distance >= 42))
    =>
    (set-strategic-number sn-safe-town-size 28)
    (set-strategic-number custom-sn-peaceful-town-size 28)
    (up-modify-sn sn-maximum-town-size c:max 28)
    )

    (defrule
    (current-age == imperial-age)
    (population > 170)
    (goal g-enemy-buildings-in-town 0)
    (or
        (up-compare-sn sn-safe-town-size < 32)
        (or
            (up-compare-sn sn-maximum-town-size < 32)
            (up-compare-sn custom-sn-peaceful-town-size < 32)))
    (or
        (military-population >= 6)
        (up-compare-goal g-nearest-enemy-defenses-distance >= 48))
    =>
    (set-strategic-number sn-safe-town-size 32)
    (set-strategic-number custom-sn-peaceful-town-size 32)
    (up-modify-sn sn-maximum-town-size c:max 32)
    )

    ;Reduced town size if enemy towers in town
    (defrule
    (military-population < 6)
    (goal g-attacking NO)
    (up-building-type-in-town c: watch-tower >= 1)
    (up-compare-sn sn-maximum-town-size > 12)
    =>
    (up-modify-sn sn-maximum-town-size c:- 1)
    (up-modify-sn sn-safe-town-size s:min sn-maximum-town-size)
    (up-modify-sn custom-sn-peaceful-town-size s:min sn-maximum-town-size)
    (up-chat-data-to-player my-player-number CHAT-REDUCE-TS-FOR-TOWERS s: sn-maximum-town-size)
    )

    ; (load "The General 1.2/Functions/Reset")

    ; (defrule
    ; (timer-triggered t-5-sec)
    ; (building-type-count town-center > 0)
    ; =>
    ; (up-set-target-point g-position-self-x)
    ; (set-goal g-temp 40)
    ; (up-modify-goal g-temp s:max custom-sn-peaceful-town-size)
    ; (up-filter-distance c: -1 g: g-temp)    ;only search within 40 tiles or custom-sn-peaceful-town-size, whichever is larger
    ; (up-full-reset-search)
    ; (up-find-local c: town-center c: 40)
    ; (up-find-local c: castle c: 40)
    ; (up-find-local c: tower-class c: 40)
    ; (up-filter-status c: status-pending c: list-active)
    ; (up-find-status-local c: town-center c: 40)
    ; (up-find-status-local c: castle c: 40)
    ; (up-find-status-local c: tower-class c: 40)
    ; (up-clean-search search-local object-data-distance search-order-desc)
    ; )

    ; (defrule
    ; (timer-triggered t-5-sec)
    ; (building-type-count town-center > 0)
    ; (up-set-target-object search-local c: 0)
    ; (up-get-object-data object-data-distance g-temp)
    ; (up-compare-sn custom-sn-peaceful-town-size g:< g-temp)
    ; =>
    ; (up-modify-sn custom-sn-peaceful-town-size g:= g-temp)
    ; (up-modify-sn custom-sn-peaceful-town-size c:min 40)
    ; (up-modify-sn sn-maximum-town-size s:max custom-sn-peaceful-town-size)
    ; (up-modify-sn sn-safe-town-size s:max custom-sn-peaceful-town-size)
    ; )

;-------------------------------
;   SN-NUMBER-EXPLORE-GROUPS
;   SN-CAP-CIVILIAN-EXPLORERS
;-------------------------------

    (load "The General 1.2/Functions/Reset")

    ;Explore with all scouts if game is early and we haven't found enemy
    (defrule
    (goal g-scouting-status AUTO-SCOUT)
    (strategic-number sn-number-explore-groups == 0)
    (game-time < 600)
    (players-building-count every-enemy == 0)
    (military-population > 0)
    =>
    (up-reset-scouts)
    (up-get-fact military-population 0 g-temp)
    (up-get-fact unit-type-count monastery-class g-temp2)
    (up-modify-goal g-temp g:- g-temp2)    ;remove monks and missionaries from military-population count
    (up-modify-sn sn-number-explore-groups g:= g-temp)
    (up-modify-sn sn-number-explore-groups c:max 1)
    )
    
    ;Explore with one scout if we have found the enemy
    (defrule
    (goal g-scouting-status AUTO-SCOUT)
    (strategic-number sn-number-explore-groups != 1)
    (or
        (game-time >= 600)
        (players-building-count any-enemy > 0))
    =>
    (up-reset-scouts)
    (set-strategic-number sn-number-explore-groups 1)
    )

    (defrule
    (up-compare-goal g-scouting-status != AUTO-SCOUT)
    (strategic-number sn-number-explore-groups != 0)
    =>
    (up-reset-scouts)
    (set-strategic-number sn-number-explore-groups 0)
    )

    ;====EXPLORING SNS

    (defrule
    (building-type-count-total town-center > 0)
    (or
        (cc-players-unit-type-count 0 livestock-class > 0)
        (cc-players-unit-type-count 0 forage-class > 0))
    (goal g-current-sheep-count 0)
    (up-gaia-type-count c: food == 0)
    (strategic-number sn-percent-civilian-explorers < 100)
    (unit-type-count villager-builder == 0)
    (game-time < 45)
    =>
    (set-strategic-number sn-percent-civilian-explorers 100)
    (set-strategic-number sn-cap-civilian-explorers 5)
    (set-strategic-number sn-total-number-explorers 6)
    )

    (defrule
    (goal g-scouting-status AUTO-SCOUT)
    (or
        (up-compare-goal g-current-sheep-count > 0)
        (up-gaia-type-count c: food > 0))
    (building-type-count-total town-center > 0)
    (strategic-number sn-percent-civilian-explorers == 100)
    =>
    (set-strategic-number sn-percent-civilian-explorers 0)
    (set-strategic-number sn-percent-civilian-gatherers 100)
    (set-strategic-number sn-cap-civilian-explorers 0)
    (set-strategic-number sn-total-number-explorers 1)
    (set-strategic-number sn-number-explore-groups 1)
    )

    (defrule
    (up-compare-goal g-duc-scouting != STOP-DUC-SCOUTING)
    (or
        (up-compare-goal g-current-sheep-count > 0)
        (up-gaia-type-count c: food > 0))
    (building-type-count-total town-center > 0)
    (strategic-number sn-percent-civilian-explorers == 100)
    =>
    (set-strategic-number sn-percent-civilian-explorers 0)
    (set-strategic-number sn-percent-civilian-gatherers 100)
    (set-strategic-number sn-cap-civilian-explorers 0)
    (set-strategic-number sn-total-number-explorers 0)
    (set-strategic-number sn-number-explore-groups 0)
    )

    ;Explore with villagers if we have no scout
    
    (defrule
    (goal g-duc-scouting STOP-DUC-SCOUTING)
    (goal g-scout1-id 0)
    (military-population == 0)
    (building-type-count town-center > 0)
    (or
        (cc-players-unit-type-count 0 livestock-class > 0)
        (or
            (cc-players-unit-type-count 0 forage-class > 0)
            (cc-players-unit-type-count 0 boar-class > 0)))
    (strategic-number sn-percent-civilian-explorers < 99)
    (up-timer-status t-villager-explore-timer == timer-disabled)
    =>
    (set-strategic-number sn-percent-civilian-explorers 99)
    (set-strategic-number sn-cap-civilian-explorers 2)
    (up-modify-sn sn-total-number-explorers c:max 2)
    (enable-timer t-villager-explore-timer 120)
    )

    ;Stop exploring villagers after 120 seconds

    (defrule
    (goal g-duc-scouting STOP-DUC-SCOUTING)
    (strategic-number sn-percent-civilian-explorers == 99)
    (or
        (and
            (timer-triggered t-villager-explore-timer)
            (up-gaia-type-count c: food > 0))
        (military-population > 0))
    =>
    (set-strategic-number sn-percent-civilian-explorers 0)
    (set-strategic-number sn-percent-civilian-gatherers 100)
    (set-strategic-number sn-cap-civilian-explorers 0)
    (set-strategic-number sn-total-number-explorers 1)
    (set-strategic-number sn-number-explore-groups 1)
    )

    (defrule
    (up-compare-goal g-duc-scouting != STOP-DUC-SCOUTING)
    (strategic-number sn-percent-civilian-explorers == 99)
    (or
        (and
            (timer-triggered t-villager-explore-timer)
            (up-gaia-type-count c: food > 0))
        (military-population > 0))
    =>
    (set-strategic-number sn-percent-civilian-explorers 0)
    (set-strategic-number sn-percent-civilian-gatherers 100)
    (set-strategic-number sn-cap-civilian-explorers 0)
    (set-strategic-number sn-total-number-explorers 0)
    (set-strategic-number sn-number-explore-groups 0)
    )

;------------------------------
;   SN-HOME-EXPLORATION-TIME
;------------------------------

    (load "The General 1.2/Functions/Reset")

    (defrule
    (building-type-count-total town-center > 0)
    =>
    (set-strategic-number sn-home-exploration-time 375)
    (up-get-fact game-time 0 g-temp)
    (up-modify-sn sn-home-exploration-time g:+ g-temp)
    (disable-self)
    )

;-----------------------------------
;   SN-MAXIMUM-FOOD-DROP-DISTANCE
;-----------------------------------

    (defrule
    (up-compare-goal g-usable-farm-count >= 20)
    (strategic-number sn-maximum-food-drop-distance < 12)
    =>
    (set-strategic-number sn-maximum-food-drop-distance 12)
    )

;---------------------------------
;   SN-LUMBER-CAMP-MAX-DISTANCE
;---------------------------------

    (defrule
    (up-compare-goal g-min-forest-distance > PENDING)
    (building-type-count-total lumber-camp == 0)
    (strategic-number sn-lumber-camp-max-distance g:< g-min-forest-distance)
    =>
    (up-modify-sn sn-lumber-camp-max-distance g:= g-min-forest-distance)
    (up-modify-sn sn-lumber-camp-max-distance c:+ 3)    ;add a bit more breathing room for optimal placement
    )

    (defrule
    (building-type-count-total lumber-camp > 0)
    =>
    (up-modify-sn sn-lumber-camp-max-distance c:+ 12)
    (disable-self)
    )

    (defrule
    (up-compare-sn sn-lumber-camp-max-distance s:< custom-sn-peaceful-town-size)
    =>
    (up-modify-sn sn-lumber-camp-max-distance s:= custom-sn-peaceful-town-size)
    )

    (load "The General 1.2/Functions/Reset")

        ;Increase sn-lumber-camp-max-distance if we don't have any forest inside the distance
        (defrule
        (building-type-count town-center > 0)
        (dropsite-min-distance wood > 3)
        (dropsite-min-distance wood < 255)
        (up-compare-goal g-desired-num-wood-vils > 0)
        =>
        (up-set-target-point g-position-self-x)
        (up-full-reset-search)
        (up-filter-distance c: -1 s: sn-lumber-camp-max-distance)
        (up-find-resource c: wood c: 40)
        (up-get-search-state g-local-total)
        (set-goal g-temp 11111)
        )

        (defrule
        (goal g-temp 11111)
        (goal g-remote-total 0) ;no forest found
        (building-type-count town-center > 0)
        (dropsite-min-distance wood > 3)
        (dropsite-min-distance wood < 255)
        (up-compare-goal g-desired-num-wood-vils > 0)
        (strategic-number sn-lumber-camp-max-distance < HUNDRED-PERCENT-MAP-SIZE)
        =>
        (set-goal g-temp 11112)
        (up-modify-sn sn-lumber-camp-max-distance c:+ 5)
        (up-jump-rule -2)
        )

    (defrule
    (goal g-temp 11112)
    (strategic-number sn-lumber-camp-max-distance < HUNDRED-PERCENT-MAP-SIZE)
    (unit-type-count villager >= 50)
    =>
    (up-modify-sn sn-lumber-camp-max-distance c:+ 10)
    )

    ;Sometimes sn-wood-dropsite-distance prevents new lumber camps to be dropped even if
    ;forests are within sn-lumber-camp-max-distance. If so, increase sn-lumber-camp-max-distance
    (defrule
    (building-type-count town-center > 0)
    (dropsite-min-distance wood > 3)
    (dropsite-min-distance wood < 255)
    (up-compare-goal g-desired-num-wood-vils > 0)
    (strategic-number sn-lumber-camp-max-distance < HUNDRED-PERCENT-MAP-SIZE)
    (wood-amount > 100)
    (up-timer-status t-lumber-camp-increase != timer-running)
    =>
    (disable-timer t-lumber-camp-increase)
    (enable-timer t-lumber-camp-increase 30)
    )

    (defrule
    (up-timer-status t-lumber-camp-increase == timer-running)
    (dropsite-min-distance wood <= 3)
    =>
    (disable-timer t-lumber-camp-increase)
    )

    (defrule
    (timer-triggered t-lumber-camp-increase)
    (building-type-count town-center > 0)
    (dropsite-min-distance wood > 3)
    (dropsite-min-distance wood < 255)
    (up-compare-goal g-desired-num-wood-vils > 0)
    (strategic-number sn-lumber-camp-max-distance < HUNDRED-PERCENT-MAP-SIZE)
    (wood-amount > 100)
    =>
    (disable-timer t-lumber-camp-increase)
    (up-modify-sn sn-lumber-camp-max-distance c:+ 10)
    )

    (defrule
    (building-type-count town-center > 0)
    (dropsite-min-distance wood < 255)
    (up-compare-goal g-desired-num-wood-vils > 0)
    (strategic-number sn-lumber-camp-max-distance != HUNDRED-PERCENT-MAP-SIZE)
    (strategic-number sn-lumber-camp-max-distance >= SIXTY-PERCENT-MAP-SIZE)
    =>
    (set-strategic-number sn-lumber-camp-max-distance HUNDRED-PERCENT-MAP-SIZE)
    )

;---------------------------------
;   SN-MINING-CAMP-MAX-DISTANCE
;---------------------------------

    (defrule
    (strategic-number sn-mining-camp-max-distance < 24)
    (building-type-count-total mining-camp > 1)
    =>
    (set-strategic-number sn-mining-camp-max-distance 24)
    ; (up-chat-data-to-player my-player-number CHAT-CAMP-MINING-SET-DISTANCE s: sn-mining-camp-max-distance)
    )

    (defrule
    (up-compare-sn sn-mining-camp-max-distance s:< custom-sn-peaceful-town-size)
    =>
    (up-modify-sn sn-mining-camp-max-distance s:= custom-sn-peaceful-town-size)
    ; (up-chat-data-to-player my-player-number CHAT-CAMP-MINING-PEACEFUL-DISTANCE s: sn-mining-camp-max-distance)
    )

    (load "The General 1.2/Functions/Reset")

        ;Increase sn-mining-camp-max-distance if we don't have any gold inside the distance
        (defrule
        (building-type-count town-center > 0)
        (dropsite-min-distance gold > 3)
        (dropsite-min-distance gold < 255)
        (up-compare-goal g-desired-num-gold-vils > 0)
        =>
        (up-set-target-point g-position-self-x)
        (up-full-reset-search)
        (up-filter-distance c: -1 s: sn-mining-camp-max-distance)
        (up-filter-status c: status-resource c: list-active)
        (up-find-resource c: gold c: 40)
        (up-get-search-state g-local-total)
        (set-goal g-temp 22222)
        )

        (defrule
        (goal g-temp 22222)
        (goal g-remote-total 0) ;no gold found
        (building-type-count town-center > 0)
        (dropsite-min-distance gold > 3)
        (dropsite-min-distance gold < 255)
        (up-compare-goal g-desired-num-gold-vils > 0)
        (strategic-number sn-mining-camp-max-distance < HUNDRED-PERCENT-MAP-SIZE)
        =>
        (set-goal g-temp -1)
        (up-modify-sn sn-mining-camp-max-distance c:+ 5)
        ; (up-chat-data-to-player my-player-number CHAT-CAMP-GOLD-INCREASE-DISTANCE s: sn-mining-camp-max-distance)
        (up-jump-rule -2)
        )

    (load "The General 1.2/Functions/Reset")

        ;Increase sn-mining-camp-max-distance if we don't have any stone inside the distance
        (defrule
        (building-type-count town-center > 0)
        (dropsite-min-distance stone > 3)
        (dropsite-min-distance stone < 255)
        (up-compare-goal g-desired-num-stone-vils > 0)
        =>
        (up-set-target-point g-position-self-x)
        (up-full-reset-search)
        (up-filter-distance c: -1 s: sn-mining-camp-max-distance)
        (up-filter-status c: status-resource c: list-active)
        (up-find-resource c: stone c: 40)
        (up-get-search-state g-local-total)
        (set-goal g-temp 33333)
        )

        (defrule
        (goal g-temp 33333)
        (goal g-remote-total 0) ;no stone found
        (building-type-count town-center > 0)
        (dropsite-min-distance stone > 3)
        (dropsite-min-distance stone < 255)
        (up-compare-goal g-desired-num-stone-vils > 0)
        (strategic-number sn-mining-camp-max-distance < HUNDRED-PERCENT-MAP-SIZE)
        =>
        (set-goal g-temp -1)
        (up-modify-sn sn-mining-camp-max-distance c:+ 5)
        ; (up-chat-data-to-player my-player-number CHAT-CAMP-STONE-INCREASE-DISTANCE s: sn-mining-camp-max-distance)
        (up-jump-rule -2)
        )

    (defrule
    (building-type-count town-center > 0)
    (or
        (and
            (dropsite-min-distance gold < 255)
            (up-compare-goal g-desired-num-gold-vils > 0))
        (and
            (dropsite-min-distance stone < 255)
            (up-compare-goal g-desired-num-stone-vils > 0)))
    (strategic-number sn-mining-camp-max-distance != HUNDRED-PERCENT-MAP-SIZE)
    (strategic-number sn-mining-camp-max-distance >= SIXTY-PERCENT-MAP-SIZE)
    =>
    (set-strategic-number sn-mining-camp-max-distance HUNDRED-PERCENT-MAP-SIZE)
    ; (up-chat-data-to-player my-player-number CHAT-CAMP-MINING-MAX-DISTANCE s: sn-mining-camp-max-distance)
    )

;------------------------------
;   SN-CAP-CIVILIAN-BUILDERS
;------------------------------

    (defrule
    (or
        (building-type-count house > 2)
        (civ-selected hun))
    (strategic-number sn-cap-civilian-builders < 200)
    =>
    (set-strategic-number sn-cap-civilian-builders 200)
    )

;----------------------------
;   SN-ENABLE-BOAR-HUNTING
;----------------------------

    (defrule
    (up-compare-goal g-scouting-status <= PUSH-DEER)
    (or
        (up-compare-goal g-boar-found-count > 0)
        (up-compare-const MIN-BOAR == 0))
    (or
        (goal g-current-boar-count 0)
        (dropsite-min-distance deer-hunting < 5))
    (or
        (strategic-number sn-enable-boar-hunting != 1)
        (strategic-number sn-maximum-hunt-drop-distance < 4))
    =>
    (set-strategic-number sn-enable-boar-hunting 1)
    (set-strategic-number sn-maximum-hunt-drop-distance 4)
    )

    (defrule
    (up-compare-goal g-scouting-status > PUSH-DEER)
    (or
        (up-compare-goal g-boar-found-count > 0)
        (up-compare-const MIN-BOAR == 0))
    (or
        (goal g-current-boar-count 0)
        (dropsite-min-distance deer-hunting < 5))
    (or
        (strategic-number sn-enable-boar-hunting != 1)
        (strategic-number sn-maximum-hunt-drop-distance < 8))
    =>
    (set-strategic-number sn-enable-boar-hunting 1)
    (set-strategic-number sn-maximum-hunt-drop-distance 8)
    )

;-------------------------------
;   SN-MINIMUM-NUMBER-HUNTERS
;-------------------------------

    (defrule
    (up-compare-sn sn-minimum-number-hunters != 4)
    (or
        (up-compare-goal g-total-decaying-hunt-food > 0)
        (and
            (up-set-target-by-id g: g-current-boar-id)
            (up-object-data object-data-hitpoints < 5)))
    =>
    (up-modify-sn sn-minimum-number-hunters c:= 4)
    )

    (defrule
    (up-compare-sn sn-minimum-number-hunters != 0)
    (up-compare-goal g-total-decaying-hunt-food == 0)
    (or
        (not
            (up-set-target-by-id g: g-current-boar-id))
        (and
            (up-set-target-by-id g: g-current-boar-id)
            (up-object-data object-data-hitpoints >= 5)))
    =>
    (up-modify-sn sn-minimum-number-hunters c:= 0)
    )

;---------------------------------
;   SN-ALLOW-ADJACENT-DROPSITES
;---------------------------------

    (defrule
    (strategic-number sn-allow-adjacent-dropsites != 1)
    =>
    (set-strategic-number sn-allow-adjacent-dropsites 1)
    )

;--------------------------------
;   SN-TASK-UNGROUPED-SOLDIERS
;--------------------------------

    ; (load "The General 1.2/Functions/Reset")

    ;If villagers are gathering near soldier gather point, periodically tell soldiers to spread out
    ; (defrule
    ; (up-compare-goal g-gather-point-x >= 0)
    ; (up-point-distance g-gather-point-x g-position-self-x > 5)
    ; =>
    ; (up-set-target-point g-gather-point-x)
    ; (up-full-reset-search)
    ; (up-filter-distance c: -1 c: 5)
    ; (up-find-local c: villager-class c: 1)
    ; (up-get-search-state g-local-total)
    ; )

    ; (defrule
    ; (timer-triggered t-spread-soldiers)
    ; (up-compare-goal g-gather-point-x >= 0)
    ; (up-point-distance g-position-self-x g-gather-point-x > 5)
    ; =>
    ; (set-strategic-number sn-task-ungrouped-soldiers 0)
    ; (disable-timer t-spread-soldiers)
    ; )

    ; (defrule
    ; (timer-triggered t-30-sec)
    ; (up-compare-goal g-gather-point-x >= 0)
    ; (up-point-distance g-position-self-x g-gather-point-x > 5)
    ; (military-population > 1)
    ; (goal g-attacking NO)
    ; (up-compare-goal g-local-total > 0)
    ; =>
    ; (set-strategic-number sn-task-ungrouped-soldiers 1)
    ; (enable-timer t-spread-soldiers 1)
    ; )

;--------------------------------------
;   SN-PERCENT-BUILDING-CANCELLATION
;--------------------------------------

    ; (load "The General 1.2/Functions/Reset")

    ; (defrule
    ; (strategic-number sn-percent-building-cancellation > 30)
    ; (or
    ;     (goal g-town-under-attack NO)
    ;     (up-compare-goal g-top-enemy-military-parity >= 0))
    ; =>
    ; (set-strategic-number sn-percent-building-cancellation 30)
    ; )

    ; ;Find a tower under construction
    ; (defrule
    ; (strategic-number sn-percent-building-cancellation > 0)
    ; (goal g-town-under-attack YES)
    ; (up-pending-objects c: tower-class > 0)
    ; (up-compare-goal g-top-enemy-military-parity < 0)
    ; =>
    ; (up-full-reset-search)
    ; (up-filter-status c: status-pending c: list-active)
    ; (up-find-local c: tower-class c: 1)
    ; )

    ; ;See if there are villagers building it
    ; (defrule
    ; (strategic-number sn-percent-building-cancellation < 100)
    ; (goal g-town-under-attack YES)
    ; (up-pending-objects c: tower-class > 0)
    ; (up-compare-goal g-top-enemy-military-parity < 0)
    ; (up-set-target-object search-local c: 0)
    ; =>
    ; (up-get-point position-object g-point-x)
    ; (up-set-target-point g-point-x)
    ; (set-strategic-number sn-focus-player-number my-player-number)
    ; (up-reset-search 0 0 1 1)
    ; (up-reset-filters)
    ; (up-filter-distance c: -1 c: 2)
    ; (up-find-remote c: villager-class c: 1)
    ; (up-get-search-state g-local-total)
    ; )

    ; ;If builders are found but they are under attack, increase sn-percent-building-cancellation
    ; (defrule
    ; (strategic-number sn-percent-building-cancellation < 70)
    ; (goal g-town-under-attack YES)
    ; (up-pending-objects c: tower-class > 0)
    ; (up-compare-goal g-top-enemy-military-parity < 0)
    ; (up-set-target-object search-local c: 0)
    ; (up-set-target-object search-remote c: 0)
    ; (up-object-data object-data-attacker-count > 0)
    ; =>
    ; (set-strategic-number sn-percent-building-cancellation 70)
    ; )

    ; ;If no builders nearby, assume they have been killed and cancel the tower
    ; (defrule
    ; (strategic-number sn-percent-building-cancellation < 100)
    ; (goal g-town-under-attack YES)
    ; (up-pending-objects c: tower-class > 0)
    ; (up-compare-goal g-top-enemy-military-parity < 0)
    ; (up-set-target-object search-local c: 0)
    ; (goal g-remote-total 0) ;no nearby builders
    ; =>
    ; (set-strategic-number sn-percent-building-cancellation 100)
    ; )

;-------------------------------------
;   CUSTOM-SN-COUNTERING-PREFERENCE
;-------------------------------------

    (defrule
    (player-in-game any-ally)
    (strategic-number custom-sn-countering-preference != 0)
    =>
    (set-strategic-number custom-sn-countering-preference 0)
    )

    (defrule
    (not
        (player-in-game any-ally))
    (strategic-number custom-sn-countering-preference != 100)
    =>
    (set-strategic-number custom-sn-countering-preference 100)
    )

    ;Adjust score more if low military or defensive
    (defrule
    (current-age >= feudal-age)
    (strategic-number custom-sn-countering-preference > 0)
    (or
        (military-population g:< g-min-military-pop)
        (or
            (goal g-game-focus DEFENSIVE)
            (up-compare-goal g-target-military-parity < -5)))
    =>
    (up-modify-sn custom-sn-countering-preference c:%* 150)
    )

;---------------------------------
;   SN-PREFERRED-TRADE-DISTANCE
;---------------------------------

    (defrule
    (building-type-count market > 1)
    =>
    (set-strategic-number sn-preferred-trade-distance EIGHTY-PERCENT-MAP-SIZE)
    )