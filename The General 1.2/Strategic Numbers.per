;Strategic Numbers.per

;----------------------------------
;	SN-SAFE-TOWN-SIZE
;	SN-MAXIMUM-TOWN-SIZE
;   csn-PEACEFUL-TOWN-SIZE
;----------------------------------

    (defrule
    (or
        (up-compare-sn sn-safe-town-size < 10)
        (or
            (up-compare-sn sn-maximum-town-size < 10)
            (up-compare-sn csn-peaceful-town-size < 10)))
    =>
    (set-strategic-number sn-safe-town-size 10)
    (set-strategic-number csn-peaceful-town-size 10)
    (up-modify-sn sn-maximum-town-size c:max 10)
    )

    (defrule
    (current-age >= feudal-age)
    (or
        (up-compare-sn sn-safe-town-size < 14)
        (or
            (up-compare-sn sn-maximum-town-size < 14)
            (up-compare-sn csn-peaceful-town-size < 14)))
    ; (goal g-enemy-buildings-in-town 0)
    (or
        (military-population >= 6)
        (up-compare-goal g-nearest-enemy-defenses-distance >= 21))
    =>
    (set-strategic-number sn-safe-town-size 14)
    (set-strategic-number csn-peaceful-town-size 14)
    (up-modify-sn sn-maximum-town-size c:max 14)
    )

    (defrule
    (current-age >= castle-age)
    (or
        (up-compare-sn sn-safe-town-size < 16)
        (or
            (up-compare-sn sn-maximum-town-size < 16)
            (up-compare-sn csn-peaceful-town-size < 16)))
    ; (goal g-enemy-buildings-in-town 0)
    (or
        (military-population >= 6)
        (up-compare-goal g-nearest-enemy-defenses-distance >= 24))
    =>
    (set-strategic-number sn-safe-town-size 16)
    (set-strategic-number csn-peaceful-town-size 16)
    (up-modify-sn sn-maximum-town-size c:max 16)
    ; (up-chat-data-to-all "Nearest Defenses %d" g: g-nearest-enemy-defenses-distance)
    )

    (defrule
    (population > 60)
    (or
        (up-compare-sn sn-safe-town-size < 18)
        (or
            (up-compare-sn sn-maximum-town-size < 18)
            (up-compare-sn csn-peaceful-town-size < 18)))
    ; (goal g-enemy-buildings-in-town 0)
    (or
        (military-population >= 6)
        (up-compare-goal g-nearest-enemy-defenses-distance >= 27))
    =>
    (set-strategic-number sn-safe-town-size 18)
    (set-strategic-number csn-peaceful-town-size 18)
    (up-modify-sn sn-maximum-town-size c:max 18)
    )

    (defrule
    (or
        (population > 100)
        (building-type-count-total town-center > 2))
    (or
        (up-compare-sn sn-safe-town-size < 22)
        (or
            (up-compare-sn sn-maximum-town-size < 22)
            (up-compare-sn csn-peaceful-town-size < 22)))
    (goal g-enemy-buildings-in-town 0)
    (or
        (military-population >= 6)
        (up-compare-goal g-nearest-enemy-defenses-distance >= 33))
    =>
    (set-strategic-number sn-safe-town-size 22)
    (set-strategic-number csn-peaceful-town-size 22)
    (up-modify-sn sn-maximum-town-size c:max 22)
    )

    (defrule
    (population > 150)
    (goal g-enemy-buildings-in-town 0)
    (or
        (up-compare-sn sn-safe-town-size < 25)
        (or
            (up-compare-sn sn-maximum-town-size < 25)
            (up-compare-sn csn-peaceful-town-size < 25)))
    (or
        (military-population >= 6)
        (up-compare-goal g-nearest-enemy-defenses-distance >= 36))
    =>
    (set-strategic-number sn-safe-town-size 25)
    (set-strategic-number csn-peaceful-town-size 25)
    (up-modify-sn sn-maximum-town-size c:max 25)
    )

    (defrule
    (current-age == imperial-age)
    (goal g-enemy-buildings-in-town 0)
    (or
        (up-compare-sn sn-safe-town-size < 28)
        (or
            (up-compare-sn sn-maximum-town-size < 28)
            (up-compare-sn csn-peaceful-town-size < 28)))
    (or
        (military-population >= 6)
        (up-compare-goal g-nearest-enemy-defenses-distance >= 42))
    =>
    (set-strategic-number sn-safe-town-size 28)
    (set-strategic-number csn-peaceful-town-size 28)
    (up-modify-sn sn-maximum-town-size c:max 28)
    )

    (defrule
    (current-age == imperial-age)
    (population > 170)
    (goal g-enemy-buildings-in-town 0)
    (or
        (up-compare-sn sn-safe-town-size < 32)
        (or
            (up-compare-sn sn-maximum-town-size < 32)
            (up-compare-sn csn-peaceful-town-size < 32)))
    (or
        (military-population >= 6)
        (up-compare-goal g-nearest-enemy-defenses-distance >= 48))
    =>
    (set-strategic-number sn-safe-town-size 32)
    (set-strategic-number csn-peaceful-town-size 32)
    (up-modify-sn sn-maximum-town-size c:max 32)
    )

    #load-if-defined TEST-LATER

        (load "The General 1.2/Functions/Reset")

        (defrule
        (current-age >= castle-age)
        (goal g-enemy-buildings-in-town 0)
        (building-type-count town-center > 0)
        =>
        (up-full-reset-search)
        (up-find-local c: town-center c: 10)
        (set-goal g-loop-counter 0)
        (set-goal g-temp 0)
        (set-goal g-temp2 0)
        (set-goal g-temp3 0)
        )

            (defrule
            (current-age >= castle-age)
            (goal g-enemy-buildings-in-town 0)
            (building-type-count town-center > 0)
            (up-set-target-object search-local g: g-loop-counter)
            =>
            (up-get-object-data object-data-point-x g-temp)
            (up-modify-goal g-temp g:- g-position-self-x)   ;get difference between position-self-b and TC location-b
            (up-modify-goal g-temp2 g:neg g-temp)   ;get opposite result of g-temp
            (up-modify-goal g-temp g:max g-temp2)   ;get absolute value of difference between position-self-x and TC location-x
            (up-modify-goal g-temp3 g:max g-temp)
            (up-get-object-data object-data-point-y g-temp)
            (up-modify-goal g-temp g:- g-position-self-y)   ;get difference between position-self-y and TC location-y
            (up-modify-goal g-temp2 g:neg g-temp)   ;get opposite result of g-temp
            (up-modify-goal g-temp g:max g-temp2)   ;get absolute value of difference between position-self-y and TC location-y
            (up-modify-goal g-temp3 g:max g-temp)
            )

            (defrule
            (current-age >= castle-age)
            (goal g-enemy-buildings-in-town 0)
            (building-type-count town-center > 0)
            (up-modify-goal g-loop-counter c:+ 1)
            (up-set-target-object search-local g: g-loop-counter)
            (up-compare-goal g-loop-counter <= 10)
            =>
            (up-jump-rule -2)
            )

        (defrule
        (current-age >= castle-age)
        (goal g-enemy-buildings-in-town 0)
        (building-type-count town-center > 0)
        (up-modify-goal g-temp3 c:+ 3)  ;to account for farms around TC
        (up-compare-sn csn-peaceful-town-size g:< g-temp3)
        =>
        (up-modify-sn sn-safe-town-size g:max g-temp3)
        (up-modify-sn csn-peaceful-town-size g:max g-temp3)
        (up-modify-sn sn-maximum-town-size g:max g-temp3)
        )

    #end-if

    ;Reduced town size if enemy towers in town
    (defrule
    (military-population < 6)
    (goal g-attacking NO)
    (or
        (up-building-type-in-town c: watch-tower >= 1)
        (up-building-type-in-town c: donjon >= 1))
    (up-compare-sn sn-maximum-town-size > 12)
    =>
    (up-modify-sn sn-maximum-town-size c:- 1)
    (up-modify-sn sn-safe-town-size s:min sn-maximum-town-size)
    (up-modify-sn csn-peaceful-town-size s:min sn-maximum-town-size)
    ; (up-chat-data-to-player my-player-number CHAT-REDUCE-TS-FOR-TOWERS s: sn-maximum-town-size)
    )

;-------------------------------
;   SN-NUMBER-EXPLORE-GROUPS
;   SN-CAP-CIVILIAN-EXPLORERS
;-------------------------------

    (load "The General 1.2/Functions/Reset Mini")

    ;Explore with all scouts if game is early and we haven't found enemy
    (defrule
    (timer-triggered t-5-sec)
    (goal g-scouting-status AUTO-SCOUT)
    (strategic-number sn-number-explore-groups == 0)
    ; (game-time < 600)
    ; (players-building-count every-enemy == 0)
    (military-population > 0)
    =>
    (up-get-fact military-population 0 g-temp)
    (up-get-fact unit-type-count monastery-class g-temp2)
    (up-modify-goal g-temp g:- g-temp2)    ;remove monks and missionaries from military-population count
    (up-modify-sn sn-total-number-explorers g:min g-temp)
    (up-modify-sn sn-total-number-explorers c:max 1)
    (up-modify-sn sn-number-explore-groups g:= g-temp)
    (up-modify-sn sn-number-explore-groups c:max 1)
    (up-reset-scouts)
    )
    
    ;Explore with one scout if we have found the enemy
    (defrule
    (goal g-scouting-status AUTO-SCOUT)
    (strategic-number sn-number-explore-groups > 1)
    ; (or
    ;     (game-time >= 600)
    ;     (players-building-count any-enemy > 0))
    (military-population > 0)
    =>
    (up-modify-sn sn-total-number-explorers c:min 1)
    (set-strategic-number sn-number-explore-groups 1)
    (up-reset-scouts)
    )

    (defrule
    (up-compare-goal g-scouting-status != AUTO-SCOUT)
    (strategic-number sn-number-explore-groups != 0)
    =>
    (set-strategic-number sn-number-explore-groups 0)
    (up-reset-scouts)
    )

    (load "The General 1.2/Functions/Reset Mini")

    (defrule
    (timer-triggered t-10-sec)
    (goal g-scouting-status AUTO-SCOUT)
    (up-set-target-by-id g: g-scout1-id)
    (up-object-data object-data-action == -1)
    (up-add-object-by-id search-local g: g-scout1-id)
    =>
    (up-get-point position-center g-point-x)
    (up-target-point g-point-x action-move -1 -1)
    ; (chat-to-all "Idle scouts")
    )

    ;====EXPLORING SNS

    (defrule
    (up-compare-goal g-map-style != NOMAD)
    (building-type-count-total town-center > 0)
    (or
        (cc-players-unit-type-count 0 livestock-class > 0)
        (cc-players-unit-type-count 0 forage-class > 0))
    (goal g-current-sheep-count 0)
    (up-gaia-type-count c: food == 0)
    (strategic-number sn-percent-civilian-explorers < 100)
    (unit-type-count villager-builder == 0)
    (game-time < 45)
    =>
    (set-strategic-number sn-percent-civilian-explorers 100)
    (set-strategic-number sn-cap-civilian-explorers 5)
    (set-strategic-number sn-total-number-explorers 6)
    )

    (defrule
    (up-compare-goal g-map-style != NOMAD)
    (goal g-scouting-status AUTO-SCOUT)
    (or
        (up-compare-goal g-current-sheep-count > 0)
        (up-gaia-type-count c: food > 0))
    (building-type-count-total town-center > 0)
    (strategic-number sn-percent-civilian-explorers == 100)
    =>
    (set-strategic-number sn-percent-civilian-explorers 0)
    (set-strategic-number sn-percent-civilian-gatherers 100)
    (set-strategic-number sn-cap-civilian-explorers 0)
    (set-strategic-number sn-total-number-explorers 1)
    (set-strategic-number sn-number-explore-groups 1)
    )

    (defrule
    (up-compare-goal g-map-style != NOMAD)
    (up-compare-goal g-duc-scouting != STOP-DUC-SCOUTING)
    (or
        (up-compare-goal g-current-sheep-count > 0)
        (up-gaia-type-count c: food > 0))
    (building-type-count-total town-center > 0)
    (strategic-number sn-percent-civilian-explorers == 100)
    =>
    (set-strategic-number sn-percent-civilian-explorers 0)
    (set-strategic-number sn-percent-civilian-gatherers 100)
    (set-strategic-number sn-cap-civilian-explorers 0)
    (set-strategic-number sn-total-number-explorers 0)
    (set-strategic-number sn-number-explore-groups 0)
    (up-reset-scouts)
    )

    ;Explore with villagers if we have no scout
    
    (defrule
    (up-compare-goal g-map-style != NOMAD)
    (goal g-duc-scouting STOP-DUC-SCOUTING)
    (goal g-scout1-id 0)
    (military-population == 0)
    (building-type-count town-center > 0)
    (or
        (cc-players-unit-type-count 0 livestock-class > 0)
        (or
            (cc-players-unit-type-count 0 forage-class > 0)
            (cc-players-unit-type-count 0 boar-class > 0)))
    (strategic-number sn-percent-civilian-explorers < 99)
    (up-timer-status t-villager-explore-timer == timer-disabled)
    =>
    (set-strategic-number sn-percent-civilian-explorers 99)
    (set-strategic-number sn-cap-civilian-explorers 2)
    (up-modify-sn sn-total-number-explorers c:max 2)
    (enable-timer t-villager-explore-timer 120)
    )

    ;Stop exploring villagers after 120 seconds

    (defrule
    (up-compare-goal g-map-style != NOMAD)
    (goal g-duc-scouting STOP-DUC-SCOUTING)
    (strategic-number sn-percent-civilian-explorers == 99)
    (or
        (and
            (timer-triggered t-villager-explore-timer)
            (up-gaia-type-count c: food > 0))
        (military-population > 0))
    =>
    (set-strategic-number sn-percent-civilian-explorers 0)
    (set-strategic-number sn-percent-civilian-gatherers 100)
    (set-strategic-number sn-cap-civilian-explorers 0)
    (set-strategic-number sn-total-number-explorers 1)
    (set-strategic-number sn-number-explore-groups 1)
    )

    (defrule
    (up-compare-goal g-map-style != NOMAD)
    (up-compare-goal g-duc-scouting != STOP-DUC-SCOUTING)
    (strategic-number sn-percent-civilian-explorers == 99)
    (or
        (and
            (timer-triggered t-villager-explore-timer)
            (up-gaia-type-count c: food > 0))
        (military-population > 0))
    =>
    (set-strategic-number sn-percent-civilian-explorers 0)
    (set-strategic-number sn-percent-civilian-gatherers 100)
    (set-strategic-number sn-cap-civilian-explorers 0)
    (set-strategic-number sn-total-number-explorers 0)
    (set-strategic-number sn-number-explore-groups 0)
    (up-reset-scouts)
    )

;------------------------------
;   SN-HOME-EXPLORATION-TIME
;------------------------------

    (load "The General 1.2/Functions/Reset Mini")

    (defrule
    (building-type-count-total town-center > 0)
    =>
    (set-strategic-number sn-home-exploration-time 375)
    (up-get-fact game-time 0 g-temp)
    (up-modify-sn sn-home-exploration-time g:+ g-temp)
    (disable-self)
    )

;-----------------------------------
;   SN-MAXIMUM-FOOD-DROP-DISTANCE
;-----------------------------------

    (defrule
    (up-compare-goal g-usable-farm-count >= 20)
    (strategic-number sn-maximum-food-drop-distance < 12)
    =>
    (set-strategic-number sn-maximum-food-drop-distance 12)
    )

;---------------------------------
;   SN-LUMBER-CAMP-MAX-DISTANCE
;---------------------------------

    (defrule
    (or
        (resource-found wood)
        (up-gaia-type-count-total c: wood >= 40))
    (building-type-count-total lumber-camp == 0)
    (strategic-number sn-lumber-camp-max-distance < 15)
    =>
    (set-strategic-number sn-lumber-camp-max-distance 15)
    )

    (defrule
    (building-type-count-total lumber-camp > 0)
    =>
    (up-modify-sn sn-lumber-camp-max-distance c:+ 12)
    (disable-self)
    )

    (defrule
    (up-compare-sn sn-lumber-camp-max-distance s:< csn-peaceful-town-size)
    =>
    (up-modify-sn sn-lumber-camp-max-distance s:= csn-peaceful-town-size)
    )

    (load "The General 1.2/Functions/Reset Mini")

        ;Increase sn-lumber-camp-max-distance if we don't have any forest inside the distance
        (defrule
        (building-type-count town-center > 0)
        (dropsite-min-distance wood > 3)
        (dropsite-min-distance wood < 255)
        (up-compare-goal g-desired-num-wood-vils > 0)
        =>
        (up-set-target-point g-position-self-x)
        (up-full-reset-search)
        (up-filter-distance c: -1 s: sn-lumber-camp-max-distance)
        (up-find-resource c: wood c: 40)
        (up-get-search-state g-local-total)
        (set-goal g-temp 11111)
        )

        (defrule
        (goal g-temp 11111)
        (goal g-remote-total 0) ;no forest found
        (building-type-count town-center > 0)
        (dropsite-min-distance wood > 3)
        (dropsite-min-distance wood < 255)
        (up-compare-goal g-desired-num-wood-vils > 0)
        (strategic-number sn-lumber-camp-max-distance < HUNDRED-PERCENT-MAP-SIZE)
        =>
        (set-goal g-temp 11112)
        (up-modify-sn sn-lumber-camp-max-distance c:+ 5)
        (up-jump-rule -2)
        )

    (defrule
    (goal g-temp 11112)
    (strategic-number sn-lumber-camp-max-distance < HUNDRED-PERCENT-MAP-SIZE)
    (unit-type-count villager >= 50)
    =>
    (up-modify-sn sn-lumber-camp-max-distance c:+ 10)
    )

    ;Sometimes sn-wood-dropsite-distance prevents new lumber camps to be dropped even if
    ;forests are within sn-lumber-camp-max-distance. If so, increase sn-lumber-camp-max-distance
    (defrule
    (building-type-count town-center > 0)
    (dropsite-min-distance wood > 3)
    (dropsite-min-distance wood < 255)
    (up-compare-goal g-desired-num-wood-vils > 0)
    (strategic-number sn-lumber-camp-max-distance < HUNDRED-PERCENT-MAP-SIZE)
    (wood-amount > 100)
    (up-timer-status t-lumber-camp-increase != timer-running)
    =>
    (disable-timer t-lumber-camp-increase)
    (enable-timer t-lumber-camp-increase 30)
    )

    (defrule
    (up-timer-status t-lumber-camp-increase == timer-running)
    (dropsite-min-distance wood <= 3)
    =>
    (disable-timer t-lumber-camp-increase)
    )

    (defrule
    (timer-triggered t-lumber-camp-increase)
    (building-type-count town-center > 0)
    (dropsite-min-distance wood > 3)
    (dropsite-min-distance wood < 255)
    (up-compare-goal g-desired-num-wood-vils > 0)
    (strategic-number sn-lumber-camp-max-distance < HUNDRED-PERCENT-MAP-SIZE)
    (wood-amount > 100)
    =>
    (disable-timer t-lumber-camp-increase)
    (up-modify-sn sn-lumber-camp-max-distance c:+ 10)
    )

    (defrule
    (building-type-count town-center > 0)
    (dropsite-min-distance wood < 255)
    (up-compare-goal g-desired-num-wood-vils > 0)
    (strategic-number sn-lumber-camp-max-distance != HUNDRED-PERCENT-MAP-SIZE)
    (strategic-number sn-lumber-camp-max-distance >= SIXTY-PERCENT-MAP-SIZE)
    =>
    (set-strategic-number sn-lumber-camp-max-distance HUNDRED-PERCENT-MAP-SIZE)
    )

;---------------------------------
;   SN-MINING-CAMP-MAX-DISTANCE
;---------------------------------

    (defrule
    (strategic-number sn-mining-camp-max-distance < 24)
    (building-type-count-total mining-camp > 1)
    =>
    (set-strategic-number sn-mining-camp-max-distance 24)
    ; (up-chat-data-to-player my-player-number CHAT-CAMP-MINING-SET-DISTANCE s: sn-mining-camp-max-distance)
    )

    (defrule
    (up-compare-sn sn-mining-camp-max-distance s:< csn-peaceful-town-size)
    =>
    (up-modify-sn sn-mining-camp-max-distance s:= csn-peaceful-town-size)
    ; (up-chat-data-to-player my-player-number CHAT-CAMP-MINING-PEACEFUL-DISTANCE s: sn-mining-camp-max-distance)
    )

    (load "The General 1.2/Functions/Reset Mini")

        ;Increase sn-mining-camp-max-distance if we don't have any gold inside the distance
        (defrule
        (building-type-count town-center > 0)
        (dropsite-min-distance gold > 3)
        (dropsite-min-distance gold < 255)
        (up-compare-goal g-desired-num-gold-vils > 0)
        =>
        (up-set-target-point g-position-self-x)
        (up-full-reset-search)
        (up-filter-distance c: -1 s: sn-mining-camp-max-distance)
        (up-filter-status c: status-resource c: list-active)
        (up-find-resource c: gold c: 40)
        (up-get-search-state g-local-total)
        (set-goal g-temp 22222)
        )

        (defrule
        (goal g-temp 22222)
        (goal g-remote-total 0) ;no gold found
        (building-type-count town-center > 0)
        (dropsite-min-distance gold > 3)
        (dropsite-min-distance gold < 255)
        (up-compare-goal g-desired-num-gold-vils > 0)
        (strategic-number sn-mining-camp-max-distance < HUNDRED-PERCENT-MAP-SIZE)
        =>
        (set-goal g-temp -1)
        (up-modify-sn sn-mining-camp-max-distance c:+ 5)
        ; (up-chat-data-to-player my-player-number CHAT-CAMP-GOLD-INCREASE-DISTANCE s: sn-mining-camp-max-distance)
        (up-jump-rule -2)
        )

    (load "The General 1.2/Functions/Reset Mini")

        ;Increase sn-mining-camp-max-distance if we don't have any stone inside the distance
        (defrule
        (building-type-count town-center > 0)
        (dropsite-min-distance stone > 3)
        (dropsite-min-distance stone < 255)
        (up-compare-goal g-desired-num-stone-vils > 0)
        =>
        (up-set-target-point g-position-self-x)
        (up-full-reset-search)
        (up-filter-distance c: -1 s: sn-mining-camp-max-distance)
        (up-filter-status c: status-resource c: list-active)
        (up-find-resource c: stone c: 40)
        (up-get-search-state g-local-total)
        (set-goal g-temp 33333)
        )

        (defrule
        (goal g-temp 33333)
        (goal g-remote-total 0) ;no stone found
        (building-type-count town-center > 0)
        (dropsite-min-distance stone > 3)
        (dropsite-min-distance stone < 255)
        (up-compare-goal g-desired-num-stone-vils > 0)
        (strategic-number sn-mining-camp-max-distance < HUNDRED-PERCENT-MAP-SIZE)
        =>
        (set-goal g-temp -1)
        (up-modify-sn sn-mining-camp-max-distance c:+ 5)
        ; (up-chat-data-to-player my-player-number CHAT-CAMP-STONE-INCREASE-DISTANCE s: sn-mining-camp-max-distance)
        (up-jump-rule -2)
        )

    (defrule
    (building-type-count town-center > 0)
    (or
        (and
            (dropsite-min-distance gold < 255)
            (up-compare-goal g-desired-num-gold-vils > 0))
        (and
            (dropsite-min-distance stone < 255)
            (up-compare-goal g-desired-num-stone-vils > 0)))
    (strategic-number sn-mining-camp-max-distance != HUNDRED-PERCENT-MAP-SIZE)
    (strategic-number sn-mining-camp-max-distance >= SIXTY-PERCENT-MAP-SIZE)
    =>
    (set-strategic-number sn-mining-camp-max-distance HUNDRED-PERCENT-MAP-SIZE)
    ; (up-chat-data-to-player my-player-number CHAT-CAMP-MINING-MAX-DISTANCE s: sn-mining-camp-max-distance)
    )

;------------------------------
;   SN-CAP-CIVILIAN-BUILDERS
;------------------------------

    (defrule
    (or
        (building-type-count house > 2)
        (civ-selected hun))
    (strategic-number sn-cap-civilian-builders < 200)
    =>
    (set-strategic-number sn-cap-civilian-builders 200)
    )

;----------------------------
;   SN-ENABLE-BOAR-HUNTING
;----------------------------

    (defrule
    (up-compare-goal g-scouting-status <= PUSH-DEER)
    (or
        (up-compare-goal g-boar-found-count > 0)
        (up-compare-const MIN-BOAR == 0))
    (or
        (goal g-current-boar-count 0)
        (dropsite-min-distance deer-hunting < 5))
    (or
        (strategic-number sn-enable-boar-hunting != 1)
        (strategic-number sn-maximum-hunt-drop-distance < 4))
    =>
    (set-strategic-number sn-enable-boar-hunting 1)
    (set-strategic-number sn-maximum-hunt-drop-distance 4)
    )

    (defrule
    (up-compare-goal g-scouting-status > PUSH-DEER)
    (or
        (up-compare-goal g-boar-found-count > 0)
        (up-compare-const MIN-BOAR == 0))
    (or
        (goal g-current-boar-count 0)
        (dropsite-min-distance deer-hunting < 5))
    (or
        (strategic-number sn-enable-boar-hunting != 1)
        (strategic-number sn-maximum-hunt-drop-distance < 8))
    =>
    (set-strategic-number sn-enable-boar-hunting 1)
    (set-strategic-number sn-maximum-hunt-drop-distance 8)
    )

;-------------------------------
;   SN-MINIMUM-NUMBER-HUNTERS
;-------------------------------

    (defrule
    (up-compare-sn sn-minimum-number-hunters != 4)
    (or
        (up-compare-goal g-total-decaying-hunt-food > 0)
        (and
            (up-set-target-by-id g: g-current-boar-id)
            (up-object-data object-data-hitpoints < 5)))
    =>
    (up-modify-sn sn-minimum-number-hunters c:= 4)
    )

    (defrule
    (up-compare-sn sn-minimum-number-hunters != 0)
    (up-compare-goal g-total-decaying-hunt-food == 0)
    (or
        (not
            (up-set-target-by-id g: g-current-boar-id))
        (and
            (up-set-target-by-id g: g-current-boar-id)
            (up-object-data object-data-hitpoints >= 5)))
    =>
    (up-modify-sn sn-minimum-number-hunters c:= 0)
    )

;---------------------------------
;   SN-ALLOW-ADJACENT-DROPSITES
;---------------------------------

    (defrule
    (strategic-number sn-allow-adjacent-dropsites != 1)
    =>
    (set-strategic-number sn-allow-adjacent-dropsites 1)
    )

;---------------------------------
;   SN-PREFERRED-TRADE-DISTANCE
;---------------------------------

    (defrule
    (building-type-count market > 1)
    =>
    (set-strategic-number sn-preferred-trade-distance NINETY-PERCENT-MAP-SIZE)
    )

;-------------------------------
;   CSN-COUNTERING-PREFERENCE
;-------------------------------

    (defrule
    (or
        (goal g-position FLANK)
        (goal g-town-under-attack YES))
    =>
    (set-strategic-number csn-countering-preference 50)
    )

    (defrule
    (goal g-position POCKET)
    (goal g-town-under-attack NO)
    =>
    (set-strategic-number csn-countering-preference 25)
    )