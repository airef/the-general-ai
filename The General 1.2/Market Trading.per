;=========================<>=========================
;			   MARKET RESOURCE TRADING
;=========================<>=========================

    (load "The General 1.2/Functions/Reset")

    ;--------------------------
    ;   Trade for Castle Age
    ;--------------------------

		(defrule
		(up-timer-status t-5-sec == timer-triggered)
		(goal g-age-status SAVE-FOR-CASTLE)
		=>
		(up-get-fact commodity-buying-price food g-food-price)
		(up-get-fact commodity-buying-price wood g-wood-price)
		(up-get-fact commodity-buying-price stone g-stone-price)
		(up-setup-cost-data 1 g-food-delta)
		(up-add-research-cost c: castle-age c: 1)
		)

        (defrule
        (up-timer-status t-5-sec == timer-triggered)
        (goal g-age-status SAVE-FOR-CASTLE)
        (up-modify-goal g-temp g:= g-food-delta)    ;Castle Age food cost
        (up-modify-goal g-temp c:+ 100)
        (food-amount g:>= g-temp)
        (gold-amount g:< g-gold-delta)              ;Castle Age gold cost
        (can-sell-commodity food)
        (goal g-traded NO)
        =>
        (sell-commodity food)
        (set-goal g-traded YES)
        ; (chat-local-to-self CHAT-SELL-FOOD-CASTLE-AGE)
        )

        (defrule
        (up-timer-status t-5-sec == timer-triggered)
        (up-compare-goal g-age-status == SAVE-FOR-CASTLE)
        (up-modify-goal g-temp g:= g-food-delta)    ;Castle Age food cost
        (up-modify-goal g-temp c:+ 100)
        (food-amount g:< g-temp)
        (gold-amount g:< g-gold-delta)              ;Castle Age gold cost
        (or
            (wood-amount > 400)
            (and
                (wood-amount > 200)
                (goal g-town-under-attack YES)))
        (can-sell-commodity wood)
        (goal g-traded NO)
        =>
        (sell-commodity wood)
        (set-goal g-traded YES)
        ; (chat-local-to-self CHAT-SELL-WOOD-CASTLE-AGE)
        )

        (defrule
        (up-timer-status t-5-sec == timer-triggered)
        (up-compare-goal g-age-status == SAVE-FOR-CASTLE)
        (food-amount g:>= g-food-delta)             ;Castle Age food cost
        (gold-amount g:< g-gold-delta)              ;Castle Age gold cost
        (or
            (stone-amount > 200)
            (goal g-town-under-attack YES))
        (can-sell-commodity stone)
        (up-compare-goal g-tower-line g:>= g-desired-num-tower)
        (up-compare-goal g-desired-num-castle == 0)
        (goal g-traded NO)
        =>
        (sell-commodity stone)
        (set-goal g-traded YES)
        ; (chat-local-to-self CHAT-SELL-STONE-CASTLE-AGE)
        )

        ;Set gold cost
        (defrule
        (up-timer-status t-5-sec == timer-triggered)
        (goal g-age-status SAVE-FOR-CASTLE)
        (goal g-traded NO)
        =>
        (up-modify-goal g-gold-cost g:= g-gold-delta)   ;Castle age gold cost
        (up-modify-goal g-gold-cost g:+ g-food-price)    ;add food buying price
        )

        (defrule
        (up-timer-status t-5-sec == timer-triggered)
        (up-compare-goal g-age-status == SAVE-FOR-CASTLE)
        (food-amount g:< g-food-delta)      ;Castle Age food cost
        (up-resource-amount gold g:>= g-gold-cost)
        (can-buy-commodity food)
        (goal g-traded NO)
        =>
        (buy-commodity food)
        (set-goal g-traded YES)
        ; (chat-local-to-self CHAT-BUY-FOOD-CASTLE-AGE)
        )

    ;----------------------------
    ;   Trade for Imperial Age
    ;----------------------------

		(defrule
		(up-timer-status t-5-sec == timer-triggered)
		(goal g-age-status SAVE-FOR-IMPERIAL)
		=>
		(up-get-fact commodity-buying-price food g-food-price)
		(up-get-fact commodity-buying-price wood g-wood-price)
		(up-get-fact commodity-buying-price stone g-stone-price)
		(up-setup-cost-data 1 g-food-delta)
		(up-add-research-cost c: imperial-age c: 1)
		)

        (defrule
        (up-timer-status t-5-sec == timer-triggered)
        (up-compare-goal g-age-status == SAVE-FOR-IMPERIAL)
        (up-modify-goal g-temp g:= g-food-delta)    ;Imperial Age food cost
        (up-modify-goal g-temp c:+ 100)
        (food-amount g:>= g-temp)
        (gold-amount g:< g-gold-delta)              ;Imperial Age gold cost
        (can-sell-commodity food)
        (goal g-traded NO)
        =>
        (sell-commodity food)
        (set-goal g-traded YES)
        ; (chat-local-to-self CHAT-SELL-FOOD-IMPERIAL-AGE)
        )

        (defrule
        (up-timer-status t-5-sec == timer-triggered)
        (up-compare-goal g-age-status == SAVE-FOR-IMPERIAL)
        (up-modify-goal g-temp g:= g-food-delta)    ;Imperial Age food cost
        (up-modify-goal g-temp c:+ 100)
        (food-amount g:< g-temp)
        (gold-amount g:< g-gold-delta)              ;Imperial Age gold cost
        (wood-amount > 300)
        (can-sell-commodity wood)
        (goal g-traded NO)
        =>
        (sell-commodity wood)
        (set-goal g-traded YES)
        ; (chat-local-to-self CHAT-SELL-WOOD-IMPERIAL-AGE)
        )

        (defrule
        (up-timer-status t-5-sec == timer-triggered)
        (up-compare-goal g-age-status == SAVE-FOR-IMPERIAL)
        (food-amount g:>= g-food-delta)             ;Imperial age food cost
        (gold-amount g:< g-gold-delta)              ;Imperial age gold cost
        (stone-amount > 200)
        (up-object-type-count-total c: castle >= g-desired-num-castle)
        (can-sell-commodity stone)
        (goal g-traded NO)
        =>
        (sell-commodity stone)
        (set-goal g-traded YES)
        ; (chat-local-to-self CHAT-SELL-STONE-IMPERIAL-AGE)
        )

        (defrule
        (up-timer-status t-5-sec == timer-triggered)
        (goal g-age-status SAVE-FOR-IMPERIAL)
        (goal g-traded NO)
        =>
        (up-modify-goal g-gold-cost g:= g-gold-delta)
        (up-modify-goal g-gold-cost g:+ g-food-price)    ;add food buying price
        )

        (defrule
        (up-timer-status t-5-sec == timer-triggered)
        (up-compare-goal g-age-status == SAVE-FOR-IMPERIAL)
        (food-amount g:< g-food-delta)              ;Imperial Age food cost
        (up-resource-amount gold g:>= g-gold-cost)
        (can-buy-commodity food)
        (goal g-traded NO)
        =>
        (buy-commodity food)
        (set-goal g-traded YES)
        ; (chat-local-to-self CHAT-BUY-FOOD-IMPERIAL-AGE)
        )

		#load-if-defined INCAN-CIV
		
			(defrule
			(up-timer-status t-5-sec == timer-triggered)
			(goal g-age-status TO-CASTLE)
			(can-sell-commodity stone)
			(goal g-current-strategy FC-EAGLES-REVENGE)
			(or
				(up-compare-goal g-tower-line g:>= g-desired-num-tower)
				(stone-amount >= 206))
			(goal g-traded NO)
			=>
			(sell-commodity stone)
			(set-goal g-traded YES)
			; (chat-local-to-self CHAT-SELL-STONE-EAGLES)
			)

		#else

			(defrule
			(up-timer-status t-5-sec == timer-triggered)
			(goal g-age-status TO-CASTLE)
			(can-sell-commodity stone)
			(goal g-current-strategy FC-EAGLES-REVENGE)
			(or
				(up-compare-goal g-tower-line g:>= g-desired-num-tower)
				(stone-amount >= 225))
			(goal g-traded NO)
			=>
			(sell-commodity stone)
			(set-goal g-traded YES)
			; (chat-local-to-self CHAT-SELL-STONE-EAGLES)
			)

		#end-if

    ;--------------------------
    ;   Trade for Researches
    ;--------------------------

        (defrule
        (up-timer-status t-5-sec == timer-triggered)
        =>
        (up-get-fact commodity-buying-price food g-food-price)
        (up-get-fact commodity-buying-price wood g-wood-price)
        (up-get-fact commodity-buying-price stone g-stone-price)
        )

		;If we have more gold than needed and need food, buy food
        (defrule
        (up-timer-status t-5-sec == timer-triggered)
        (up-resource-amount food g:< g-max-food-needed)
        (up-modify-goal g-temp g:= g-max-gold-needed)
        (up-modify-goal g-temp g:+ g-food-price)         ;add food buying price
        (up-resource-amount gold g:>= g-temp)    ;we have more gold than we need, including price to buy food
        (gold-amount >= 250)
        (can-buy-commodity food)
        (goal g-traded NO)
        =>
        (buy-commodity food)
        (set-goal g-traded YES)
        ; (chat-local-to-self CHAT-BUY-FOOD-TECHS)
        )

		;If we have more gold than needed and need wood, buy wood
        (defrule
        (up-timer-status t-5-sec == timer-triggered)
        (up-resource-amount wood g:< g-max-wood-needed)
        (up-modify-goal g-temp g:= g-max-gold-needed)
        (up-modify-goal g-temp g:+ g-wood-price)         ;add wood buying price
        (up-resource-amount gold g:>= g-temp)    ;we have more gold than we need, including price to buy wood
        (gold-amount >= 300)
        (can-buy-commodity wood)
        (goal g-traded NO)
        =>
        (buy-commodity wood)
        (set-goal g-traded YES)
        ; (chat-local-to-self CHAT-BUY-WOOD-TECHS)
        )

		;If we have more gold than needed and need stone, buy stone
        (defrule
        (up-timer-status t-5-sec == timer-triggered)
        (up-resource-amount stone g:< g-max-stone-needed)
        (up-modify-goal g-temp g:= g-max-gold-needed)
        (up-modify-goal g-temp g:+ g-stone-price)        ;add stone buying price
        (up-resource-amount gold g:>= g-temp)    ;we have more gold than we need, including price to buy food
        (gold-amount >= 350)
        (can-buy-commodity stone)
        (goal g-traded NO)
        =>
        (buy-commodity stone)
        (set-goal g-traded YES)
        ; (chat-local-to-self CHAT-BUY-STONE-TECHS)
        )

		;If we have more stone than needed and need gold, sell stone
        (defrule
        (up-timer-status t-5-sec == timer-triggered)
        (up-resource-amount gold g:< g-max-gold-needed)
        (up-modify-goal g-temp g:= g-max-stone-needed)
        (up-modify-goal g-temp c:+ 100)
        (up-resource-amount stone g:>= g-temp)    ;we have >= 100 stone more than we need
        (stone-amount >= 250)
        (can-sell-commodity stone)
        (goal g-traded NO)
        =>
        (sell-commodity stone)
        (set-goal g-traded YES)
        ; (chat-local-to-self CHAT-SELL-STONE-TECHS)
        )

		;If we have more wood than needed and need gold, sell wood
        (defrule
        (up-timer-status t-5-sec == timer-triggered)
        (up-resource-amount gold g:< g-max-gold-needed)
        (up-modify-goal g-temp g:= g-max-wood-needed)
        (up-modify-goal g-temp c:+ 100)
        (up-resource-amount wood g:>= g-temp)    ;we have >= 100 wood more than we need
        (wood-amount >= 250)
        (can-sell-commodity wood)
        (goal g-traded NO)
        =>
        (sell-commodity wood)
        (set-goal g-traded YES)
        ; (chat-local-to-self CHAT-SELL-WOOD-TECHS)
        )

		;If we have more food than needed and don't have enough gold, sell food
        (defrule
        (up-timer-status t-5-sec == timer-triggered)
        (up-resource-amount gold g:< g-max-gold-needed)
        (up-modify-goal g-temp g:= g-max-food-needed)
        (up-modify-goal g-temp c:+ 100)
        (up-resource-amount food g:>= g-temp)    ;we have >= 100 food more than we need
        (food-amount >= 250)
        (can-sell-commodity food)
        (goal g-traded NO)
        =>
        (sell-commodity food)
        (set-goal g-traded YES)
        ; (chat-local-to-self CHAT-SELL-FOOD-TECHS)
        )

		;If we have more stone than needed and don't have enough gold to buy food, sell stone
        (defrule
        (up-timer-status t-5-sec == timer-triggered)
        (up-resource-amount gold g:< g-max-gold-needed)
        (up-resource-amount food g:< g-max-food-needed)
        (up-modify-goal g-temp g:= g-max-stone-needed)
        (up-modify-goal g-temp c:+ 100)
        (up-resource-amount stone g:>= g-temp)    ;we have >= 100 stone more than we need
        (stone-amount >= 300)
        (can-sell-commodity stone)
        (goal g-traded NO)
        =>
        (sell-commodity stone)
        (set-goal g-traded YES)
        ; (chat-local-to-self CHAT-SELL-STONE-FOR-FOOD)
        )

		;If we have more stone than needed and don't have enough gold to buy wood, sell stone
        (defrule
        (up-timer-status t-5-sec == timer-triggered)
        (up-resource-amount gold g:< g-max-gold-needed)
        (up-resource-amount wood g:< g-max-wood-needed)
        (up-modify-goal g-temp g:= g-max-stone-needed)
        (up-modify-goal g-temp c:+ 100)
        (up-resource-amount stone g:>= g-temp)    ;we have >= 100 stone more than we need
        (stone-amount >= 350)
        (can-sell-commodity stone)
        (goal g-traded NO)
        =>
        (sell-commodity stone)
        (set-goal g-traded YES)
        ; (chat-local-to-self CHAT-SELL-STONE-FOR-WOOD)
        )

		;If we have more wood than needed and don't have enough gold to buy food, sell wood
        (defrule
        (up-timer-status t-5-sec == timer-triggered)
        (up-resource-amount gold g:< g-max-gold-needed)
        (up-resource-amount food g:< g-max-food-needed)
        (up-modify-goal g-temp g:= g-max-wood-needed)
        (up-modify-goal g-temp c:+ 100)
        (up-resource-amount wood g:>= g-temp)    ;we have >= 100 wood more than we need
        (wood-amount >= 300)
        (can-sell-commodity wood)
        (goal g-traded NO)
        =>
        (sell-commodity wood)
        (set-goal g-traded YES)
        ; (chat-local-to-self CHAT-SELL-WOOD-FOR-FOOD)
        )

		;If we have more wood than needed and don't have enough gold to buy stone, sell wood
        (defrule
        (up-timer-status t-5-sec == timer-triggered)
        (up-resource-amount gold g:< g-max-gold-needed)
        (up-resource-amount stone g:< g-max-stone-needed)
        (up-modify-goal g-temp g:= g-max-wood-needed)
        (up-modify-goal g-temp c:+ 100)
        (up-resource-amount wood g:>= g-temp)    ;we have >= 100 wood more than we need
        (wood-amount >= 350)
        (can-sell-commodity wood)
        (goal g-traded NO)
        =>
        (sell-commodity wood)
        (set-goal g-traded YES)
        ; (chat-local-to-self CHAT-SELL-WOOD-FOR-STONE)
        )

		;If we have more food than needed and don't have enough gold to buy wood, sell food
        (defrule
        (up-timer-status t-5-sec == timer-triggered)
        (up-resource-amount wood g:< g-max-wood-needed)
        (up-resource-amount gold g:< g-max-gold-needed)
        (up-modify-goal g-temp g:= g-max-food-needed)
        (up-modify-goal g-temp c:+ 100)
        (up-resource-amount food g:>= g-temp)    ;we have >= 100 food more than we need
        (food-amount >= 300)
        (can-sell-commodity food)
        (goal g-traded NO)
        =>
        (sell-commodity food)
        (set-goal g-traded YES)
        ; (chat-local-to-self CHAT-SELL-FOOD-FOR-WOOD)
        )

		;If we have more food than needed and don't have enough gold to buy stone, sell food
        (defrule
        (up-timer-status t-5-sec == timer-triggered)
        (up-resource-amount gold g:< g-max-gold-needed)
        (up-resource-amount stone g:< g-max-stone-needed)
        (up-modify-goal g-temp g:= g-max-food-needed)
        (up-modify-goal g-temp c:+ 100)
        (up-resource-amount food g:>= g-temp)    ;we have >= 100 food more than we need
        (food-amount >= 350)
        (can-sell-commodity food)
        (goal g-traded NO)
        =>
        (sell-commodity food)
        (set-goal g-traded YES)
        ; (chat-local-to-self CHAT-SELL-FOOD-FOR-STONE)
        )

    ;---------------------
    ;   Trade Resources
    ;---------------------

        (defrule
        (current-age == imperial-age)
        (up-timer-status t-5-sec == timer-triggered)
        (food-amount < 200)
        (gold-amount >= 600)
        (can-buy-commodity food)
        (goal g-traded NO)
        =>
        (buy-commodity food)
        (set-goal g-traded YES)
        ; (chat-local-to-self CHAT-BUY-FOOD)
        )

        (defrule
        (current-age == imperial-age)
        (up-timer-status t-5-sec == timer-triggered)
        (wood-amount < 200)
        (gold-amount >= 600)
        (can-buy-commodity wood)
        (goal g-traded NO)
        =>
        (buy-commodity wood)
        (set-goal g-traded YES)
        ; (chat-local-to-self CHAT-BUY-WOOD)
        )

        (defrule
        (current-age == imperial-age)
        (up-timer-status t-5-sec == timer-triggered)
        (wood-amount >= 1200)
        (gold-amount < 200)
        (can-sell-commodity wood)
        (goal g-traded NO)
        =>
        (sell-commodity wood)
        (set-goal g-traded YES)
        ; (chat-local-to-self CHAT-SELL-WOOD)
        )

        (defrule
        (current-age == imperial-age)
        (up-timer-status t-5-sec == timer-triggered)
        (food-amount >= 1400)
        (gold-amount < 200)
        (can-sell-commodity food)
        (goal g-traded NO)
        =>
        (sell-commodity food)
        (set-goal g-traded YES)
        ; (chat-local-to-self CHAT-SELL-FOOD)
        )

    ;----------------------------
    ;   Excess Trade Resources
    ;----------------------------

        (defrule
        (current-age == castle-age)
        (food-amount < 200)
        (gold-amount >= 1000)
        (can-buy-commodity food)
        (goal g-traded NO)
        =>
        (buy-commodity food)
        (set-goal g-traded YES)
        ; (chat-local-to-self CHAT-BUY-FOOD-EXCESS-GOLD)
        )

        (defrule
        (current-age == castle-age)
        (wood-amount < 200)
        (gold-amount >= 1000)
        (can-buy-commodity wood)
        (goal g-traded NO)
        =>
        (buy-commodity wood)
        (set-goal g-traded YES)
        ; (chat-local-to-self CHAT-BUY-WOOD-EXCESS-GOLD)
        )

        (defrule
        (current-age == castle-age)
        (wood-amount >= 800)
        (gold-amount < 200)
        (can-sell-commodity wood)
        (goal g-traded NO)
        =>
        (sell-commodity wood)
        (set-goal g-traded YES)
        ; (chat-local-to-self CHAT-SELL-WOOD-EXCESS)
        )

        (defrule
        (current-age == castle-age)
        (food-amount >= 1200)
        (gold-amount < 200)
        (can-sell-commodity food)
        (goal g-traded NO)
        =>
        (sell-commodity food)
        (set-goal g-traded YES)
        ; (chat-local-to-self CHAT-SELL-FOOD-EXCESS)
        )

        (defrule
        (current-age == imperial-age)
        (food-amount < 300)
        (gold-amount >= 1600)
        (can-buy-commodity food)
        (goal g-traded NO)
        =>
        (buy-commodity food)
        (set-goal g-traded YES)
        ; (chat-local-to-self CHAT-BUY-FOOD-EXCESS-GOLD)
        )

        (defrule
        (current-age == imperial-age)
        (wood-amount < 300)
        (gold-amount >= 1600)
        (can-buy-commodity wood)
        (goal g-traded NO)
        =>
        (buy-commodity wood)
        (set-goal g-traded YES)
        ; (chat-local-to-self CHAT-BUY-WOOD-EXCESS-GOLD)
        )

        (defrule
        (current-age == imperial-age)
        (wood-amount >= 1500)
        (gold-amount < 300)
        (can-sell-commodity wood)
        (goal g-traded NO)
        =>
        (sell-commodity wood)
        (set-goal g-traded YES)
        ; (chat-local-to-self CHAT-SELL-WOOD-EXCESS)
        )

        (defrule
        (current-age == imperial-age)
        (food-amount >= 1800)
        (gold-amount < 300)
        (can-sell-commodity food)
        (goal g-traded NO)
        =>
        (sell-commodity food)
        (set-goal g-traded YES)
        ; (chat-local-to-self CHAT-SELL-FOOD-EXCESS)
        )

