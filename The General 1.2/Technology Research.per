;Technology Research.per

;=========================<>=========================
;				      ECO TECHS
;=========================<>=========================
			
	;--------------------
	;	Double-Bit Axe
	;--------------------

		#load-if-not-defined BURGUNDIANS-CIV

			(defrule
			(or
				(goal g-strategy-type DRUSH)
				(or
					(goal g-strategy-type FLUSH)
					(and
						(up-compare-goal g-current-strategy >= BOOMI-STRATS-START)	;Boom Imperial Strats
						(up-compare-goal g-current-strategy <= BOOMI-STRATS-END))))
			(can-research-with-escrow ri-double-bit-axe)
			(up-compare-goal g-age-status >= TO-FEUDAL)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: ri-double-bit-axe c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow wood g:- g-wood-cost)
			(research ri-double-bit-axe)
			)

		#else

			(defrule
			(or
				(goal g-strategy-type DRUSH)
				(or
					(goal g-strategy-type FLUSH)
					(and
						(up-compare-goal g-current-strategy >= BOOMI-STRATS-START)	;Boom Imperial Strats
						(up-compare-goal g-current-strategy <= BOOMI-STRATS-END))))
			(can-research-with-escrow ri-double-bit-axe)
			(unit-type-count villager >= 10)
			(unit-type-count-total villager >= 11)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: ri-double-bit-axe c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow wood g:- g-wood-cost)
			(research ri-double-bit-axe)
			)

		#end-if

		(defrule
		(or
			(goal g-strategy-type FAST-CASTLE)
			(goal g-strategy-type FAST-IMPERIAL))
		(or
			(up-compare-goal g-current-strategy != FC-STRONGBOW)
			(up-compare-goal g-primary-unit-tech-progress >= PRIORITY-CASTLE-COMPLETE))
		(can-research-with-escrow ri-double-bit-axe)
		(up-compare-goal g-age-status >= TO-CASTLE)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-double-bit-axe c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow wood g:- g-wood-cost)
		(research ri-double-bit-axe)
		)

    ;-------------
    ;   Bow Saw
    ;-------------

		(defrule
		(can-research-with-escrow ri-bow-saw)
		(or
			(up-compare-goal g-current-strategy != FC-STRONGBOW)
			(up-compare-goal g-age-status >= MID-CASTLE))
		(up-compare-goal g-primary-unit-tech-progress >= REQUIRED-CASTLE-COMPLETE)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-bow-saw c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow wood g:- g-wood-cost)
		(research ri-bow-saw)
		)

    ;-----------------
    ;   Two-Man Saw
    ;-----------------

		(defrule
		(can-research ri-two-man-saw)
		(up-compare-goal g-game-focus != DEFENSIVE)
		(up-compare-goal g-primary-unit-tech-progress >= SECONDARY-IMPERIAL-COMPLETE)
		(population >= NINETY-PERCENT-POP)
		=>
		(research ri-two-man-saw)
		)

	;--------------------
	;	Horse Collar
	;--------------------

		(defrule
		(or
			(goal g-strategy-type FAST-CASTLE)
			(goal g-strategy-type FAST-IMPERIAL))
		(or
			(up-compare-goal g-current-strategy != FC-STRONGBOW)
			(up-compare-goal g-primary-unit-tech-progress >= SECONDARY-CASTLE-COMPLETE))
		(up-research-status c: ri-double-bit-axe >= research-pending)
		(can-research-with-escrow ri-horse-collar)
		(up-compare-goal g-age-status >= TO-CASTLE)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-horse-collar c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow wood g:- g-wood-cost)
		(research ri-horse-collar)
		)

    ;----------------
    ;   Heavy Plow
    ;----------------

		(defrule
		(or
			(up-compare-goal g-usable-farm-count < 20)
			(or
				(and
					(up-object-type-count c: town-center g:< g-desired-num-town-center)
					(and
						(building-type-count-total town-center < 2)
						(up-compare-goal g-usable-farm-count < 25)))
				(and
					(goal g-game-focus DEFENSIVE)
					(military-population g:< g-min-military-pop))))
		=>
		(up-jump-rule 1)
		)

			(defrule
			(can-research-with-escrow ri-heavy-plow)
			(up-research-status c: ri-bow-saw >= research-pending)
			(or
				(up-compare-goal g-age-status >= MID-CASTLE)
				(and
					(goal g-position POCKET)
					(up-compare-goal g-primary-unit-tech-progress >= PRIORITY-CASTLE-COMPLETE)))
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: ri-heavy-plow c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow wood g:- g-wood-cost)
			(research ri-heavy-plow)
			)

    ;-------------------
    ;   Crop Rotation
    ;-------------------

		(defrule
		(can-research ri-crop-rotation)
		(up-compare-goal g-game-focus != DEFENSIVE)
		(up-compare-goal g-primary-unit-tech-progress >= SECONDARY-IMPERIAL-COMPLETE)
		(population >= NINETY-FIVE-PERCENT-POP)
		=>
		(research ri-crop-rotation)
		)

	;-----------------
	;	Wheelbarrow
	;-----------------

        (defrule
        (can-research-with-escrow ri-wheel-barrow)
		(or
			(building-type-count town-center > 1)
			(up-compare-goal g-usable-farm-count >= 18))
		(up-compare-goal g-usable-farm-count >= 14)
		(or
			(up-compare-goal g-game-focus != DEFENSIVE)
			(military-population g:>= g-min-military-pop))
        =>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-wheel-barrow c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow wood g:- g-wood-cost)
		(research ri-wheel-barrow)
        )

	;---------------
	;	Hand Cart
	;---------------

		(defrule
		(goal g-game-focus BOOM)
		(can-research-with-escrow ri-hand-cart)
		(or
			(building-type-count town-center > 1)
			(up-compare-goal g-usable-farm-count >= 25))
		(up-compare-goal g-usable-farm-count >= 20)
		(population >= 80)
		(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
		(up-research-status c: ri-heavy-plow >= research-pending)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-hand-cart c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow wood g:- g-wood-cost)
		(research ri-hand-cart)
		)

		(defrule
		(or
			(population >= 100)
			(up-compare-goal g-usable-farm-count >= 25))
		(or
			(up-compare-goal g-game-focus != DEFENSIVE)
			(military-population g:>= g-min-military-pop))
		(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
		(up-research-status c: ri-heavy-plow >= research-pending)
		(can-research ri-hand-cart)
		=>
		(research ri-hand-cart)
		)

    ;-----------------
    ;   Gold Mining
    ;-----------------

        (defrule
        (up-compare-goal g-eco-tech-progress >= SECONDARY-CASTLE-COMPLETE)
		(or
			(up-compare-goal g-game-focus != DEFENSIVE)
			(military-population g:>= g-min-military-pop))
		(unit-type-count villager-gold >= 7)
        (can-research ri-gold-mining)
        =>
		(research ri-gold-mining)
        )
		
	;-----------------------
    ;   Gold Shaft Mining
    ;-----------------------

        (defrule
		(current-age == castle-age)
        (or
			(up-compare-flag g-flag == FIRST-ATTACK-LAUNCHED)
			(goal g-position POCKET))
		(up-compare-goal g-eco-tech-progress >= SECONDARY-CASTLE-COMPLETE)
		(or
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(food-amount >= 1200))
		(up-compare-goal g-game-focus != DEFENSIVE)
		(unit-type-count villager-gold >= 15)
        (can-research ri-gold-shaft-mining)
        =>
		(research ri-gold-shaft-mining)
        )

        (defrule
        (or
			(up-compare-flag g-flag == FIRST-ATTACK-LAUNCHED)
			(goal g-position POCKET))
		(up-compare-goal g-eco-tech-progress >= ALL-CASTLE-COMPLETE)
		(or
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(food-amount >= 1200))
		(up-compare-goal g-game-focus != DEFENSIVE)
		(unit-type-count villager-gold >= 12)
        (can-research ri-gold-shaft-mining)
        =>
		(research ri-gold-shaft-mining)
        )

		(defrule
		(up-compare-goal g-eco-tech-progress >= ALL-CASTLE-COMPLETE)
		(or
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(food-amount >= 1200))
		(up-compare-goal g-game-focus != DEFENSIVE)
		(population >= EIGHTY-PERCENT-POP)
		(can-research ri-gold-shaft-mining)
		(dropsite-min-distance gold < 255)
		=>
		(research ri-gold-shaft-mining)
		)
		
    ;------------------
    ;   Stone Mining
    ;------------------

        (defrule
        (up-compare-goal g-eco-tech-progress >= SECONDARY-CASTLE-COMPLETE)
		(or
			(goal g-current-strategy FC-CASTLE-DROP)
			(or
				(up-compare-goal g-desired-num-castle >= 3)
				(or
					(up-compare-goal g-desired-num-tower >= 3)
					(up-compare-goal g-desired-num-bombard-tower >= 3))))
		(or
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(food-amount >= 1100))
		(up-compare-goal g-game-focus != DEFENSIVE)
		(unit-type-count villager-stone > 6)
        (can-research ri-stone-mining)
        =>
		(research ri-stone-mining)
        )

		(defrule
		(up-compare-goal g-eco-tech-progress >= ALL-CASTLE-COMPLETE)
		(or
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(food-amount >= 1100))
		(up-compare-goal g-game-focus != DEFENSIVE)
		(population >= SEVENTY-PERCENT-POP)
		(can-research ri-stone-mining)
		(unit-type-count villager-stone > 6)
		(dropsite-min-distance stone < 255)
		=>
		(research ri-stone-mining)
		)

	;------------------------
    ;   Stone Shaft Mining
    ;------------------------

		(defrule
		(current-age == imperial-age)
		(up-compare-goal g-eco-tech-progress >= ALL-IMPERIAL-COMPLETE)
		(up-compare-goal g-game-focus != DEFENSIVE)
		(population >= NINETY-FIVE-PERCENT-POP)
		(unit-type-count villager-stone > 6)
		(can-research ri-stone-shaft-mining)
		(dropsite-min-distance stone < 255)
		=>
		(research ri-stone-shaft-mining)
		)

;=========================<>=========================
;				    CRITICAL TECHS
;=========================<>=========================

	;----------
	;	Loom
	;----------

		(defrule
		(can-research-with-escrow ri-loom)
		(or
			(cc-players-unit-type-count 0 elephant > 0)
			(cc-players-unit-type-count 0 rhino > 0))
		(unit-type-count villager >= 10)
		(up-compare-goal g-current-boar-count > 0)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-loom c: 1)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-loom)
		)

		(defrule
		(can-research-with-escrow ri-loom)
		(or
			(up-object-type-count c: villager g:>= g-required-num-villager)
			(or
				(goal g-town-under-attack YES)
				(players-military-population any-enemy > 1)))
		(unit-type-count villager >= 15)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-loom c: 1)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-loom)
		)

		(defrule
		(can-research-with-escrow ri-loom)
		(or
			(food-amount < 35)
			(housing-headroom == 0))
		(not
			(can-train villager))
		(unit-type-count villager >= 7)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-loom c: 1)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-loom)
		)

		#load-if-defined DE-AVAILABLE

			(defrule
			(can-research-with-escrow ri-loom)
			(or
				(civ-selected mayan)
				(or
					(civ-selected chinese)
					(civ-selected gothic)))
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: ri-loom c: 1)
			(up-modify-escrow gold g:- g-gold-cost)
			(research ri-loom)
			)

		#else

			(defrule
			(can-research-with-escrow ri-loom)
			(or
				(civ-selected mayan)
				(civ-selected chinese))
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: ri-loom c: 1)
			(up-modify-escrow gold g:- g-gold-cost)
			(research ri-loom)
			)
		
		#end-if

	;-----------------
	;	Feudal Age
	;-----------------

		(defrule
		(can-research-with-escrow feudal-age)
		(or
			(up-object-type-count c: villager g:>= g-required-num-villager)
			(or
				(goal g-game-focus REBUILD)
				(goal g-game-focus DEFENSIVE)))
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: feudal-age c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(research feudal-age)
		)
		
	;-----------------
	;	Castle Age
	;-----------------

		(defrule
		(can-research-with-escrow castle-age)
		(or
			(up-object-type-count c: villager g:>= g-required-num-villager)
			(or
				(goal g-game-focus REBUILD)
				(or
					(goal g-game-focus DEFENSIVE)
					(goal g-strategy-type FAST-CASTLE))))
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: castle-age c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow gold g:- g-gold-cost)
		(research castle-age)
		)
		
	;-----------------
	;	Imperial Age
	;-----------------

		(defrule
		(can-research-with-escrow imperial-age)
		(or
			(goal g-age-status SAVE-FOR-IMPERIAL)
			(or
				(up-object-type-count c: villager g:>= g-required-num-villager)
				(or
					(goal g-game-focus REBUILD)
					(goal g-game-focus DEFENSIVE))))
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: imperial-age c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow gold g:- g-gold-cost)
		(research imperial-age)
		)
		(defrule
		(up-get-fact current-age 0 g-temp)						;store our current age
		(up-get-fact-max any-enemy current-age 0 g-temp2)		;store current age of most advanced enemy
		(can-research-with-escrow imperial-age)
		(up-compare-goal g-temp g:< g-temp2)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: imperial-age c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow gold g:- g-gold-cost)
		(research imperial-age)
		)

	;---------------------------------------------------------------
	;	Unique Unit Upgrade - Priority or Secondary Imperial Tech
	;---------------------------------------------------------------

		(defrule
		(or
			(goal g-game-focus BOOM)
			(goal g-game-focus REBUILD))
		(up-object-type-count-total c: town-center g:< g-desired-num-town-center)
		(or
			(civ-selected berbers)
			(civ-selected mayan))
		=>
		(up-jump-rule 3)
		)

			(defrule
			(goal g-primary-unit my-unique-unit)
			; (or
			; 	(up-compare-goal g-unique-unit-line >= 7)
			; 	(up-compare-goal g-unique-unit-line g:>= g-desired-num-unique-unit))
			(can-research-with-escrow my-unique-unit-upgrade)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-unique-unit-upgrade c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow wood g:- g-wood-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-unique-unit-upgrade)
			)
			(defrule
			(goal g-support-unit my-unique-unit)
			(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)
			; (or
			; 	(up-compare-goal g-unique-unit-line >= 7)
			; 	(up-compare-goal g-unique-unit-line g:>= g-desired-num-unique-unit))
			(can-research-with-escrow my-unique-unit-upgrade)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-unique-unit-upgrade c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow wood g:- g-wood-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-unique-unit-upgrade)
			)

			(defrule
			(or
				(up-compare-goal g-unique-unit-line >= 12)
				(and
					(population >= NINETY-PERCENT-POP)
					(or
						(up-compare-goal g-desired-num-unique-unit > 0)
						(up-compare-goal g-unique-unit-line >= 6))))
			(can-research my-unique-unit-upgrade)
			=>
			(research my-unique-unit-upgrade)
			)

;=========================<>=========================
;				     UNIQUE TECHS
;=========================<>=========================

	#load-if-defined AZTEC-CIV

		;------------------
		;	Garland Wars
		;------------------

			(defrule
			(up-research-status c: ri-champion < research-pending)
			(up-research-status c: ri-elite-eagle-warrior < research-pending)
			(up-research-status c: my-unique-unit-upgrade < research-pending)
			(population < NINETY-FIVE-PERCENT-POP)
			=>
			(up-jump-rule 3)
			)

				(defrule
				(goal g-primary-unit-class infantry-class)
				; (unit-type-count infantry-class >= 10)
				(up-compare-goal g-primary-unit-tech-progress >= PRIORITY-IMPERIAL-COMPLETE)
				(can-research-with-escrow my-unique-research)
				=>
				(up-setup-cost-data 1 g-food-cost)
				(up-add-research-cost c: my-unique-research c: 1)
				(up-modify-escrow food g:- g-food-cost)
				(up-modify-escrow gold g:- g-gold-cost)
				(research my-unique-research)
				)
				(defrule
				(goal g-support-unit-class infantry-class)
				(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)
				; (unit-type-count infantry-class >= 15)
				(up-compare-goal g-primary-unit-tech-progress >= PRIORITY-IMPERIAL-COMPLETE)
				(can-research-with-escrow my-unique-research)
				=>
				(up-setup-cost-data 1 g-food-cost)
				(up-add-research-cost c: my-unique-research c: 1)
				(up-modify-escrow food g:- g-food-cost)
				(up-modify-escrow gold g:- g-gold-cost)
				(research my-unique-research)
				)

				(defrule
				(up-compare-goal g-primary-unit-tech-progress >= PRIORITY-IMPERIAL-COMPLETE)
				(or
					(unit-type-count infantry-class > 20)
					(and
						(population >= NINETY-PERCENT-POP)
						(or
							(up-compare-goal g-desired-num-infantry > 0)
							(unit-type-count infantry-class >= 6))))
				(can-research my-unique-research)
				=>
				(research my-unique-research)
				)

		;------------
		;	Atlatl
		;------------

			(defrule
			(goal g-primary-unit skirmisher)
			(or
				(up-compare-goal g-skirmisher-line >= 10)
				(up-compare-goal g-skirmisher-line g:>= g-desired-num-skirmisher))
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(up-compare-goal g-skirmisher-tech-progress >= SECONDARY-CASTLE-COMPLETE)
			(can-research-with-escrow my-second-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-second-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-second-unique-research)
			)

			(defrule
			(goal g-support-unit skirmisher)
			(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)
			(or
				(up-compare-goal g-skirmisher-line >= 15)
				(up-compare-goal g-skirmisher-line g:>= g-desired-num-skirmisher))
			(up-compare-goal g-skirmisher-tech-progress >= SECONDARY-CASTLE-COMPLETE)
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(can-research-with-escrow my-second-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-second-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-second-unique-research)
			)

			(defrule
			(current-age == imperial-age)
			(up-compare-goal g-primary-unit-tech-progress >= PRIORITY-IMPERIAL-COMPLETE)
			(or
				(up-compare-goal g-skirmisher-line >= 12)
				(and
					(population >= NINETY-PERCENT-POP)
					(or
						(up-compare-goal g-desired-num-skirmisher > 0)
						(up-compare-goal g-skirmisher-line >= 6))))
			(can-research my-second-unique-research)
			=>
			(research my-second-unique-research)
			)

	#end-if

	#load-if-defined BERBERS-CIV

		;---------------------
		;	Maghrabi Camels
		;---------------------

			(defrule
			(or
				(goal g-primary-unit camel)
				(goal g-primary-unit my-unique-unit))
			(or
				(up-compare-goal g-camel-line >= 10)
				(up-compare-goal g-unique-unit-line >= 12))
			(up-compare-goal g-primary-unit-tech-progress >= PRIORITY-IMPERIAL-COMPLETE)
			(can-research-with-escrow my-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-unique-research)
			)
			(defrule
			(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)
			(or
				(goal g-support-unit camel)
				(goal g-support-unit my-unique-unit))
			(or
				(up-compare-goal g-camel-line >= 12)
				(up-compare-goal g-unique-unit-line >= 15))
			(up-compare-goal g-primary-unit-tech-progress >= PRIORITY-IMPERIAL-COMPLETE)
			(can-research-with-escrow my-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-unique-research)
			)

			(defrule
			(up-compare-goal g-primary-unit-tech-progress >= PRIORITY-IMPERIAL-COMPLETE)
			(or
				(up-compare-goal g-camel-line >= 12)
				(and
					(population >= NINETY-PERCENT-POP)
					(or
						(up-compare-goal g-desired-num-camel > 0)
						(up-compare-goal g-camel-line >= 6))))
			(can-research my-unique-research)
			=>
			(research my-unique-research)
			)
			(defrule
			(up-compare-goal g-primary-unit-tech-progress >= PRIORITY-IMPERIAL-COMPLETE)
			(up-research-status c: my-second-unique-research >= research-pending)
			(or
				(up-compare-goal g-unique-unit-line >= 15)
				(and
					(population >= NINETY-PERCENT-POP)
					(or
						(up-compare-goal g-desired-num-unique-unit > 0)
						(up-compare-goal g-unique-unit-line >= 8))))
			(can-research my-unique-research)
			=>
			(research my-unique-research)
			)

		;------------
		;	Kasbah
		;------------

			(defrule
			(goal g-primary-unit my-unique-unit)
			(or
				(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
				(food-amount >= 1250))
			(up-compare-goal g-primary-unit-tech-progress >= SECONDARY-CASTLE-COMPLETE)
			(up-compare-goal g-current-age-primary-unit-tech-progress >= PRIORITY-COMPLETE)
			(can-research-with-escrow my-second-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-second-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-second-unique-research)
			)
			(defrule
			(goal g-support-unit my-unique-unit)
			(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)
			(up-compare-goal g-desired-num-support-unit >= 15)
			(up-compare-goal g-primary-unit-tech-progress >= PRIORITY-IMPERIAL-COMPLETE)
			(can-research-with-escrow my-second-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-second-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-second-unique-research)
			)

			(defrule
			(up-compare-goal g-primary-unit-tech-progress >= PRIORITY-IMPERIAL-COMPLETE)
			(or
				(players-building-type-count any-ally castle > 1)
				(or
					(up-compare-goal g-desired-num-unique-unit >= 15)
					(and
						(population >= NINETY-PERCENT-POP)
						(up-compare-goal g-desired-num-unique-unit > 10))))
			(can-research my-second-unique-research)
			=>
			(research my-second-unique-research)
			)

	#end-if

	#load-if-defined BRITON-CIV

		;------------
		;	Yeomen
		;------------

			;Archers
			(defrule
			(or
				(goal g-primary-unit-class archery-class)
				(and
					(goal g-support-unit-class archery-class)
					(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)))
			(or
				(unit-type-count archery-class >= 12)
				(unit-type-count archery-class g:>= g-desired-num-foot-archers))
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(can-research-with-escrow my-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-unique-research c: 1)
			(up-modify-escrow wood g:- g-wood-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-unique-research)
			)

			(defrule
			(up-compare-goal g-desired-num-archer > 6)
			(unit-type-count archery-class > 10)
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(can-research my-unique-research)
			=>
			(research my-unique-research)
			)
			(defrule
			(population >= NINETY-PERCENT-POP)
			(unit-type-count archery-class > 10)
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(can-research my-unique-research)
			=>
			(research my-unique-research)
			)

			;Towers
			(defrule
			(up-compare-goal g-desired-num-tower > 2)
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(research-completed ri-keep)
			(can-research my-unique-research)
			=>
			(research my-unique-research)
			)

		;-------------
		;	Warwolf
		;-------------

			(defrule
			(or
				(goal g-game-focus BOOM)
				(goal g-game-focus REBUILD))
			(up-object-type-count-total c: town-center g:< g-desired-num-town-center)
			=>
			(up-jump-rule 3)
			)

				(defrule
				(up-compare-goal g-desired-num-trebuchet > 3)
				(unit-type-count trebuchet-set > 1)
				(can-research-with-escrow my-second-unique-research)
				=>
				(up-setup-cost-data 1 g-food-cost)
				(up-add-research-cost c: my-second-unique-research c: 1)
				(up-modify-escrow wood g:- g-wood-cost)
				(up-modify-escrow gold g:- g-gold-cost)
				(research my-second-unique-research)
				)

				(defrule
				(up-compare-goal g-desired-num-trebuchet > 0)
				(unit-type-count trebuchet-set > 0)
				(can-research my-second-unique-research)
				=>
				(research my-second-unique-research)
				)
				(defrule
				(population >= NINETY-PERCENT-POP)
				(unit-type-count trebuchet-set > 0)
				(can-research my-second-unique-research)
				=>
				(research my-second-unique-research)
				)

	#end-if

	#load-if-defined BULGARIANS-CIV

		;--------------
		;	Stirrups
		;--------------

			(defrule
			(or
				(goal g-primary-unit-class cavalry-class)
				(and
					(goal g-support-unit-class cavalry-class)
					(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)))
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(can-research-with-escrow my-second-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-second-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-second-unique-research)
			)

			(defrule
			(or
				(up-compare-goal g-desired-num-cavalry > 6)
				(unit-type-count cavalry-class > 10))
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(can-research my-second-unique-research)
			=>
			(research my-second-unique-research)
			)

		;-------------
		;	Bagains
		;-------------

			(defrule
			(or
				(goal g-primary-unit militiaman)
				(and
					(goal g-support-unit militiaman)
					(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)))
			(can-research-with-escrow my-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-unique-research)
			)

			(defrule
			(or
				(up-compare-goal g-desired-num-militia > 10)
				(unit-type-count militiaman-line > 15))
			(can-research my-unique-research)
			=>
			(research my-unique-research)
			)

	#end-if

	#load-if-defined BURMESE-CIV

		;-------------
		;	Howdah
		;-------------

			(defrule
			(or
				(goal g-game-focus BOOM)
				(goal g-game-focus REBUILD))
			(up-object-type-count-total c: town-center g:< g-desired-num-town-center)
			=>
			(up-jump-rule 2)
			)

				(defrule
				(or
					(goal g-primary-unit battle-elephant)
					(and
						(goal g-support-unit battle-elephant)
						(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)))
				(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
				(can-research-with-escrow my-second-unique-research)
				=>
				(up-setup-cost-data 1 g-food-cost)
				(up-add-research-cost c: my-second-unique-research c: 1)
				(up-modify-escrow food g:- g-food-cost)
				(up-modify-escrow wood g:- g-wood-cost)
				(research my-second-unique-research)
				)

				(defrule
				(or
					(up-compare-goal g-desired-num-battle-elephant > 6)
					(up-compare-goal g-battle-elephant-line > 10))
				(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
				(can-research my-second-unique-research)
				=>
				(research my-second-unique-research)
				)

		;---------------------
		;	Manipur Cavalry
		;---------------------

			(defrule
			(or
				(goal g-primary-unit-class cavalry-class)
				(and
					(goal g-support-unit-class cavalry-class)
					(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)))
			(can-research-with-escrow my-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-unique-research)
			)

			#load-if-not-defined DE-AVAILABLE
			(defrule
			(or
				(goal g-primary-unit my-unique-unit)
				(and
					(goal g-support-unit my-unique-unit)
					(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)))
			(can-research-with-escrow my-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-unique-research)
			)
			#end-if

			(defrule
			(or
				(up-compare-goal g-desired-num-cavalry > 10)
				(or
					(unit-type-count cavalry-class > 15)
					(or
						(up-compare-goal g-desired-num-unique-unit > 10)
						(up-compare-goal g-unique-unit-line > 15))))
			(can-research my-unique-research)
			=>
			(research my-unique-research)
			)

	#end-if

	#load-if-defined BYZANTINE-CIV

		;----------------
		;	Greek Fire
		;----------------

			(defrule
			(up-compare-goal g-desired-num-fire-ship >= 10)
			(research-completed ri-war-galley)
			(or
				(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
				(food-amount >= 1250))
			(can-research-with-escrow my-second-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-second-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow wood g:- g-wood-cost)
			(research my-second-unique-research)
			)

			(defrule
			(up-compare-goal g-desired-num-fire-ship > 5)
			(research-completed ri-war-galley)
			(or
				(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
				(food-amount >= 1250))
			(can-research my-second-unique-research)
			=>
			(research my-second-unique-research)
			)

		;---------------
		;	Logistica
		;---------------

			(defrule
			(or
				(goal g-primary-unit my-unique-unit)
				(and
					(goal g-support-unit my-unique-unit)
					(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)))
			(can-research-with-escrow my-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-unique-research)
			)

			(defrule
			(or
				(up-compare-goal g-desired-num-unique-unit > 10)
				(up-compare-goal g-unique-unit-line > 15))
			(can-research my-unique-research)
			=>
			(research my-unique-research)
			)

	#end-if

	#load-if-defined CELTIC-CIV

		;-------------------
		;	Furor Celtica
		;-------------------

			(defrule
			(or
				(up-compare-goal g-desired-num-ram-type >= 3)
				(or
					(up-compare-goal g-desired-num-mangonel >= 3)
					(up-compare-goal g-desired-num-scorpion >= 5)))
			(can-research-with-escrow my-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-unique-research)
			)

			(defrule
			(or
				(up-compare-goal g-desired-num-ram-type > 0)
				(or
					(up-compare-goal g-desired-num-mangonel > 0)
					(or
						(up-compare-goal g-desired-num-scorpion > 0)
						(or
							(unit-type-count battering-ram-line > 2)
							(or
								(up-compare-goal g-mangonel-line > 2)
								(unit-type-count scorpion-line > 2))))))
			(can-research my-unique-research)
			=>
			(research my-unique-research)
			)

	#end-if

	#load-if-defined CHINESE-CIV

		;----------------
		;	Great Wall
		;----------------

			(defrule
			(or
				(up-compare-goal g-desired-num-tower > 4)
				(or
					(up-compare-goal g-desired-num-bombard-tower > 4)
					(unit-type-count gate-class > 2)))
			(can-research my-second-unique-research)
			=>
			(research my-second-unique-research)
			)

		;--------------
		;	Rocketry
		;--------------

			(defrule
			(or
				(goal g-primary-unit my-unique-unit)
				(or
					(and
						(goal g-support-unit my-unique-unit)
						(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS))
					(up-compare-goal g-desired-num-scorpion > 4)))
			(can-research-with-escrow my-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-unique-research c: 1)
			(up-modify-escrow wood g:- g-wood-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-unique-research)
			)

			(defrule
			(or
				(up-compare-goal g-desired-num-unique-unit > 10)
				(or
					(up-compare-goal g-unique-unit-line > 15)
					(or
						(up-compare-goal g-desired-num-scorpion > 2)
						(unit-type-count scorpion-line > 3))))
			(can-research my-unique-research)
			=>
			(research my-unique-research)
			)

	#end-if

	#load-if-defined CUMANS-CIV

		;----------------------
		;	Steppe Husbandry
		;----------------------

			(defrule
			(or
				(goal g-primary-unit scout-cavalry)
				(or
					(goal g-primary-unit cavalry-archer)
					(goal g-primary-unit steppe-lancer)))
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(can-research-with-escrow my-second-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-second-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow wood g:- g-wood-cost)
			(research my-second-unique-research)
			)
			(defrule
			(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)
			(or
				(goal g-support-unit scout-cavalry)
				(or
					(goal g-support-unit cavalry-archer)
					(goal g-support-unit steppe-lancer)))
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(can-research-with-escrow my-second-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-second-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow wood g:- g-wood-cost)
			(research my-second-unique-research)
			)

			(defrule
			(or
				(up-compare-goal g-desired-num-scout-cavalry > 10)
				(or
					(unit-type-count scout-cavalry-line > 15)
					(or
						(up-compare-goal g-desired-num-cavalry-archer > 10)
						(or
							(unit-type-count cavalry-archer-line > 15)
							(or
								(up-compare-goal g-desired-num-steppe-lancer > 10)
								(unit-type-count steppe-lancer-line > 15))))))
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(can-research my-second-unique-research)
			=>
			(research my-second-unique-research)
			)

	#end-if

	#load-if-defined ETHIOPIAN-CIV

		;-----------------
		;	Royal Heirs
		;-----------------

			(defrule
			(or
				(goal g-primary-unit my-unique-unit)
				(and
					(goal g-support-unit my-unique-unit)
					(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)))
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(can-research-with-escrow my-second-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-second-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-second-unique-research)
			)

			(defrule
			(or
				(up-compare-goal g-desired-num-unique-unit > 10)
				(up-compare-goal g-unique-unit-line > 15))
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(can-research my-second-unique-research)
			=>
			(research my-second-unique-research)
			)

		;---------------------
		;	Torsion Engines
		;---------------------

			(defrule
			(or
				(up-compare-goal g-desired-num-ram-type >= 3)
				(or
					(up-compare-goal g-desired-num-mangonel >= 3)
					(up-compare-goal g-desired-num-scorpion >= 5)))
			(can-research-with-escrow my-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-unique-research)
			)

			(defrule
			(or
				(up-compare-goal g-desired-num-ram-type > 0)
				(or
					(up-compare-goal g-desired-num-mangonel > 0)
					(or
						(up-compare-goal g-desired-num-scorpion > 0)
						(or
							(unit-type-count battering-ram-line > 2)
							(or
								(up-compare-goal g-mangonel-line > 2)
								(unit-type-count scorpion-line > 2))))))
			(can-research my-unique-research)
			=>
			(research my-unique-research)
			)

	#end-if

	#load-if-defined FRANKISH-CIV

		;--------------
		;	Chivalry
		;--------------

			(defrule
			(or
				(goal g-game-focus BOOM)
				(goal g-game-focus REBUILD))
			(up-object-type-count-total c: town-center g:< g-desired-num-town-center)
			=>
			(up-jump-rule 2)
			)

				(defrule
				(or
					(goal g-primary-unit-class cavalry-class)
					(and
						(goal g-support-unit-class cavalry-class)
						(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)))
				(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
				(can-research-with-escrow my-second-unique-research)
				=>
				(up-setup-cost-data 1 g-food-cost)
				(up-add-research-cost c: my-second-unique-research c: 1)
				(up-modify-escrow wood g:- g-wood-cost)
				(up-modify-escrow gold g:- g-gold-cost)
				(research my-second-unique-research)
				)

				(defrule
				(or
					(up-compare-goal g-desired-num-scout-cavalry > 10)
					(up-compare-goal g-desired-num-knight > 10))
				(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
				(can-research my-second-unique-research)
				=>
				(research my-second-unique-research)
				)

		;-----------------
		;	Bearded Axe
		;-----------------

			(defrule
			(or
				(goal g-primary-unit my-unique-unit)
				(and
					(goal g-support-unit my-unique-unit)
					(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)))
			(can-research-with-escrow my-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-unique-research)
			)

			(defrule
			(up-compare-goal g-desired-num-unique-unit > 6)
			(can-research my-unique-research)
			=>
			(research my-unique-research)
			)

	#end-if

	#load-if-defined GOTHIC-CIV
	
		;-------------
		;	Anarchy
		;-------------

			(defrule
			(or
				(goal g-primary-unit my-unique-unit)
				(and
					(goal g-support-unit my-unique-unit)
					(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)))
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(can-research-with-escrow my-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-unique-research)
			)

			(defrule
			(up-compare-goal g-desired-num-unique-unit > 10)
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(can-research my-unique-research)
			=>
			(research my-unique-research)
			)
	
		;---------------
		;	Perfusion
		;---------------

			(defrule
			(or
				(goal g-primary-unit-class infantry-class)
				(and
					(goal g-support-unit-class infantry-class)
					(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)))
			(can-research-with-escrow my-second-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-second-unique-research c: 1)
			(up-modify-escrow wood g:- g-wood-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-second-unique-research)
			)

			(defrule
			(up-compare-goal g-desired-num-infantry > 10)
			(can-research my-second-unique-research)
			=>
			(research my-second-unique-research)
			)

	#end-if

	#load-if-defined HUN-CIV

		;---------------
		;	Marauders
		;---------------

			(defrule
			(or
				(goal g-game-focus BOOM)
				(goal g-game-focus REBUILD))
			(up-object-type-count-total c: town-center g:< g-desired-num-town-center)
			=>
			(up-jump-rule 3)
			)

				(defrule
				(goal g-primary-unit my-unique-unit)
				(up-compare-goal g-unique-unit-tech-progress >= SECONDARY-CASTLE-COMPLETE)
				(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
				(can-research-with-escrow my-second-unique-research)
				=>
				(up-setup-cost-data 1 g-food-cost)
				(up-add-research-cost c: my-second-unique-research c: 1)
				(up-modify-escrow wood g:- g-wood-cost)
				(up-modify-escrow gold g:- g-gold-cost)
				(research my-second-unique-research)
				)
				(defrule
				(goal g-support-unit my-unique-unit)
				(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)
				(up-compare-goal g-unique-unit-tech-progress >= SECONDARY-CASTLE-COMPLETE)
				(up-compare-goal g-desired-num-unique-unit >= 12)
				(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
				(can-research-with-escrow my-second-unique-research)
				=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-second-unique-research c: 1)
			(up-modify-escrow wood g:- g-wood-cost)
			(up-modify-escrow gold g:- g-gold-cost)
				(research my-second-unique-research)
				)

				(defrule
				(or
					(up-compare-goal g-desired-num-unique-unit > 15)
					(and
						(population >= NINETY-PERCENT-POP)
						(up-compare-goal g-desired-num-unique-unit > 10)))
				(up-compare-goal g-unique-unit-tech-progress >= PRIORITY-IMPERIAL-COMPLETE)
				(can-research my-second-unique-research)
				=>
				(research my-second-unique-research)
				)

	#end-if

	#load-if-defined INCAN-CIV

		;------------------
		;	Andean Sling
		;------------------

			(defrule
			(goal g-primary-unit skirmisher)
			(or
				(up-compare-goal g-skirmisher-line >= 10)
				(up-compare-goal g-skirmisher-line g:>= g-desired-num-skirmisher))
			(or
				(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
				(food-amount >= 1200))
			(can-research-with-escrow my-second-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-second-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-second-unique-research)
			)
			(defrule
			(goal g-primary-unit slinger)
			(or
				(unit-type-count slinger >= 10)
				(unit-type-count slinger g:>= g-desired-num-skirmisher))
			(or
				(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
				(food-amount >= 1200))
			(can-research-with-escrow my-second-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-second-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-second-unique-research)
			)
			(defrule
			(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)
			(goal g-support-unit skirmisher)
			(or
				(up-compare-goal g-skirmisher-line >= 12)
				(up-compare-goal g-skirmisher-line g:>= g-desired-num-skirmisher))
			(or
				(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
				(food-amount >= 1200))
			(can-research-with-escrow my-second-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-second-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-second-unique-research)
			)
			(defrule
			(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)
			(goal g-support-unit slinger)
			(or
				(unit-type-count slinger >= 12)
				(unit-type-count slinger g:>= g-desired-num-skirmisher))
			(or
				(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
				(food-amount >= 1200))
			(can-research-with-escrow my-second-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-second-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-second-unique-research)
			)

			(defrule
			(or
				(up-compare-goal g-desired-num-skirmisher > 10)
				(up-compare-goal g-desired-num-second-unique-unit > 10))
			(or
				(up-compare-goal g-skirmisher-line > 15)
				(unit-type-count slinger > 15))
			(or
				(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
				(food-amount >= 1200))
			(can-research my-second-unique-research)
			=>
			(research my-second-unique-research)
			)
			(defrule
			(population >= NINETY-PERCENT-POP)
			(or
				(up-compare-goal g-skirmisher-line > 5)
				(unit-type-count slinger > 5))
			(or
				(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
				(food-amount >= 1200))
			(can-research my-second-unique-research)
			=>
			(research my-second-unique-research)
			)

		;-----------------------------
		;	Couriers/Fabric Shields
		;-----------------------------

			(defrule
			(or
				(goal g-primary-unit eagle-warrior)
				(or
					(goal g-primary-unit slinger)
					(goal g-primary-unit my-unique-unit)))
			(can-research-with-escrow my-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-unique-research)
			)
			(defrule
			(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)
			(or
				(goal g-support-unit eagle-warrior)
				(or
					(goal g-support-unit slinger)
					(goal g-support-unit my-unique-unit)))
			(can-research-with-escrow my-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-unique-research)
			)

			(defrule
			(or
				(up-compare-goal g-desired-num-eagle-warrior > 10)
				(or
					(up-compare-goal g-desired-num-second-unique-unit > 10)
					(up-compare-goal g-desired-num-unique-unit > 10)))
			(or
				(up-compare-goal g-eagle-scout-line > 15)
				(or
					(unit-type-count slinger > 15)
					(up-compare-goal g-unique-unit-line > 15)))
			(can-research my-unique-research)
			=>
			(research my-unique-research)
			)
			(defrule
			(population >= NINETY-PERCENT-POP)
			(or
				(up-compare-goal g-eagle-scout-line > 5)
				(or
					(unit-type-count slinger > 5)
					(up-compare-goal g-unique-unit-line > 5)))
			(can-research my-unique-research)
			=>
			(research my-unique-research)
			)

	#end-if

	#load-if-defined INDIAN-CIV

		;-------------
		;	Sultans
		;-------------

			(defrule
			(or
				(goal g-game-focus BOOM)
				(goal g-game-focus REBUILD))
			(up-object-type-count-total c: town-center g:< g-desired-num-town-center)
			=>
			(up-jump-rule 1)
			)

				(defrule
				(goal g-town-under-attack NO)
				(up-get-fact resource-amount amount-relics g-temp)
				(or
					(unit-type-count villager-gold > 10)
					(or
						(unit-type-count trade-cart > 10)
						(up-compare-goal g-temp >= 2)))
				(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
				(can-research-with-escrow my-second-unique-research)
				=>
				(up-setup-cost-data 1 g-food-cost)
				(up-add-research-cost c: my-second-unique-research c: 1)
				(up-modify-escrow food g:- g-food-cost)
				(up-modify-escrow wood g:- g-wood-cost)
				(research my-second-unique-research)
				)

			(defrule
			(goal g-town-under-attack NO)
			(up-get-fact resource-amount amount-relics g-temp)
			(or
				(unit-type-count villager-gold > 5)
				(or
					(unit-type-count trade-cart > 5)
					(up-compare-goal g-temp >= 2)))
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(up-research-status c: my-second-unique-research == research-available)
			(building-type-count castle > 0)
			(can-research my-second-unique-research)
			=>
			(research my-second-unique-research)
			)

			(defrule
			(goal g-town-under-attack NO)
			(up-get-fact resource-amount amount-relics g-temp)
			(or
				(unit-type-count villager-gold > 5)
				(or
					(unit-type-count trade-cart > 5)
					(up-compare-goal g-temp >= 1)))
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(can-research my-second-unique-research)
			=>
			(research my-second-unique-research)
			)

		;--------------
		;	Shatagni
		;--------------

			(defrule
			(or
				(goal g-primary-unit hand-cannoneer)
				(and
					(goal g-support-unit hand-cannoneer)
					(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)))
			(can-research-with-escrow my-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-unique-research)
			)

			(defrule
			(or
				(up-compare-goal g-desired-num-hand-cannoneer > 10)
				(unit-type-count hand-cannoneer > 15))
			(can-research my-unique-research)
			=>
			(research my-unique-research)
			)

	#end-if

	#load-if-defined ITALIAN-CIV

		;------------
		;	Pavise
		;------------

			#load-if-defined DE-AVAILABLE

				(defrule
				(or
					(goal g-primary-unit condottiero)
					(and
						(goal g-support-unit condottiero)
						(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)))
				(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
				(up-research-status c: ri-chain-mail >= research-pending)
				(up-research-status c: ri-iron-casting >= research-pending)
				(can-research-with-escrow my-second-unique-research)
				=>
				(up-setup-cost-data 1 g-food-cost)
				(up-add-research-cost c: my-second-unique-research c: 1)
				(up-modify-escrow food g:- g-food-cost)
				(up-modify-escrow gold g:- g-gold-cost)
				(research my-second-unique-research)
				)

				(defrule
				(or
					(up-compare-goal g-desired-num-condottiero > 10)
					(unit-type-count condottiero > 15))
				(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
				(up-research-status c: ri-chain-mail >= research-pending)
				(up-research-status c: ri-iron-casting >= research-pending)
				(can-research my-second-unique-research)
				=>
				(research my-second-unique-research)
				)

			#end-if

			(defrule
			(or
				(goal g-primary-unit archer)
				(goal g-primary-unit my-unique-unit))
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(up-research-status c: ri-leather-archer-armor >= research-pending)
			(up-research-status c: ri-bodkin-arrow >= research-pending)
			(can-research-with-escrow my-second-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-second-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-second-unique-research)
			)
			(defrule
			(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)
			(or
				(goal g-support-unit archer)
				(goal g-support-unit my-unique-unit))
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(up-research-status c: ri-leather-archer-armor >= research-pending)
			(up-research-status c: ri-bodkin-arrow >= research-pending)
			(can-research-with-escrow my-second-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-second-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-second-unique-research)
			)

			(defrule
			(or
				(up-compare-goal g-desired-num-archer > 10)
				(or
					(unit-type-count archer-line > 15)
					(or
						(up-compare-goal g-desired-num-unique-unit > 10)
						(up-compare-goal g-unique-unit-line > 15))))
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(up-research-status c: ri-leather-archer-armor >= research-pending)
			(up-research-status c: ri-bodkin-arrow >= research-pending)
			(can-research my-second-unique-research)
			=>
			(research my-second-unique-research)
			)

		;---------------
		;	Silk Road
		;---------------

			(defrule
			(or
				(up-compare-goal g-desired-num-trade-cart > 0)
				(up-compare-goal g-desired-num-trade-cog > 0))
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(up-research-status c: ri-caravan >= research-pending)
			(can-research-with-escrow my-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-unique-research)
			)

	#end-if

	#load-if-defined JAPANESE-CIV

		;------------
		;	Yasama
		;------------

			(defrule
			(or
				(goal g-game-focus BOOM)
				(goal g-game-focus REBUILD))
			(up-object-type-count-total c: town-center g:< g-desired-num-town-center)
			=>
			(up-jump-rule 2)
			)

				(defrule
				(up-compare-goal g-desired-num-tower >= 4)
				(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
				(can-research-with-escrow my-second-unique-research)
				=>
				(up-setup-cost-data 1 g-food-cost)
				(up-add-research-cost c: my-second-unique-research c: 1)
				(up-modify-escrow food g:- g-food-cost)
				(up-modify-escrow wood g:- g-wood-cost)
				(research my-second-unique-research)
				)

				(defrule
				(or
					(up-compare-goal g-desired-num-tower > 1)
					(up-compare-goal g-tower-line > 3))
				(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
				(can-research my-second-unique-research)
				=>
				(research my-second-unique-research)
				)

		;----------------
		;	Kataparuto
		;----------------

			(defrule
			(up-compare-goal g-desired-num-trebuchet >= 3)
			(can-research-with-escrow my-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-unique-research c: 1)
			(up-modify-escrow wood g:- g-wood-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-unique-research)
			)

			(defrule
			(or
				(up-compare-goal g-desired-num-trebuchet > 1)
				(unit-type-count trebuchet-set > 1))
			(can-research my-unique-research)
			=>
			(research my-unique-research)
			)

	#end-if

	#load-if-defined KHMER-CIV

		;-----------------
		;	Tusk Swords
		;-----------------

			(defrule
			(or
				(goal g-game-focus BOOM)
				(goal g-game-focus REBUILD))
			(up-object-type-count-total c: town-center g:< g-desired-num-town-center)
			=>
			(up-jump-rule 2)
			)
			
				(defrule
				(or
					(goal g-primary-unit battle-elephant)
					(and
						(goal g-support-unit battle-elephant)
						(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)))
				(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
				(can-research-with-escrow my-second-unique-research)
				=>
				(up-setup-cost-data 1 g-food-cost)
				(up-add-research-cost c: my-second-unique-research c: 1)
				(up-modify-escrow wood g:- g-wood-cost)
				(up-modify-escrow gold g:- g-gold-cost)
				(research my-second-unique-research)
				)

				(defrule
				(or
					(up-compare-goal g-desired-num-battle-elephant > 6)
					(up-compare-goal g-battle-elephant-line > 10))
				(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
				(can-research my-second-unique-research)
				=>
				(research my-second-unique-research)
				)

		;---------------------
		;	Double Crossbow
		;---------------------
			
			(defrule
			(or
				(goal g-primary-unit my-unique-unit)
				(or
					(and
						(goal g-support-unit my-unique-unit)
						(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS))
					(up-compare-goal g-desired-num-scorpion > 5)))
			(can-research-with-escrow my-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-unique-research)
			)

			(defrule
			(or
				(up-compare-goal g-desired-num-unique-unit > 6)
				(or
					(up-compare-goal g-unique-unit-line > 10)
					(or
						(up-compare-goal g-desired-num-scorpion > 2)
						(unit-type-count scorpion-line > 4))))
			(can-research my-unique-research)
			=>
			(research my-unique-research)
			)

	#end-if

	#load-if-defined KOREAN-CIV

		#load-if-defined DE-AVAILABLE

			;--------------
			;	Eupseong
			;--------------

				(defrule
				(or
					(goal g-game-focus BOOM)
					(goal g-game-focus REBUILD))
				(up-object-type-count-total c: town-center g:< g-desired-num-town-center)
				=>
				(up-jump-rule 2)
				)
			
					(defrule
					(up-compare-goal g-desired-num-tower >= 3)
					(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
					(can-research-with-escrow my-second-unique-research)
					=>
					(up-setup-cost-data 1 g-food-cost)
					(up-add-research-cost c: my-second-unique-research c: 1)
					(up-modify-escrow food g:- g-food-cost)
					(up-modify-escrow wood g:- g-wood-cost)
					(research my-second-unique-research)
					)

					(defrule
					(or
						(up-compare-goal g-desired-num-tower > 2)
						(up-compare-goal g-tower-line > 3))
					(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
					(can-research my-second-unique-research)
					=>
					(research my-second-unique-research)
					)

		#else

			;---------------
			;	Panokseon
			;---------------

				(defrule
				(up-compare-goal g-desired-num-unique-ship > 2)
				(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
				(can-research my-second-unique-research)
				=>
				(research my-second-unique-research)
				)

		#end-if

		;----------------
		;	Shinkichon
		;----------------

			(defrule
			(up-compare-goal g-desired-num-mangonel >= 3)
			(can-research-with-escrow my-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-unique-research c: 1)
			(up-modify-escrow wood g:- g-wood-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-unique-research)
			)

			(defrule
			(or
				(up-compare-goal g-desired-num-mangonel > 2)
				(up-compare-goal g-mangonel-line > 4))
			(can-research my-unique-research)
			=>
			(research my-unique-research)
			)

	#end-if

	#load-if-defined LITHUANIANS-CIV

		;----------------
		;	Hill Forts
		;----------------

			(defrule
			(building-type-count town-center >= 3)
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(can-research my-second-unique-research)
			=>
			(research my-second-unique-research)
			)

		;-------------------
		;	Tower Shields
		;-------------------

			(defrule
			(or
				(goal g-primary-unit spearman)
				(goal g-primary-unit skirmisher))
			(can-research-with-escrow my-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-unique-research)
			)
			(defrule
			(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)
			(or
				(goal g-support-unit spearman)
				(goal g-support-unit skirmisher))
			(can-research-with-escrow my-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-unique-research)
			)

			(defrule
			(or
				(up-compare-goal g-desired-num-spearman > 10)
				(or
					(unit-type-count spearman-line > 15)
					(or
						(up-compare-goal g-desired-num-skirmisher > 10)
						(up-compare-goal g-skirmisher-line > 15))))
			(can-research my-unique-research)
			=>
			(research my-unique-research)
			)

	#end-if

	#load-if-defined MAGYAR-CIV

		;--------------------
		;	Corvinian Army
		;--------------------

			(defrule
			(goal g-primary-unit my-unique-unit)
			(up-compare-goal g-desired-num-unique-unit >= 15)
			(up-compare-goal g-unique-unit-tech-progress >= REQUIRED-IMPERIAL-COMPLETE)
			(can-research-with-escrow my-second-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-second-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-second-unique-research)
			)
			(defrule
			(goal g-support-unit my-unique-unit)
			(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)
			(up-compare-goal g-desired-num-unique-unit >= 15)
			(up-compare-goal g-primary-unit-tech-progress >= PRIORITY-IMPERIAL-COMPLETE)
			(up-compare-goal g-unique-unit-tech-progress >= REQUIRED-IMPERIAL-COMPLETE)
			(can-research-with-escrow my-second-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-second-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-second-unique-research)
			)
			(defrule
			(goal g-age-status LATE-IMPERIAL)
			(or
				(goal g-primary-unit my-unique-unit)
				(and
					(goal g-support-unit my-unique-unit)
					(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)))
			(up-compare-goal g-desired-num-unique-unit > 10)
			(can-research-with-escrow my-second-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-second-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-second-unique-research)
			)

			(defrule
			(or
				(up-compare-goal g-desired-num-unique-unit > 15)
				(and
					(population >= NINETY-PERCENT-POP)
					(up-compare-goal g-desired-num-unique-unit > 10)))
			(up-compare-goal g-unique-unit-tech-progress >= PRIORITY-IMPERIAL-COMPLETE)
			(up-compare-goal g-desired-num-unique-unit > 10)
			(can-research my-second-unique-research)
			=>
			(research my-second-unique-research)
			)
			(defrule
			(goal g-age-status LATE-IMPERIAL)
			(up-compare-goal g-desired-num-unique-unit > 10)
			(can-research my-second-unique-research)
			=>
			(research my-second-unique-research)
			)

		;-----------------
		;	Recurve Bow
		;-----------------

			(defrule
			(goal g-primary-unit cavalry-archer)
			(up-compare-goal g-cavalry-archer-tech-progress >= PRIORITY-IMPERIAL-COMPLETE)
			(can-research-with-escrow my-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-unique-research c: 1)
			(up-modify-escrow wood g:- g-wood-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-unique-research)
			)
			(defrule
			(goal g-support-unit cavalry-archer)
			(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)
			(up-compare-goal g-primary-unit-tech-progress >= PRIORITY-IMPERIAL-COMPLETE)
			(up-compare-goal g-cavalry-archer-tech-progress >= PRIORITY-IMPERIAL-COMPLETE)
			(can-research-with-escrow my-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-unique-research c: 1)
			(up-modify-escrow wood g:- g-wood-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-unique-research)
			)

			(defrule
			(or
				(unit-type-count-total cavalry-archer-line > 15)
				(and
					(population >= NINETY-PERCENT-POP)
					(unit-type-count-total cavalry-archer-line > 8)))
			(up-compare-goal g-unique-unit-tech-progress >= PRIORITY-IMPERIAL-COMPLETE)
			(can-research my-unique-research)
			=>
			(research my-unique-research)
			)

	#end-if

	#load-if-defined MALAY-CIV

		;-------------------
		;	Thalassocracy
		;-------------------

			(defrule
			(up-compare-goal g-desired-num-dock >= 4)
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(can-research-with-escrow my-second-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-second-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-second-unique-research)
			)

			(defrule
			(up-compare-goal g-desired-num-dock > 1)
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(can-research my-second-unique-research)
			=>
			(research my-second-unique-research)
			)

		;-----------------
		;	Forced Levy
		;-----------------

			(defrule
			(or
				(goal g-primary-unit militiaman)
				(and
					(goal g-support-unit militiaman)
					(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)))
			(can-research-with-escrow my-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-unique-research)
			)
			(defrule
			(up-compare-goal g-desired-num-militia > 10)
			(goal g-age-status LATE-IMPERIAL)
			(can-research-with-escrow my-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-unique-research)
			)

			(defrule
			(up-compare-goal g-desired-num-militia > 10)
			(can-research my-unique-research)
			=>
			(research my-unique-research)
			)

	#end-if

	#load-if-defined MALIAN-CIV
	
		;-----------
		;	Tigui
		;-----------

			(defrule
			(building-type-count town-center >= 3)
			(or
				(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
				(food-amount >= 1200))
			(can-research my-second-unique-research)
			=>
			(research my-second-unique-research)
			)

		;-------------
		;	Farimba
		;-------------

			(defrule
			(or
				(goal g-primary-unit-class cavalry-class)
				(and
					(goal g-support-unit-class cavalry-class)
					(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)))
			(can-research-with-escrow my-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-unique-research)
			)

			(defrule
			(or
				(up-compare-goal g-desired-num-cavalry > 10)
				(unit-type-count cavalry-class > 15))
			(can-research my-unique-research)
			=>
			(research my-unique-research)
			)

	#end-if

	#load-if-defined MAYAN-CIV

		#load-if-not-defined DE-AVAILABLE
		
			;---------------------
			;	Obsidian Arrows
			;---------------------
				
				(defrule
				(or
					(goal g-primary-unit archer)
					(and
						(goal g-support-unit archer)
						(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)))
				(unit-type-count archer-line >= 12)
				(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
				(can-research-with-escrow my-second-unique-research)
				=>
				(up-setup-cost-data 1 g-food-cost)
				(up-add-research-cost c: my-second-unique-research c: 1)
				(up-modify-escrow food g:- g-food-cost)
				(up-modify-escrow gold g:- g-gold-cost)
				(research my-second-unique-research)
				)

				(defrule
				(up-compare-goal g-desired-num-archer > 10)
				(unit-type-count archer-line > 15)
				(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
				(can-research my-second-unique-research)
				=>
				(research my-second-unique-research)
				)
				(defrule
				(population >= NINETY-PERCENT-POP)
				(unit-type-count archer-line > 7)
				(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
				(can-research my-second-unique-research)
				=>
				(research my-second-unique-research)
				)

		#else
		
			;-------------------------
			;	Hul'Che Javelineers
			;-------------------------
				
				(defrule
				(or
					(goal g-primary-unit skirmisher)
					(and
						(goal g-support-unit skirmisher)
						(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)))
				(or
					(up-compare-goal g-skirmisher-line >= 12)
					(up-compare-goal g-skirmisher-line g:>= g-desired-num-skirmisher))
				(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
				(can-research-with-escrow my-second-unique-research)
				=>
				(up-setup-cost-data 1 g-food-cost)
				(up-add-research-cost c: my-second-unique-research c: 1)
				(up-modify-escrow food g:- g-food-cost)
				(up-modify-escrow gold g:- g-gold-cost)
				(research my-second-unique-research)
				)

				(defrule
				(up-compare-goal g-desired-num-skirmisher > 10)
				(up-compare-goal g-skirmisher-line > 15)
				(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
				(can-research my-second-unique-research)
				=>
				(research my-second-unique-research)
				)
				(defrule
				(population >= NINETY-PERCENT-POP)
				(up-compare-goal g-skirmisher-line > 5)
				(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
				(can-research my-second-unique-research)
				=>
				(research my-second-unique-research)
				)

		#end-if

		;---------------
		;	El Dorado
		;---------------
			
			(defrule
			(or
				(goal g-primary-unit eagle-warrior)
				(and
					(goal g-support-unit eagle-warrior)
					(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)))
			(up-research-status c: ri-elite-eagle-warrior >= research-pending)
			(can-research-with-escrow my-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-unique-research)
			)

			(defrule
			(up-compare-goal g-desired-num-eagle-warrior > 10)
			(up-compare-goal g-eagle-scout-line > 15)
			(up-research-status c: ri-elite-eagle-warrior >= research-pending)
			(can-research my-unique-research)
			=>
			(research my-unique-research)
			)
			(defrule
			(population >= EIGHTY-FIVE-PERCENT-POP)
			(up-compare-goal g-eagle-scout-line > 5)
			(up-research-status c: ri-elite-eagle-warrior >= research-pending)
			(can-research my-unique-research)
			=>
			(research my-unique-research)
			)

	#end-if

	#load-if-defined MONGOL-CIV

		;------------
		;	Nomads
		;------------

			(defrule
			(or
				(goal g-game-focus DEFENSIVE)
				(or
					(goal g-game-focus REBUILD)
					(players-military-population any-enemy >= 40)))
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(can-research my-second-unique-research)
			=>
			(research my-second-unique-research)
			)

		;-----------
		;	Drill
		;-----------

			(defrule
			(or
				(up-compare-goal g-desired-num-ram-type >= 3)
				(or
					(up-compare-goal g-desired-num-mangonel >= 3)
					(up-compare-goal g-desired-num-scorpion >= 5)))
			(can-research-with-escrow my-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-unique-research)
			)

			(defrule
			(or
				(up-compare-goal g-desired-num-ram-type > 0)
				(or
					(up-compare-goal g-desired-num-mangonel > 0)
					(or
						(up-compare-goal g-desired-num-scorpion > 0)
						(or
							(unit-type-count battering-ram-line > 2)
							(or
								(up-compare-goal g-mangonel-line > 2)
								(unit-type-count scorpion-line > 2))))))
			(can-research my-unique-research)
			=>
			(research my-unique-research)
			)

	#end-if

	#load-if-defined PERSIAN-CIV

		;----------------
		;	Kamandaran
		;----------------
			
			#load-if-defined DE-AVAILABLE
			
				(defrule
				(or
					(goal g-primary-unit archer)
					(and
						(goal g-support-unit archer)
						(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)))
				(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
				(can-research-with-escrow my-second-unique-research)
				=>
				(up-setup-cost-data 1 g-food-cost)
				(up-add-research-cost c: my-second-unique-research c: 1)
				(up-modify-escrow food g:- g-food-cost)
				(up-modify-escrow gold g:- g-gold-cost)
				(research my-second-unique-research)
				)
				(defrule
				(up-compare-goal g-desired-num-archer > 10)
				(goal g-age-status LATE-IMPERIAL)
				(can-research-with-escrow my-second-unique-research)
				=>
				(up-setup-cost-data 1 g-food-cost)
				(up-add-research-cost c: my-second-unique-research c: 1)
				(up-modify-escrow food g:- g-food-cost)
				(up-modify-escrow gold g:- g-gold-cost)
				(research my-second-unique-research)
				)

				(defrule
				(or
					(up-compare-goal g-desired-num-archer > 10)
					(unit-type-count archer-line > 15))
				(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
				(can-research my-second-unique-research)
				=>
				(research my-second-unique-research)
				)

			#end-if

		;-------------
		;	Mahouts
		;-------------
			
			(defrule
			(or
				(goal g-primary-unit my-unique-unit)
				(and
					(goal g-support-unit my-unique-unit)
					(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)))
			(can-research-with-escrow my-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-unique-research)
			)

			(defrule
			(or
				(up-compare-goal g-desired-num-unique-unit > 10)
				(up-compare-goal g-unique-unit-line > 15))
			(can-research my-unique-research)
			=>
			(research my-unique-research)
			)

	#end-if

	#load-if-defined PORTUGUESE-CIV

		;-------------
		;	Carrack
		;-------------

			(defrule
			(up-compare-goal g-desired-num-unique-ship > 2)
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(can-research my-second-unique-research)
			=>
			(research my-second-unique-research)
			)

		;--------------
		;	Arquebus
		;--------------
			
			(defrule
			(or
				(goal g-primary-unit hand-cannoneer)
				(or
					(goal g-primary-unit my-unique-unit)
					(up-compare-goal g-desired-num-bombard-cannon > 2)))
			(can-research-with-escrow my-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-unique-research)
			)
			(defrule
			(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)
			(or
				(goal g-support-unit hand-cannoneer)
				(goal g-support-unit my-unique-unit))
			(can-research-with-escrow my-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-unique-research)
			)

			(defrule
			(or
				(up-compare-goal g-desired-num-hand-cannoneer > 10)
				(or
					(unit-type-count hand-cannoneer > 15)
					(or
						(up-compare-goal g-desired-num-unique-unit > 6)
						(or
							(up-compare-goal g-unique-unit-line > 10)
							(or
								(up-compare-goal g-desired-num-bombard-cannon > 1)
								(or
									(unit-type-count bombard-cannon > 2)
									(unit-type-count cannon-galleon-line > 2)))))))
			(can-research my-unique-research)
			=>
			(research my-unique-research)
			)

	#end-if

	#load-if-defined SARACEN-CIV

		#load-if-defined DE-AVAILABLE

			;------------------------------
			;	Counterweight Trebuchets
			;------------------------------

				(defrule
				(up-research-status c: ri-siege-engineers >= research-pending)
				(or
					(up-compare-goal g-desired-num-trebuchet >= 2)
					(or
						(unit-type-count trebuchet-set >= 3)
						(or
							(up-compare-goal g-desired-num-mangonel >= 2)
							(up-compare-goal g-mangonel-line >= 3))))
				(can-research my-second-unique-research)
				=>
				(research my-second-unique-research)
				)

		#else

			;--------------
			;	Madrasah
			;--------------

				(defrule
				(up-research-status c: ri-block-printing >= research-pending)
				(or
					(up-compare-goal g-desired-num-monk > 5)
					(unit-type-count monk > 6))
				(or
					(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
					(food-amount >= 1200))
				(can-research my-second-unique-research)
				=>
				(research my-second-unique-research)
				)

		#end-if

		;--------------
		;	Zealotry
		;--------------
			
			(defrule
			(or
				(goal g-primary-unit camel)
				(goal g-primary-unit my-unique-unit))
			(can-research-with-escrow my-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-unique-research)
			)
			(defrule
			(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)
			(or
				(goal g-support-unit camel)
				(goal g-support-unit my-unique-unit))
			(can-research-with-escrow my-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-unique-research)
			)

			(defrule
			(or
				(up-compare-goal g-desired-num-camel > 10)
				(or
					(up-compare-goal g-camel-line > 15)
					(or
						(up-compare-goal g-desired-num-unique-unit > 10)
						(up-compare-goal g-unique-unit-line > 15))))
			(can-research my-unique-research)
			=>
			(research my-unique-research)
			)

	#end-if

	#load-if-defined SICILIANS-CIV

		;-------------
		;	Hauberk
		;-------------
			
			(defrule
			(or
				(goal g-primary-unit knight)
				(and
					(goal g-support-unit knight)
					(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)))
			(can-research-with-escrow my-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-unique-research)
			)

			(defrule
			(or
				(up-compare-goal g-desired-num-knight > 10)
				(unit-type-count knight-line > 15))
			(can-research my-unique-research)
			=>
			(research my-unique-research)
			)

	#end-if

	#load-if-defined SLAVIC-CIV

		#load-if-defined DE-AVAILABLE

			;--------------
			;	Detinets
			;--------------

				(defrule
				(or
					(up-object-type-count-total c: castle g:< g-desired-num-castle)
					(up-compare-goal g-tower-line g:< g-desired-num-tower))
				(can-research my-second-unique-research)
				=>
				(research my-second-unique-research)
				)

		#else

			;---------------
			;	Orthodoxy
			;---------------

				(defrule
				(up-research-status c: ri-theocracy >= research-pending)
				(or
					(up-compare-goal g-desired-num-monk > 3)
					(unit-type-count monk > 5))
				(or
					(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
					(food-amount >= 1200))
				(can-research my-second-unique-research)
				=>
				(research my-second-unique-research)
				)

		#end-if
			
		;--------------
		;	Druzhina
		;--------------
			
			(defrule
			(or
				(goal g-primary-unit-class infantry-class)
				(and
					(goal g-support-unit-class infantry-class)
					(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)))
			(can-research-with-escrow my-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-unique-research)
			)

			(defrule
			(or
				(up-compare-goal g-desired-num-infantry > 10)
				(unit-type-count infantry-class > 15))
			(can-research my-unique-research)
			=>
			(research my-unique-research)
			)

	#end-if

	#load-if-defined SPANISH-CIV

		;-----------------
		;	Inquisition
		;-----------------

			(defrule
			(up-research-status c: ri-block-printing >= research-pending)
			(or
				(up-compare-goal g-desired-num-monk > 3)
				(unit-type-count monk > 5))
			(or
				(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
				(food-amount >= 1100))
			(can-research my-second-unique-research)
			=>
			(research my-second-unique-research)
			)

		;---------------
		;	Supremacy
		;---------------

			(defrule
			(up-modify-goal g-temp g:= g-desired-num-villager)
			(up-modify-goal g-temp c:- 10)
			(up-object-type-count c: villager g:>= g-temp)	;we have at least ten less than the total number of villagers we want
			(can-research my-unique-research)
			=>
			(research my-unique-research)
			)

	#end-if

	#load-if-defined TATARS-CIV

		;----------------
		;	Silk Armor
		;----------------
			
			(defrule
			(or
				(goal g-primary-unit scout-cavalry)
				(or
					(goal g-primary-unit cavalry-archer)
					(goal g-primary-unit steppe-lancer)))
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(can-research-with-escrow my-second-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-second-unique-research c: 1)
			(up-modify-escrow wood g:- g-wood-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-second-unique-research)
			)
			(defrule
			(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)
			(or
				(goal g-support-unit scout-cavalry)
				(or
					(goal g-support-unit cavalry-archer)
					(goal g-support-unit steppe-lancer)))
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(can-research-with-escrow my-second-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-second-unique-research c: 1)
			(up-modify-escrow wood g:- g-wood-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-second-unique-research)
			)

			(defrule
			(or
				(up-compare-goal g-desired-num-scout-cavalry > 10)
				(or
					(unit-type-count scout-cavalry-line > 15)
					(or
						(up-compare-goal g-desired-num-cavalry-archer > 10)
						(or
							(unit-type-count cavalry-archer-line > 15)
							(or
								(up-compare-goal g-desired-num-steppe-lancer > 10)
								(unit-type-count steppe-lancer-line > 15))))))
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(can-research my-second-unique-research)
			=>
			(research my-second-unique-research)
			)

		;------------------------
		;	Timurid Siegecraft
		;------------------------

			(defrule
			(up-compare-goal g-desired-num-trebuchet >= 4)
			(can-research-with-escrow my-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-unique-research c: 1)
			(up-modify-escrow wood g:- g-wood-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-unique-research)
			)

			(defrule
			(or
				(up-compare-goal g-desired-num-trebuchet >= 2)
				(unit-type-count trebuchet-set >= 3))
			(can-research my-unique-research)
			=>
			(research my-unique-research)
			)

	#end-if

	#load-if-defined TEUTONIC-CIV

		;--------------
		;	Ironclad
		;--------------

			(defrule
			(or
				(goal g-game-focus BOOM)
				(goal g-game-focus REBUILD))
			(up-object-type-count-total c: town-center g:< g-desired-num-town-center)
			=>
			(up-jump-rule 2)
			)

				(defrule
				(or
					(up-compare-goal g-desired-num-ram-type >= 3)
					(or
						(up-compare-goal g-desired-num-mangonel >= 3)
						(or
							(up-compare-goal g-desired-num-scorpion >= 5)
							(or
								(up-compare-goal g-desired-num-bombard-cannon >= 3)
								(up-compare-goal g-desired-num-trebuchet >= 3)))))
				(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
				(can-research-with-escrow my-second-unique-research)
				=>
				(up-setup-cost-data 1 g-food-cost)
				(up-add-research-cost c: my-second-unique-research c: 1)
				(up-modify-escrow wood g:- g-wood-cost)
				(up-modify-escrow gold g:- g-gold-cost)
				(research my-second-unique-research)
				)

				(defrule
				(or
					(up-compare-goal g-desired-num-ram-type >= 2)
					(or
						(up-compare-goal g-desired-num-mangonel >= 2)
						(or
							(up-compare-goal g-desired-num-scorpion >= 2)
							(or
								(up-compare-goal g-desired-num-bombard-cannon >= 2)
								(up-compare-goal g-desired-num-trebuchet >= 2)))))
				(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
				(can-research my-second-unique-research)
				=>
				(research my-second-unique-research)
				)

		;-------------------
		;	Crenellations
		;-------------------
			
			(defrule
			(or
				(up-compare-goal g-desired-num-castle >= 2)
				(and
					(up-compare-goal g-desired-num-castle >= 1)
					(goal g-game-focus DEFENSIVE)))
			(can-research-with-escrow my-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow stone g:- g-stone-cost)
			(research my-unique-research)
			)

			(defrule
			(up-compare-goal g-desired-num-castle > 0)
			(can-research my-unique-research)
			=>
			(research my-unique-research)
			)

	#end-if

	#load-if-defined TURKISH-CIV

		;------------
		;	Sipahi
		;------------
			
			(defrule
			(or
				(goal g-primary-unit cavalry-archer)
				(goal g-primary-unit genitour))
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(can-research-with-escrow my-second-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-second-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-second-unique-research)
			)
			(defrule
			(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)
			(or
				(goal g-support-unit cavalry-archer)
				(goal g-support-unit genitour))
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(can-research-with-escrow my-second-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-second-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-second-unique-research)
			)

			(defrule
			(or
				(up-compare-goal g-desired-num-cavalry-archer > 10)
				(or
					(unit-type-count cavalry-archer-line > 15)
					(or
						(up-compare-goal g-desired-num-genitour > 10)
						(up-compare-goal g-genitour-line > 15))))
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(can-research my-second-unique-research)
			=>
			(research my-second-unique-research)
			)

		;---------------
		;	Artillery
		;---------------

			#load-if-defined DE-AVAILABLE

				(defrule
				(or
					(goal g-game-focus BOOM)
					(goal g-game-focus REBUILD))
				(up-object-type-count-total c: town-center g:< g-desired-num-town-center)
				=>
				(up-jump-rule 1)
				)
					(defrule
					(or
						(up-compare-goal g-desired-num-bombard-tower >= 3)
						(or
							(up-compare-goal g-desired-num-bombard-cannon >= 2)
							(up-compare-goal g-desired-num-cannon-galleon >= 2)))
					(can-research-with-escrow my-unique-research)
					=>
					(up-setup-cost-data 1 g-food-cost)
					(up-add-research-cost c: my-unique-research c: 1)
					(up-modify-escrow wood g:- g-wood-cost)
					(up-modify-escrow gold g:- g-gold-cost)
					(research my-unique-research)
					)

			#else

				(defrule
				(or
					(up-compare-goal g-desired-num-bombard-tower >= 3)
					(or
						(up-compare-goal g-desired-num-bombard-cannon >= 2)
						(up-compare-goal g-desired-num-cannon-galleon >= 2)))
				(can-research-with-escrow my-unique-research)
				=>
				(up-setup-cost-data 1 g-food-cost)
				(up-add-research-cost c: my-unique-research c: 1)
				(up-modify-escrow stone g:- g-stone-cost)
				(up-modify-escrow gold g:- g-gold-cost)
				(research my-unique-research)
				)

			#end-if

			(defrule
			(or
				(up-compare-goal g-desired-num-bombard-tower > 2)
				(or
					(building-type-count bombard-tower > 3)
					(or
						(up-compare-goal g-desired-num-bombard-cannon > 1)
						(or
							(unit-type-count bombard-cannon > 2)
							(or
								(up-compare-goal g-desired-num-cannon-galleon > 1)
								(unit-type-count cannon-galleon-line > 2))))))
			(can-research my-unique-research)
			=>
			(research my-unique-research)
			)

	#end-if

	#load-if-defined VIETNAMESE-CIV

		;-------------
		;	Chatras
		;-------------
			
			(defrule
			(or
				(goal g-primary-unit battle-elephant)
				(and
					(goal g-support-unit battle-elephant)
					(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)))
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(can-research-with-escrow my-second-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-second-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-second-unique-research)
			)

			(defrule
			(or
				(up-compare-goal g-desired-num-battle-elephant > 6)
				(up-compare-goal g-battle-elephant-line > 10))
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(can-research my-second-unique-research)
			=>
			(research my-second-unique-research)
			)

		;-----------------
		;	Paper Money
		;-----------------

			#load-if-defined DE-AVAILABLE

				(defrule
				(can-research my-unique-research)
				(up-compare-goal g-game-focus != DEFENSIVE)
				(up-compare-goal g-primary-unit-tech-progress >= SECONDARY-IMPERIAL-COMPLETE)
				(population >= EIGHTY-PERCENT-POP)
				=>
				(research my-unique-research)
				)

			#else

				(defrule
				(or
					(up-players-in-game ally >= 3)
					(goal g-age-status LATE-IMPERIAL))
				(can-research my-unique-research)
				=>
				(research my-unique-research)
				)

				(defrule
				(food-amount >= 1000)
				(wood-amount >= 1000)
				(can-research my-unique-research)
				=>
				(research my-unique-research)
				)

			#end-if

	#end-if

	#load-if-defined VIKING-CIV

		;----------------
		;	Chieftains
		;----------------
			
			(defrule
			(or
				(goal g-primary-unit-class infantry-class)
				(and
					(goal g-support-unit-class infantry-class)
					(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)))
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(can-research-with-escrow my-second-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-second-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-second-unique-research)
			)

			(defrule
			(or
				(up-compare-goal g-desired-num-infantry > 10)
				(unit-type-count infantry-class > 15))
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(can-research my-second-unique-research)
			=>
			(research my-second-unique-research)
			)

		;-------------------
		;	Berserkergang
		;-------------------

			(defrule
			(or
				(goal g-primary-unit my-unique-unit)
				(and
					(goal g-support-unit my-unique-unit)
					(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)))
			(can-research-with-escrow my-unique-research)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: my-unique-research c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research my-unique-research)
			)

			(defrule
			(or
				(up-compare-goal g-desired-num-unique-unit > 10)
				(up-compare-goal g-unique-unit-line > 15))
			(can-research my-unique-research)
			=>
			(research my-unique-research)
			)

	#end-if

;=========================<>=========================
;				    CAVALRY TECHS
;=========================<>=========================

	;-----------------------------------------------------
	;	Elite Battle Elephant - Secondary Imperial Tech
	;-----------------------------------------------------

		#load-if-defined UP-GAME-WK

			(defrule
			(or
				(goal g-primary-unit battle-elephant)
				(and
					(goal g-support-unit battle-elephant)
					(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)))
			(can-research-with-escrow ri-elite-battle-elephant)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: ri-elite-battle-elephant c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research ri-elite-battle-elephant)
			)

			(defrule
			(or
				(up-compare-goal g-desired-num-battle-elephant > 6)
				(up-compare-goal g-battle-elephant-line > 10))
			(can-research ri-elite-battle-elephant)
			=>
			(research ri-elite-battle-elephant)
			)

		#end-if

	;---------------------------------------
	;	Cavalier - Required Imperial Tech
	;---------------------------------------

		(defrule
		(or
			(goal g-primary-unit knight)
			(goal g-support-unit knight))
		(can-research-with-escrow ri-cavalier)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-cavalier c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-cavalier)
		)

		(defrule
		(up-compare-goal g-desired-num-knight > 10)
		(unit-type-count knight-line > 15)
		(can-research ri-cavalier)
		=>
		(research ri-cavalier)
		)
		(defrule
		(population >= NINETY-PERCENT-POP)
		(unit-type-count knight-line >= 5)
		(can-research ri-cavalier)
		=>
		(research ri-cavalier)
		)

	;---------------------------------------
	;	Paladin - Secondary Imperial Tech
	;---------------------------------------

		(defrule
		(or
			(goal g-primary-unit knight)
			(goal g-support-unit knight))
		(player-in-game any-ally)
		(can-research-with-escrow ri-paladin)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-paladin c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-paladin)
		)

		(defrule
		(up-compare-goal g-desired-num-knight > 10)
		(unit-type-count knight-line >= 25)
		(can-research ri-paladin)
		=>
		(research ri-paladin)
		)
		(defrule
		(population >= NINETY-FIVE-PERCENT-POP)
		(player-in-game any-ally)
		(unit-type-count knight-line >= 10)
		(can-research ri-paladin)
		=>
		(research ri-paladin)
		)

	;-----------------------------------------------------
	;	Elite Shrivamsha Rider - Priority Imperial Tech
	;-----------------------------------------------------

		#load-if-defined GURJARAS-CIV

			(defrule
			(or
				(goal g-primary-unit shrivamsha-rider)
				(goal g-support-unit shrivamsha-rider))
			(can-research-with-escrow ri-elite-shrivamsha-rider)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: ri-elite-shrivamsha-rider c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research ri-elite-shrivamsha-rider)
			)

			(defrule
			(up-compare-goal g-desired-num-second-unique-unit > 10)
			(unit-type-count shrivamsha-rider-line > 15)
			(can-research ri-elite-shrivamsha-rider)
			=>
			(research ri-elite-shrivamsha-rider)
			)
			(defrule
			(population >= NINETY-PERCENT-POP)
			(unit-type-count shrivamsha-rider-line > 7)
			(can-research ri-elite-shrivamsha-rider)
			=>
			(research ri-elite-shrivamsha-rider)
			)

		#end-if

	;------------------------------------------
	;	Heavy Camel - Required Imperial Tech
	;------------------------------------------

		(defrule
		(or
			(goal g-primary-unit camel)
			(goal g-support-unit camel))
		(can-research-with-escrow ri-heavy-camel)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-heavy-camel c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-heavy-camel)
		)

		(defrule
		(up-compare-goal g-desired-num-camel > 10)
		(up-compare-goal g-camel-line >= 15)
		(can-research ri-heavy-camel)
		=>
		(research ri-heavy-camel)
		)
		(defrule
		(population >= NINETY-PERCENT-POP)
		(up-compare-goal g-camel-line >= 5)
		(can-research ri-heavy-camel)
		=>
		(research ri-heavy-camel)
		)

	;----------------------------------------------
	;	Imperial Camel - Secondary Imperial Tech
	;----------------------------------------------

		#load-if-defined INDIAN-CIV

			(defrule
			(or
				(goal g-primary-unit camel)
				(goal g-support-unit camel))
			(player-in-game any-ally)
			(can-research-with-escrow ri-imperial-camel)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: ri-imperial-camel c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research ri-imperial-camel)
			)

			(defrule
			(up-compare-goal g-desired-num-camel > 10)
			(up-compare-goal g-camel-line > 25)
			(can-research ri-imperial-camel)
			=>
			(research ri-imperial-camel)
			)
			(defrule
			(population >= NINETY-FIVE-PERCENT-POP)
			(player-in-game any-ally)
			(up-compare-goal g-camel-line > 10)
			(can-research ri-imperial-camel)
			=>
			(research ri-imperial-camel)
			)

		#end-if

	;--------------------------------------------------
	;	Elite Steppe Lancer - Priority Imperial Tech
	;--------------------------------------------------

		(defrule
		(or
			(goal g-primary-unit steppe-lancer)
			(goal g-support-unit steppe-lancer))
		(can-research-with-escrow ri-elite-steppe-lancer)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-elite-steppe-lancer c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-elite-steppe-lancer)
		)

		(defrule
		(up-compare-goal g-desired-num-steppe-lancer > 10)
		(unit-type-count steppe-lancer-line > 15)
		(can-research ri-elite-steppe-lancer)
		=>
		(research ri-elite-steppe-lancer)
		)
		(defrule
		(population >= NINETY-PERCENT-POP)
		(unit-type-count steppe-lancer-line > 7)
		(can-research ri-elite-steppe-lancer)
		=>
		(research ri-elite-steppe-lancer)
		)

	;------------------------------------------
	;	Light Cavalry - Required Castle Tech
	;------------------------------------------

		(defrule
		(or
			(up-compare-goal g-game-focus != BOOM)
			(up-compare-goal g-eco-tech-progress >= PRIORITY-CASTLE-COMPLETE))
		(up-compare-goal g-desired-num-scout-cavalry > 0)
		(can-research-with-escrow ri-light-cavalry)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-light-cavalry c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-light-cavalry)
		)

		(defrule
		(unit-type-count scout-cavalry-line > 3)
		(can-research ri-light-cavalry)
		=>
		(research ri-light-cavalry)
		)

	;-------------------------------------
	;	Hussar - Priority Imperial Tech
	;-------------------------------------

		(defrule
		(or
			(goal g-primary-unit scout-cavalry)
			(or
				(goal g-support-unit scout-cavalry)
				(and
					(up-compare-goal g-desired-num-scout-cavalry > 0)
					(goal g-age-status LATE-IMPERIAL))))
		(can-research-with-escrow ri-hussar)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-hussar c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-hussar)
		)

		(defrule
		(up-compare-goal g-desired-num-scout-cavalry > 10)
		(unit-type-count scout-cavalry-line >= 15)
		(can-research ri-hussar)
		=>
		(research ri-hussar)
		)
		(defrule
		(or
			(population >= NINETY-PERCENT-POP)
			(goal g-age-status LATE-IMPERIAL))
		(unit-type-count scout-cavalry-line >= 5)
		(can-research ri-hussar)
		=>
		(research ri-hussar)
		)

    ;------------------------------------------------
    ;   Scale Barding Armor - Priority Castle Tech (>= 3 cavalry)
    ;------------------------------------------------

		(defrule
		(current-age == feudal-age)
		(up-compare-goal g-strategy-type < FAST-CASTLE)
		(goal g-primary-unit scout-cavalry)
		(or
			(up-compare-goal g-age-status != SAVE-FOR-CASTLE)
			(food-amount >= 950))
		(unit-type-count scout-cavalry-line >= 4)
		(can-research-with-escrow ri-scale-barding)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-scale-barding c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(research ri-scale-barding)
		)

		(defrule
		(current-age >= castle-age)
		(or
			(up-compare-goal g-game-focus != BOOM)
			(up-compare-goal g-eco-tech-progress >= PRIORITY-CASTLE-COMPLETE))
		(goal g-primary-unit-class cavalry-class)
		(or
			(unit-type-count cavalry-class >= 3)
			(or
				(goal g-primary-unit scout-cavalry)
				(or
					(goal g-primary-unit camel)
					(up-compare-goal g-age-status >= TO-IMPERIAL))))
		(can-research-with-escrow ri-scale-barding)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-scale-barding c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(research ri-scale-barding)
		)
		(defrule
		(current-age >= castle-age)
		(or
			(up-compare-goal g-game-focus != BOOM)
			(up-compare-goal g-eco-tech-progress >= PRIORITY-CASTLE-COMPLETE))
		(goal g-support-unit-class cavalry-class)
		(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)
		(unit-type-count cavalry-class >= 3)
		(can-research-with-escrow ri-scale-barding)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-scale-barding c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(research ri-scale-barding)
		)

		(defrule
		(up-compare-goal g-desired-num-cavalry > 5)
		(unit-type-count cavalry-class >= 5)
		(or
			(and
				(up-compare-goal g-age-status >= TO-CASTLE)
				(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL))
			(or
				(food-amount >= 950)
				(up-compare-goal g-strategy-type < FAST-CASTLE)))
		(can-research ri-scale-barding)
		=>
		(research ri-scale-barding)
		)
		(defrule
		(up-compare-goal g-desired-num-cavalry > 0)
		(unit-type-count cavalry-class > 0)
		(or
			(up-compare-goal g-age-status >= TO-IMPERIAL)
			(population >= EIGHTY-PERCENT-POP))
		(can-research ri-scale-barding)
		=>
		(research ri-scale-barding)
		)

    ;-------------------------------------------------
    ;   Chain Barding Armor - Secondary Castle Tech (>= 12/15 cavalry or >= 8/10 camels)
    ;-------------------------------------------------

		(defrule
		(goal g-primary-unit-class cavalry-class)
		(or
			(unit-type-count cavalry-class >= 12)
			(or
				(up-compare-goal g-camel-line >= 8)
				(or
					(up-compare-goal g-age-status >= TO-IMPERIAL)
					(unit-type-count cavalry-class g:>= g-desired-num-cavalry))))
		(can-research-with-escrow ri-chain-barding)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-chain-barding c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-chain-barding)
		)
		(defrule
		(goal g-support-unit-class cavalry-class)
		(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)
		(or
			(unit-type-count cavalry-class >= 15)
			(or
				(up-compare-goal g-camel-line >= 10)
				(or
					(up-compare-goal g-age-status >= TO-IMPERIAL)
					(unit-type-count cavalry-class g:>= g-desired-num-cavalry))))
		(can-research-with-escrow ri-chain-barding)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-chain-barding c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-chain-barding)
		)

		(defrule
		(unit-type-count cavalry-class >= 12)
		(or
			(unit-type-count cavalry-class >= 18)
			(up-compare-goal g-age-status >= TO-IMPERIAL))
		(can-research ri-chain-barding)
		=>
		(research ri-chain-barding)
		)

    ;--------------------------------------------------
    ;   Plate Barding Armor - Priority Imperial Tech
    ;--------------------------------------------------

		(defrule
		(current-age == imperial-age)
		(up-compare-const CAVALIER-AVAILABLE == YES)
		(up-compare-goal g-desired-num-knight >= 10)
		(up-research-status c: ri-cavalier < research-pending)
		=>
		(up-jump-rule 2)
		)
		(defrule
		(current-age == imperial-age)
		(up-compare-const HEAVY-CAMEL-AVAILABLE == YES)
		(up-compare-goal g-desired-num-camel >= 10)
		(up-research-status c: ri-heavy-camel < research-pending)
		=>
		(up-jump-rule 1)
		)
			(defrule
			(or
				(goal g-primary-unit-class cavalry-class)
				(and
					(goal g-support-unit-class cavalry-class)
					(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)))
			(can-research-with-escrow ri-plate-barding)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: ri-plate-barding c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research ri-plate-barding)
			)

		(defrule
		(current-age == imperial-age)
		(up-compare-const CAVALIER-AVAILABLE == YES)
		(up-compare-goal g-desired-num-knight >= 10)
		(up-research-status c: ri-cavalier < research-pending)
		=>
		(up-jump-rule 2)
		)
		(defrule
		(current-age == imperial-age)
		(up-compare-const HEAVY-CAMEL-AVAILABLE == YES)
		(up-compare-goal g-desired-num-camel >= 10)
		(up-research-status c: ri-heavy-camel < research-pending)
		=>
		(up-jump-rule 1)
		)
			(defrule
			(up-compare-goal g-desired-num-cavalry > 10)
			(or
				(unit-type-count cavalry-class >= 20)
				(population >= NINETY-FIVE-PERCENT-POP))
			(can-research ri-plate-barding)
			=>
			(research ri-plate-barding)
			)

    ;----------------------------------------
    ;   Bloodlines - Secondary Castle Tech (>= 6 cavalry)
    ;----------------------------------------
		
		(defrule
		(or
			(unit-type-count cavalry-class < 4)
			(and
				(current-age >= castle-age)
				(unit-type-count cavalry-class < 6)))
		(unit-type-count cavalry-class g:< g-desired-num-cavalry)
		=>
		(up-jump-rule 2)
		)

			(defrule
			(current-age >= castle-age)
			(or
				(up-compare-goal g-game-focus != BOOM)
				(up-compare-goal g-eco-tech-progress >= PRIORITY-CASTLE-COMPLETE))
			(goal g-primary-unit-class cavalry-class)
			(can-research-with-escrow ri-bloodlines)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: ri-bloodlines c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research ri-bloodlines)
			)
			(defrule
			(current-age >= castle-age)
			(or
				(up-compare-goal g-game-focus != BOOM)
				(up-compare-goal g-eco-tech-progress >= PRIORITY-CASTLE-COMPLETE))
			(goal g-support-unit-class cavalry-class)
			(up-compare-goal g-support-unit != battle-elephant)
			(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(can-research-with-escrow ri-bloodlines)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: ri-bloodlines c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research ri-bloodlines)
			)

		(defrule
		(or
			(up-compare-goal g-game-focus != BOOM)
			(up-compare-goal g-eco-tech-progress >= PRIORITY-CASTLE-COMPLETE))
		(or
			(and
				(up-compare-goal g-age-status >= TO-CASTLE)
				(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL))
			(or
				(food-amount >= 950)
				(up-compare-goal g-strategy-type < FAST-CASTLE)))
		(up-compare-goal g-desired-num-cavalry > 6)
		(unit-type-count cavalry-class > 10)
		(can-research ri-bloodlines)
		=>
		(research ri-bloodlines)
		)
		(defrule
		(population >= EIGHTY-PERCENT-POP)
		(or
			(up-compare-goal g-game-focus != BOOM)
			(up-compare-goal g-eco-tech-progress >= PRIORITY-CASTLE-COMPLETE))
		(up-compare-goal g-age-status >= TO-CASTLE)
		(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
		(unit-type-count cavalry-class > 5)
		(can-research ri-bloodlines)
		=>
		(research ri-bloodlines)
		)

    ;----------------
    ;   Husbandry
    ;----------------

		(defrule
		(or
			(up-compare-goal g-game-focus != BOOM)
			(up-compare-goal g-eco-tech-progress >= PRIORITY-CASTLE-COMPLETE))
		(up-compare-goal g-desired-num-cavalry >= 10)
		(unit-type-count cavalry-class >= 15)
		(or
			(up-compare-goal g-strategy-type < FAST-CASTLE)
			(military-population > 12))
		(or
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(food-amount >= 1150))
		(can-research ri-husbandry)
		=>
		(research ri-husbandry)
		)

;=========================<>=========================
;				    ARCHERY TECHS
;=========================<>=========================

	;-----------------------------------------------------
	;	Elite Elephant Archer - Secondary Imperial Tech
	;-----------------------------------------------------

		(defrule
		(or
			(goal g-primary-unit elephant-archer)
			(goal g-support-unit elephant-archer))
		(can-research-with-escrow ri-elite-elephant-archer)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-elite-elephant-archer c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-elite-elephant-archer)
		)

		(defrule
		(up-compare-goal g-desired-num-elephant-archer > 10)
		(unit-type-count elephant-archer-line > 15)
		(can-research ri-elite-elephant-archer)
		=>
		(research ri-elite-elephant-archer)
		)
		(defrule
		(population >= NINETY-PERCENT-POP)
		(unit-type-count elephant-archer-line > 8)
		(can-research ri-elite-elephant-archer)
		=>
		(research ri-elite-elephant-archer)
		)

	;----------------------------------------------------
	;	Heavy Cavalry Archer - Secondary Imperial Tech
	;----------------------------------------------------

		(defrule
		(or
			(goal g-primary-unit cavalry-archer)
			(goal g-support-unit cavalry-archer))
		(can-research-with-escrow ri-heavy-cavalry-archer)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-heavy-cavalry-archer c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-heavy-cavalry-archer)
		)

		(defrule
		(up-compare-goal g-desired-num-cavalry-archer > 10)
		(unit-type-count cavalry-archer-line > 15)
		(can-research ri-heavy-cavalry-archer)
		=>
		(research ri-heavy-cavalry-archer)
		)
		(defrule
		(population >= NINETY-PERCENT-POP)
		(unit-type-count cavalry-archer-line > 8)
		(can-research ri-heavy-cavalry-archer)
		=>
		(research ri-heavy-cavalry-archer)
		)

	;----------------------------------------------
	;	Elite Genitour - Secondary Imperial Tech
	;----------------------------------------------

		#load-if-defined UP-GAME-WK

			(defrule
			(or
				(goal g-game-focus BOOM)
				(goal g-game-focus REBUILD))
			(up-object-type-count-total c: town-center g:< g-desired-num-town-center)
			=>
			(up-jump-rule 3)
			)

				(defrule
				(or
					(goal g-primary-unit genitour)
					(goal g-support-unit genitour))
				(can-research-with-escrow ri-elite-genitour)
				=>
				(up-setup-cost-data 1 g-food-cost)
				(up-add-research-cost c: ri-elite-genitour c: 1)
				(up-modify-escrow food g:- g-food-cost)
				(up-modify-escrow wood g:- g-wood-cost)
				(research ri-elite-genitour)
				)

				(defrule
				(up-compare-goal g-desired-num-genitour > 10)
				(up-compare-goal g-genitour-line > 15)
				(can-research ri-elite-genitour)
				=>
				(research ri-elite-genitour)
				)
				(defrule
				(population >= NINETY-PERCENT-POP)
				(up-compare-goal g-genitour-line > 5)
				(can-research ri-elite-genitour)
				=>
				(research ri-elite-genitour)
				)

		#end-if

	;----------------------------------------
	;	Crossbowman - Required Castle Tech (>= 5 archers)
	;----------------------------------------

		(defrule
		(or
			(up-compare-goal g-game-focus != BOOM)
			(up-compare-goal g-eco-tech-progress >= PRIORITY-CASTLE-COMPLETE))
		(or
			(goal g-primary-unit archer)
			(goal g-support-unit archer))
		(or
			(unit-type-count archer-line >= 5)
			(or
				(unit-type-count archer-line g:>= g-desired-num-archer)
				(up-compare-goal g-age-status >= TO-IMPERIAL)))
		(can-research-with-escrow ri-crossbow)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-crossbow c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-crossbow)
		)

		(defrule
		(up-compare-goal g-desired-num-archer > 5)
		(unit-type-count archer-line > 10)
		(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
		(can-research ri-crossbow)
		=>
		(research ri-crossbow)
		)
		(defrule
		(population >= EIGHTY-PERCENT-POP)
		(unit-type-count archer-line > 4)
		(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
		(can-research ri-crossbow)
		=>
		(research ri-crossbow)
		)

	;---------------------------------------
	;	Arbalest - Priority Imperial Tech
	;---------------------------------------

		(defrule
		(or
			(goal g-primary-unit archer)
			(goal g-support-unit archer))
		(can-research-with-escrow ri-arbalest)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-arbalest c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-arbalest)
		)

		(defrule
		(up-compare-goal g-desired-num-archer > 10)
		(unit-type-count archer-line >= 15)
		(can-research ri-arbalest)
		=>
		(research ri-arbalest)
		)
		(defrule
		(population >= NINETY-PERCENT-POP)
		(unit-type-count archer-line >= 5)
		(can-research ri-arbalest)
		=>
		(research ri-arbalest)
		)

	;---------------------------------------------
	;	Elite Skirmisher - Priority Castle Tech (>= 8 skirms)
	;---------------------------------------------

		(defrule
		(or
			(goal g-game-focus BOOM)
			(goal g-game-focus REBUILD))
		(up-object-type-count-total c: town-center g:< g-desired-num-town-center)
		=>
		(up-jump-rule 4)
		)

			(defrule
			(or
				(up-compare-goal g-game-focus != BOOM)
				(up-compare-goal g-eco-tech-progress >= PRIORITY-CASTLE-COMPLETE))
			(goal g-primary-unit skirmisher)
			(can-research-with-escrow ri-elite-skirmisher)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: ri-elite-skirmisher c: 1)
			(up-modify-escrow wood g:- g-wood-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research ri-elite-skirmisher)
			)
			(defrule
			(or
				(up-compare-goal g-game-focus != BOOM)
				(up-compare-goal g-eco-tech-progress >= PRIORITY-CASTLE-COMPLETE))
			(goal g-support-unit skirmisher)
			(or
				(up-compare-goal g-skirmisher-line >= 6)
				(or
					(up-compare-goal g-skirmisher-line g:>= g-desired-num-skirmisher)
					(up-compare-goal g-age-status >= TO-IMPERIAL)))
			(can-research-with-escrow ri-elite-skirmisher)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: ri-elite-skirmisher c: 1)
			(up-modify-escrow wood g:- g-wood-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research ri-elite-skirmisher)
			)

			(defrule
			(up-compare-goal g-desired-num-skirmisher > 6)
			(up-compare-goal g-skirmisher-line > 10)
			(or
				(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
				(gold-amount >= 950))
			(can-research ri-elite-skirmisher)
			=>
			(research ri-elite-skirmisher)
			)
			(defrule
			(population >= EIGHTY-PERCENT-POP)
			(up-compare-goal g-skirmisher-line > 5)
			(or
				(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
				(gold-amount >= 950))
			(can-research ri-elite-skirmisher)
			=>
			(research ri-elite-skirmisher)
			)

	;--------------------------------------------------
	;	Imperial Skirmisher - Priority Imperial Tech
	;--------------------------------------------------

		#load-if-defined UP-GAME-WK

			(defrule
			(or
				(goal g-primary-unit skirmisher)
				(and
					(goal g-support-unit skirmisher)
					(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)))
			(can-research-with-escrow ri-imperial-skirmisher)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: ri-imperial-skirmisher c: 1)
			(up-modify-escrow wood g:- g-wood-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research ri-imperial-skirmisher)
			)

			(defrule
			(up-compare-goal g-desired-num-skirmisher > 10)
			(up-compare-goal g-skirmisher-line > 15)
			(can-research ri-imperial-skirmisher)
			=>
			(research ri-imperial-skirmisher)
			)
			(defrule
			(population >= NINETY-PERCENT-POP)
			(up-compare-goal g-skirmisher-line > 15)
			(can-research ri-imperial-skirmisher)
			=>
			(research ri-imperial-skirmisher)
			)

		#end-if

	;---------------
	;	Chemistry
	;---------------

        (defrule
        (can-research-with-escrow ri-chemistry)
		(or
			(up-compare-goal g-desired-num-hand-cannoneer > 0)
			(or
				(up-compare-goal g-desired-num-bombard-cannon > 0)
				(or
					(up-compare-goal g-desired-num-cannon-galleon > 0)
					(up-compare-goal g-desired-num-bombard-cannon > 0))))
        =>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-chemistry c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-chemistry)
        )

		(defrule
		(up-compare-goal g-desired-num-foot-archers > 10)
		(or
			(unit-type-count archery-class > 15)
			(or
				(up-compare-goal g-desired-num-cavalry-archers > 10)
				(unit-type-count cavalry-archer-class > 15)))
		(can-research ri-chemistry)
		=>
		(research ri-chemistry)
		)

	;--------------------------------------
	;	Fletching - Priority Castle Tech (>= 4 ranged units)
	;--------------------------------------

		(defrule
		(current-age == feudal-age)
		(up-compare-goal g-age-status < SAVE-FOR-CASTLE)
		(up-compare-goal g-strategy-type < FAST-CASTLE)
		(or
			(goal g-primary-unit-class archery-class)
			(goal g-support-unit-class archery-class))
		(or
			(unit-type-count archery-class >= 4)
			(up-compare-goal g-age-status >= TO-IMPERIAL))
		(can-research-with-escrow ri-fletching)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-fletching c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-fletching)
		)

		(defrule
		(current-age >= castle-age)
		(up-compare-const CROSSBOWMAN-AVAILABLE == YES)
		(up-compare-goal g-desired-num-archer >= 5)
		(up-research-status c: ri-crossbow < research-pending)
		=>
		(up-jump-rule 2)
		)

			(defrule
			(up-compare-goal g-age-status >= TO-CASTLE)
			(or
				(goal g-primary-unit-class archery-class)
				(goal g-primary-unit-class cavalry-archer-class))
			(or
				(unit-type-count archery-class >= 4)
				(or
					(unit-type-count cavalry-archer-class >= 4)
					(up-compare-goal g-age-status >= TO-IMPERIAL)))
			(can-research-with-escrow ri-fletching)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: ri-fletching c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research ri-fletching)
			)

			(defrule
			(up-compare-goal g-age-status >= TO-CASTLE)
			(or
				(goal g-support-unit-class archery-class)
				(goal g-support-unit-class cavalry-archer-class))
			(or
				(unit-type-count archery-class >= 4)
				(or
					(unit-type-count cavalry-archer-class >= 4)
					(up-compare-goal g-age-status >= TO-IMPERIAL)))
			(can-research-with-escrow ri-fletching)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: ri-fletching c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research ri-fletching)
			)

		(defrule
		(current-age >= castle-age)
		(up-compare-const CROSSBOWMAN-AVAILABLE == YES)
		(up-compare-goal g-desired-num-archer >= 10)
		(up-research-status c: ri-crossbow < research-pending)
		=>
		(up-jump-rule 1)
		)

			(defrule
			(current-age >= castle-age)
			(or
				(up-compare-goal g-game-focus != BOOM)
				(up-compare-goal g-eco-tech-progress >= PRIORITY-CASTLE-COMPLETE))
			(or
				(and
					(up-compare-goal g-desired-num-foot-archers > 6)
					(unit-type-count archery-class > 10))
				(and
					(up-compare-goal g-desired-num-cavalry-archers > 6)
					(unit-type-count cavalry-archer-class > 10)))
			(can-research ri-fletching)
			=>
			(research ri-fletching)
			)

		(defrule
		(population >= SEVENTY-PERCENT-POP)
		(or
			(up-compare-goal g-game-focus != BOOM)
			(up-compare-goal g-eco-tech-progress >= PRIORITY-CASTLE-COMPLETE))
		(or
			(unit-type-count archery-class > 5)
			(unit-type-count cavalry-archer-class > 5))
		(can-research ri-fletching)
		=>
		(research ri-fletching)
		)

	;------------------------------------------
	;	Bodkin Arrow - Secondary Castle Tech (>= 8/12 ranged units)
	;------------------------------------------

		(defrule
		(goal g-primary-unit-class archery-class)
		(or
			(unit-type-count archery-class >= 8)
			(unit-type-count archery-class g:>= g-desired-num-foot-archers))
		(or
			(up-compare-goal g-target-military-parity > 3)
			(up-compare-flag g-flag == FIRST-ATTACK-LAUNCHED))
		(can-research-with-escrow ri-bodkin-arrow)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-bodkin-arrow c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-bodkin-arrow)
		)
		(defrule
		(goal g-support-unit-class archery-class)
		(or
			(unit-type-count archery-class >= 12)
			(unit-type-count archery-class g:>= g-desired-num-foot-archers))
		(or
			(up-compare-goal g-target-military-parity > 3)
			(up-compare-flag g-flag == FIRST-ATTACK-LAUNCHED))
		(can-research-with-escrow ri-bodkin-arrow)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-bodkin-arrow c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-bodkin-arrow)
		)
		(defrule
		(goal g-primary-unit-class cavalry-archer-class)
		(or
			(unit-type-count cavalry-archer-class >= 8)
			(unit-type-count cavalry-archer-class g:>= g-desired-num-cavalry-archers))
		(or
			(up-compare-goal g-target-military-parity > 3)
			(up-compare-flag g-flag == FIRST-ATTACK-LAUNCHED))
		(can-research-with-escrow ri-bodkin-arrow)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-bodkin-arrow c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-bodkin-arrow)
		)
		(defrule
		(goal g-support-unit-class cavalry-archer-class)
		(or
			(unit-type-count cavalry-archer-class >= 12)
			(unit-type-count cavalry-archer-class g:>= g-desired-num-cavalry-archers))
		(or
			(up-compare-goal g-target-military-parity > 3)
			(up-compare-flag g-flag == FIRST-ATTACK-LAUNCHED))
		(can-research-with-escrow ri-bodkin-arrow)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-bodkin-arrow c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-bodkin-arrow)
		)
		(defrule
		(or
			(goal g-primary-unit-class archery-class)
			(or
				(goal g-support-unit-class archery-class)
				(or
					(goal g-primary-unit-class cavalry-archer-class)
					(goal g-support-unit-class cavalry-archer-class))))
		(up-compare-goal g-age-status >= TO-IMPERIAL)
		(can-research-with-escrow ri-bodkin-arrow)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-bodkin-arrow c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-bodkin-arrow)
		)

		(defrule
		(or
			(and
				(up-compare-goal g-desired-num-foot-archers > 6)
				(unit-type-count archery-class > 10))
			(and
				(up-compare-goal g-desired-num-cavalry-archers > 6)
				(unit-type-count cavalry-archer-class > 10)))
		(or
			(up-compare-goal g-age-status >= MID-CASTLE)
			(up-compare-flag g-flag == FIRST-ATTACK-LAUNCHED))
		(can-research ri-bodkin-arrow)
		=>
		(research ri-bodkin-arrow)
		)
		(defrule
		(population >= EIGHTY-PERCENT-POP)
		(or
			(unit-type-count archery-class > 5)
			(unit-type-count cavalry-archer-class > 5))
		(or
			(up-compare-goal g-age-status >= MID-CASTLE)
			(up-compare-flag g-flag == FIRST-ATTACK-LAUNCHED))
		(can-research ri-bodkin-arrow)
		=>
		(research ri-bodkin-arrow)
		)

	;-------------------------------------
	;	Bracer - Priority Imperial Tech
	;-------------------------------------

		(defrule
		(or
			(goal g-primary-unit-class archery-class)
			(goal g-support-unit-class archery-class))
		(current-age-time > 5)
		(can-research-with-escrow ri-bracer)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-bracer c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-bracer)
		)
		(defrule
		(or
			(goal g-primary-unit-class cavalry-archer-class)
			(goal g-support-unit-class cavalry-archer-class))
		(current-age-time > 5)
		(can-research-with-escrow ri-bracer)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-bracer c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-bracer)
		)

		(defrule
		(or
			(and
				(up-compare-goal g-desired-num-foot-archers > 10)
				(unit-type-count archery-class > 15))
			(and
				(up-compare-goal g-desired-num-cavalry-archers > 10)
				(unit-type-count cavalry-archer-class > 15)))
		(current-age-time > 5)
		(can-research ri-bracer)
		=>
		(research ri-bracer)
		)
		(defrule
		(population >= NINETY-PERCENT-POP)
		(or
			(unit-type-count archery-class > 5)
			(unit-type-count cavalry-archer-class > 5))
		(current-age-time > 5)
		(can-research ri-bracer)
		=>
		(research ri-bracer)
		)

	;-----------------------------------------
	;	Thumb Ring - Priority Imperial Tech (Secondary Castle for cav archers) (>= 7/10 cav archers)
	;-----------------------------------------

		(defrule
		(or
			(goal g-game-focus BOOM)
			(goal g-game-focus REBUILD))
		(up-object-type-count-total c: town-center g:< g-desired-num-town-center)
		=>
		(up-jump-rule 3)
		)

			(defrule
			(or
				(and
					(goal g-primary-unit cavalry-archer)
					(or
						(unit-type-count cavalry-archer-line >= 7)
						(unit-type-count cavalry-archer-line g:>= g-desired-num-cavalry-archer)))
				(and
					(goal g-support-unit cavalry-archer)
					(or
						(unit-type-count cavalry-archer-line >= 10)
						(unit-type-count cavalry-archer-line g:>= g-desired-num-cavalry-archer))))
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(can-research-with-escrow ri-thumb-ring)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: ri-thumb-ring c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow wood g:- g-wood-cost)
			(research ri-thumb-ring)
			)

			(defrule
			(or
				(up-compare-goal g-desired-num-foot-archers > 6)
				(up-compare-goal g-desired-num-cavalry-archers > 6))
			(or
				(unit-type-count archery-class > 12)
				(unit-type-count cavalry-archer-class > 10))
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(can-research ri-thumb-ring)
			=>
			(research ri-thumb-ring)
			)
			(defrule
			(population >= NINETY-PERCENT-POP)
			(or
				(unit-type-count archery-class > 6)
				(unit-type-count cavalry-archer-class > 6))
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(can-research ri-thumb-ring)
			=>
			(research ri-thumb-ring)
			)

    ;----------------
    ;   Bloodlines
    ;----------------
		
		(defrule
		(unit-type-count cavalry-archer-class < 4)
		(unit-type-count cavalry-cannon-class < 4)
		=>
		(up-jump-rule 3)
		)

			(defrule
			(or
				(up-compare-goal g-game-focus != BOOM)
				(up-compare-goal g-eco-tech-progress >= PRIORITY-CASTLE-COMPLETE))
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(up-compare-goal g-desired-num-cavalry-archers > 6)
			(unit-type-count cavalry-archer-class > 10)
			(can-research ri-bloodlines)
			=>
			(research ri-bloodlines)
			)
			(defrule
			(or
				(up-compare-goal g-game-focus != BOOM)
				(up-compare-goal g-eco-tech-progress >= PRIORITY-CASTLE-COMPLETE))
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(up-compare-goal g-desired-num-unique-unit > 6)
			(up-compare-const UNIQUE-UNIT-CLASS == cavalry-cannon-class)
			(up-compare-goal g-unique-unit-line > 10)
			(can-research ri-bloodlines)
			=>
			(research ri-bloodlines)
			)
			(defrule
			(or
				(up-compare-goal g-game-focus != BOOM)
				(up-compare-goal g-eco-tech-progress >= PRIORITY-CASTLE-COMPLETE))
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(population >= NINETY-PERCENT-POP)
			(or
				(unit-type-count cavalry-archer-class > 5)
				(and
					(up-compare-const UNIQUE-UNIT-CLASS == cavalry-cannon-class)
					(up-compare-goal g-unique-unit-line > 5)))
			(can-research ri-bloodlines)
			=>
			(research ri-bloodlines)
			)

    ;----------------
    ;   Husbandry
    ;----------------
		
		(defrule
		(unit-type-count cavalry-archer-class < 6)
		(unit-type-count cavalry-cannon-class < 6)
		=>
		(up-jump-rule 3)
		)

			(defrule
			(or
				(up-compare-goal g-game-focus != BOOM)
				(up-compare-goal g-eco-tech-progress >= PRIORITY-CASTLE-COMPLETE))
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(up-compare-goal g-desired-num-cavalry-archers > 6)
			(unit-type-count cavalry-archer-class > 10)
			(can-research ri-husbandry)
			=>
			(research ri-husbandry)
			)
			(defrule
			(or
				(up-compare-goal g-game-focus != BOOM)
				(up-compare-goal g-eco-tech-progress >= PRIORITY-CASTLE-COMPLETE))
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(up-compare-goal g-desired-num-unique-unit > 6)
			(up-compare-const UNIQUE-UNIT-CLASS == cavalry-cannon-class)
			(up-compare-goal g-unique-unit-line > 10)
			(can-research ri-husbandry)
			=>
			(research ri-husbandry)
			)
			(defrule
			(or
				(up-compare-goal g-game-focus != BOOM)
				(up-compare-goal g-eco-tech-progress >= PRIORITY-CASTLE-COMPLETE))
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(population >= NINETY-PERCENT-POP)
			(or
				(unit-type-count cavalry-archer-class > 5)
				(and
					(up-compare-const UNIQUE-UNIT-CLASS == cavalry-cannon-class)
					(up-compare-goal g-unique-unit-line > 5)))
			(can-research ri-husbandry)
			=>
			(research ri-husbandry)
			)

	;----------------------
	;	Parthian Tactics
	;----------------------

		(defrule
		(up-compare-goal g-desired-num-cavalry-archers > 10)
		(unit-type-count cavalry-archer-class > 12)
		(can-research ri-parthian-tactics)
		=>
		(research ri-parthian-tactics)
		)
		(defrule
		(population >= NINETY-PERCENT-POP)
		(unit-type-count cavalry-archer-class > 6)
		(can-research ri-parthian-tactics)
		=>
		(research ri-parthian-tactics)
		)

	;-------------------------------------------------
	;	Padded Archer Armor - Secondary Castle Tech (>= 7/10 ranged units)
	;-------------------------------------------------

		(defrule
		(current-age == feudal-age)
		(up-compare-goal g-strategy-type < FAST-CASTLE)
		(goal g-primary-unit-class archery-class)
		(unit-type-count archery-class >= 7)
		(or
			(up-compare-goal g-age-status != SAVE-FOR-CASTLE)
			(food-amount >= 900))
		(can-research-with-escrow ri-padded-archer-armor)
		(or
			(up-research-status c: ri-fletching >= research-pending)
			(food-amount > 250))
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-padded-archer-armor c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(research ri-padded-archer-armor)
		)
		(defrule
		(current-age == feudal-age)
		(up-compare-goal g-strategy-type < FAST-CASTLE)
		(goal g-support-unit-class archery-class)
		(unit-type-count archery-class >= 10)
		(or
			(up-compare-goal g-age-status != SAVE-FOR-CASTLE)
			(food-amount >= 900))
		(can-research-with-escrow ri-padded-archer-armor)
		(or
			(up-research-status c: ri-fletching >= research-pending)
			(food-amount > 250))
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-padded-archer-armor c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(research ri-padded-archer-armor)
		)

		(defrule
		(or
			(and
				(up-compare-goal g-game-focus == BOOM)
				(up-compare-goal g-eco-tech-progress < PRIORITY-CASTLE-COMPLETE))
			(and
				(up-research-status c: ri-fletching < research-pending)
				(food-amount <= 250)))
		=>
		(up-jump-rule 2)
		)

			(defrule
			(current-age >= castle-age)
			(or
				(goal g-primary-unit-class archery-class)
				(goal g-support-unit-class archery-class))
			(or
				(unit-type-count archery-class >= 8)
				(or
					(unit-type-count archery-class g:>= g-desired-num-foot-archers)
					(up-compare-goal g-age-status >= TO-IMPERIAL)))
			(can-research-with-escrow ri-padded-archer-armor)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: ri-padded-archer-armor c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(research ri-padded-archer-armor)
			)
			(defrule
			(current-age >= castle-age)
			(or
				(goal g-primary-unit-class cavalry-archer-class)
				(goal g-support-unit-class cavalry-archer-class))
			(or
				(unit-type-count cavalry-archer-class >= 8)
				(or
					(unit-type-count cavalry-archer-class g:>= g-desired-num-cavalry-archers)
					(up-compare-goal g-age-status >= TO-IMPERIAL)))
			(can-research-with-escrow ri-padded-archer-armor)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: ri-padded-archer-armor c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(research ri-padded-archer-armor)
			)

		(defrule
		(current-age >= castle-age)
		(or
			(goal g-primary-unit-class archery-cannon-class)
			(goal g-support-unit-class archery-cannon-class))
		(or
			(unit-type-count archery-cannon-class >= 8)
			(or
				(unit-type-count archery-cannon-class g:>= g-desired-num-hand-cannoneers)
				(up-compare-goal g-age-status >= TO-IMPERIAL)))
		(can-research-with-escrow ri-padded-archer-armor)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-padded-archer-armor c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(research ri-padded-archer-armor)
		)
		(defrule
		(current-age >= castle-age)
		(or
			(goal g-primary-unit-class cavalry-cannon-class)
			(goal g-support-unit-class cavalry-cannon-class))
		(or
			(unit-type-count cavalry-cannon-class >= 8)
			(or
				(unit-type-count cavalry-cannon-class g:>= g-desired-num-hand-cannoneers)
				(up-compare-goal g-age-status >= TO-IMPERIAL)))
		(can-research-with-escrow ri-padded-archer-armor)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-padded-archer-armor c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(research ri-padded-archer-armor)
		)

		(defrule
		(current-age == feudal-age)
		(up-compare-goal g-desired-num-foot-archers > 6)
		(unit-type-count archery-class > 10)
		(or
			(up-research-status c: ri-fletching >= research-pending)
			(food-amount > 250))
		(or
			(up-compare-goal g-age-status >= TO-CASTLE)
			(or
				(food-amount >= 900)
				(up-compare-goal g-strategy-type < FAST-CASTLE)))
		(can-research ri-padded-archer-armor)
		=>
		(research ri-padded-archer-armor)
		)

		(defrule
		(current-age >= castle-age)
		(up-compare-const CROSSBOWMAN-AVAILABLE == YES)
		(up-compare-goal g-desired-num-archer >= 10)
		(up-research-status c: ri-crossbow < research-pending)
		=>
		(up-jump-rule 4)
		)

			(defrule
			(or
				(up-compare-goal g-game-focus != BOOM)
				(up-compare-goal g-eco-tech-progress >= PRIORITY-CASTLE-COMPLETE))
			(current-age >= castle-age)
			(up-compare-goal g-desired-num-foot-archers > 6)
			(unit-type-count archery-class > 10)
			(or
				(up-research-status c: ri-fletching >= research-pending)
				(food-amount > 250))
			(can-research ri-padded-archer-armor)
			=>
			(research ri-padded-archer-armor)
			)

			(defrule
			(or
				(up-compare-goal g-game-focus != BOOM)
				(up-compare-goal g-eco-tech-progress >= PRIORITY-CASTLE-COMPLETE))
			(current-age >= castle-age)
			(up-compare-goal g-desired-num-cavalry-archers > 6)
			(unit-type-count cavalry-archer-class > 10)
			(or
				(up-research-status c: ri-fletching >= research-pending)
				(food-amount > 250))
			(can-research ri-padded-archer-armor)
			=>
			(research ri-padded-archer-armor)
			)

			(defrule
			(or
				(up-compare-goal g-game-focus != BOOM)
				(up-compare-goal g-eco-tech-progress >= PRIORITY-CASTLE-COMPLETE))
			(current-age >= castle-age)
			(up-compare-goal g-desired-num-hand-cannoneer > 6)
			(or
				(unit-type-count archery-cannon-class > 10)
				(unit-type-count cavalry-cannon-class > 10))
			(can-research ri-padded-archer-armor)
			=>
			(research ri-padded-archer-armor)
			)

			(defrule
			(or
				(up-compare-goal g-game-focus != BOOM)
				(up-compare-goal g-eco-tech-progress >= PRIORITY-CASTLE-COMPLETE))
			(current-age >= castle-age)
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(or
				(civ-selected turkish)
				(or
					(civ-selected spanish)
					(civ-selected burmese)))
			(up-compare-goal g-desired-num-unique-unit > 6)
			(up-compare-goal g-unique-unit-line > 6)
			(can-research ri-padded-archer-armor)
			=>
			(research ri-padded-archer-armor)
			)

	;---------------------------------------------------
	;	Leather Archer Armor - Priority Imperial Tech (Secondary Castle for Skirms) (>= 11 ranged units)
	;---------------------------------------------------

		(defrule
		(or
			(goal g-primary-unit-class archery-class)
			(goal g-support-unit-class archery-class))
		(or
			(unit-type-count archery-class >= 11)
			(or
				(unit-type-count archery-class g:>= g-desired-num-foot-archers)
				(up-compare-goal g-age-status >= TO-IMPERIAL)))
		(or
			(up-research-status c: ri-bodkin-arrow >= research-pending)
			(food-amount > 350))
		(can-research-with-escrow ri-leather-archer-armor)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-leather-archer-armor c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-leather-archer-armor)
		)
		(defrule
		(or
			(goal g-primary-unit-class cavalry-archer-class)
			(goal g-support-unit-class cavalry-archer-class))
		(or
			(unit-type-count cavalry-archer-class >= 11)
			(or
				(unit-type-count cavalry-archer-class g:>= g-desired-num-cavalry-archers)
				(up-compare-goal g-age-status >= TO-IMPERIAL)))
		(or
			(up-research-status c: ri-bodkin-arrow >= research-pending)
			(food-amount > 350))
		(can-research-with-escrow ri-leather-archer-armor)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-leather-archer-armor c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-leather-archer-armor)
		)
		(defrule
		(or
			(goal g-primary-unit-class archery-cannon-class)
			(goal g-support-unit-class archery-cannon-class))
		(or
			(unit-type-count archery-cannon-class >= 11)
			(or
				(unit-type-count archery-cannon-class g:>= g-desired-num-hand-cannoneers)
				(up-compare-goal g-age-status >= TO-IMPERIAL)))
		(can-research-with-escrow ri-leather-archer-armor)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-leather-archer-armor c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-leather-archer-armor)
		)
		(defrule
		(or
			(goal g-primary-unit-class cavalry-cannon-class)
			(goal g-support-unit-class cavalry-cannon-class))
		(or
			(unit-type-count cavalry-cannon-class >= 11)
			(or
				(unit-type-count cavalry-cannon-class g:>= g-desired-num-hand-cannoneers)
				(up-compare-goal g-age-status >= TO-IMPERIAL)))
		(can-research-with-escrow ri-leather-archer-armor)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-leather-archer-armor c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-leather-archer-armor)
		)

		(defrule
		(or
			(and
				(goal g-primary-unit-class archery-cannon-class)
				(unit-type-count archery-cannon-class >= 10))
			(and
				(goal g-primary-unit-class cavalry-cannon-class)
				(unit-type-count cavalry-cannon-class >= 10)))
		(can-research-with-escrow ri-leather-archer-armor)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-leather-archer-armor c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-leather-archer-armor)
		)
		(defrule
		(or
			(and
				(goal g-support-unit-class archery-cannon-class)
				(unit-type-count archery-cannon-class >= 12))
			(and
				(goal g-support-unit-class cavalry-cannon-class)
				(unit-type-count cavalry-cannon-class >= 12)))
		(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)
		(can-research-with-escrow ri-leather-archer-armor)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-leather-archer-armor c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-leather-archer-armor)
		)
		(defrule
		(or
			(goal g-primary-unit-class archery-cannon-class)
			(or
				(goal g-primary-unit-class cavalry-cannon-class)
				(or
					(goal g-support-unit-class archery-cannon-class)
					(goal g-support-unit-class cavalry-cannon-class))))
		(up-compare-goal g-age-status >= TO-IMPERIAL)
		(can-research-with-escrow ri-leather-archer-armor)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-leather-archer-armor c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-leather-archer-armor)
		)

		(defrule
		(or
			(and
				(up-compare-goal g-desired-num-foot-archers > 6)
				(unit-type-count archery-class > 10))
			(and
				(up-compare-goal g-desired-num-cavalry-archers > 6)
				(unit-type-count cavalry-archer-class > 10)))
		(or
			(up-research-status c: ri-bodkin-arrow >= research-pending)
			(food-amount > 350))
		(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
		(can-research ri-leather-archer-armor)
		=>
		(research ri-leather-archer-armor)
		)

		(defrule
		(up-compare-goal g-desired-num-hand-cannoneer > 6)
		(unit-type-count hand-cannoneer > 10)
		(can-research ri-leather-archer-armor)
		=>
		(research ri-leather-archer-armor)
		)

		(defrule
		(or
			(civ-selected turkish)
			(civ-selected spanish))
		(up-compare-goal g-desired-num-unique-unit > 6)
		(or
			(unit-type-count archery-cannon-class > 10)
			(unit-type-count cavalry-cannon-class > 10))
		(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
		(can-research ri-leather-archer-armor)
		=>
		(research ri-leather-archer-armor)
		)

	;-------------------------------------------------
	;	Ring Archer Armor - Secondary Imperial Tech (Priority Imperial for Skirms)
	;-------------------------------------------------			

		(defrule
		(or
			(goal g-primary-unit-class archery-class)
			(or
				(goal g-primary-unit-class cavalry-archer-class)
				(or
					(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)
					(and
						(goal g-support-unit-class archery-class)
						(goal g-support-unit-class cavalry-archer-class)))))
		(or
			(up-research-status c: ri-bracer >= research-pending)
			(up-compare-const BRACER-AVAILABLE == NO))
		(can-research-with-escrow ri-ring-archer-armor)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-ring-archer-armor c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-ring-archer-armor)
		)
		(defrule
		(or
			(goal g-primary-unit-class archery-cannon-class)
			(or
				(goal g-primary-unit-class cavalry-cannon-class)
				(or
					(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)
					(and
						(goal g-support-unit-class archery-cannon-class)
						(goal g-support-unit-class cavalry-cannon-class)))))
		(can-research-with-escrow ri-ring-archer-armor)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-ring-archer-armor c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-ring-archer-armor)
		)

		(defrule
		(or
			(and
				(up-compare-goal g-desired-num-foot-archers > 6)
				(unit-type-count archery-class > 10))
			(and
				(up-compare-goal g-desired-num-cavalry-archers > 6)
				(unit-type-count cavalry-archer-class > 10)))
		(or
			(up-research-status c: ri-bracer >= research-pending)
			(up-compare-const BRACER-AVAILABLE == NO))
		(can-research ri-ring-archer-armor)
		=>
		(research ri-ring-archer-armor)
		)

		(defrule
		(up-compare-goal g-desired-num-hand-cannoneer > 6)
		(unit-type-count hand-cannoneer > 10)
		(can-research ri-ring-archer-armor)
		=>
		(research ri-ring-archer-armor)
		)

		(defrule
		(or
			(civ-selected turkish)
			(civ-selected spanish))
		(up-compare-goal g-desired-num-unique-unit > 6)
		(or
			(unit-type-count archery-cannon-class > 10)
			(unit-type-count cavalry-cannon-class > 10))
		(can-research ri-ring-archer-armor)
		=>
		(research ri-ring-archer-armor)
		)

	;-----------------------------------------
	;	Ballistics - Priority Imperial Tech (>= 11 ranged units)
	;-----------------------------------------

		(defrule
		(or
			(goal g-game-focus BOOM)
			(goal g-game-focus REBUILD))
		(up-object-type-count-total c: town-center g:< g-desired-num-town-center)
		=>
		(up-jump-rule 2)
		)

			(defrule
			(or
				(goal g-primary-unit-class archery-class)
				(goal g-support-unit-class archery-class))
			(or
				(unit-type-count archery-class >= 11)
				(or
					(unit-type-count archery-class g:>= g-desired-num-foot-archers)
					(up-compare-goal g-age-status >= TO-IMPERIAL)))
			(or
				(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
				(gold-amount >= 975))
			(can-research-with-escrow ri-ballistics)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: ri-ballistics c: 1)
			(up-modify-escrow wood g:- g-wood-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research ri-ballistics)
			)
			(defrule
			(or
				(goal g-primary-unit-class cavalry-archer-class)
				(goal g-support-unit-class cavalry-archer-class))
			(or
				(unit-type-count cavalry-archer-class >= 11)
				(or
					(unit-type-count cavalry-archer-class g:>= g-desired-num-cavalry-archers)
					(up-compare-goal g-age-status >= TO-IMPERIAL)))
			(or
				(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
				(gold-amount >= 975))
			(can-research-with-escrow ri-ballistics)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: ri-ballistics c: 1)
			(up-modify-escrow wood g:- g-wood-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research ri-ballistics)
			)

		(defrule
		(or
			(up-compare-goal g-desired-num-foot-archers > 6)
			(or
				(unit-type-count archery-class > 10)
				(or
					(up-compare-goal g-desired-num-cavalry-archers > 6)
					(unit-type-count cavalry-archer-class > 10))))
		(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
		(can-research ri-ballistics)
		=>
		(research ri-ballistics)
		)
		(defrule
		(population >= NINETY-PERCENT-POP)
		(or
			(unit-type-count archery-class > 6)
			(unit-type-count cavalry-archer-class > 6))
		(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
		(can-research ri-ballistics)
		=>
		(research ri-ballistics)
		)

		#load-if-defined BURMESE-CIV

			(defrule
			(or
				(goal g-game-focus BOOM)
				(goal g-game-focus REBUILD))
			(up-object-type-count-total c: town-center g:< g-desired-num-town-center)
			=>
			(up-jump-rule 1)
			)

				(defrule
				(or
					(goal g-primary-unit my-unique-unit)
					(goal g-support-unit my-unique-unit))
				(or
					(up-compare-goal g-unique-unit-line >= 11)
					(or
						(up-compare-goal g-unique-unit-line g:>= g-desired-num-unique-unit)
						(up-compare-goal g-age-status >= TO-IMPERIAL)))
				(or
					(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
					(gold-amount >= 975))
				(can-research-with-escrow ri-ballistics)
				=>
				(up-setup-cost-data 1 g-food-cost)
				(up-add-research-cost c: ri-ballistics c: 1)
				(up-modify-escrow wood g:- g-wood-cost)
				(up-modify-escrow gold g:- g-gold-cost)
				(research ri-ballistics)
				)

			(defrule
			(up-compare-goal g-desired-num-unique-unit > 6)
			(up-compare-goal g-unique-unit-line > 10)
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(can-research ri-ballistics)
			=>
			(research ri-ballistics)
			)
			(defrule
			(population >= NINETY-PERCENT-POP)
			(up-compare-goal g-unique-unit-line > 6)
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(can-research ri-ballistics)
			=>
			(research ri-ballistics)
			)

		#end-if

;=========================<>=========================
;				    INFANTRY TECHS
;=========================<>=========================

	;------------------------------------------
	;	Eagle Warrior - Required Castle Tech
	;------------------------------------------

		#load-if-defined UP-GAME-WK

			(defrule
			(or
				(up-compare-goal g-game-focus != BOOM)
				(up-compare-goal g-eco-tech-progress >= PRIORITY-CASTLE-COMPLETE))
			(or
				(goal g-primary-unit eagle-warrior)
				(goal g-support-unit eagle-warrior))
			(can-research-with-escrow ri-eagle-warrior)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: ri-eagle-warrior c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research ri-eagle-warrior)
			)

			(defrule
			(or
				(up-compare-goal g-game-focus != BOOM)
				(up-compare-goal g-eco-tech-progress >= PRIORITY-CASTLE-COMPLETE))
			(or
				(up-compare-goal g-desired-num-eagle-warrior > 6)
				(up-compare-goal g-eagle-scout-line > 10))
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(can-research ri-eagle-warrior)
			=>
			(research ri-eagle-warrior)
			)

		#end-if

	;--------------------------------------------------
	;	Elite Eagle Warrior - Required Imperial Tech
	;--------------------------------------------------

		(defrule
		(or
			(goal g-primary-unit eagle-warrior)
			(goal g-support-unit eagle-warrior))
		(can-research-with-escrow ri-elite-eagle-warrior)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-elite-eagle-warrior c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-elite-eagle-warrior)
		)

		(defrule
		(up-compare-goal g-desired-num-eagle-warrior > 10)
		(up-compare-goal g-eagle-scout-line > 15)
		(can-research ri-elite-eagle-warrior)
		=>
		(research ri-elite-eagle-warrior)
		)
		(defrule
		(population >= SEVENTY-PERCENT-POP)
		(up-compare-goal g-eagle-scout-line > 5)
		(can-research ri-elite-eagle-warrior)
		=>
		(research ri-elite-eagle-warrior)
		)

	;------------------------------------
	;	Pikeman - Required Castle Tech
	;------------------------------------

		(defrule
		(or
			(up-compare-goal g-game-focus != BOOM)
			(up-compare-goal g-eco-tech-progress >= PRIORITY-CASTLE-COMPLETE))
		(up-compare-goal g-desired-num-spearman > 0)
		(can-research-with-escrow ri-pikeman)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-pikeman c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-pikeman)
		)

		(defrule
		(or
			(up-compare-goal g-desired-num-spearman >= 4)
			(unit-type-count spearman-line >= 6))
		(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
		(can-research ri-pikeman)
		=>
		(research ri-pikeman)
		)

	;-----------------------------------------
	;	Halberdier - Priority Imperial Tech
	;-----------------------------------------

		(defrule
		(or
			(goal g-primary-unit spearman)
			(goal g-support-unit spearman))
		(can-research-with-escrow ri-halberdier)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-halberdier c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-halberdier)
		)

		(defrule
		(up-compare-goal g-desired-num-spearman > 10)
		(unit-type-count spearman-line >= 15)
		(can-research ri-halberdier)
		=>
		(research ri-halberdier)
		)
		(defrule
		(population >= EIGHTY-PERCENT-POP)
		(unit-type-count spearman-line >= 5)
		(can-research ri-halberdier)
		=>
		(research ri-halberdier)
		)

	;----------------------------------------
	;	Man-at-Arms - Priority Feudal Tech
	;----------------------------------------

        (defrule
		(or
			(goal g-primary-unit militiaman)
			(goal g-support-unit militiaman))
		(up-compare-goal g-age-status != SAVE-FOR-CASTLE)
        (can-research-with-escrow ri-man-at-arms)
        =>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-man-at-arms c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow gold g:- g-gold-cost)
        (research ri-man-at-arms)
        )

        (defrule
		(or
			(up-compare-goal g-desired-num-militia > 3)
			(unit-type-count militiaman-line >= 8))
		(up-compare-goal g-age-status != SAVE-FOR-CASTLE)
        (can-research ri-man-at-arms)
        =>
        (research ri-man-at-arms)
        )

		#load-if-defined AZTEC-CIV

			(defrule
			(goal g-position FLANK)
			(goal g-primary-unit eagle-warrior)
			(up-compare-goal g-enemy-swordsmen-count > 0)
			(up-compare-goal g-eagle-warrior-tech-progress >= SECONDARY-CASTLE-COMPLETE)
			(can-research ri-man-at-arms)
			=>
			(research ri-man-at-arms)
			)

		#end-if

	;-------------------------------------------
	;	Long Swordsman - Required Castle Tech
	;-------------------------------------------

		(defrule
		(or
			(up-compare-goal g-game-focus != BOOM)
			(up-compare-goal g-eco-tech-progress >= PRIORITY-CASTLE-COMPLETE))
		(or
			(goal g-primary-unit militiaman)
			(goal g-support-unit militiaman))
		(can-research-with-escrow ri-long-swordsman)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-long-swordsman c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-long-swordsman)
		)

		(defrule
		(or
			(up-compare-goal g-desired-num-militia > 6)
			(or
				(and
					(up-compare-goal g-desired-num-militia > 0)
					(up-compare-goal g-enemy-eagle-warriors-count > 6))
				(unit-type-count militiaman-line >= 10)))
		(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
		(can-research ri-long-swordsman)
		=>
		(research ri-long-swordsman)
		)

		#load-if-defined AZTEC-CIV

			(defrule
			(goal g-position FLANK)
			(goal g-primary-unit eagle-warrior)
			(up-compare-goal g-enemy-swordsmen-count > 0)
			(can-research ri-long-swordsman)
			=>
			(research ri-long-swordsman)
			)

		#end-if

	;---------------------------------------------------
	;	Two-Handed Swordsman - Required Imperial Tech
	;---------------------------------------------------

		(defrule
		(or
			(goal g-primary-unit militiaman)
			(goal g-support-unit militiaman))
		(can-research-with-escrow ri-two-handed-swordsman)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-two-handed-swordsman c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-two-handed-swordsman)
		)

		(defrule
		(up-compare-goal g-desired-num-militia > 10)
		(unit-type-count militiaman-line > 15)
		(can-research ri-two-handed-swordsman)
		=>
		(research ri-two-handed-swordsman)
		)
		(defrule
		(population >= EIGHTY-PERCENT-POP)
		(unit-type-count militiaman-line > 15)
		(can-research ri-two-handed-swordsman)
		=>
		(research ri-two-handed-swordsman)
		)

	;---------------------------------------
	;	Champion - Priority Imperial Tech
	;---------------------------------------

		(defrule
		(or
			(goal g-primary-unit militiaman)
			(and
				(goal g-support-unit militiaman)
				(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)))
		(can-research-with-escrow ri-champion)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-champion c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-champion)
		)

		(defrule
		(up-compare-goal g-desired-num-militia > 10)
		(unit-type-count militiaman-line > 15)
		(can-research ri-champion)
		=>
		(research ri-champion)
		)
		(defrule
		(population >= NINETY-PERCENT-POP)
		(unit-type-count militiaman-line > 5)
		(can-research ri-champion)
		=>
		(research ri-champion)
		)

	;---------------------------------------
	;	Scale Mail - Priority Castle Tech (>= 4 infantry)
	;---------------------------------------

		(defrule
		(current-age == feudal-age)
		(or
			(goal g-primary-unit-class infantry-class)
			(and
				(goal g-support-unit-class infantry-class)
				(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)))
		(unit-type-count infantry-class >= 4)
		(or
			(up-compare-goal g-age-status != SAVE-FOR-CASTLE)
			(food-amount >= 900))
		(can-research-with-escrow ri-scale-mail)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-scale-mail c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(research ri-scale-mail)
		)

		(defrule
		(current-age >= castle-age)
		(up-compare-goal g-desired-num-militia >= 10)
		(up-research-status c: ri-long-swordsman < research-pending)
		=>
		(up-jump-rule 2)
		)

		(defrule
		(current-age >= castle-age)
		(up-compare-const PIKEMAN-AVAILABLE == YES)
		(up-compare-goal g-desired-num-spearman >= 10)
		(up-research-status c: ri-pikeman < research-pending)
		=>
		(up-jump-rule 1)
		)

			(defrule
			(or
				(up-compare-goal g-game-focus != BOOM)
				(up-compare-goal g-eco-tech-progress >= PRIORITY-CASTLE-COMPLETE))
			(current-age >= castle-age)
			(or
				(goal g-primary-unit-class infantry-class)
				(and
					(goal g-support-unit-class infantry-class)
					(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)))
			(unit-type-count infantry-class >= 4)
			(can-research-with-escrow ri-scale-mail)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: ri-scale-mail c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(research ri-scale-mail)
			)

		(defrule
		(current-age >= castle-age)
		(up-compare-goal g-desired-num-militia >= 10)
		(up-research-status c: ri-long-swordsman < research-pending)
		=>
		(up-jump-rule 2)
		)

		(defrule
		(current-age >= castle-age)
		(up-compare-const PIKEMAN-AVAILABLE == YES)
		(up-compare-goal g-desired-num-spearman >= 10)
		(up-research-status c: ri-pikeman < research-pending)
		=>
		(up-jump-rule 1)
		)

			(defrule
			(up-compare-goal g-desired-num-infantry > 5)
			(unit-type-count infantry-class > 8)
			(or
				(up-compare-goal g-age-status >= TO-CASTLE)
				(or
					(food-amount >= 900)
					(up-compare-goal g-strategy-type < FAST-CASTLE)))
			(can-research ri-scale-mail)
			=>
			(research ri-scale-mail)
			)

		(defrule
		(population >= SEVENTY-PERCENT-POP)
		(unit-type-count infantry-class > 4)
		(or
			(up-compare-goal g-age-status >= TO-CASTLE)
			(or
				(food-amount >= 900)
				(up-compare-goal g-strategy-type < FAST-CASTLE)))
		(can-research ri-scale-mail)
		=>
		(research ri-scale-mail)
		)

	;----------------------------------------
	;	Chain Mail - Secondary Castle Tech (>= 9 infantry)
	;----------------------------------------

		(defrule
		(current-age >= castle-age)
		(up-compare-const PIKEMAN-AVAILABLE == YES)
		(up-compare-goal g-desired-num-spearman >= 10)
		(up-research-status c: ri-pikeman < research-pending)
		=>
		(up-jump-rule 2)
		)

			(defrule
			(goal g-primary-unit-class infantry-class)
			(or
				(unit-type-count infantry-class >= 9)
				(or
					(unit-type-count infantry-class g:>= g-desired-num-infantry)
					(up-compare-goal g-age-status >= TO-IMPERIAL)))
			(or
				(up-compare-goal g-target-military-parity > 0)
				(up-compare-goal g-age-status >= MID-CASTLE))
			(can-research-with-escrow ri-chain-mail)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: ri-chain-mail c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research ri-chain-mail)
			)
			(defrule
			(goal g-support-unit-class infantry-class)
			(or
				(unit-type-count infantry-class >= 9)
				(or
					(unit-type-count infantry-class g:>= g-desired-num-infantry)
					(up-compare-goal g-age-status >= TO-IMPERIAL)))
			(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)
			(up-compare-goal g-age-status >= MID-CASTLE)
			(can-research-with-escrow ri-chain-mail)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: ri-chain-mail c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research ri-chain-mail)
			)

		(defrule
		(current-age >= castle-age)
		(up-compare-const PIKEMAN-AVAILABLE == YES)
		(up-compare-goal g-desired-num-spearman >= 10)
		(up-research-status c: ri-pikeman < research-pending)
		=>
		(up-jump-rule 1)
		)

			(defrule
			(up-compare-goal g-desired-num-infantry > 6)
			(unit-type-count infantry-class > 10)
			(or
				(up-compare-goal g-current-strategy != FC-EAGLES-REVENGE)
				(or
					(up-compare-goal g-age-status >= MID-CASTLE)
					(military-population > 14)))
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(can-research ri-chain-mail)
			=>
			(research ri-chain-mail)
			)

		(defrule
		(population >= EIGHTY-PERCENT-POP)
		(unit-type-count infantry-class > 5)
		(or
			(up-compare-goal g-current-strategy != FC-EAGLES-REVENGE)
			(or
				(up-compare-goal g-age-status >= MID-CASTLE)
				(military-population > 14)))
		(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
		(can-research ri-chain-mail)
		=>
		(research ri-chain-mail)
		)

	;-----------------------------------------
	;	Plate Mail - Priority Imperial Tech
	;-----------------------------------------

		(defrule
		(or
			(goal g-primary-unit-class infantry-class)
			(and
				(goal g-support-unit-class infantry-class)
				(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)))
		(can-research-with-escrow ri-plate-mail)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-plate-mail c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-plate-mail)
		)

		(defrule
		(up-compare-goal g-desired-num-infantry > 10)
		(unit-type-count infantry-class > 15)
		(can-research ri-plate-mail)
		=>
		(research ri-plate-mail)
		)
		(defrule
		(population >= NINETY-PERCENT-POP)
		(unit-type-count infantry-class > 5)
		(can-research ri-plate-mail)
		=>
		(research ri-plate-mail)
		)

	;--------------------------------------
	;	Forging - Required Imperial Tech (>= 9 infantry or >= 12 cavalry)
	;--------------------------------------

		(defrule
		(current-age >= castle-age)
		(up-compare-goal g-game-focus == BOOM)
		(up-compare-goal g-eco-tech-progress < PRIORITY-CASTLE-COMPLETE)
		=>
		(up-jump-rule 9)
		)

		(defrule
		(current-age >= castle-age)
		(up-compare-goal g-desired-num-militia >= 10)
		(up-research-status c: ri-long-swordsman < research-pending)
		(food-amount <= 450)
		=>
		(up-jump-rule 8)
		)

		(defrule
		(current-age >= castle-age)
		(up-compare-const PIKEMAN-AVAILABLE == YES)
		(up-compare-goal g-desired-num-spearman >= 10)
		(up-research-status c: ri-pikeman < research-pending)
		(food-amount <= 465)
		=>
		(up-jump-rule 7)
		)

		(defrule
		(current-age >= castle-age)
		(up-compare-const EAGLE-SCOUT-AVAILABLE == YES)
		(up-compare-goal g-desired-num-eagle-warrior >= 10)
		(up-research-status c: ri-eagle-warrior < research-pending)
		(food-amount <= 450)
		=>
		(up-jump-rule 6)
		)

		(defrule
		(current-age >= castle-age)
		(up-compare-const LIGHT-CAVALRY-AVAILABLE == YES)
		(up-compare-goal g-desired-num-scout-cavalry >= 6)
		(up-research-status c: ri-light-cavalry < research-pending)
		(food-amount <= 400)
		=>
		(up-jump-rule 5)
		)

		(defrule
		(current-age >= castle-age)
		(or
			(goal g-primary-unit-class infantry-class)
			(goal g-support-unit-class infantry-class))
		(up-research-status c: ri-scale-mail < research-pending)
		(food-amount <= 350)
		=>
		(up-jump-rule 4)
		)

		(defrule
		(current-age >= castle-age)
		(or
			(goal g-primary-unit-class cavalry-class)
			(goal g-support-unit-class cavalry-class))
		(up-research-status c: ri-scale-barding < research-pending)
		(food-amount <= 400)
		=>
		(up-jump-rule 3)
		)

			(defrule
			(current-age >= castle-age)
			(or
				(goal g-primary-unit-class infantry-class)
				(goal g-support-unit-class infantry-class))
			(can-research-with-escrow ri-forging)
			(or
				(unit-type-count infantry-class >= 9)
				(or
					(unit-type-count infantry-class g:>= g-desired-num-infantry)
					(up-compare-goal g-age-status >= TO-IMPERIAL)))
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: ri-forging c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(research ri-forging)
			)
			(defrule
			(current-age >= castle-age)
			(or
				(goal g-primary-unit-class cavalry-class)
				(goal g-support-unit-class cavalry-class))
			(can-research-with-escrow ri-forging)
			(or
				(unit-type-count cavalry-class >= 12)
				(or
					(unit-type-count steppe-lancer-line >= 8)
					(or
						(unit-type-count cavalry-class g:>= g-desired-num-cavalry)
						(up-compare-goal g-age-status >= TO-IMPERIAL))))
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: ri-forging c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(research ri-forging)
			)

			(defrule
			(or
				(unit-type-count infantry-class > 12)
				(or
					(unit-type-count cavalry-class >= 15)
					(unit-type-count steppe-lancer-line >= 10)))
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(can-research ri-forging)
			=>
			(research ri-forging)
			)

	;-------------------------------------------
	;	Iron Casting - Priority Imperial Tech (>= 15 infantry or cavalry)
	;-------------------------------------------

		(defrule
		(or
			(goal g-primary-unit-class infantry-class)
			(goal g-support-unit-class infantry-class))
		(or
			(up-research-status c: ri-chain-mail < research-pending)
			(up-compare-const CHAIN-MAIL-ARMOR-AVAILABLE == NO))
		(or
			(food-amount <= 420)
			(gold-amount <= 220))
		=>
		(up-jump-rule 6)
		)

		(defrule
		(or
			(goal g-primary-unit-class cavalry-class)
			(goal g-support-unit-class cavalry-class))
		(or
			(up-research-status c: ri-chain-barding < research-pending)
			(up-compare-const CHAIN-BARDING-ARMOR-AVAILABLE == NO))
		(or
			(food-amount <= 470)
			(gold-amount <= 270))
		=>
		(up-jump-rule 5)
		)

			(defrule
			(goal g-primary-unit-class infantry-class)
			(or
				(unit-type-count infantry-class >= 15)
				(or
					(unit-type-count infantry-class g:>= g-desired-num-infantry)
					(up-compare-goal g-age-status >= TO-IMPERIAL)))
			(up-compare-goal g-age-status >= MID-CASTLE)
			(can-research-with-escrow ri-iron-casting)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: ri-iron-casting c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research ri-iron-casting)
			)
			(defrule
			(goal g-primary-unit-class cavalry-class)
			(or
				(unit-type-count cavalry-class >= 15)
				(or
					(unit-type-count steppe-lancer-line >= 12)
					(or
						(unit-type-count cavalry-class g:>= g-desired-num-cavalry)
						(up-compare-goal g-age-status >= TO-IMPERIAL))))
			(up-compare-goal g-age-status >= MID-CASTLE)
			(can-research-with-escrow ri-iron-casting)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: ri-iron-casting c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research ri-iron-casting)
			)
			(defrule
			(goal g-support-unit-class infantry-class)
			(or
				(unit-type-count infantry-class >= 15)
				(or
					(unit-type-count infantry-class g:>= g-desired-num-infantry)
					(up-compare-goal g-age-status >= TO-IMPERIAL)))
			(up-compare-goal g-age-status >= MID-CASTLE)
			(can-research-with-escrow ri-iron-casting)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: ri-iron-casting c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research ri-iron-casting)
			)
			(defrule
			(goal g-support-unit-class cavalry-class)
			(or
				(unit-type-count cavalry-class >= 15)
				(or
					(unit-type-count steppe-lancer-line >= 12)
					(or
						(unit-type-count cavalry-class g:>= g-desired-num-cavalry)
						(up-compare-goal g-age-status >= TO-IMPERIAL))))
			(up-compare-goal g-age-status >= MID-CASTLE)
			(can-research-with-escrow ri-iron-casting)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: ri-iron-casting c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research ri-iron-casting)
			)

			(defrule
			(or
				(up-compare-goal g-desired-num-infantry >= 10)
				(up-compare-goal g-desired-num-cavalry >= 10))
			(or
				(unit-type-count infantry-class >= 24)
				(or
					(unit-type-count cavalry-class >= 24)
					(or
						(unit-type-count steppe-lancer-line >= 16)
						(population >= EIGHTY-FIVE-PERCENT-POP))))
			(up-compare-goal g-age-status >= MID-CASTLE)
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(can-research ri-iron-casting)
			=>
			(research ri-iron-casting)
			)

	;--------------------------------------------
	;	Blast Furnace - Secondary Imperial Tech (Priority for Throwing Axemen)
	;--------------------------------------------

		(defrule
		(or
			(goal g-primary-unit-class infantry-class)
			(goal g-support-unit-class infantry-class))
		(or
			(up-research-status c: ri-plate-mail < research-pending)
			(up-compare-const PLATE-MAIL-ARMOR-AVAILABLE == NO))
		(or
			(food-amount <= 575)
			(gold-amount <= 375))
		=>
		(up-jump-rule 5)
		)

		(defrule
		(or
			(goal g-primary-unit-class cavalry-class)
			(goal g-support-unit-class cavalry-class))
		(or
			(up-research-status c: ri-plate-barding < research-pending)
			(up-compare-const PLATE-BARDING-ARMOR-AVAILABLE == NO))
		(or
			(food-amount <= 625)
			(gold-amount <= 425))
		=>
		(up-jump-rule 4)
		)

			(defrule
			(or
				(goal g-primary-unit-class infantry-class)
				(goal g-primary-unit-class cavalry-class))
			(can-research-with-escrow ri-blast-furnace)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: ri-blast-furnace c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research ri-blast-furnace)
			)
			(defrule
			(or
				(goal g-support-unit-class infantry-class)
				(goal g-support-unit-class cavalry-class))
			(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)
			(can-research-with-escrow ri-blast-furnace)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: ri-blast-furnace c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research ri-blast-furnace)
			)

			(defrule
			(or
				(unit-type-count infantry-class >= 24)
				(or
					(unit-type-count cavalry-class >= 24)
					(unit-type-count steppe-lancer-line >= 16)))
			(can-research ri-blast-furnace)
			=>
			(research ri-blast-furnace)
			)
			(defrule
			(population >= NINETY-PERCENT-POP)
			(or
				(unit-type-count infantry-class >= 12)
				(or
					(unit-type-count cavalry-class >= 12)
					(unit-type-count steppe-lancer-line >= 8)))
			(can-research ri-blast-furnace)
			=>
			(research ri-blast-furnace)
			)

	;--------------------------------------
	;	Supplies - Secondary Castle Tech
	;--------------------------------------

		#load-if-defined DE-AVAILABLE

			(defrule
			(or
				(goal g-primary-unit militiaman)
				(and
					(goal g-support-unit militiaman)
					(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)))
			(can-research-with-escrow ri-supplies)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: ri-supplies c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research ri-supplies)
			)

			(defrule
			(up-compare-goal g-desired-num-militia > 6)
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(can-research ri-supplies)
			=>
			(research ri-supplies)
			)

		#end-if

	;-------------
	;	Squires
	;-------------

		(defrule
		(or
			(up-research-status c: ri-chain-mail >= research-pending)
			(up-compare-const CHAIN-MAIL-ARMOR-AVAILABLE == NO))
		(up-research-status c: ri-iron-casting >= research-pending)
		(up-compare-goal g-desired-num-infantry > 6)
		(unit-type-count infantry-class > 14)
		(or
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(food-amount >= 1100))
		(can-research ri-squires)
		=>
		(research ri-squires)
		)

	;-----------
	;	Arson
	;-----------

		(defrule
		(current-age == castle-age)
		(up-research-status c: ri-iron-casting >= research-pending)
		(or
			(up-research-status c: ri-chain-mail >= research-pending)
			(up-compare-const CHAIN-MAIL-ARMOR-AVAILABLE == NO))
		(unit-type-count infantry-class > 20)
		(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
		(can-research ri-arson)
		=>
		(research ri-arson)
		)
		(defrule
		(current-age == imperial-age)
		(or
			(up-research-status c: ri-blast-furnace >= research-pending)
			(up-compare-const BLAST-FURNACE-AVAILABLE == NO))
		(or
			(up-research-status c: ri-plate-mail >= research-pending)
			(up-compare-const PLATE-MAIL-ARMOR-AVAILABLE == NO))
		(unit-type-count infantry-class > 20)
		(can-research ri-arson)
		=>
		(research ri-arson)
		)

;=========================<>=========================
;				     SIEGE TECHS
;=========================<>=========================

	;------------
	;	Onager
	;------------

		(defrule
		(up-compare-goal g-desired-num-mangonel > 2)
		(can-research-with-escrow ri-onager)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-onager c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-onager)
		)

		(defrule
		(or
			(up-compare-goal g-desired-num-mangonel > 1)
			(up-compare-goal g-mangonel-line > 2))
		(can-research ri-onager)
		=>
		(research ri-onager)
		)

	;------------------
	;	Siege Onager
	;------------------

		(defrule
		(up-compare-goal g-desired-num-mangonel > 2)
		(can-research-with-escrow ri-siege-onager)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-siege-onager c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-siege-onager)
		)

		(defrule
		(or
			(up-compare-goal g-desired-num-mangonel > 1)
			(up-compare-goal g-mangonel-line > 3))
		(can-research ri-siege-onager)
		=>
		(research ri-siege-onager)
		)

	;--------------------
	;	Heavy Scorpion
	;--------------------

		(defrule
		(or
			(goal g-game-focus BOOM)
			(goal g-game-focus REBUILD))
		(up-object-type-count-total c: town-center g:< g-desired-num-town-center)
		=>
		(up-jump-rule 2)
		)

			(defrule
			(up-compare-goal g-desired-num-scorpion > 3)
			(can-research-with-escrow ri-heavy-scorpion)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: ri-heavy-scorpion c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow wood g:- g-wood-cost)
			(research ri-heavy-scorpion)
			)

			(defrule
			(or
				(up-compare-goal g-desired-num-scorpion > 1)
				(unit-type-count scorpion-line > 3))
			(can-research ri-heavy-scorpion)
			=>
			(research ri-heavy-scorpion)
			)

	;----------------
	;	Capped Ram
	;----------------

		(defrule
		(up-compare-goal g-desired-num-ram-type > 1)
		(can-research-with-escrow ri-capped-ram)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-capped-ram c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(research ri-capped-ram)
		)

		(defrule
		(or
			(up-compare-goal g-desired-num-ram-type > 0)
			(unit-type-count battering-ram-line > 2))
		(can-research ri-capped-ram)
		=>
		(research ri-capped-ram)
		)

	;---------------
	;	Siege Ram
	;---------------

		(defrule
		(up-compare-goal g-desired-num-ram-type > 2)
		(can-research-with-escrow ri-siege-ram)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-siege-ram c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(research ri-siege-ram)
		)

		(defrule
		(or
			(up-compare-goal g-desired-num-ram-type > 1)
			(unit-type-count battering-ram-line > 3))
		(can-research ri-siege-ram)
		=>
		(research ri-siege-ram)
		)

	;---------------------
	;	Siege Engineers
	;---------------------

		(defrule
		(or
			(goal g-game-focus BOOM)
			(goal g-game-focus REBUILD))
		(up-object-type-count-total c: town-center g:< g-desired-num-town-center)
		=>
		(up-jump-rule 2)
		)

			(defrule
			(up-compare-goal g-desired-num-siege > 0)
			(can-research-with-escrow ri-siege-engineers)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: ri-siege-engineers c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow wood g:- g-wood-cost)
			(research ri-siege-engineers)
			)

			(defrule
			(up-compare-goal g-desired-num-siege > 0)
			(can-research ri-siege-engineers)
			=>
			(research ri-siege-engineers)
			)

;=========================<>=========================
;				      MONK TECHS
;=========================<>=========================

	;------------
	;	Heresy
	;------------

		(defrule
		(up-compare-goal g-enemy-monks-count >= 12)
		(can-research-with-escrow ri-heresy)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-heresy c: 1)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-heresy)
		)

		(defrule
		(up-compare-goal g-enemy-monks-count >= 5)
		(can-research ri-heresy)
		=>
		(research ri-heresy)
		)

		;Battle Elephants
		(defrule
		(goal g-primary-unit battle-elephant)
		(can-research-with-escrow ri-heresy)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-heresy c: 1)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-heresy)
		)

		(defrule
		(or
			(up-compare-goal g-desired-num-battle-elephant > 6)
			(up-compare-goal g-battle-elephant-line > 10))
		(up-compare-goal g-battle-elephant-tech-progress >= SECONDARY-IMPERIAL-COMPLETE)
		(can-research ri-heresy)
		=>
		(research ri-heresy)
		)

	;-----------
	;	Faith
	;-----------

		(defrule
		(up-compare-goal g-enemy-monks-count >= 12)
		(up-compare-const HERESY-AVAILABLE == NO)
		(can-research-with-escrow ri-faith)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-faith c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-faith)
		)

		(defrule
		(up-compare-goal g-enemy-monks-count >= 5)
		(up-compare-const HERESY-AVAILABLE == NO)
		(up-compare-goal g-primary-unit-tech-progress >= SECONDARY-IMPERIAL-COMPLETE)
		(can-research ri-faith)
		=>
		(research ri-faith)
		)

		;Battle Elephants
		(defrule
		(goal g-primary-unit battle-elephant)
		(can-research-with-escrow ri-faith)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-faith c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-faith)
		)

		(defrule
		(or
			(up-compare-goal g-desired-num-battle-elephant > 6)
			(up-compare-goal g-battle-elephant-line > 10))
		(up-compare-goal g-primary-unit-tech-progress >= SECONDARY-IMPERIAL-COMPLETE)
		(can-research ri-faith)
		=>
		(research ri-faith)
		)

		;War Elephants
		#load-if-defined PERSIAN-CIV

			(defrule
			(goal g-primary-unit my-unique-unit)
			(can-research-with-escrow ri-faith)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: ri-faith c: 1)
			(up-modify-escrow food g:- g-food-cost)
			(up-modify-escrow gold g:- g-gold-cost)
			(research ri-faith)
			)

			(defrule
			(or
				(up-compare-goal g-desired-num-unique-unit > 6)
				(up-compare-goal g-unique-unit-line > 10))
			(can-research ri-faith)
			=>
			(research ri-faith)
			)

		#end-if

	;----------------
	;	Redemption
	;----------------

		(defrule
		(or
			(goal g-primary-unit monk)
			(and
				(goal g-support-unit monk)
				(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)))
		(or
			(up-compare-goal g-enemy-field-siege-count > 0)
			(or
				(up-research-status c: ri-sanctity >= research-pending)
				(up-compare-const SANCTITY-AVAILABLE == NO)))
		(or
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(gold-amount >= 1275))
		(can-research-with-escrow ri-redemption)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-redemption c: 1)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-redemption)
		)

		(defrule
		(up-compare-goal g-desired-num-monk >= 4)
		(or
			(up-compare-goal g-enemy-field-siege-count > 0)
			(and
				(up-compare-goal g-desired-num-monk >= 7)
				(or
					(up-research-status c: ri-sanctity >= research-pending)
					(up-compare-const SANCTITY-AVAILABLE == NO))))
		(up-compare-goal g-current-age-primary-unit-tech-progress >= SECONDARY-COMPLETE)
		(can-research ri-redemption)
		=>
		(research ri-redemption)
		)

	;--------------------
	;	Block Printing
	;--------------------

		(defrule
		(or
			(goal g-primary-unit monk)
			(and
				(goal g-support-unit monk)
				(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)))
		(can-research-with-escrow ri-block-printing)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-block-printing c: 1)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-block-printing)
		)

		(defrule
		(up-compare-goal g-desired-num-monk >= 4)
		(up-compare-goal g-current-age-primary-unit-tech-progress >= SECONDARY-COMPLETE)
		(can-research ri-block-printing)
		=>
		(research ri-block-printing)
		)

	;--------------
	;	Sanctity
	;--------------

		(defrule
		(or
			(goal g-primary-unit monk)
			(and
				(goal g-support-unit monk)
				(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)))
		(or
			(up-research-status c: ri-block-printing >= research-pending)
			(or
				(current-age == castle-age)
				(up-compare-const BLOCK-PRINTING-AVAILABLE == NO)))
		(or
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(gold-amount >= 920))
		(can-research-with-escrow ri-sanctity)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-sanctity c: 1)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-sanctity)
		)

		(defrule
		(military-population g:>= g-min-military-pop)
		(up-compare-goal g-desired-num-monk >= 3)
		(unit-type-count monastery-class >= 3)
		(up-compare-goal g-current-age-primary-unit-tech-progress >= SECONDARY-COMPLETE)
		(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
		(can-research ri-sanctity)
		=>
		(research ri-sanctity)
		)

	;------------
	;	Fervor
	;------------

		#load-if-defined UP-GAME-WK		;Fervor actually had no effect in the original game

			(defrule
			(or
				(goal g-primary-unit monk)
				(and
					(goal g-support-unit monk)
					(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)))
			(or
				(up-research-status c: ri-block-printing >= research-pending)
				(or
					(current-age == castle-age)
					(up-compare-const BLOCK-PRINTING-AVAILABLE == NO)))
			(or
				(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
				(gold-amount >= 940))
			(can-research-with-escrow ri-fervor)
			=>
			(up-setup-cost-data 1 g-food-cost)
			(up-add-research-cost c: ri-fervor c: 1)
			(up-modify-escrow gold g:- g-gold-cost)
			(research ri-fervor)
			)

			(defrule
			(military-population g:>= g-min-military-pop)
			(up-compare-goal g-desired-num-monk >= 3)
			(unit-type-count monastery-class >= 3)
			(up-compare-goal g-current-age-primary-unit-tech-progress >= SECONDARY-COMPLETE)
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(can-research ri-fervor)
			=>
			(research ri-fervor)
			)

		#end-if

	;---------------
	;	Theocracy
	;---------------

		(defrule
		(or
			(goal g-primary-unit monk)
			(and
				(goal g-support-unit monk)
				(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)))
		(or
			(up-research-status c: ri-block-printing >= research-pending)
			(up-compare-const BLOCK-PRINTING-AVAILABLE == NO))
		(can-research-with-escrow ri-theocracy)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-theocracy c: 1)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-theocracy)
		)

		(defrule
		(or
			(up-research-status c: ri-block-printing >= research-pending)
			(up-compare-const BLOCK-PRINTING-AVAILABLE == NO))
		(up-compare-goal g-desired-num-monk >= 5)
		(can-research ri-theocracy)
		=>
		(research ri-theocracy)
		)

		(defrule
		(or
			(up-research-status c: ri-block-printing >= research-pending)
			(up-compare-const BLOCK-PRINTING-AVAILABLE == NO))
		(up-compare-goal g-desired-num-monk >= 4)
		(up-compare-goal g-current-age-primary-unit-tech-progress >= SECONDARY-COMPLETE)
		(can-research ri-theocracy)
		=>
		(research ri-theocracy)
		)

	;------------------
	;	Illumination
	;------------------

		(defrule
		(or
			(goal g-primary-unit monk)
			(and
				(goal g-support-unit monk)
				(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)))
		(or
			(up-research-status c: ri-theocracy >= research-pending)
			(up-compare-const THEOCRACY-AVAILABLE == NO))
		(can-research-with-escrow ri-illumination)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-illumination c: 1)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-illumination)
		)

		(defrule
		(or
			(up-research-status c: ri-theocracy >= research-pending)
			(or
				(up-compare-const THEOCRACY-AVAILABLE == NO)
				(up-compare-goal g-desired-num-monk < 5)))
		(up-compare-goal g-desired-num-monk >= 4)
		(or
			(unit-type-count monastery-class >= 5)
			(population >= NINETY-FIVE-PERCENT-POP))
		(can-research ri-illumination)
		=>
		(research ri-illumination)
		)

	;---------------
	;	Atonement
	;---------------

		(defrule
		(or
			(goal g-primary-unit monk)
			(and
				(goal g-support-unit monk)
				(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS)))
		(up-compare-goal g-enemy-monks-count >= 5)
		(or
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(gold-amount >= 1125))
		(can-research-with-escrow ri-atonement)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-atonement c: 1)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-atonement)
		)

		(defrule
		(up-compare-goal g-desired-num-monks >= 5)
		(up-compare-goal g-enemy-monks-count >= 5)
		(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
		(or
			(unit-type-count monastery-class >= 5)
			(population >= NINETY-FIVE-PERCENT-POP))
		(can-research ri-atonement)
		=>
		(research ri-atonement)
		)

	;----------------------
	;	Aztec Monk Techs
	;----------------------

		#load-if-defined AZTEC-CIV

			(defrule
			(or
				(goal g-primary-unit monk)
				(and
					(goal g-support-unit monk)
					(and
						(up-compare-goal g-primary-unit-tech-progress >= SECONDARY-CASTLE-COMPLETE)
						(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS))))
			(research-completed ri-redemption)
			(research-completed ri-sanctity)
			(research-completed ri-fervor)
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(can-research ri-atonement)
			=>
			(research ri-atonement)
			)

			(defrule
			(or
				(goal g-primary-unit monk)
				(and
					(goal g-support-unit monk)
					(and
						(up-compare-goal g-primary-unit-tech-progress >= SECONDARY-IMPERIAL-COMPLETE)
						(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS))))
			(research-completed ri-redemption)
			(research-completed ri-sanctity)
			(research-completed ri-fervor)
			(research-completed ri-atonement)
			(or
				(unit-type-count monastery-class >= 7)
				(population >= NINETY-FIVE-PERCENT-POP))
			(can-research ri-herbal-medicine)
			=>
			(research ri-herbal-medicine)
			)

			(defrule
			(or
				(goal g-primary-unit monk)
				(and
					(goal g-support-unit monk)
					(and
						(up-compare-goal g-primary-unit-tech-progress >= SECONDARY-CASTLE-COMPLETE)
						(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS))))
			(research-completed ri-redemption)
			(research-completed ri-sanctity)
			(research-completed ri-fervor)
			(research-completed ri-atonement)
			(or
				(up-compare-goal g-enemy-monks-count >= 5)
				(population >= NINETY-PERCENT-POP))
			(can-research ri-heresy)
			=>
			(research ri-heresy)
			)

			(defrule
			(or
				(goal g-primary-unit monk)
				(and
					(goal g-support-unit monk)
					(and
						(up-compare-goal g-primary-unit-tech-progress >= SECONDARY-CASTLE-COMPLETE)
						(up-compare-flag g-flag == SAVE-FOR-SUPPORT-UNIT-TECHS))))
			(research-completed ri-redemption)
			(research-completed ri-sanctity)
			(research-completed ri-fervor)
			(research-completed ri-atonement)
			(research-completed ri-herbal-medicine)
			(research-completed ri-heresy)
			(can-research ri-faith)
			=>
			(research ri-faith)
			)

		#end-if

;=========================<>=========================
;				     OTHER TECHS
;=========================<>=========================

	;------------------
	;	Conscription
	;------------------

		(defrule
		(can-research-with-escrow ri-conscription)
		=>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-conscription c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-conscription)
		)

	;-----------------
	;	Cartography
	;-----------------

		(defrule
		(current-age >= castle-age)
		(or
			(up-compare-flag g-flag == FIRST-ATTACK-LAUNCHED)
			(and
				(goal g-position POCKET)
				(up-compare-goal g-eco-tech-progress >= ALL-CASTLE-COMPLETE)))
		(goal g-town-under-attack NO)
		(player-in-game any-ally)
		(can-research ri-cartography)
		=>
		(research ri-cartography)
		)

	;--------------
	;	Caravan
	;--------------

        (defrule
		(or
			(up-compare-goal g-desired-num-trade-cart > 0)
			(up-compare-goal g-desired-num-trade-cog > 0))
        (can-research-with-escrow ri-caravan)
        =>
		(up-setup-cost-data 1 g-food-cost)
		(up-add-research-cost c: ri-caravan c: 1)
		(up-modify-escrow food g:- g-food-cost)
		(up-modify-escrow gold g:- g-gold-cost)
		(research ri-caravan)
        )

	;-------------
	;	Coinage
	;-------------

		(defrule
		(or
			(current-age == imperial-age)
			(population >= EIGHTY-PERCENT-POP))
		(population >= SEVENTY-PERCENT-POP)
		(player-in-game any-ally)
		(can-research ri-coinage)
		=>
		(research ri-coinage)
		)

	;-------------
	;	Banking
	;-------------

		(defrule
		(population >= EIGHTY-PERCENT-POP)
		(up-compare-goal g-primary-unit-tech-progress >= PRIORITY-IMPERIAL-COMPLETE)
		(player-in-game any-ally)
		(can-research ri-banking)
		=>
		(research ri-banking)
		)

	;------------
	;	Guilds
	;------------

		(defrule
		(or
			(dropsite-min-distance gold > 50)
			(or
				(unit-type-count villager-gold == 0)
				(up-compare-goal g-current-strategy >= 1301)))	;Late Imperial
		(can-research ri-guilds)
		=>
		(research ri-guilds)
		)

	;------------------
	;	Murder Holes
	;------------------

		(defrule
		(can-research ri-murder-holes)
		(or
			(building-type-count castle > 2)
			(building-type-count tower-class > 5))
		(current-age == imperial-age)
		(population >= EIGHTY-FIVE-PERCENT-POP)
		=>
		(research ri-murder-holes)
		)

	;---------------
	;	Hoardings
	;---------------

		(defrule
		(can-research ri-hoardings)
		(building-type-count castle > 1)
		(up-get-fact military-population 0 g-temp)
		(up-get-fact-max any-enemy military-population 0 g-temp2)
		(up-modify-goal g-temp g:- g-temp2)
		(or
			(goal g-town-under-attack YES)
			(or
				(up-compare-goal g-temp < 10)
				(and
					(building-type-count castle > 2)
					(population >= NINETY-PERCENT-POP))))
		=>
		(research ri-hoardings)
		)

	;----------------
	;	Town Watch
	;----------------

		(defrule
		(current-age >= castle-age)
		(building-type-count town-center > 1)
		(or
			(and
				(goal g-game-focus DEFENSIVE)
				(goal g-town-under-attack NO))
			(up-building-type-in-town c: watch-tower >= 4))
		(or
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(food-amount >= 1075))
		(can-research ri-town-watch)
		=>
		(research ri-town-watch)
		)

		(defrule
		(building-type-count town-center > 1)
		(up-compare-goal g-eco-tech-progress >= ALL-CASTLE-COMPLETE)
		(population >= SEVENTY-PERCENT-POP)
		(or
			(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
			(food-amount >= 1075))
		(can-research ri-town-watch)
		=>
		(research ri-town-watch)
		)

	;-----------------
	;	Town Patrol
	;-----------------

		(defrule
		(building-type-count town-center > 2)
		(up-compare-goal g-eco-tech-progress >= SECONDARY-IMPERIAL-COMPLETE)
		(population >= NINETY-PERCENT-POP)
		(can-research ri-town-patrol)
		=>
		(research ri-town-patrol)
		)
		
	;-------------
	;	Masonry
	;-------------

		(defrule
		(current-age >= castle-age)
		(up-compare-goal g-eco-tech-progress >= ALL-CASTLE-COMPLETE)
		(up-compare-goal g-current-age-primary-unit-tech-progress >= PRIORITY-COMPLETE)
		(up-compare-goal g-age-status != SAVE-FOR-IMPERIAL)
		(can-research ri-masonry)
		=>
		(research ri-masonry)
		)
		
	;------------------
	;	Architecture
	;------------------

		(defrule
		(up-compare-goal g-eco-tech-progress >= ALL-CASTLE-COMPLETE)
		(up-compare-goal g-primary-unit-tech-progress >= SECONDARY-IMPERIAL-COMPLETE)
		(building-type-count castle >= 2)
		(population >= NINETY-PERCENT-POP)
		(can-research ri-architecture)
		=>
		(research ri-architecture)
		)
		
	;---------------------
	;	Treadmill Crane
	;---------------------

		(defrule
		(current-age == imperial-age)
		(up-compare-goal g-primary-unit-tech-progress >= SECONDARY-IMPERIAL-COMPLETE)
		(up-compare-goal g-support-unit-tech-progress >= SECONDARY-IMPERIAL-COMPLETE)
		(up-compare-goal g-eco-tech-progress >= SECONDARY-IMPERIAL-COMPLETE)
		(can-research ri-treadmill-crane)
		=>
		(research ri-treadmill-crane)
		)

		(defrule
		(up-compare-goal g-desired-num-wonder > 0)
		(building-type-count wonder == 0)
		(can-research ri-treadmill-crane)
		=>
		(research ri-treadmill-crane)
		)