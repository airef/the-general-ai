;----------------------
;   Swordsmen Strats
;----------------------

    (defrule
    (up-compare-goal g-desired-num-military > 0)
    (goal g-current-strategy LATEI-CHAMPIONS)
    =>
    (up-modify-goal g-desired-num-militia g:= g-desired-num-military)
    (up-modify-goal g-desired-num-militia c:%* 65)
    (set-goal g-support-unit skirmisher)
    (up-modify-goal g-desired-num-skirmisher g:= g-desired-num-military)
    (up-modify-goal g-desired-num-skirmisher c:%* 35)
    )

;---------------------
;   Spearmen Strats
;---------------------

    (defrule
    (up-compare-goal g-desired-num-military > 0)
    (goal g-current-strategy LATEI-TRASHALBS)
    =>
    (up-modify-goal g-desired-num-spearman g:= g-desired-num-military)
    (up-modify-goal g-desired-num-spearman c:%* 65)
    (set-goal g-support-unit scout-cavalry)
    (up-modify-goal g-desired-num-scout-cavalry g:= g-desired-num-military)
    (up-modify-goal g-desired-num-scout-cavalry c:%* 35)
    )

;-------------------
;   Archer Strats
;-------------------

    (defrule
    (up-compare-goal g-desired-num-military > 0)
    (goal g-primary-unit archer)
    (up-compare-goal g-current-strategy != FL-GUERILLA)
    (up-compare-goal g-current-strategy != MIDF-FULL-RANGED)
    (up-compare-goal g-age-status >= SAVE-FOR-CASTLE)
    (up-compare-goal g-age-status < MID-CASTLE)
    (or
        (goal g-town-under-attack YES)
        (up-compare-goal g-age-status >= TO-CASTLE))
    =>
    (up-modify-goal g-desired-num-archer g:= g-desired-num-military)
    (up-modify-goal g-desired-num-archer c:%* 85)
    (up-modify-goal g-desired-num-skirmisher g:= g-desired-num-military)
    (up-modify-goal g-desired-num-skirmisher c:%* 15)
    )

    (defrule
    (up-compare-goal g-desired-num-military > 0)
    (goal g-primary-unit archer)
    (current-age == castle-age)
    (up-compare-goal g-age-status >= MID-CASTLE)
    =>
    (up-modify-goal g-desired-num-archer g:= g-desired-num-military)
    (up-modify-goal g-desired-num-archer c:%* 90)
    (up-modify-goal g-desired-num-knight g:= g-desired-num-military)
    (up-modify-goal g-desired-num-knight c:%* 10)
    )

    (defrule
    (goal g-current-strategy MIDI-ARBS)
    (current-age == imperial-age)
    (or
        (players-civ target-player aztec)
        (or
            (players-civ target-player incan)
            (players-civ target-player mayan)))
    =>
    (up-modify-goal g-desired-num-archer g:= g-desired-num-military)
    (up-modify-goal g-desired-num-archer c:%* 80)
    (set-goal g-support-unit scout-cavalry)
    (up-modify-goal g-desired-num-scout-cavalry g:= g-desired-num-military)
    (up-modify-goal g-desired-num-scout-cavalry c:%* 20)
    (up-jump-rule 1)
    )
        (defrule
        (goal g-current-strategy MIDI-ARBS)
        (current-age == imperial-age)
        =>
        (up-modify-goal g-desired-num-archer g:= g-desired-num-military)
        (up-modify-goal g-desired-num-archer c:%* 80)
        (set-goal g-support-unit spearman)
        (up-modify-goal g-desired-num-spearman g:= g-desired-num-military)
        (up-modify-goal g-desired-num-spearman c:%* 20)
        )

;-----------------------
;   Skirmisher Strats
;-----------------------

    ;-------------
    ;   Defense
    ;-------------

        (defrule
        (goal g-game-focus DEFENSIVE)
        (up-compare-goal g-desired-num-military > 0)
        (current-age >= castle-age)
        (goal g-primary-unit skirmisher)
        (military-population g:< g-min-military-pop)
        (or
            (up-compare-goal g-enemy-heavy-cavalry-count > 3)
            (or
                (up-compare-goal g-enemy-light-cavalry-count > 5)
                (up-compare-goal g-enemy-elephants-count > 2)))
        (or
            (goal g-primary-enemy-threat g-enemy-heavy-cavalry-count)
            (or
                (goal g-primary-enemy-threat g-enemy-light-cavalry-count)
                (goal g-primary-enemy-threat g-enemy-elephants-count)))
        =>
        (up-modify-goal g-desired-num-spearman c:max 6)
        )

        (defrule
        (goal g-game-focus DEFENSIVE)
        (up-compare-goal g-desired-num-military > 0)
        (current-age >= castle-age)
        (goal g-primary-unit skirmisher)
        (military-population g:< g-min-military-pop)
        (or
            (up-compare-goal g-enemy-eagle-warrior-count > 6)
            (and
                (up-compare-goal g-enemy-special-unique-unit-count > 5)
                (up-compare-flag g-enemy-special-unique-unit-flag == HUSKARL-FLAG)))
        (or
            (goal g-primary-enemy-threat g-enemy-eagle-warrior-count)
            (and
                (goal g-primary-enemy-threat g-enemy-special-unique-unit-count)
                (up-compare-flag g-enemy-special-unique-unit-flag == HUSKARL-FLAG)))
        =>
        (up-modify-goal g-desired-num-knight c:max 6)
        )

        (defrule
        (goal g-game-focus DEFENSIVE)
        (up-compare-goal g-desired-num-military > 0)
        (current-age >= castle-age)
        (goal g-primary-unit skirmisher)
        (military-population g:< g-min-military-pop)
        (up-compare-goal g-enemy-field-siege-count > 2)
        =>
        (up-modify-goal g-desired-num-knight c:max 6)
        )

        (defrule
        (goal g-game-focus DEFENSIVE)
        (up-compare-goal g-desired-num-military > 0)
        (current-age >= castle-age)
        (goal g-primary-unit skirmisher)
        (military-population g:< g-min-military-pop)
        (or
            (up-compare-goal g-enemy-elephants-count > 2)
            (or
                (up-compare-goal g-enemy-heavy-cavalry-count > 4)
                (up-compare-goal g-enemy-camel-count > 4)))
        (or
            (goal g-primary-enemy-threat g-enemy-heavy-cavalry-count)
            (or
                (goal g-primary-enemy-threat g-enemy-camel-count)
                (goal g-primary-enemy-threat g-enemy-elephants-count)))
        =>
        (up-modify-goal g-desired-num-monk c:max 2)
        )

    ;---------------
    ;   Imp Flank
    ;---------------
        
        ;Select support unit
        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDI-SKIRMS)
        (goal g-support-unit 0)
        (goal g-position FLANK)
        (or
            (up-compare-goal g-enemy-light-cavalry-count > 12)
            (or
                (up-compare-goal g-enemy-heavy-cavalry-count > 8)
                (or
                    (up-compare-goal g-enemy-elephants-count > 5)
                    (and
                        (up-compare-goal g-enemy-special-unique-unit-count > 8)
                        (up-compare-flag g-enemy-special-unique-unit-flag == LEITIS-FLAG)))))
        =>
        (set-goal g-support-unit spearman)
        )
        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDI-SKIRMS)
        (goal g-support-unit 0)
        (goal g-position FLANK)
        (military-population < 12)
        (or
            (up-compare-goal g-enemy-light-cavalry-count > 8)
            (or
                (up-compare-goal g-enemy-heavy-cavalry-count > 5)
                (or
                    (up-compare-goal g-enemy-elephants-count > 3)
                    (and
                        (up-compare-goal g-enemy-special-unique-unit-count > 5)
                        (up-compare-flag g-enemy-special-unique-unit-flag == LEITIS-FLAG)))))
        =>
        (set-goal g-support-unit spearman)
        )

        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDI-SKIRMS)
        (goal g-support-unit 0)
        (goal g-position FLANK)
        (or
            (up-compare-goal g-enemy-eagle-warrior-count > 10)
            (or
                (up-compare-goal g-enemy-field-siege-count > 2)
                (and
                    (up-compare-goal g-enemy-special-unique-unit-count > 5)
                    (up-compare-flag g-enemy-special-unique-unit-flag == HUSKARL-FLAG))))
        =>
        (set-goal g-support-unit knight)
        )

        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDI-SKIRMS)
        (goal g-support-unit 0)
        (goal g-position FLANK)
        (or
            (up-compare-goal g-enemy-swordsmen-count > 8)
            (up-compare-goal g-enemy-condottiero-count > 8))
        =>
        (set-goal g-support-unit cavalry-archer)
        )
        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDI-SKIRMS)
        (goal g-support-unit 0)
        (goal g-position FLANK)
        (up-compare-goal g-enemy-special-unique-unit-count > 8)
        (or
            (up-compare-goal g-enemy-special-unique-unit-flag == JAGUAR-WARRIOR-FLAG)
            (or
                (up-compare-goal g-enemy-special-unique-unit-flag == CATAPHRACT-FLAG)
                (or
                    (up-compare-goal g-enemy-special-unique-unit-flag == SAMURAI-FLAG)
                    (up-compare-goal g-enemy-special-unique-unit-flag == TEUTONIC-KNIGHT-FLAG))))
        =>
        (set-goal g-support-unit cavalry-archer)
        )

        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDI-SKIRMS)
        (goal g-support-unit 0)
        (goal g-position FLANK)
        (up-compare-goal g-spearman-tech-progress >= SECONDARY-CASTLE-COMPLETE)
        =>
        (set-goal g-support-unit spearman)
        )

        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDI-SKIRMS)
        (goal g-support-unit 0)
        (goal g-position FLANK)
        =>
        (set-goal g-support-unit cavalry-archer)
        )

        (defrule
        (goal g-position FLANK)
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDI-SKIRMS)
        (goal g-support-unit militiaman)
        =>
        (up-modify-goal g-desired-num-skirmisher g:= g-desired-num-military)
        (up-modify-goal g-desired-num-skirmisher c:%* 65)
        (up-modify-goal g-desired-num-militia g:= g-desired-num-military)
        (up-modify-goal g-desired-num-militia c:%* 35)
        )

        (defrule
        (goal g-position FLANK)
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDI-SKIRMS)
        (goal g-support-unit spearman)
        =>
        (up-modify-goal g-desired-num-skirmisher g:= g-desired-num-military)
        (up-modify-goal g-desired-num-skirmisher c:%* 60)
        (up-modify-goal g-desired-num-spearman g:= g-desired-num-military)
        (up-modify-goal g-desired-num-spearman c:%* 40)
        )

        (defrule
        (goal g-position FLANK)
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDI-SKIRMS)
        (goal g-support-unit knight)
        =>
        (up-modify-goal g-desired-num-skirmisher g:= g-desired-num-military)
        (up-modify-goal g-desired-num-skirmisher c:%* 75)
        (up-modify-goal g-desired-num-knight g:= g-desired-num-military)
        (up-modify-goal g-desired-num-knight c:%* 25)
        )

        (defrule
        (goal g-position FLANK)
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDI-SKIRMS)
        (goal g-support-unit cavalry-archer)
        =>
        (up-modify-goal g-desired-num-skirmisher g:= g-desired-num-military)
        (up-modify-goal g-desired-num-skirmisher c:%* 65)
        (up-modify-goal g-desired-num-cavalry-archer g:= g-desired-num-military)
        (up-modify-goal g-desired-num-cavalry-archer c:%* 35)
        )

    ;----------------
    ;   Imp Pocket
    ;----------------
        
        ;Select support unit
        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDI-SKIRMS)
        (goal g-support-unit 0)
        (goal g-position POCKET)
        (or
            (up-compare-goal g-enemy-light-cavalry-count > 12)
            (or
                (up-compare-goal g-enemy-heavy-cavalry-count > 8)
                (or
                    (up-compare-goal g-enemy-elephants-count > 5)
                    (and
                        (up-compare-goal g-enemy-special-unique-unit-count > 8)
                        (up-compare-flag g-enemy-special-unique-unit-flag == LEITIS-FLAG)))))
        =>
        (set-goal g-support-unit spearman)
        )
        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDI-SKIRMS)
        (goal g-support-unit 0)
        (goal g-position POCKET)
        (military-population < 12)
        (or
            (up-compare-goal g-enemy-light-cavalry-count > 8)
            (or
                (up-compare-goal g-enemy-heavy-cavalry-count > 5)
                (or
                    (up-compare-goal g-enemy-elephants-count > 3)
                    (and
                        (up-compare-goal g-enemy-special-unique-unit-count > 5)
                        (up-compare-flag g-enemy-special-unique-unit-flag == LEITIS-FLAG)))))
        =>
        (set-goal g-support-unit spearman)
        )

        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDI-SKIRMS)
        (goal g-support-unit 0)
        (goal g-position POCKET)
        (or
            (up-compare-goal g-enemy-eagle-warrior-count > 10)
            (or
                (up-compare-goal g-enemy-field-siege-count > 2)
                (and
                    (up-compare-goal g-enemy-special-unique-unit-count > 5)
                    (up-compare-flag g-enemy-special-unique-unit-flag == HUSKARL-FLAG))))
        =>
        (set-goal g-support-unit knight)
        )

        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDI-SKIRMS)
        (goal g-support-unit 0)
        (goal g-position POCKET)
        (or
            (up-compare-goal g-enemy-swordsmen-count > 12)
            (up-compare-goal g-enemy-condottiero-count > 12))
        =>
        (set-goal g-support-unit cavalry-archer)
        )
        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDI-SKIRMS)
        (goal g-support-unit 0)
        (goal g-position POCKET)
        (up-compare-goal g-enemy-special-unique-unit-count > 12)
        (or
            (up-compare-goal g-enemy-special-unique-unit-flag == JAGUAR-WARRIOR-FLAG)
            (or
                (up-compare-goal g-enemy-special-unique-unit-flag == CATAPHRACT-FLAG)
                (or
                    (up-compare-goal g-enemy-special-unique-unit-flag == SAMURAI-FLAG)
                    (up-compare-goal g-enemy-special-unique-unit-flag == TEUTONIC-KNIGHT-FLAG))))
        =>
        (set-goal g-support-unit cavalry-archer)
        )

        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDI-SKIRMS)
        (goal g-support-unit 0)
        (goal g-position POCKET)
        (up-compare-goal g-spearman-tech-progress >= SECONDARY-CASTLE-COMPLETE)
        =>
        (set-goal g-support-unit spearman)
        )

        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDI-SKIRMS)
        (goal g-support-unit 0)
        (goal g-position POCKET)
        =>
        (set-goal g-support-unit cavalry-archer)
        )

        (defrule
        (goal g-position POCKET)
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDI-SKIRMS)
        (goal g-support-unit militiaman)
        =>
        (up-modify-goal g-desired-num-skirmisher g:= g-desired-num-military)
        (up-modify-goal g-desired-num-skirmisher c:%* 65)
        (up-modify-goal g-desired-num-militia g:= g-desired-num-military)
        (up-modify-goal g-desired-num-militia c:%* 35)
        )

        (defrule
        (goal g-position POCKET)
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDI-SKIRMS)
        (goal g-support-unit spearman)
        =>
        (up-modify-goal g-desired-num-skirmisher g:= g-desired-num-military)
        (up-modify-goal g-desired-num-skirmisher c:%* 60)
        (up-modify-goal g-desired-num-spearman g:= g-desired-num-military)
        (up-modify-goal g-desired-num-spearman c:%* 40)
        )

        (defrule
        (goal g-position POCKET)
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDI-SKIRMS)
        (goal g-support-unit knight)
        =>
        (up-modify-goal g-desired-num-skirmisher g:= g-desired-num-military)
        (up-modify-goal g-desired-num-skirmisher c:%* 75)
        (up-modify-goal g-desired-num-knight g:= g-desired-num-military)
        (up-modify-goal g-desired-num-knight c:%* 25)
        )

        (defrule
        (goal g-position POCKET)
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDI-SKIRMS)
        (goal g-support-unit cavalry-archer)
        =>
        (up-modify-goal g-desired-num-skirmisher g:= g-desired-num-military)
        (up-modify-goal g-desired-num-skirmisher c:%* 65)
        (up-modify-goal g-desired-num-cavalry-archer g:= g-desired-num-military)
        (up-modify-goal g-desired-num-cavalry-archer c:%* 35)
        )

    ;-------------------
    ;   Late Imperial
    ;-------------------

        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy LATEI-TRASHKIRMS)
        =>
        (up-modify-goal g-desired-num-skirmisher g:= g-desired-num-military)
        (up-modify-goal g-desired-num-skirmisher c:%* 65)
        (set-goal g-support-unit scout-cavalry)
        (up-modify-goal g-desired-num-scout-cavalry g:= g-desired-num-military)
        (up-modify-goal g-desired-num-scout-cavalry c:%* 35)
        )

;------------------
;   Scout Strats
;------------------

    (defrule
    (up-compare-goal g-desired-num-military > 0)
    (goal g-current-strategy LATEI-TRASHUSSARS)
    =>
    (up-modify-goal g-desired-num-scout-cavalry g:= g-desired-num-military)
    (up-modify-goal g-desired-num-scout-cavalry c:%* 65)
    (set-goal g-support-unit skirmisher)
    (up-modify-goal g-desired-num-skirmisher g:= g-desired-num-military)
    (up-modify-goal g-desired-num-skirmisher c:%* 35)
    )

;-------------------
;   Knight Strats
;-------------------

    ;-------------
    ;   Defense
    ;-------------

        (defrule
        (goal g-game-focus DEFENSIVE)
        (up-compare-goal g-desired-num-military > 0)
        (current-age >= castle-age)
        (goal g-primary-unit knight)
        (military-population g:< g-min-military-pop)
        (up-compare-goal g-enemy-spearmen-count > 5)
        (goal g-primary-enemy-threat g-enemy-spearmen-count)
        =>
        (up-modify-goal g-desired-num-archer c:max 6)
        )

        (defrule
        (goal g-game-focus DEFENSIVE)
        (up-compare-goal g-desired-num-military > 0)
        (current-age >= castle-age)
        (goal g-primary-unit knight)
        (military-population g:< g-min-military-pop)
        (or
            (up-compare-goal g-enemy-heavy-cavalry-count > 4)
            (or
                (up-compare-goal g-enemy-camel-count > 4)
                (up-compare-goal g-enemy-elephants-count > 2)))
        (or
            (goal g-primary-enemy-threat g-enemy-heavy-cavalry-count)
            (or
                (goal g-primary-enemy-threat g-enemy-camel-count)
                (goal g-primary-enemy-threat g-enemy-elephants-count)))
        =>
        (up-modify-goal g-desired-num-spearman c:max 6)
        )

        (defrule
        (goal g-game-focus DEFENSIVE)
        (up-compare-goal g-desired-num-military > 0)
        (current-age >= castle-age)
        (goal g-primary-unit knight)
        (military-population g:< g-min-military-pop)
        (up-compare-goal g-enemy-monks-count > 2)
        (research-completed ri-light-cavalry)
        =>
        (up-modify-goal g-desired-num-scout-cavalry c:max 6)
        )

        (defrule
        (goal g-game-focus DEFENSIVE)
        (up-compare-goal g-desired-num-military > 0)
        (current-age >= castle-age)
        (goal g-primary-unit knight)
        (military-population g:< g-min-military-pop)
        (or
            (up-compare-goal g-enemy-elephants-count > 2)
            (or
                (up-compare-goal g-enemy-heavy-cavalry-count > 4)
                (up-compare-goal g-enemy-camel-count > 4)))
        (or
            (goal g-primary-enemy-threat g-enemy-heavy-cavalry-count)
            (or
                (goal g-primary-enemy-threat g-enemy-camel-count)
                (goal g-primary-enemy-threat g-enemy-elephants-count)))
        =>
        (up-modify-goal g-desired-num-monk c:max 2)
        )

    ;----------------
    ;   Mid Castle
    ;----------------

        ;Select support unit
        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDC-KNIGHTS)
        (or
            (goal g-support-unit 0)
            (goal g-support-unit cavalry-archer))
        (goal g-position FLANK)
        (up-compare-goal g-enemy-spearmen-count > 8)
        =>
        (set-goal g-support-unit archer)
        )

        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDC-KNIGHTS)
        (goal g-support-unit 0)
        (goal g-position FLANK)
        (or
            (up-compare-goal g-enemy-foot-archers-count > 10)
            (or
                (up-compare-goal g-enemy-cavalry-archers-count > 8)
                (up-compare-goal g-enemy-hand-cannoneer-slinger-count > 8)))
        =>
        (set-goal g-support-unit skirmisher)
        )
        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDC-KNIGHTS)
        (or
            (goal g-support-unit 0)
            (goal g-support-unit cavalry-archer))
        (goal g-position FLANK)
        (or
            (up-compare-goal g-enemy-special-unique-unit-count > 8)
            (and
                (up-compare-goal g-enemy-special-unique-unit-count > 5)
                (military-population < 12)))
        (or
            (up-compare-flag g-enemy-special-unique-unit-flag == GENOESE-CROSSBOWMAN-FLAG)
            (up-compare-flag g-enemy-special-unique-unit-flag == MAMELUKE-FLAG))
        =>
        (set-goal g-support-unit skirmisher)
        )

        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDC-KNIGHTS)
        (or
            (goal g-support-unit 0)
            (or
                (goal g-support-unit skirmisher)
                (goal g-support-unit archer)))
        (goal g-position FLANK)
        (or
            (up-compare-goal g-enemy-heavy-cavalry-count > 8)
            (or
                (up-compare-goal g-enemy-elephants-count > 5)
                (and
                    (up-compare-goal g-enemy-special-unique-unit-count > 8)
                    (up-compare-flag g-enemy-special-unique-unit-flag == LEITIS-FLAG))))
        =>
        (set-goal g-support-unit spearman)
        )
        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDC-KNIGHTS)
        (or
            (goal g-support-unit 0)
            (or
                (goal g-support-unit skirmisher)
                (goal g-support-unit archer)))
        (goal g-position FLANK)
        (military-population < 12)
        (or
            (up-compare-goal g-enemy-heavy-cavalry-count > 5)
            (up-compare-goal g-enemy-elephants-count > 3))
        =>
        (set-goal g-support-unit spearman)
        )
        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDC-KNIGHTS)
        (or
            (goal g-support-unit 0)
            (or
                (goal g-support-unit skirmisher)
                (goal g-support-unit archer)))
        (goal g-position FLANK)
        (military-population < 12)
        (up-compare-goal g-enemy-special-unique-unit-count > 5)
        (up-compare-flag g-enemy-special-unique-unit-flag == LEITIS-FLAG)
        =>
        (set-goal g-support-unit spearman)
        )

        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDC-KNIGHTS)
        (or
            (goal g-support-unit 0)
            (or
                (goal g-support-unit skirmisher)
                (goal g-support-unit cavalry-archer)))
        (goal g-position FLANK)
        (up-compare-goal g-enemy-camel-count > 5)
        =>
        (set-goal g-support-unit archer)
        )

        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDC-KNIGHTS)
        (goal g-support-unit 0)
        (goal g-position FLANK)
        (or
            (up-compare-goal g-enemy-special-unique-unit-count > 8)
            (and
                (up-compare-goal g-enemy-special-unique-unit-count > 5)
                (military-population < 12)))
        (or
            (up-compare-flag g-enemy-special-unique-unit-flag == CATAPHRACT-FLAG)
            (up-compare-flag g-enemy-special-unique-unit-flag == TEUTONIC-KNIGHT-FLAG))
        =>
        (set-goal g-support-unit cavalry-archer)
        )

        ;Modify unit counts
        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDC-KNIGHTS)
        (goal g-support-unit spearman)
        (goal g-position FLANK)
        =>
        (up-modify-goal g-desired-num-knight g:= g-desired-num-military)
        (up-modify-goal g-desired-num-knight c:%* 65)
        (up-modify-goal g-desired-num-spearman g:= g-desired-num-military)
        (up-modify-goal g-desired-num-spearman c:%* 35)
        )

        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDC-KNIGHTS)
        (goal g-support-unit archer)
        (goal g-position FLANK)
        =>
        (up-modify-goal g-desired-num-knight g:= g-desired-num-military)
        (up-modify-goal g-desired-num-knight c:%* 65)
        (up-modify-goal g-desired-num-archer g:= g-desired-num-military)
        (up-modify-goal g-desired-num-archer c:%* 35)
        )

        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDC-KNIGHTS)
        (goal g-support-unit skirmisher)
        (goal g-position FLANK)
        =>
        (up-modify-goal g-desired-num-knight g:= g-desired-num-military)
        (up-modify-goal g-desired-num-knight c:%* 65)
        (up-modify-goal g-desired-num-skirmisher g:= g-desired-num-military)
        (up-modify-goal g-desired-num-skirmisher c:%* 35)
        )

        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDC-KNIGHTS)
        (goal g-support-unit cavalry-archer)
        (goal g-position FLANK)
        =>
        (up-modify-goal g-desired-num-knight g:= g-desired-num-military)
        (up-modify-goal g-desired-num-knight c:%* 65)
        (up-modify-goal g-desired-num-cavalry-archer g:= g-desired-num-military)
        (up-modify-goal g-desired-num-cavalry-archer c:%* 35)
        )

    ;---------------
    ;   Imp Flank
    ;---------------

        ;---------------
        ;   Early Imp
        ;---------------

            ;Select support unit
            (defrule
            (up-compare-goal g-desired-num-military > 0)
            (goal g-current-strategy EARLYI-PALASTORM)
            (goal g-support-unit 0)
            (goal g-position FLANK)
            (up-compare-goal g-spearman-tech-progress >= PRIORITY-CASTLE-COMPLETE)
            (or
                (up-compare-goal g-enemy-heavy-cavalry-count > 8)
                (or
                    (up-compare-goal g-enemy-elephants-count > 5)
                    (and
                        (up-compare-goal g-enemy-special-unique-unit-count > 8)
                        (up-compare-flag g-enemy-special-unique-unit-flag == LEITIS-FLAG))))
            =>
            (set-goal g-support-unit spearman)
            )
            (defrule
            (up-compare-goal g-desired-num-military > 0)
            (goal g-current-strategy EARLYI-PALASTORM)
            (goal g-support-unit 0)
            (goal g-position FLANK)
            (up-compare-goal g-spearman-tech-progress >= PRIORITY-CASTLE-COMPLETE)
            (military-population < 12)
            (or
                (up-compare-goal g-enemy-heavy-cavalry-count > 5)
                (or
                    (up-compare-goal g-enemy-elephants-count > 3)
                    (and
                        (up-compare-goal g-enemy-special-unique-unit-count > 5)
                        (up-compare-flag g-enemy-special-unique-unit-flag == LEITIS-FLAG))))
            =>
            (set-goal g-support-unit spearman)
            )

            (defrule
            (up-compare-goal g-desired-num-military > 0)
            (goal g-current-strategy EARLYI-PALASTORM)
            (goal g-support-unit 0)
            (goal g-position FLANK)
            (up-compare-goal g-cavalry-archer-tech-progress >= PRIORITY-CASTLE-COMPLETE)
            (or
                (up-compare-goal g-enemy-special-unique-unit-count > 8)
                (and
                    (up-compare-goal g-enemy-special-unique-unit-count > 5)
                    (military-population < 12)))
            (or
                (up-compare-flag g-enemy-special-unique-unit-flag == CATAPHRACT-FLAG)
                (up-compare-flag g-enemy-special-unique-unit-flag == TEUTONIC-KNIGHT-FLAG))
            =>
            (set-goal g-support-unit cavalry-archer)
            )

            ;Modify unit counts
            (defrule
            (up-compare-goal g-desired-num-military > 0)
            (goal g-current-strategy EARLYI-PALASTORM)
            (goal g-support-unit spearman)
            (goal g-position FLANK)
            =>
            (up-modify-goal g-desired-num-knight g:= g-desired-num-military)
            (up-modify-goal g-desired-num-knight c:%* 75)
            (up-modify-goal g-desired-num-spearman g:= g-desired-num-military)
            (up-modify-goal g-desired-num-spearman c:%* 25)
            )

            (defrule
            (up-compare-goal g-desired-num-military > 0)
            (goal g-current-strategy EARLYI-PALASTORM)
            (goal g-support-unit cavalry-archer)
            (goal g-position FLANK)
            =>
            (up-modify-goal g-desired-num-knight g:= g-desired-num-military)
            (up-modify-goal g-desired-num-knight c:%* 75)
            (up-modify-goal g-desired-num-cavalry-archer g:= g-desired-num-military)
            (up-modify-goal g-desired-num-cavalry-archer c:%* 25)
            )

        ;-------------
        ;   Mid Imp
        ;-------------

            ;Select support unit
            (defrule
            (up-compare-goal g-desired-num-military > 0)
            (goal g-current-strategy MIDI-PALADINS)
            (goal g-support-unit 0)
            (goal g-position FLANK)
            (or
                (up-compare-goal g-enemy-spearmen-count > 15)
                (and
                    (up-compare-goal g-enemy-spearmen-count > 8)
                    (military-population < 20)))
            =>
            (set-goal g-support-unit skirmisher)
            )

            (defrule
            (up-compare-goal g-desired-num-military > 0)
            (goal g-current-strategy MIDI-PALADINS)
            (goal g-support-unit 0)
            (goal g-position FLANK)
            (or
                (up-compare-goal g-enemy-special-unique-unit-count > 8)
                (and
                    (up-compare-goal g-enemy-special-unique-unit-count > 5)
                    (military-population < 12)))
            (or
                (up-compare-flag g-enemy-special-unique-unit-flag == CATAPHRACT-FLAG)
                (up-compare-flag g-enemy-special-unique-unit-flag == TEUTONIC-KNIGHT-FLAG))
            =>
            (set-goal g-support-unit cavalry-archer)
            )

            (defrule
            (up-compare-goal g-desired-num-military > 0)
            (goal g-current-strategy MIDI-PALADINS)
            (goal g-support-unit 0)
            (goal g-position FLANK)
            (or
                (up-compare-goal g-enemy-heavy-cavalry-count > 8)
                (or
                    (up-compare-goal g-enemy-camel-count > 8)
                    (or
                        (up-compare-goal g-enemy-elephants-count > 5)
                        (and
                            (up-compare-goal g-enemy-special-unique-unit-count > 8)
                            (up-compare-flag g-enemy-special-unique-unit-flag == LEITIS-FLAG)))))
            =>
            (set-goal g-support-unit spearman)
            )
            (defrule
            (up-compare-goal g-desired-num-military > 0)
            (goal g-current-strategy MIDI-PALADINS)
            (goal g-support-unit 0)
            (goal g-position FLANK)
            (military-population < 12)
            (or
                (up-compare-goal g-enemy-heavy-cavalry-count > 5)
                (or
                    (up-compare-goal g-enemy-camel-count > 5)
                    (or
                        (up-compare-goal g-enemy-elephants-count > 3)
                        (and
                            (up-compare-goal g-enemy-special-unique-unit-count > 5)
                            (up-compare-flag g-enemy-special-unique-unit-flag == LEITIS-FLAG)))))
            =>
            (set-goal g-support-unit spearman)
            )

            (defrule
            (up-compare-goal g-desired-num-military > 0)
            (goal g-current-strategy MIDI-PALADINS)
            (goal g-support-unit 0)
            (goal g-position FLANK)
            (or
                (up-compare-goal g-enemy-foot-archers-count > 15)
                (and
                    (military-population < 20)
                    (up-compare-goal g-enemy-foot-archers-count > 8)))
            =>
            (set-goal g-support-unit skirmisher)
            )
            (defrule
            (up-compare-goal g-desired-num-military > 0)
            (goal g-current-strategy MIDI-PALADINS)
            (goal g-support-unit 0)
            (goal g-position FLANK)
            (or
                (up-compare-goal g-enemy-cavalry-archers-count > 15)
                (and
                    (military-population < 20)
                    (up-compare-goal g-enemy-cavalry-archers-count > 8)))
            =>
            (set-goal g-support-unit skirmisher)
            )
            (defrule
            (up-compare-goal g-desired-num-military > 0)
            (goal g-current-strategy MIDI-PALADINS)
            (goal g-support-unit 0)
            (goal g-position FLANK)
            (or
                (up-compare-goal g-enemy-special-unique-unit-count > 8)
                (and
                    (up-compare-goal g-enemy-special-unique-unit-count > 5)
                    (military-population < 12)))
            (or
                (up-compare-flag g-enemy-special-unique-unit-flag == GENOESE-CROSSBOWMAN-FLAG)
                (up-compare-flag g-enemy-special-unique-unit-flag == MAMELUKE-FLAG))
            =>
            (set-goal g-support-unit skirmisher)
            )

            (defrule
            (up-compare-goal g-desired-num-military > 0)
            (goal g-current-strategy MIDI-PALADINS)
            (goal g-support-unit 0)
            (goal g-position FLANK)
            =>
            (set-goal g-support-unit cavalry-archer)
            )

            ;Modify unit counts
            (defrule
            (up-compare-goal g-desired-num-military > 0)
            (goal g-current-strategy MIDI-PALADINS)
            (goal g-support-unit spearman)
            (goal g-position FLANK)
            =>
            (up-modify-goal g-desired-num-knight g:= g-desired-num-military)
            (up-modify-goal g-desired-num-knight c:%* 65)
            (up-modify-goal g-desired-num-spearman g:= g-desired-num-military)
            (up-modify-goal g-desired-num-spearman c:%* 35)
            )

            (defrule
            (up-compare-goal g-desired-num-military > 0)
            (goal g-current-strategy MIDI-PALADINS)
            (goal g-support-unit archer)
            (goal g-position FLANK)
            =>
            (up-modify-goal g-desired-num-knight g:= g-desired-num-military)
            (up-modify-goal g-desired-num-knight c:%* 65)
            (up-modify-goal g-desired-num-archer g:= g-desired-num-military)
            (up-modify-goal g-desired-num-archer c:%* 35)
            )

            (defrule
            (up-compare-goal g-desired-num-military > 0)
            (goal g-current-strategy MIDI-PALADINS)
            (goal g-support-unit skirmisher)
            (goal g-position FLANK)
            =>
            (up-modify-goal g-desired-num-knight g:= g-desired-num-military)
            (up-modify-goal g-desired-num-knight c:%* 75)
            (up-modify-goal g-desired-num-skirmisher g:= g-desired-num-military)
            (up-modify-goal g-desired-num-skirmisher c:%* 25)
            )
            (defrule
            (up-compare-goal g-desired-num-military > 0)
            (goal g-current-strategy MIDI-PALADINS)
            (goal g-support-unit skirmisher)
            (goal g-position FLANK)
            (or
                (up-compare-goal g-enemy-special-unique-unit-count > 8)
                (and
                    (up-compare-goal g-enemy-special-unique-unit-count > 5)
                    (military-population < 12)))
            (or
                (up-compare-flag g-enemy-special-unique-unit-flag == GENOESE-CROSSBOWMAN-FLAG)
                (up-compare-flag g-enemy-special-unique-unit-flag == MAMELUKE-FLAG))
            =>
            (up-modify-goal g-desired-num-knight g:= g-desired-num-military)
            (up-modify-goal g-desired-num-knight c:%* 50)
            (up-modify-goal g-desired-num-skirmisher g:= g-desired-num-military)
            (up-modify-goal g-desired-num-skirmisher c:%* 50)
            )

            (defrule
            (up-compare-goal g-desired-num-military > 0)
            (goal g-current-strategy MIDI-PALADINS)
            (goal g-support-unit cavalry-archer)
            (goal g-position FLANK)
            =>
            (up-modify-goal g-desired-num-knight g:= g-desired-num-military)
            (up-modify-goal g-desired-num-knight c:%* 60)
            (up-modify-goal g-desired-num-cavalry-archer g:= g-desired-num-military)
            (up-modify-goal g-desired-num-cavalry-archer c:%* 40)
            )

            (defrule
            (up-compare-goal g-desired-num-military > 0)
            (goal g-current-strategy MIDI-PALADINS)
            (goal g-support-unit my-unique-unit)
            (goal g-position FLANK)
            =>
            (up-modify-goal g-desired-num-knight g:= g-desired-num-military)
            (up-modify-goal g-desired-num-knight c:%* 65)
            (up-modify-goal g-desired-num-unique-unit g:= g-desired-num-military)
            (up-modify-goal g-desired-num-unique-unit c:%* 35)
            )

    ;----------------
    ;   Imp Pocket
    ;----------------

        ;Select support unit
        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (or
            (goal g-current-strategy EARLYI-PALASTORM)
            (goal g-current-strategy MIDI-PALADINS))
        (goal g-support-unit 0)
        (goal g-position POCKET)
        (or
            (up-compare-goal g-enemy-spearmen-count > 15)
            (and
                (up-compare-goal g-enemy-spearmen-count > 8)
                (military-population < 20)))
        =>
        (set-goal g-support-unit skirmisher)
        )

        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (or
            (goal g-current-strategy EARLYI-PALASTORM)
            (goal g-current-strategy MIDI-PALADINS))
        (goal g-support-unit 0)
        (goal g-position POCKET)
        (or
            (up-compare-goal g-enemy-special-unique-unit-count > 12)
            (and
                (up-compare-goal g-enemy-special-unique-unit-count > 8)
                (military-population < 12)))
        (or
            (up-compare-flag g-enemy-special-unique-unit-flag == CATAPHRACT-FLAG)
            (up-compare-flag g-enemy-special-unique-unit-flag == TEUTONIC-KNIGHT-FLAG))
        =>
        (set-goal g-support-unit cavalry-archer)
        )

        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (or
            (goal g-current-strategy EARLYI-PALASTORM)
            (goal g-current-strategy MIDI-PALADINS))
        (goal g-support-unit 0)
        (goal g-position POCKET)
        (or
            (up-compare-goal g-enemy-heavy-cavalry-count > 12)
            (or
                (up-compare-goal g-enemy-camel-count > 12)
                (or
                    (up-compare-goal g-enemy-elephants-count > 8)
                    (and
                        (up-compare-goal g-enemy-special-unique-unit-count > 12)
                        (up-compare-flag g-enemy-special-unique-unit-flag == LEITIS-FLAG)))))
        =>
        (set-goal g-support-unit spearman)
        )
        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (or
            (goal g-current-strategy EARLYI-PALASTORM)
            (goal g-current-strategy MIDI-PALADINS))
        (goal g-support-unit 0)
        (goal g-position POCKET)
        (military-population < 12)
        (or
            (up-compare-goal g-enemy-heavy-cavalry-count > 8)
            (or
                (up-compare-goal g-enemy-camel-count > 8)
                (up-compare-goal g-enemy-elephants-count > 5)))
        =>
        (set-goal g-support-unit spearman)
        )
        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (or
            (goal g-current-strategy EARLYI-PALASTORM)
            (goal g-current-strategy MIDI-PALADINS))
        (goal g-support-unit 0)
        (goal g-position POCKET)
        (military-population < 12)
        (up-compare-goal g-enemy-special-unique-unit-count > 8)
        (up-compare-flag g-enemy-special-unique-unit-flag == LEITIS-FLAG)
        =>
        (set-goal g-support-unit spearman)
        )

        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (or
            (goal g-current-strategy EARLYI-PALASTORM)
            (goal g-current-strategy MIDI-PALADINS))
        (goal g-support-unit 0)
        (goal g-position POCKET)
        (or
            (up-compare-goal g-enemy-foot-archers-count > 15)
            (and
                (military-population < 20)
                (up-compare-goal g-enemy-foot-archers-count > 8)))
        =>
        (set-goal g-support-unit skirmisher)
        )
        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (or
            (goal g-current-strategy EARLYI-PALASTORM)
            (goal g-current-strategy MIDI-PALADINS))
        (goal g-support-unit 0)
        (goal g-position POCKET)
        (or
            (up-compare-goal g-enemy-cavalry-archers-count > 15)
            (and
                (military-population < 20)
                (up-compare-goal g-enemy-cavalry-archers-count > 8)))
        =>
        (set-goal g-support-unit skirmisher)
        )
        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (or
            (goal g-current-strategy EARLYI-PALASTORM)
            (goal g-current-strategy MIDI-PALADINS))
        (goal g-support-unit 0)
        (goal g-position POCKET)
        (or
            (up-compare-goal g-enemy-special-unique-unit-count > 12)
            (and
                (up-compare-goal g-enemy-special-unique-unit-count > 8)
                (military-population < 12)))
        (or
            (up-compare-flag g-enemy-special-unique-unit-flag == GENOESE-CROSSBOWMAN-FLAG)
            (up-compare-flag g-enemy-special-unique-unit-flag == MAMELUKE-FLAG))
        =>
        (set-goal g-support-unit skirmisher)
        )

        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDI-PALADINS)
        (goal g-support-unit 0)
        (goal g-position POCKET)
        =>
        (set-goal g-support-unit cavalry-archer)
        )

        ;Modify unit counts
        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (or
            (goal g-current-strategy EARLYI-PALASTORM)
            (goal g-current-strategy MIDI-PALADINS))
        (goal g-support-unit spearman)
        (goal g-position POCKET)
        =>
        (up-modify-goal g-desired-num-knight g:= g-desired-num-military)
        (up-modify-goal g-desired-num-knight c:%* 65)
        (up-modify-goal g-desired-num-spearman g:= g-desired-num-military)
        (up-modify-goal g-desired-num-spearman c:%* 35)
        )

        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (or
            (goal g-current-strategy EARLYI-PALASTORM)
            (goal g-current-strategy MIDI-PALADINS))
        (goal g-support-unit archer)
        (goal g-position POCKET)
        =>
        (up-modify-goal g-desired-num-knight g:= g-desired-num-military)
        (up-modify-goal g-desired-num-knight c:%* 65)
        (up-modify-goal g-desired-num-archer g:= g-desired-num-military)
        (up-modify-goal g-desired-num-archer c:%* 35)
        )

        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (or
            (goal g-current-strategy EARLYI-PALASTORM)
            (goal g-current-strategy MIDI-PALADINS))
        (goal g-support-unit skirmisher)
        (goal g-position POCKET)
        =>
        (up-modify-goal g-desired-num-knight g:= g-desired-num-military)
        (up-modify-goal g-desired-num-knight c:%* 75)
        (up-modify-goal g-desired-num-skirmisher g:= g-desired-num-military)
        (up-modify-goal g-desired-num-skirmisher c:%* 25)
        )
        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (or
            (goal g-current-strategy EARLYI-PALASTORM)
            (goal g-current-strategy MIDI-PALADINS))
        (goal g-support-unit skirmisher)
        (goal g-position POCKET)
        (up-compare-goal g-enemy-special-unique-unit-count > 12)
        (or
            (up-compare-flag g-enemy-special-unique-unit-flag == GENOESE-CROSSBOWMAN-FLAG)
            (up-compare-flag g-enemy-special-unique-unit-flag == MAMELUKE-FLAG))
        =>
        (up-modify-goal g-desired-num-knight g:= g-desired-num-military)
        (up-modify-goal g-desired-num-knight c:%* 50)
        (up-modify-goal g-desired-num-skirmisher g:= g-desired-num-military)
        (up-modify-goal g-desired-num-skirmisher c:%* 50)
        )
        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (or
            (goal g-current-strategy EARLYI-PALASTORM)
            (goal g-current-strategy MIDI-PALADINS))
        (goal g-support-unit skirmisher)
        (goal g-position POCKET)
        (up-compare-goal g-enemy-special-unique-unit-count > 8)
        (military-population < 12)
        (or
            (up-compare-flag g-enemy-special-unique-unit-flag == GENOESE-CROSSBOWMAN-FLAG)
            (up-compare-flag g-enemy-special-unique-unit-flag == MAMELUKE-FLAG))
        =>
        (up-modify-goal g-desired-num-knight g:= g-desired-num-military)
        (up-modify-goal g-desired-num-knight c:%* 50)
        (up-modify-goal g-desired-num-skirmisher g:= g-desired-num-military)
        (up-modify-goal g-desired-num-skirmisher c:%* 50)
        )

        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (or
            (goal g-current-strategy EARLYI-PALASTORM)
            (goal g-current-strategy MIDI-PALADINS))
        (goal g-support-unit cavalry-archer)
        (goal g-position POCKET)
        =>
        (up-modify-goal g-desired-num-knight g:= g-desired-num-military)
        (up-modify-goal g-desired-num-knight c:%* 65)
        (up-modify-goal g-desired-num-cavalry-archer g:= g-desired-num-military)
        (up-modify-goal g-desired-num-cavalry-archer c:%* 35)
        )

        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (or
            (goal g-current-strategy EARLYI-PALASTORM)
            (goal g-current-strategy MIDI-PALADINS))
        (goal g-support-unit my-unique-unit)
        (goal g-position POCKET)
        =>
        (up-modify-goal g-desired-num-knight g:= g-desired-num-military)
        (up-modify-goal g-desired-num-knight c:%* 65)
        (up-modify-goal g-desired-num-unique-unit g:= g-desired-num-military)
        (up-modify-goal g-desired-num-unique-unit c:%* 35)
        )

;------------------------
;   Unique Unit Strats
;------------------------

    (defrule
    (up-compare-goal g-desired-num-military > 0)
    (goal g-current-strategy LATEI-UNIQUE)
    =>
    (up-modify-goal g-desired-num-unique-unit g:= g-desired-num-military)
    (up-modify-goal g-desired-num-unique-unit c:%* 65)
    (set-goal g-support-unit skirmisher)
    (up-modify-goal g-desired-num-skirmisher g:= g-desired-num-military)
    (up-modify-goal g-desired-num-skirmisher c:%* 35)
    )