;----------------------
;   Swordsmen Strats
;----------------------

    (defrule
    (up-compare-goal g-desired-num-military > 0)
    (goal g-current-strategy LATEI-CHAMPIONS)
    =>
    (up-modify-goal g-desired-num-militia g:= g-desired-num-military)
    (up-modify-goal g-desired-num-militia c:%* 65)
    (set-goal g-support-unit skirmisher)
    (up-modify-goal g-desired-num-skirmisher g:= g-desired-num-military)
    (up-modify-goal g-desired-num-skirmisher c:%* 35)
    )

;---------------------
;   Spearmen Strats
;---------------------

    (defrule
    (up-compare-goal g-desired-num-military > 0)
    (goal g-current-strategy LATEI-TRASHALBS)
    =>
    (up-modify-goal g-desired-num-spearman g:= g-desired-num-military)
    (up-modify-goal g-desired-num-spearman c:%* 65)
    (set-goal g-support-unit skirmisher)
    (up-modify-goal g-desired-num-skirmisher g:= g-desired-num-military)
    (up-modify-goal g-desired-num-skirmisher c:%* 35)
    )

;--------------------------
;   Eagle Warrior Strats
;--------------------------

    ;-------------
    ;   Defense
    ;-------------

        (defrule
        (goal g-game-focus DEFENSIVE)
        (up-compare-goal g-desired-num-military > 0)
        (current-age >= castle-age)
        (goal g-primary-unit eagle-warrior)
        (military-population g:< g-min-military-pop)
        (up-compare-goal g-enemy-swordsmen-count > 5)
        (goal g-primary-enemy-threat g-enemy-swordsmen-count)
        =>
        (up-modify-goal g-desired-num-archer c:max 6)
        )

        (defrule
        (goal g-game-focus DEFENSIVE)
        (up-compare-goal g-desired-num-military > 0)
        (current-age >= castle-age)
        (goal g-primary-unit eagle-warrior)
        (military-population g:< g-min-military-pop)
        (or
            (up-compare-goal g-enemy-heavy-cavalry-count > 4)
            (or
                (up-compare-goal g-enemy-camel-count > 6)
                (up-compare-goal g-enemy-elephants-count > 2)))
        (or
            (goal g-primary-enemy-threat g-enemy-heavy-cavalry-count)
            (or
                (goal g-primary-enemy-threat g-enemy-camel-count)
                (goal g-primary-enemy-threat g-enemy-elephants-count)))
        =>
        (up-modify-goal g-desired-num-spearman c:max 6)
        )

        (defrule
        (goal g-game-focus DEFENSIVE)
        (up-compare-goal g-desired-num-military > 0)
        (current-age >= castle-age)
        (goal g-primary-unit eagle-warrior)
        (military-population g:< g-min-military-pop)
        (up-compare-goal g-enemy-hand-cannoneer-slinger-count > 5)
        =>
        (up-modify-goal g-desired-num-skirmisher c:max 6)
        )

        (defrule
        (goal g-game-focus DEFENSIVE)
        (up-compare-goal g-desired-num-military > 0)
        (current-age >= castle-age)
        (goal g-primary-unit eagle-warrior)
        (military-population g:< g-min-military-pop)
        (up-compare-goal g-enemy-eagle-warrior-count > 6)
        (goal g-primary-enemy-threat g-enemy-eagle-warrior-count)
        =>
        (up-modify-goal g-desired-num-militia c:max 6)
        )

        (defrule
        (goal g-game-focus DEFENSIVE)
        (up-compare-goal g-desired-num-military > 0)
        (current-age >= castle-age)
        (goal g-primary-unit eagle-warrior)
        (military-population g:< g-min-military-pop)
        (up-compare-goal g-enemy-elephants-count > 2)
        (goal g-primary-enemy-threat g-enemy-elephants-count)
        =>
        (up-modify-goal g-desired-num-monk c:max 2)
        )

    ;----------------
    ;   Mid Castle
    ;----------------

        ;Select support unit
        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDC-EAGLES)
        (goal g-support-unit 0)
        (goal g-position FLANK)
        (or
            (up-compare-goal g-enemy-special-unique-unit-count > 6)
            (and
                (up-compare-goal g-enemy-special-unique-unit-count > 3)
                (military-population < 12)))
        (or
            (up-compare-flag g-enemy-special-unique-unit-flag == CATAPHRACT-FLAG)
            (or
                (up-compare-flag g-enemy-special-unique-unit-flag == TEUTONIC-KNIGHT-FLAG)
                (up-compare-flag g-enemy-special-unique-unit-flag == JAGUAR-WARRIOR-FLAG)))
        =>
        (set-goal g-support-unit archer)
        )

        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDC-EAGLES)
        (or
            (goal g-support-unit 0)
            (goal g-support-unit archer))
        (goal g-position FLANK)
        (or
            (up-compare-goal g-enemy-foot-archers-count > 12)
            (or
                (up-compare-goal g-enemy-cavalry-archers-count > 10)
                (up-compare-goal g-enemy-hand-cannoneer-slinger-count > 6)))
        =>
        (set-goal g-support-unit skirmisher)
        )
        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDC-EAGLES)
        (or
            (goal g-support-unit 0)
            (goal g-support-unit archer))
        (goal g-position FLANK)
        (military-population < 12)
        (or
            (up-compare-goal g-enemy-foot-archers-count > 9)
            (or
                (up-compare-goal g-enemy-cavalry-archers-count > 7)
                (up-compare-goal g-enemy-hand-cannoneer-slinger-count > 3)))
        =>
        (set-goal g-support-unit skirmisher)
        )
        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDC-EAGLES)
        (goal g-support-unit 0)
        (goal g-position FLANK)
        (or
            (up-compare-goal g-enemy-special-unique-unit-count > 8)
            (and
                (up-compare-goal g-enemy-special-unique-unit-count > 5)
                (military-population < 12)))
        (or
            (up-compare-flag g-enemy-special-unique-unit-flag == CAMEL-ARCHER-FLAG)
            (up-compare-flag g-enemy-special-unique-unit-flag == MAMELUKE-FLAG))
        =>
        (set-goal g-support-unit skirmisher)
        )

        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDC-EAGLES)
        (or
            (goal g-support-unit 0)
            (goal g-support-unit skirmisher))
        (goal g-position FLANK)
        (or
            (up-compare-goal g-enemy-light-cavalry-count > 5)
            (or
                (up-compare-goal g-enemy-heavy-cavalry-count > 4)
                (or
                    (up-compare-goal g-enemy-elephants-count > 2)
                    (and
                        (up-compare-goal g-enemy-special-unique-unit-count > 4)
                        (up-compare-flag g-enemy-special-unique-unit-flag == LEITIS-FLAG)))))
        =>
        (set-goal g-support-unit spearman)
        )
        
        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDC-EAGLES)
        (or
            (goal g-support-unit 0)
            (goal g-support-unit archer))
        (goal g-position FLANK)
        (building-type-count castle > 0)
        (or
            (up-compare-goal g-enemy-swordsmen-count > 6)
            (or
                (up-compare-goal g-enemy-eagle-warrior-count > 6)
                (up-compare-goal g-enemy-condottiero-count > 6)))
        =>
        (set-goal g-support-unit my-unique-unit)
        )
        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDC-EAGLES)
        (or
            (goal g-support-unit 0)
            (goal g-support-unit archer))
        (goal g-position FLANK)
        (building-type-count castle > 0)
        (or
            (up-compare-goal g-enemy-special-unique-unit-count > 6)
            (and
                (up-compare-goal g-enemy-special-unique-unit-count > 3)
                (military-population < 12)))
        (or
            (up-compare-flag g-enemy-special-unique-unit-flag == TEUTONIC-KNIGHT-FLAG)
            (up-compare-flag g-enemy-special-unique-unit-flag == HUSKARL-FLAG))
        =>
        (set-goal g-support-unit my-unique-unit)
        )

        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDC-EAGLES)
        (or
            (goal g-support-unit 0)
            (goal g-support-unit skirmisher))
        (goal g-position FLANK)
        (up-compare-goal g-enemy-eagle-warrior-count > 8)
        =>
        (set-goal g-support-unit militiaman)
        )

        ;Modify unit counts
        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDC-EAGLES)
        (goal g-support-unit militiaman)
        (goal g-position FLANK)
        =>
        (up-modify-goal g-desired-num-eagle-warrior g:= g-desired-num-military)
        (up-modify-goal g-desired-num-eagle-warrior c:%* 65)
        (up-modify-goal g-desired-num-militia g:= g-desired-num-military)
        (up-modify-goal g-desired-num-militia c:%* 35)
        )

        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDC-EAGLES)
        (goal g-support-unit spearman)
        (goal g-position FLANK)
        =>
        (up-modify-goal g-desired-num-eagle-warrior g:= g-desired-num-military)
        (up-modify-goal g-desired-num-eagle-warrior c:%* 65)
        (up-modify-goal g-desired-num-spearman g:= g-desired-num-military)
        (up-modify-goal g-desired-num-spearman c:%* 35)
        )

        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDC-EAGLES)
        (goal g-support-unit archer)
        (goal g-position FLANK)
        =>
        (up-modify-goal g-desired-num-eagle-warrior g:= g-desired-num-military)
        (up-modify-goal g-desired-num-eagle-warrior c:%* 65)
        (up-modify-goal g-desired-num-archer g:= g-desired-num-military)
        (up-modify-goal g-desired-num-archer c:%* 35)
        )
        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDC-EAGLES)
        (goal g-support-unit archer)
        (goal g-position FLANK)
        (up-compare-goal g-enemy-special-unique-unit-count > 6)
        (or
            (up-compare-flag g-enemy-special-unique-unit-flag == CATAPHRACT-FLAG)
            (or
                (up-compare-flag g-enemy-special-unique-unit-flag == TEUTONIC-KNIGHT-FLAG)
                (up-compare-flag g-enemy-special-unique-unit-flag == JAGUAR-WARRIOR-FLAG)))
        =>
        (up-modify-goal g-desired-num-eagle-warrior g:= g-desired-num-military)
        (up-modify-goal g-desired-num-eagle-warrior c:%* 40)
        (up-modify-goal g-desired-num-archer g:= g-desired-num-military)
        (up-modify-goal g-desired-num-archer c:%* 60)
        )
        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDC-EAGLES)
        (goal g-support-unit archer)
        (goal g-position FLANK)
        (up-compare-goal g-enemy-special-unique-unit-count > 3)
        (military-population < 12)
        (or
            (up-compare-flag g-enemy-special-unique-unit-flag == CATAPHRACT-FLAG)
            (or
                (up-compare-flag g-enemy-special-unique-unit-flag == TEUTONIC-KNIGHT-FLAG)
                (up-compare-flag g-enemy-special-unique-unit-flag == JAGUAR-WARRIOR-FLAG)))
        =>
        (up-modify-goal g-desired-num-eagle-warrior g:= g-desired-num-military)
        (up-modify-goal g-desired-num-eagle-warrior c:%* 40)
        (up-modify-goal g-desired-num-archer g:= g-desired-num-military)
        (up-modify-goal g-desired-num-archer c:%* 60)
        )
        
        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDC-EAGLES)
        (goal g-support-unit skirmisher)
        (goal g-position FLANK)
        =>
        (up-modify-goal g-desired-num-eagle-warrior g:= g-desired-num-military)
        (up-modify-goal g-desired-num-eagle-warrior c:%* 65)
        (up-modify-goal g-desired-num-skirmisher g:= g-desired-num-military)
        (up-modify-goal g-desired-num-skirmisher c:%* 35)
        )

        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDC-EAGLES)
        (goal g-support-unit my-unique-unit)
        (goal g-position FLANK)
        =>
        (up-modify-goal g-desired-num-eagle-warrior g:= g-desired-num-military)
        (up-modify-goal g-desired-num-eagle-warrior c:%* 65)
        (up-modify-goal g-desired-num-unique-unit g:= g-desired-num-military)
        (up-modify-goal g-desired-num-unique-unit c:%* 35)
        )

    ;---------------
    ;   Early Imp
    ;---------------

        ;Select support unit
        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy EARLYI-EL-DORADO)
        (goal g-support-unit 0)
        (goal g-position FLANK)
        (up-compare-goal g-spearman-tech-progress >= PRIORITY-CASTLE-COMPLETE)
        (or
            (up-compare-goal g-enemy-light-cavalry-count > 12)
            (or
                (up-compare-goal g-enemy-heavy-cavalry-count > 8)
                (or
                    (up-compare-goal g-enemy-elephants-count > 5)
                    (and
                        (up-compare-goal g-enemy-special-unique-unit-count > 8)
                        (up-compare-flag g-enemy-special-unique-unit-flag == LEITIS-FLAG)))))
        =>
        (set-goal g-support-unit spearman)
        )
        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy EARLYI-EL-DORADO)
        (goal g-support-unit 0)
        (goal g-position FLANK)
        (up-compare-goal g-spearman-tech-progress >= PRIORITY-CASTLE-COMPLETE)
        (military-population < 12)
        (or
            (up-compare-goal g-enemy-light-cavalry-count > 8)
            (or
                (up-compare-goal g-enemy-heavy-cavalry-count > 5)
                (or
                    (up-compare-goal g-enemy-elephants-count > 3)
                    (and
                        (up-compare-goal g-enemy-special-unique-unit-count > 5)
                        (up-compare-flag g-enemy-special-unique-unit-flag == LEITIS-FLAG)))))
        =>
        (set-goal g-support-unit spearman)
        )

        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy EARLYI-EL-DORADO)
        (goal g-support-unit 0)
        (goal g-position FLANK)
        (up-compare-goal g-unique-unit-tech-progress >= PRIORITY-CASTLE-COMPLETE)
        (building-type-count castle > 0)
        (or
            (up-compare-goal g-enemy-swordsmen-count > 6)
            (or
                (up-compare-goal g-enemy-eagle-warrior-count > 6)
                (up-compare-goal g-enemy-condottiero-count > 6)))
        =>
        (set-goal g-support-unit my-unique-unit)
        )
        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy EARLYI-EL-DORADO)
        (goal g-support-unit 0)
        (goal g-position FLANK)
        (up-compare-goal g-unique-unit-tech-progress >= PRIORITY-CASTLE-COMPLETE)
        (building-type-count castle > 0)
        (or
            (up-compare-goal g-enemy-special-unique-unit-count > 6)
            (and
                (up-compare-goal g-enemy-special-unique-unit-count > 3)
                (military-population < 12)))
        (or
            (up-compare-flag g-enemy-special-unique-unit-flag == TEUTONIC-KNIGHT-FLAG)
            (up-compare-flag g-enemy-special-unique-unit-flag == HUSKARL-FLAG))
        =>
        (set-goal g-support-unit my-unique-unit)
        )

        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy EARLYI-EL-DORADO)
        (goal g-support-unit 0)
        (goal g-position FLANK)
        (up-compare-goal g-militia-tech-progress >= PRIORITY-CASTLE-COMPLETE)
        (up-compare-goal g-enemy-eagle-warrior-count > 6)
        =>
        (set-goal g-support-unit militiaman)
        )

        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy EARLYI-EL-DORADO)
        (goal g-support-unit 0)
        (goal g-position FLANK)
        (up-compare-goal g-archer-tech-progress >= PRIORITY-CASTLE-COMPLETE)
        (up-compare-goal g-enemy-swordsmen-count > 6)
        =>
        (set-goal g-support-unit archer)
        )
        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy EARLYI-EL-DORADO)
        (goal g-support-unit 0)
        (goal g-position FLANK)
        (up-compare-goal g-archer-tech-progress >= PRIORITY-CASTLE-COMPLETE)
        (or
            (up-compare-goal g-enemy-special-unique-unit-count > 6)
            (and
                (up-compare-goal g-enemy-special-unique-unit-count > 3)
                (military-population < 12)))
        (or
            (up-compare-flag g-enemy-special-unique-unit-flag == CATAPHRACT-FLAG)
            (or
                (up-compare-flag g-enemy-special-unique-unit-flag == TEUTONIC-KNIGHT-FLAG)
                (up-compare-flag g-enemy-special-unique-unit-flag == JAGUAR-WARRIOR-FLAG)))
        =>
        (set-goal g-support-unit archer)
        )
        
        ;Modify unit counts
        (defrule
        (goal g-position FLANK)
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy EARLYI-EL-DORADO)
        (goal g-support-unit militiaman)
        =>
        (up-modify-goal g-desired-num-eagle-warrior g:= g-desired-num-military)
        (up-modify-goal g-desired-num-eagle-warrior c:%* 65)
        (up-modify-goal g-desired-num-militia g:= g-desired-num-military)
        (up-modify-goal g-desired-num-militia c:%* 35)
        )

        (defrule
        (goal g-position FLANK)
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy EARLYI-EL-DORADO)
        (goal g-support-unit spearman)
        =>
        (up-modify-goal g-desired-num-eagle-warrior g:= g-desired-num-military)
        (up-modify-goal g-desired-num-eagle-warrior c:%* 75)
        (up-modify-goal g-desired-num-spearman g:= g-desired-num-military)
        (up-modify-goal g-desired-num-spearman c:%* 25)
        )

        (defrule
        (goal g-position FLANK)
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy EARLYI-EL-DORADO)
        (goal g-support-unit archer)
        =>
        (up-modify-goal g-desired-num-eagle-warrior g:= g-desired-num-military)
        (up-modify-goal g-desired-num-eagle-warrior c:%* 75)
        (up-modify-goal g-desired-num-archer g:= g-desired-num-military)
        (up-modify-goal g-desired-num-archer c:%* 25)
        )

        (defrule
        (goal g-position FLANK)
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy EARLYI-EL-DORADO)
        (goal g-support-unit my-unique-unit)
        =>
        (up-modify-goal g-desired-num-eagle-warrior g:= g-desired-num-military)
        (up-modify-goal g-desired-num-eagle-warrior c:%* 65)
        (up-modify-goal g-desired-num-unique-unit g:= g-desired-num-military)
        (up-modify-goal g-desired-num-unique-unit c:%* 35)
        )

    ;-------------
    ;   Mid Imp
    ;-------------

        ;Select support unit
        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDI-EAGLES)
        (goal g-support-unit 0)
        (goal g-position FLANK)
        (or
            (up-compare-goal g-enemy-heavy-cavalry-count > 8)
            (or
                (up-compare-goal g-enemy-elephants-count > 5)
                (and
                    (up-compare-goal g-enemy-special-unique-unit-count > 8)
                    (up-compare-flag g-enemy-special-unique-unit-flag == LEITIS-FLAG))))
        =>
        (set-goal g-support-unit spearman)
        )
        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDI-EAGLES)
        (goal g-support-unit 0)
        (goal g-position FLANK)
        (military-population < 12)
        (or
            (up-compare-goal g-enemy-heavy-cavalry-count > 5)
            (or
                (up-compare-goal g-enemy-elephants-count > 3)
                (and
                    (up-compare-goal g-enemy-special-unique-unit-count > 5)
                    (up-compare-flag g-enemy-special-unique-unit-flag == LEITIS-FLAG))))
        =>
        (set-goal g-support-unit spearman)
        )

        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDI-EAGLES)
        (goal g-support-unit 0)
        (goal g-position FLANK)
        (building-type-count castle > 0)
        (or
            (up-compare-goal g-enemy-swordsmen-count > 6)
            (or
                (up-compare-goal g-enemy-eagle-warrior-count > 6)
                (up-compare-goal g-enemy-condottiero-count > 6)))
        =>
        (set-goal g-support-unit my-unique-unit)
        )
        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDI-EAGLES)
        (goal g-support-unit 0)
        (goal g-position FLANK)
        (building-type-count castle > 0)
        (or
            (up-compare-goal g-enemy-special-unique-unit-count > 6)
            (and
                (up-compare-goal g-enemy-special-unique-unit-count > 3)
                (military-population < 12)))
        (or
            (up-compare-flag g-enemy-special-unique-unit-flag == TEUTONIC-KNIGHT-FLAG)
            (up-compare-flag g-enemy-special-unique-unit-flag == HUSKARL-FLAG))
        =>
        (set-goal g-support-unit my-unique-unit)
        )

        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDI-EAGLES)
        (goal g-support-unit 0)
        (goal g-position FLANK)
        (or
            (up-compare-goal g-enemy-foot-archers-count > 15)
            (and
                (military-population < 20)
                (up-compare-goal g-enemy-foot-archers-count > 8)))
        =>
        (set-goal g-support-unit skirmisher)
        )
        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDI-EAGLES)
        (goal g-support-unit 0)
        (goal g-position FLANK)
        (or
            (up-compare-goal g-enemy-cavalry-archers-count > 15)
            (and
                (military-population < 20)
                (up-compare-goal g-enemy-cavalry-archers-count > 8)))
        =>
        (set-goal g-support-unit skirmisher)
        )
        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDI-EAGLES)
        (goal g-support-unit 0)
        (goal g-position FLANK)
        (or
            (up-compare-goal g-enemy-special-unique-unit-count > 8)
            (and
                (up-compare-goal g-enemy-special-unique-unit-count > 5)
                (military-population < 12)))
        (or
            (up-compare-flag g-enemy-special-unique-unit-flag == GENOESE-CROSSBOWMAN-FLAG)
            (up-compare-flag g-enemy-special-unique-unit-flag == MAMELUKE-FLAG))
        =>
        (set-goal g-support-unit skirmisher)
        )

        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDI-EAGLES)
        (goal g-support-unit 0)
        (goal g-position FLANK)
        (building-type-count castle == 0)
        (up-compare-goal g-enemy-eagle-warrior-count > 8)
        =>
        (set-goal g-support-unit militiaman)
        )

        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDI-EAGLES)
        (goal g-support-unit 0)
        (goal g-position FLANK)
        =>
        (set-goal g-support-unit archer)
        )

        ;Modify unit counts
        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDI-EAGLES)
        (goal g-support-unit militiaman)
        (goal g-position FLANK)
        =>
        (up-modify-goal g-desired-num-eagle-warrior g:= g-desired-num-military)
        (up-modify-goal g-desired-num-eagle-warrior c:%* 65)
        (up-modify-goal g-desired-num-militia g:= g-desired-num-military)
        (up-modify-goal g-desired-num-militia c:%* 35)
        )

        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDI-EAGLES)
        (goal g-support-unit spearman)
        (goal g-position FLANK)
        =>
        (up-modify-goal g-desired-num-eagle-warrior g:= g-desired-num-military)
        (up-modify-goal g-desired-num-eagle-warrior c:%* 65)
        (up-modify-goal g-desired-num-spearman g:= g-desired-num-military)
        (up-modify-goal g-desired-num-spearman c:%* 35)
        )
        
        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDI-EAGLES)
        (goal g-support-unit archer)
        (goal g-position FLANK)
        =>
        (up-modify-goal g-desired-num-eagle-warrior g:= g-desired-num-military)
        (up-modify-goal g-desired-num-eagle-warrior c:%* 65)
        (up-modify-goal g-desired-num-archer g:= g-desired-num-military)
        (up-modify-goal g-desired-num-archer c:%* 35)
        )

        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDI-EAGLES)
        (goal g-support-unit skirmisher)
        (goal g-position FLANK)
        =>
        (up-modify-goal g-desired-num-eagle-warrior g:= g-desired-num-military)
        (up-modify-goal g-desired-num-eagle-warrior c:%* 65)
        (up-modify-goal g-desired-num-skirmisher g:= g-desired-num-military)
        (up-modify-goal g-desired-num-skirmisher c:%* 35)
        )
        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDI-EAGLES)
        (goal g-support-unit skirmisher)
        (goal g-position FLANK)
        (or
            (up-compare-goal g-enemy-special-unique-unit-count > 8)
            (and
                (up-compare-goal g-enemy-special-unique-unit-count > 5)
                (military-population < 12)))
        (or
            (up-compare-flag g-enemy-special-unique-unit-flag == GENOESE-CROSSBOWMAN-FLAG)
            (up-compare-flag g-enemy-special-unique-unit-flag == MAMELUKE-FLAG))
        =>
        (up-modify-goal g-desired-num-eagle-warrior g:= g-desired-num-military)
        (up-modify-goal g-desired-num-eagle-warrior c:%* 70)
        (up-modify-goal g-desired-num-skirmisher g:= g-desired-num-military)
        (up-modify-goal g-desired-num-skirmisher c:%* 30)
        )

        (defrule
        (up-compare-goal g-desired-num-military > 0)
        (goal g-current-strategy MIDI-EAGLES)
        (goal g-support-unit my-unique-unit)
        (goal g-position FLANK)
        =>
        (up-modify-goal g-desired-num-eagle-warrior g:= g-desired-num-military)
        (up-modify-goal g-desired-num-eagle-warrior c:%* 50)
        (up-modify-goal g-desired-num-unique-unit g:= g-desired-num-military)
        (up-modify-goal g-desired-num-unique-unit c:%* 50)
        )

;-------------------
;   Archer Strats
;-------------------

    (defrule
    (up-compare-goal g-desired-num-military > 0)
    (goal g-primary-unit archer)
    (up-compare-goal g-current-strategy != FL-GUERILLA)
    (up-compare-goal g-current-strategy != MIDF-FULL-RANGED)
    (up-compare-goal g-current-strategy != MIDC-RANGED)
    (up-compare-goal g-age-status >= SAVE-FOR-CASTLE)
    (up-compare-goal g-age-status < MID-CASTLE)
    (or
        (goal g-town-under-attack YES)
        (up-compare-goal g-age-status >= TO-CASTLE))
    =>
    (up-modify-goal g-desired-num-archer g:= g-desired-num-military)
    (up-modify-goal g-desired-num-archer c:%* 85)
    (up-modify-goal g-desired-num-skirmisher g:= g-desired-num-military)
    (up-modify-goal g-desired-num-skirmisher c:%* 15)
    )

    (defrule
    (up-compare-goal g-desired-num-military > 0)
    (goal g-primary-unit archer)
    (up-compare-goal g-current-strategy != MIDC-RANGED)
    (current-age == castle-age)
    (up-compare-goal g-age-status >= MID-CASTLE)
    =>
    (up-modify-goal g-desired-num-archer g:= g-desired-num-military)
    (up-modify-goal g-desired-num-archer c:%* 90)
    (up-modify-goal g-desired-num-eagle-warrior g:= g-desired-num-military)
    (up-modify-goal g-desired-num-eagle-warrior c:%* 10)
    )

    (defrule
    (goal g-current-strategy MIDI-ARBS)
    (current-age == imperial-age)
    (or
        (players-civ target-player aztec)
        (or
            (players-civ target-player incan)
            (players-civ target-player mayan)))
    =>
    (up-modify-goal g-desired-num-archer g:= g-desired-num-military)
    (up-modify-goal g-desired-num-archer c:%* 80)
    (set-goal g-support-unit militiaman)
    (up-modify-goal g-desired-num-militia g:= g-desired-num-military)
    (up-modify-goal g-desired-num-militia c:%* 20)
    (up-jump-rule 1)
    )
        (defrule
        (goal g-current-strategy MIDI-ARBS)
        (current-age == imperial-age)
        =>
        (up-modify-goal g-desired-num-archer g:= g-desired-num-military)
        (up-modify-goal g-desired-num-archer c:%* 80)
        (set-goal g-support-unit spearman)
        (up-modify-goal g-desired-num-spearman g:= g-desired-num-military)
        (up-modify-goal g-desired-num-spearman c:%* 20)
        )

;-----------------------
;   Skirmisher Strats
;-----------------------

    (defrule
    (up-compare-goal g-desired-num-military > 0)
    (goal g-current-strategy LATEI-TRASHKIRMS)
    =>
    (up-modify-goal g-desired-num-skirmisher g:= g-desired-num-military)
    (up-modify-goal g-desired-num-skirmisher c:%* 65)
    (set-goal g-support-unit spearman)
    (up-modify-goal g-desired-num-spearman g:= g-desired-num-military)
    (up-modify-goal g-desired-num-spearman c:%* 35)
    )

;------------------------
;   Unique Unit Strats
;------------------------

    (defrule
    (up-compare-goal g-desired-num-military > 0)
    (goal g-current-strategy LATEI-UNIQUE)
    =>
    (up-modify-goal g-desired-num-unique-unit g:= g-desired-num-military)
    (up-modify-goal g-desired-num-unique-unit c:%* 65)
    (set-goal g-support-unit skirmisher)
    (up-modify-goal g-desired-num-skirmisher g:= g-desired-num-military)
    (up-modify-goal g-desired-num-skirmisher c:%* 35)
    )