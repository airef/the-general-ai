;Resign rules

#load-if-not-defined UP-ALLY-IN-GAME

	(load "The General 3.1/Functions/Reset")

	;Resign if we have < 40% of the enemy pop
	(defrule
	(goal g-resign NO)
	(game-time > 600)
	(population < TWENTY-FIVE-PERCENT-POP)
	(players-population any-enemy >= TWENTY-FIVE-PERCENT-POP)
	(up-get-fact population 0 g-temp)	;Example, say we have 30 pop. We have < 40% of enemy pop if enemy has > 75 pop.
	(up-modify-goal g-temp c:%* -150)	;Multiply 30 * 1.5 = 45. This equals the min pop difference required to resign (75 minus 30).
	(up-compare-goal g-top-enemy-pop-parity g:< g-temp)
	(or
		(up-compare-goal g-age-status >= MID-CASTLE)
		(population < 12))
	=>
	(set-goal g-resign DECIDE-TO-RESIGN)
	)
	
#end-if

#load-if-defined UP-ALLY-IN-GAME

	;Ask for resources if out of villagers and don't have food to train them.
	(defrule
	(unit-type-count-total villager == 0)
	(not
		(can-afford-unit villager))
	(building-type-count-total town-center > 0)
	(building-type-count-total market == 0)
	(up-compare-goal g-resign < ASK-FOR-RES)
	(player-in-game any-ally)
	=>
	(chat-to-allies CHAT-REQUEST-FOOD-FOR-VILLAGERS1)
	(chat-to-allies CHAT-REQUEST-FOOD-FOR-VILLAGERS2)
	(set-goal g-resign ASK-FOR-RES)
	(enable-timer t-resign 30)
	)
	
	;Impossible to develop if wood can't be gathered. Ask for wood.
	(defrule
	(building-type-count-total lumber-camp == 0)
	(building-type-count-total town-center == 0)
	(building-type-count-total market == 0)
	(not
		(can-afford-building lumber-camp))
	(up-compare-goal g-resign < ASK-FOR-RES)
	(player-in-game any-ally)
	=>
	(chat-to-allies CHAT-REQUEST-WOOD-FOR-CAMP2)
	(chat-to-allies CHAT-REQUEST-WOOD-FOR-CAMP2)
	(set-goal g-resign ASK-FOR-RES)
	(enable-timer t-resign 30)
	)
	
	;Tribute resources away before being defeated
	(defrule
	(game-time > 600)
	(unit-type-count villager == 0)
	(not
		(can-train villager))
	(players-population any-ally > 60)
	(goal g-resign ASK-FOR-RES)
	(timer-triggered t-resign)
	=>
	(tribute-to-player this-any-ally wood 100000)
	(tribute-to-player this-any-ally food 100000)
	(tribute-to-player this-any-ally gold 100000)
	(tribute-to-player this-any-ally stone 100000)
	(chat-to-allies CHAT-DEFEATED)
	(disable-self)
	)

	(load "The General 3.1/Functions/Reset")

	;Resign if team has < 20% pop of enemy team
	(defrule
	(up-compare-goal g-resign < DECIDE-TO-RESIGN)
	(population < FIFTY-PERCENT-POP)
	(not
		(player-in-game any-ally))
	(up-get-fact population 0 g-temp)
	(up-get-fact-sum any-ally population 0 g-temp-2)
	(up-modify-goal g-temp g:+ g-temp-2)
	(up-get-fact-sum any-enemy population 0 g-temp-3)
	(up-modify-goal g-temp g:%/ g-temp-3)
	(up-compare-goal g-temp < 20)
	=>
	(set-goal g-resign DECIDE-TO-RESIGN)
	)

	(defrule
	(up-compare-goal g-resign < DECIDE-TO-RESIGN)
	(population < FIFTY-PERCENT-POP)
	(civilian-population < THIRTY-FIVE-PERCENT-POP)
	(players-population every-ally < THIRTY-FIVE-PERCENT-POP)
	(players-population any-enemy >= FIFTY-PERCENT-POP)
	(up-get-fact population 0 g-temp)
	(up-get-fact-sum any-ally population 0 g-temp-2)
	(up-modify-goal g-temp g:+ g-temp-2)
	(up-get-fact-sum any-enemy population 0 g-temp-3)
	(up-modify-goal g-temp g:%/ g-temp-3)
	(up-compare-goal g-temp < 20)
	=>
	(set-goal g-resign DECIDE-TO-RESIGN)
	)
	
#end-if

(defrule
(goal g-resign DECIDE-TO-RESIGN)
(up-timer-status t-resign != timer-running)
=>
(chat-to-all CHAT-RESIGN)
(chat-to-all CHAT-GG)
(set-goal g-resign RESIGN)
(enable-timer t-resign 5)
)

(defrule
(up-timer-status t-resign == timer-triggered)
(goal g-resign RESIGN)
=>
(resign)
)